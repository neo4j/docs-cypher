:description: The `CREATE` clause is used to create nodes and relationships.

[[query-create]]
= CREATE

[abstract]
--
The `CREATE` clause is used to create nodes and relationships.
--

* xref::clauses/create.adoc#create-nodes[Create nodes]
** xref::clauses/create.adoc#create-create-single-node[Create single node]
** xref::clauses/create.adoc#create-create-multiple-nodes[Create multiple nodes]
** xref::clauses/create.adoc#create-create-a-node-with-a-label[Create a node with a label]
** xref::clauses/create.adoc#create-create-a-node-with-multiple-labels[Create a node with multiple labels]
** xref::clauses/create.adoc#create-create-node-and-add-labels-and-properties[Create node and add labels and properties]
** xref::clauses/create.adoc#create-return-created-node[Return created node]
* xref::clauses/create.adoc#create-relationships[Create relationships]
** xref::clauses/create.adoc#create-create-a-relationship-between-two-nodes[Create a relationship between two nodes]
** xref::clauses/create.adoc#create-create-a-relationship-and-set-properties[Create a relationship and set properties]
* xref::clauses/create.adoc#create-create-a-full-path[Create a full path]
* xref::clauses/create.adoc#use-parameters-with-create[Use parameters with `CREATE`]
** xref::clauses/create.adoc#create-create-node-with-a-parameter-for-the-properties[Create node with a parameter for the properties]
** xref::clauses/create.adoc#create-create-multiple-nodes-with-a-parameter-for-their-properties[Create multiple nodes with a parameter for their properties]

[TIP]
====
In the `CREATE` clause, patterns are used extensively.
Read xref::syntax/patterns.adoc[Patterns] for an introduction.
====

[[create-nodes]]
== Create nodes

[[create-create-single-node]]
=== Create single node

Creating a single node is done by issuing the following query:

.Query
[source, cypher, indent=0]
----
CREATE (n)
----

.Result
[role="queryresult",options="footer",cols="1*<m"]
|===
1+|(empty result)
1+d|Rows: 0 +
Nodes created: 1
|===


[[create-create-multiple-nodes]]
=== Create multiple nodes

Creating multiple nodes is done by separating them with a comma.

.Query
[source, cypher, indent=0]
----
CREATE (n), (m)
----

.Result
[role="queryresult",options="footer",cols="1*<m"]
|===
1+|(empty result)
1+d|Rows: 0 +
Nodes created: 2
|===


[[create-create-a-node-with-a-label]]
=== Create a node with a label

To add a label when creating a node, use the syntax below:

.Query
[source, cypher, indent=0]
----
CREATE (n:Person)
----

.Result
[role="queryresult",options="footer",cols="1*<m"]
|===
1+|(empty result)
1+d|Rows: 0 +
Nodes created: 1 +
Labels added: 1
|===


[[create-create-a-node-with-multiple-labels]]
=== Create a node with multiple labels

To add labels when creating a node, use the syntax below.
In this case, we add two labels.

.Query
[source, cypher, indent=0]
----
CREATE (n:Person:Swedish)
----

.Result
[role="queryresult",options="footer",cols="1*<m"]
|===
1+|(empty result)
1+d|Rows: 0 +
Nodes created: 1 +
Labels added: 2
|===


[[create-create-node-and-add-labels-and-properties]]
=== Create node and add labels and properties

When creating a new node with labels, you can add properties at the same time.

.Query
[source, cypher, indent=0]
----
CREATE (n:Person {name: 'Andy', title: 'Developer'})
----

.Result
[role="queryresult",options="footer",cols="1*<m"]
|===
1+|(empty result)
1+d|Rows: 0 +
Nodes created: 1 +
Properties set: 2 +
Labels added: 1
|===


[[create-return-created-node]]
=== Return created node

Creating a single node is done by issuing the following query:

.Query
[source, cypher, indent=0]
----
CREATE (a {name: 'Andy'})
RETURN a.name
----

The name of the newly-created node is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +a.name+
| +"Andy"+
1+d|Rows: 1 +
Nodes created: 1 +
Properties set: 1
|===


[[create-relationships]]
== Create relationships

[[create-create-a-relationship-between-two-nodes]]
=== Create a relationship between two nodes

To create a relationship between two nodes, we first get the two nodes.
Once the nodes are loaded, we simply create a relationship between them.

////
[source, cypher, role=test-setup]
----
CREATE
  (a:Person {name: 'A'}),
  (b:Person {name: 'B'})
----
////

.Query
[source, cypher]
----
MATCH
  (a:Person),
  (b:Person)
WHERE a.name = 'A' AND b.name = 'B'
CREATE (a)-[r:RELTYPE]->(b)
RETURN type(r)
----

The created relationship is returned by the query.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +type(r)+
| +"RELTYPE"+
1+d|Rows: 1 +
Relationships created: 1
|===


[[create-create-a-relationship-and-set-properties]]
=== Create a relationship and set properties

Setting properties on relationships is done in a similar manner to how it's done when creating nodes.
Note that the values can be any expression.

////
CREATE
  (a:Person {name: 'A'}),
  (b:Person {name: 'B'})
////

.Query
[source, cypher]
----
MATCH
  (a:Person),
  (b:Person)
WHERE a.name = 'A' AND b.name = 'B'
CREATE (a)-[r:RELTYPE {name: a.name + '<->' + b.name}]->(b)
RETURN type(r), r.name
----

The type and name of the newly-created relationship is returned by the example query.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +type(r)+ | +r.name+
| +"RELTYPE"+ | +"A<->B"+
2+d|Rows: 1 +
Relationships created: 1 +
Properties set: 1
|===


[[create-create-a-full-path]]
== Create a full path

When you use `CREATE` and a pattern, all parts of the pattern that are not already in scope at this time will be created.

.Query
[source, cypher]
----
CREATE p = (andy {name:'Andy'})-[:WORKS_AT]->(neo)<-[:WORKS_AT]-(michael {name: 'Michael'})
RETURN p
----

This query creates three nodes and two relationships in one go, assigns it to a path variable, and returns it.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +p+
| +(2)-[WORKS_AT,0]->(3)<-[WORKS_AT,1]-(4)+
1+d|Rows: 1 +
Nodes created: 3 +
Relationships created: 2 +
Properties set: 2
|===


[[use-parameters-with-create]]
== Use parameters with `CREATE`

[[create-create-node-with-a-parameter-for-the-properties]]
=== Create node with a parameter for the properties

You can also create a graph entity from a map.
All the key/value pairs in the map will be set as properties on the created relationship or node.
In this case we add a `Person` label to the node as well.

.Parameters
[source,javascript, indent=0]
----
{
  "props": {
    "name": "Andy",
    "position": "Developer"
  }
}
----

.Query
[source, cypher]
----
CREATE (n:Person $props)
RETURN n
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +n+
| +Node[2]{name:"Andy",position:"Developer"}+
1+d|Rows: 1 +
Nodes created: 1 +
Properties set: 2 +
Labels added: 1
|===


[[create-create-multiple-nodes-with-a-parameter-for-their-properties]]
=== Create multiple nodes with a parameter for their properties

By providing Cypher an array of maps, it will create a node for each map.

.Parameters
[source,javascript, indent=0]
----
{
  "props": [ {
    "name": "Andy",
    "position": "Developer"
  }, {
    "name": "Michael",
    "position": "Developer"
  } ]
}
----

.Query
[source, cypher, indent=0]
----
UNWIND $props AS map
CREATE (n)
SET n = map
----

.Result
[role="queryresult",options="footer",cols="1*<m"]
|===
1+|(empty result)
1+d|Rows: 0 +
Nodes created: 2 +
Properties set: 4
|===

