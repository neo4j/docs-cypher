:description: The `CREATE` clause is used to create nodes and relationships.

[[query-create]]
= CREATE

== Introduction

The `CREATE` clause allows you to create nodes and relationships.
To define these entities, `CREATE` uses a syntax similar to that of xref::clauses/match.adoc[`MATCH`].
However, while xref::patterns/index.adoc[patterns] only need to evaluate to either true or false, the syntax for `CREATE` needs to specify exactly what nodes and relationships to create.

[[create-nodes]]
== Syntax for nodes

The `CREATE` clause allows you to create one or more nodes.
Each node can be assigned labels and properties.
You can bind each node to a variable that you can refer to later in the query.
Multiple labels are separated by colons.

.Query
[source, cypher, indent=0]
----
CREATE (charlie:Person:Actor {name: 'Charlie Sheen'}), (oliver:Person:Director {name: 'Oliver Stone'})
----

Multiple labels can also be separated by an ampersand `&`, in the same manner as it is used in xref:patterns/reference.adoc#label-expressions[label expressions].
Separation by colon `:` and ampersand `&` cannot be mixed in the same query.

.Query
[source, cypher, indent=0]
----
CREATE (charlie:Person&Actor {name: 'Charlie Sheen'}), (oliver:Person&Director {name: 'Oliver Stone'})
----

Both of the above queries create two nodes, bound to the variables `charlie` and `oliver`, each with a `Person` label and a `name` property.
The node representing Charlie Sheen also has the label `Actor` while the node representing Oliver Stone is assigned the label `Director`.

[[create-relationships]]
== Syntax for relationships

Relationships can also be created using the `CREATE` clause.
Unlike nodes, relationships always need exactly one relationship type and a direction.
Similar to nodes, relationships can be assigned properties and relationship types and be bound to variables.

.Query
[source, cypher, indent=0]
----
CREATE (charlie:Person:Actor {name: 'Charlie Sheen'})-[:ACTED_IN {role: 'Bud Fox'}]->(wallStreet:Movie {title: 'Wall Street'})<-[:DIRECTED]-(oliver:Person:Director {name: 'Oliver Stone'})
----

This query creates the `Person` nodes for Charlie Sheen and Oliver Stone and the `Movie` node for Wall Street.
It also created the relationships of the types `ACTED_IN` and `DIRECTED` between them.

[[reusing-variables]]
== Reusing variables
The previous example created a path between the specified nodes.
Note, that these newly created nodes and relationships are not connected to what was previously in the graph.
To connect them to already existing data, bind the desired nodes and relationships to variables.
These variables can then be passed along to subsequent clauses in a query that target pre-existing elements in the graph.

.Query
[source, cypher, indent=0]
----
MATCH (charlie:Person {name: 'Charlie Sheen'}), (oliver:Person {name: 'Oliver Stone'})
CREATE (charlie)-[:ACTED_IN {role: 'Bud Fox'}]->(wallStreet:Movie {title: 'Wall Street'})<-[:DIRECTED]-(oliver)
----

In this example, the `MATCH` clause finds the nodes Charlie Sheen and Oliver Stone and binds them to the `charlie` and `oliver` variables respectively.
These variables are then passed along to the subsequent `CREATE` clause, which creates new relationships from the bound nodes.

You can also reuse variables from the same `CREATE`, both in the same or a later clause.
This way, you can, for example, define constructs that are more complex than just a linear path.

.Query
[source, cypher, indent=0]
----
CREATE p = (charlie:Person:Actor {name: 'Charlie Sheen'})-[:ACTED_IN {role: 'Bud Fox'}]->(wallStreet:Movie {title: 'Wall Street'})<-[:DIRECTED]-(oliver:Person:Director {name: 'Oliver Stone'}), (wallStreet)<-[:ACTED_IN {role: 'Gordon Gekko'}]-(michael:Person:Actor {name: 'Michael Douglas'})
RETURN length(p)
----

Creates all three nodes for Charlie Sheen, Oliver Stone and Michael Douglas and connects them all to the node representing the Wall Street movie.
It then returns the length of the path from Charlie Sheen to Oliver Stone.

Note that when repeating a node's variable, you may not add labels or properties to the repetition.

.Query
[source, cypher, role=test-fail]
----
MATCH (charlie:Person {name: 'Charlie Sheen'})
CREATE (charlie:Actor)
----

This query will fail because the variable `charlie` has already been bound to a pre-existing node, and therefore it cannot be reused to create a new node.
If you intend to add a label, use the xref:clauses/set.adoc#set-set-a-label-on-a-node[`SET` clause] instead.

[[reusing-variables-properties]]
== Reusing variables in properties
The value that can be assigned to a node's or a relationship's property can be defined by an xref::syntax/expressions.adoc[expression].

////
.Parameters
[source,javascript, role=test-setup]
----
{
  "age": 23
}
----
////

.Query
[source, cypher, indent=0]
----
MATCH (person:Person)
  WHERE person.name IS NOT NULL
CREATE (anotherPerson:Person {name: person.name, age: $age})
----

This example created a `Person` node with the same name as another person and the age from a xref:syntax/parameters.adoc[parameter] called `age`.

Such an expression may not contain a reference to a variable that is defined in the same `CREATE` statement.
This is to ensure that the value of a property is always clear.

.Query
[source, cypher, role=test-fail]
----
CREATE (charlie {score: oliver.score + 1}), (oliver {score: charlie.score + 1})
----

This query tries to create nodes such that Charlie's score is higher than Oliver's and vice versa, which is a contradiction.
The query therefore fails.

[[use-parameters-with-create]]
== Use parameters with `CREATE`

[[create-create-node-with-a-parameter-for-the-properties]]
=== Create node with a parameter for the properties

You can also create a graph entity from a map.
All the key/value pairs in the map will be set as properties on the created relationship or node.
In this case we add a `Person` label to the node as well.

.Parameters
[source,javascript, indent=0]
----
{
  "props": {
    "name": "Andy",
    "position": "Developer"
  }
}
----

.Query
[source, cypher]
----
CREATE (n:Person $props)
RETURN n
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +n+
| +Node[2]{name:"Andy",position:"Developer"}+
1+d|Rows: 1 +
Nodes created: 1 +
Properties set: 2 +
Labels added: 1
|===


[[create-create-multiple-nodes-with-a-parameter-for-their-properties]]
=== Create multiple nodes with a parameter for their properties

By providing Cypher an array of maps, it will create a node for each map.

.Parameters
[source,javascript, indent=0]
----
{
  "props": [ {
    "name": "Andy",
    "position": "Developer"
  }, {
    "name": "Michael",
    "position": "Developer"
  } ]
}
----

.Query
[source, cypher, indent=0]
----
UNWIND $props AS map
CREATE (n)
SET n = map
----

.Result
[role="queryresult",options="footer",cols="1*<m"]
|===
1+|(empty result)
1+d|Rows: 0 +
Nodes created: 2 +
Properties set: 4
|===

[[insert-as-synonym-of-create]]
== `INSERT` as a synonym of `CREATE`

`INSERT` can be used as a synonym to `CREATE` for creating nodes and relationships.
`INSERT` requires that multiple labels are separated by an ampersand `&` and not by colon `:`.

.Query
[source, cypher, indent=0]
----
INSERT (tom:Person&Actor&Director {name: 'Tom Hanks'})
----

Creates a node, bound to the variable `tom`, with the labels `Person`, `Actor` and `Director` and a `name` property.

