:description: `WHERE` adds constraints to the patterns in a `MATCH` or `OPTIONAL MATCH` clause or filters the results of a `WITH` clause.

[[query-where]]
= WHERE

[abstract]
--
`WHERE` adds constraints to the patterns in a `MATCH` or `OPTIONAL MATCH` clause or filters the results of a `WITH` clause.
--

* xref::clauses/where.adoc#where-introduction[Introduction]
* xref::clauses/where.adoc#query-where-basic[Basic usage]
 ** xref::clauses/where.adoc#node-pattern-predicates[Node pattern predicates]
 ** xref::clauses/where.adoc#boolean-operations[Boolean operations]
 ** xref::clauses/where.adoc#filter-on-node-label[Filter on node label]
 ** xref::clauses/where.adoc#filter-on-node-property[Filter on node property]
 ** xref::clauses/where.adoc#filter-on-relationship-property[Filter on relationship property]
 ** xref::clauses/where.adoc#filter-on-dynamic-property[Filter on dynamically-computed property]
 ** xref::clauses/where.adoc#property-existence-checking[Property existence checking]
* xref::clauses/where.adoc#query-where-string[String matching]
 ** xref::clauses/where.adoc#match-string-start[Prefix string search using `STARTS WITH`]
 ** xref::clauses/where.adoc#match-string-end[Suffix string search using `ENDS WITH`]
 ** xref::clauses/where.adoc#match-string-contains[Substring search using `CONTAINS`]
 ** xref::clauses/where.adoc#match-string-negation[String matching negation]
* xref::clauses/where.adoc#query-where-regex[Regular expressions]
 ** xref::clauses/where.adoc#matching-using-regular-expressions[Matching using regular expressions]
 ** xref::clauses/where.adoc#escaping-in-regular-expressions[Escaping in regular expressions]
 ** xref::clauses/where.adoc#case-insensitive-regular-expressions[Case-insensitive regular expressions]
* xref::clauses/where.adoc#query-where-patterns[Using path patterns in `WHERE`]
 ** xref::clauses/where.adoc#filter-on-patterns[Filter on patterns]
 ** xref::clauses/where.adoc#filter-on-patterns-using-not[Filter on patterns using `NOT`]
 ** xref::clauses/where.adoc#filter-on-patterns-with-properties[Filter on patterns with properties]
 ** xref::clauses/where.adoc#filter-on-relationship-type[Filter on relationship type]
* xref::clauses/where.adoc#query-where-lists[Lists]
 ** xref::clauses/where.adoc#where-in-operator[`IN` operator]
* xref::clauses/where.adoc#missing-properties-and-values[Missing properties and values]
 ** xref::clauses/where.adoc#default-to-false-missing-property[Default to `false` if property is missing]
 ** xref::clauses/where.adoc#default-to-true-missing-property[Default to `true` if property is missing]
 ** xref::clauses/where.adoc#filter-on-null[Filter on `null`]
* xref::clauses/where.adoc#query-where-ranges[Using ranges]
 ** xref::clauses/where.adoc#simple-range[Simple range]
 ** xref::clauses/where.adoc#composite-range[Composite range]
* xref::clauses/where.adoc#pattern-element-predicates[Pattern element predicates]
 ** xref::clauses/where.adoc#relationship-pattern-predicates[Relationship pattern predicates]


[[where-introduction]]
== Introduction

`WHERE` is not a clause in its own right -- rather, it is part of `MATCH`, `OPTIONAL MATCH`, and `WITH`.

In the case of `WITH`, `WHERE` simply filters the results.

For `MATCH` and `OPTIONAL MATCH` on the other hand, `WHERE` adds constraints to the patterns described.
_It should not be seen as a filter after the matching is finished._

[IMPORTANT]
====
In the case of multiple `MATCH` / `OPTIONAL MATCH` clauses, the predicate in `WHERE` is always a part of the patterns in the directly preceding `MATCH` / `OPTIONAL MATCH`.
Both results and performance may be impacted if the `WHERE` is put inside the wrong `MATCH` clause.
====

[NOTE]
====
xref::indexes-for-search-performance.adoc[Indexes] may be used to optimize queries using `WHERE` in a variety of cases.
====

The following graph is used for the examples below:

image:graph_where_clause.svg[]

////
CREATE (andy:Swedish:Person {name: 'Andy', age: 36, belt: 'white'}),
(timothy:Person {name: 'Timothy', age: 25, address: 'Sweden/Malmo'}),
(peter:Person {name: 'Peter', age: 35, email: 'peter_n@example.com'}),
(andy)-[:KNOWS {since: 2012}]->(timothy),
(andy)-[:KNOWS {since: 1999}]->(peter),
(andy)-[:HAS_DOG {since: 2016}]->(:Dog {name:'Andy'}),
(fido:Dog {name:'Fido'})<-[:HAS_DOG {since: 2010}]-(peter)-[:HAS_DOG {since: 2018}]->(:Dog {name:'Ozzy'}),
(fido)-[:HAS_TOY]->(:Toy{name:'Banana'})
////


[[query-where-basic]]
== Basic usage

[[node-pattern-predicates]]
=== Node pattern predicates

`WHERE` can appear inside a node pattern in a `MATCH` clause or a pattern comprehension:


.+WHERE+
======

.Query
[source, cypher, indent=0]
----
WITH 30 AS minAge
MATCH (a:Person WHERE a.name = 'Andy')-[:KNOWS]->(b:Person WHERE b.age > minAge)
RETURN b.name
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +b.name+
| +"Peter"+
1+d|Rows: 1
|===

======

When used this way, predicates in `WHERE` can reference the node variable that the `WHERE` clause belongs to, but not other elements of the `MATCH` pattern.

The same rule applies to pattern comprehensions.

.+WHERE+
======

.Query
[source, cypher, indent=0]
----
MATCH (a:Person {name: 'Andy'})
RETURN [(a)-->(b WHERE b:Person) | b.name] AS friends
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +friends+
| +["Peter","Timothy"]+
1+d|Rows: 1
|===

======

[[boolean-operations]]
=== Boolean operations

You can use the boolean operators `AND`, `OR`, `XOR` and `NOT`.
See xref::syntax/working-with-null.adoc[] for more information on how this works with `null`.

.Query
[source, cypher, indent=0]
----
MATCH (n:Person)
WHERE n.name = 'Peter' XOR (n.age < 30 AND n.name = 'Timothy') OR NOT (n.name = 'Timothy' OR n.name = 'Peter')
RETURN
  n.name AS name,
  n.age AS age
ORDER BY name
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +name+ | +age+
| +"Andy"+ | +36+
| +"Peter"+ | +35+
| +"Timothy"+ | +25+
2+d|Rows: 3
|===


[[filter-on-node-label]]
=== Filter on node label

To filter nodes by label, write a label predicate after the `WHERE` keyword using `WHERE n:foo`.

.Query
[source, cypher, indent=0]
----
MATCH (n)
WHERE n:Swedish
RETURN n.name, n.age
----

The name and age for the *'Andy'* node will be returned.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +n.name+ | +n.age+
| +"Andy"+ | +36+
2+d|Rows: 1
|===


[[filter-on-node-property]]
=== Filter on node property

To filter on a node property, write your clause after the `WHERE` keyword.

.Query
[source, cypher, indent=0]
----
MATCH (n:Person)
WHERE n.age < 30
RETURN n.name, n.age
----

The name and age values for the *'Timothy'* node are returned because he is less than 30 years of age.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +n.name+ | +n.age+
| +"Timothy"+ | +25+
2+d|Rows: 1
|===


[[filter-on-relationship-property]]
=== Filter on relationship property

To filter on a relationship property, write your clause after the `WHERE` keyword.

.Query
[source, cypher, indent=0]
----
MATCH (n:Person)-[k:KNOWS]->(f)
WHERE k.since < 2000
RETURN f.name, f.age, f.email
----

The name, age and email values for the *'Peter'* node are returned because Andy has known him since before 2000.

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===
| +f.name+ | +f.age+ | +f.email+
| +"Peter"+ | +35+ | +"peter_n@example.com"+
3+d|Rows: 1
|===


[[filter-on-dynamic-property]]
=== Filter on dynamically-computed node property

To filter on a property using a dynamically computed name, use square bracket syntax.

.Query
[source, cypher, indent=0]
----
WITH 'AGE' AS propname
MATCH (n:Person)
WHERE n[toLower(propname)] < 30
RETURN n.name, n.age
----

The name and age values for the *'Timothy'* node are returned because he is less than 30 years of age.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +n.name+ | +n.age+
| +"Timothy"+ | +25+
2+d|Rows: 1
|===


[[property-existence-checking]]
=== Property existence checking

Use the `IS NOT NULL` predicate to only include nodes or relationships in which a property exists.

.Query
[source, cypher, indent=0]
----
MATCH (n:Person)
WHERE n.belt IS NOT NULL
RETURN n.name, n.belt
----

The name and belt for the *'Andy'* node are returned because he is the only one with a `belt` property.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +n.name+ | +n.belt+
| +"Andy"+ | +"white"+
2+d|Rows: 1
|===


[[usage-with-with-clause]]
=== Usage with `WITH`

As `WHERE` is not considered a clause in its own right, its scope is not limited by a `WITH` directly before it.

.Query
[source, cypher, indent=0]
----
MATCH (n:Person)
WITH n.name as name
WHERE n.age = 25
RETURN name
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +name+
| +"Timothy"+
1+d|Rows: 1
|===


The name for the *'Timothy'* node is returned because the `WHERE` clause still acts as a filter on the `MATCH`.
The `WITH` reduces the scope for the rest of the query moving forward.
In this case 'name' is now the only variable in scope for the `RETURN` clause.


[[query-where-string]]
== String matching

The prefix and suffix of a string can be matched using `STARTS WITH` and `ENDS WITH`.
To undertake a substring search - i.e. match regardless of location within a string - use `CONTAINS`.
The matching is _case-sensitive_.
Attempting to use these operators on values which are not strings will return `null`.

[[match-string-start]]
=== Prefix string search using `STARTS WITH`

The `STARTS WITH` operator is used to perform case-sensitive matching on the beginning of a string.

.Query
[source, cypher, indent=0]
----
MATCH (n:Person)
WHERE n.name STARTS WITH 'Pet'
RETURN n.name, n.age
----

The name and age for the *'Peter'* node are returned because his name starts with *'Pet'*.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +n.name+ | +n.age+
| +"Peter"+ | +35+
2+d|Rows: 1
|===


[[match-string-end]]
=== Suffix string search using `ENDS WITH`

The `ENDS WITH` operator is used to perform case-sensitive matching on the ending of a string.

.Query
[source, cypher, indent=0]
----
MATCH (n:Person)
WHERE n.name ENDS WITH 'ter'
RETURN n.name, n.age
----

The name and age for the *'Peter'* node are returned because his name ends with *'ter'*.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +n.name+ | +n.age+
| +"Peter"+ | +35+
2+d|Rows: 1
|===


[[match-string-contains]]
=== Substring search using `CONTAINS`

The `CONTAINS` operator is used to perform case-sensitive matching regardless of location within a string.

.Query
[source, cypher, indent=0]
----
MATCH (n:Person)
WHERE n.name CONTAINS 'ete'
RETURN n.name, n.age
----

The name and age for the *'Peter'* node are returned because his name contains with *'ete'*.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +n.name+ | +n.age+
| +"Peter"+ | +35+
2+d|Rows: 1
|===


[[match-string-negation]]
=== String matching negation

Use the `NOT` keyword to exclude all matches on given string from your result:

.Query
[source, cypher, indent=0]
----
MATCH (n:Person)
WHERE NOT n.name ENDS WITH 'y'
RETURN n.name, n.age
----

The name and age for the *'Peter'* node are returned because his name does not end with *'y'*.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +n.name+ | +n.age+
| +"Peter"+ | +35+
2+d|Rows: 1
|===


[[query-where-regex]]
== Regular expressions

Cypher supports filtering using regular expressions.
The regular expression syntax is inherited from the link:https://docs.oracle.com/en/java/javase/11/docs/api/java.base/java/util/regex/Pattern.html[Java regular expressions].
This includes support for flags that change how strings are matched, including case-insensitive `(?i)`, multiline `(?m)` and dotall `(?s)`.
Flags are given at the beginning of the regular expression, for example:

.Query
[source, cypher, role="noheader"]
----
MATCH (n) WHERE n.name =~ '(?i)Lon.*'
RETURN n
----

will return nodes with name `'London'` or with name `'LonDoN'`.

[[matching-using-regular-expressions]]
=== Matching using regular expressions

You can match on regular expressions by using `=~ 'regexp'`, like this:


.Query
[source, cypher, indent=0]
----
MATCH (n:Person)
WHERE n.name =~ 'Tim.*'
RETURN n.name, n.age
----

The name and age for the `'Timothy'` node are returned because his name starts with `'Tim'`.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +n.name+ | +n.age+
| +"Timothy"+ | +25+
2+d|Rows: 1
|===


[[escaping-in-regular-expressions]]
=== Escaping in regular expressions

Characters like `.` or `*` have special meaning in a regular expression.
To use these as ordinary characters, without special meaning, escape them.

.Query
[source, cypher, indent=0]
----
MATCH (n:Person)
WHERE n.email =~ '.*\\.com'
RETURN n.name, n.age, n.email
----

The name, age and email for the 'Peter' node are returned because his email ends with '.com'.

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===
| +n.name+ | +n.age+ | +n.email+
| +"Peter"+ | +35+ | +"peter_n@example.com"+
3+d|Rows: 1
|===


[[case-insensitive-regular-expressions]]
=== Case-insensitive regular expressions

By pre-pending a regular expression with `(?i)`, the whole expression becomes case-insensitive.

.Query
[source, cypher, indent=0]
----
MATCH (n:Person)
WHERE n.name =~ '(?i)AND.*'
RETURN n.name, n.age
----

The name and age for the `'Andy'` node are returned because his name starts with `'AND'` irrespective of casing.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +n.name+ | +n.age+
| +"Andy"+ | +36+
2+d|Rows: 1
|===


[[query-where-patterns]]
== Using path patterns in `WHERE`

[[filter-on-patterns]]
=== Filter on patterns

Patterns are expressions in Cypher, expressions that return a list of paths.
List expressions are also predicates -- an empty list represents `false`, and a non-empty represents `true`.

So, patterns are not only expressions, they are also predicates.
The only limitation to your pattern is that you must be able to express it in a single path.
You cannot use commas between multiple paths like you do in `MATCH`.
You can achieve  the same effect by combining multiple patterns with `AND`.

Note that you cannot introduce new variables here.
Although it might look very similar to the `MATCH` patterns, the `WHERE` clause is all about eliminating matched paths.
`+MATCH (a)-[*]->(b)+` is very different from `+WHERE (a)-[*]->(b)+`.
The first will produce a path for every path it can find between `a` and `b`, whereas the latter will eliminate any matched paths where `a` and `b` do not have a directed relationship chain between them.


.Query
[source, cypher, indent=0]
----
MATCH
  (timothy:Person {name: 'Timothy'}),
  (other:Person)
WHERE other.name IN ['Andy', 'Peter'] AND (other)-->(timothy)
RETURN other.name, other.age
----

The name and age for nodes that have an outgoing relationship to the `'Timothy'` node are returned.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +other.name+ | +other.age+
| +"Andy"+ | +36+
2+d|Rows: 1
|===


[[filter-on-patterns-using-not]]
=== Filter on patterns using `NOT`

The `NOT` operator can be used to exclude a pattern.

.Query
[source, cypher, indent=0]
----
MATCH
  (person:Person),
  (peter:Person {name: 'Peter'})
WHERE NOT (person)-->(peter)
RETURN person.name, person.age
----

Name and age values for nodes that do not have an outgoing relationship to the `'Peter'` node are returned.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +person.name+ | +person.age+
| +"Timothy"+ | +25+
| +"Peter"+ | +35+
2+d|Rows: 2
|===


[[filter-on-patterns-with-properties]]
=== Filter on patterns with properties

You can also add properties to your patterns:

.Query
[source, cypher, indent=0]
----
MATCH (n:Person)
WHERE (n)-[:KNOWS]-({name: 'Timothy'})
RETURN n.name, n.age
----

Finds all name and age values for nodes that have a relationship with the `KNOWS`-type, to a node with the property-key `name` and value `'Timothy'`.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +n.name+ | +n.age+
| +"Andy"+ | +36+
2+d|Rows: 1
|===


[[filter-on-relationship-type]]
=== Filter on relationship type

You can put the exact relationship type in the `MATCH` pattern, but sometimes you want to be able to do more advanced filtering on the type.
You can use the special property `type` to compare the type with something else.
In this example, the query does a regular expression comparison with the name of the relationship type.

.Query
[source, cypher, indent=0]
----
MATCH (n:Person)-[r]->()
WHERE n.name='Andy' AND type(r) =~ 'K.*'
RETURN type(r), r.since
----

This returns all relationships having a type whose name starts with `'K'`.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +type(r)+ | +r.since+
| +"KNOWS"+ | +1999+
| +"KNOWS"+ | +2012+
2+d|Rows: 2
|===


[[query-where-lists]]
== Lists

[[where-in-operator]]
=== `IN` operator

To check if an element exists in a list, you can use the `IN` operator.

.Query
[source, cypher, indent=0]
----
MATCH (a:Person)
WHERE a.name IN ['Peter', 'Timothy']
RETURN a.name, a.age
----

This query shows how to check if a property exists in a literal list.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +a.name+ | +a.age+
| +"Timothy"+ | +25+
| +"Peter"+ | +35+
2+d|Rows: 2
|===


[[missing-properties-and-values]]
== Missing properties and values

[[default-to-false-missing-property]]
=== Default to `false` if property is missing

As missing properties evaluate to `null`, the comparison in the example will evaluate to `false` for nodes without the `belt` property.

.Query
[source, cypher, indent=0]
----
MATCH (n:Person)
WHERE n.belt = 'white'
RETURN n.name, n.age, n.belt
----

Only the name, age, and belt values of nodes with white belts are returned.

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===
| +n.name+ | +n.age+ | +n.belt+
| +"Andy"+ | +36+ | +"white"+
3+d|Rows: 1
|===


[[default-to-true-missing-property]]
=== Default to `true` if property is missing

If you want to compare a property on a node or relationship, but only if it exists, you can compare the property against both the value you are looking for and `null`, like:

.Query
[source, cypher, indent=0]
----
MATCH (n:Person)
WHERE n.belt = 'white' OR n.belt IS NULL
RETURN n.name, n.age, n.belt
ORDER BY n.name
----

This returns all values for all nodes, even those without the belt property.

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===
| +n.name+ | +n.age+ | +n.belt+
| +"Andy"+ | +36+ | +"white"+
| +"Peter"+ | +35+ | +<null>+
| +"Timothy"+ | +25+ | +<null>+
3+d|Rows: 3
|===


[[filter-on-null]]
=== Filter on `null`

Sometimes you might want to test if a value or a variable is `null`.
This is done just like SQL does it, using `IS NULL`.
Also like SQL, the negative is `IS NOT NULL`, although `NOT(IS NULL x)` also works.

.Query
[source, cypher, indent=0]
----
MATCH (person:Person)
WHERE person.name = 'Peter' AND person.belt IS NULL
RETURN person.name, person.age, person.belt
----

The name and age values for nodes that have name `'Peter'` but no belt property are returned.

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===
| +person.name+ | +person.age+ | +person.belt+
| +"Peter"+ | +35+ | +<null>+
3+d|Rows: 1
|===


[[query-where-ranges]]
== Using ranges

[[simple-range]]
=== Simple range

To check for an element being inside a specific range, use the inequality operators `+<+`, `+<=+`, `+>=+`, `+>+`.

.Query
[source, cypher, indent=0]
----
MATCH (a:Person)
WHERE a.name >= 'Peter'
RETURN a.name, a.age
----

The name and age values of nodes having a name property lexicographically greater than or equal to `'Peter'` are returned.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +a.name+ | +a.age+
| +"Timothy"+ | +25+
| +"Peter"+ | +35+
2+d|Rows: 2
|===


[[composite-range]]
=== Composite range

Several inequalities can be used to construct a range.

.Query
[source, cypher, indent=0]
----
MATCH (a:Person)
WHERE a.name > 'Andy' AND a.name < 'Timothy'
RETURN a.name, a.age
----

The name and age values of nodes having a name property lexicographically between `'Andy'` and `'Timothy'` are returned.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +a.name+ | +a.age+
| +"Peter"+ | +35+
2+d|Rows: 1
|===


[[pattern-element-predicates]]
== Pattern element predicates

`WHERE` clauses can be added to <<cypher-patterns,pattern elements>> in order to specify additional constraints.

[[relationship-pattern-predicates]]
=== Relationship pattern predicates

`WHERE` can also appear inside a relationship pattern in a `MATCH` clause.


.+WHERE+
======

////
CREATE (andy:Swedish:Person {name: 'Andy', age: 36, belt: 'white'}),
(timothy:Person {name: 'Timothy', age: 25, address: 'Sweden/Malmo'}),
(peter:Person {name: 'Peter', age: 35, email: 'peter_n@example.com'}),
(andy)-[:KNOWS {since: 2012}]->(timothy),
(andy)-[:KNOWS {since: 1999}]->(peter),
(andy)-[:HAS_DOG {since: 2016}]->(:Dog {name:'Andy'}),
(fido:Dog {name:'Fido'})<-[:HAS_DOG {since: 2010}]-(peter)-[:HAS_DOG {since: 2018}]->(:Dog {name:'Ozzy'}),
(fido)-[:HAS_TOY]->(:Toy{name:'Banana'})
////

.Query
[source, cypher]
----
WITH 2000 AS minYear
MATCH (a:Person)-[r:KNOWS WHERE r.since < minYear]->(b:Person)
RETURN r.since
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +r.since+
| +1999+
1+d|Rows: 1
|===

======


However, it cannot be used inside of variable length relationships, as this would lead to an error.

.+WHERE+
======

For example:

////
CREATE (andy:Swedish:Person {name: 'Andy', age: 36, belt: 'white'}),
(timothy:Person {name: 'Timothy', age: 25, address: 'Sweden/Malmo'}),
(peter:Person {name: 'Peter', age: 35, email: 'peter_n@example.com'}),
(andy)-[:KNOWS {since: 2012}]->(timothy),
(andy)-[:KNOWS {since: 1999}]->(peter),
(andy)-[:HAS_DOG {since: 2016}]->(:Dog {name:'Andy'}),
(fido:Dog {name:'Fido'})<-[:HAS_DOG {since: 2010}]-(peter)-[:HAS_DOG {since: 2018}]->(:Dog {name:'Ozzy'}),
(fido)-[:HAS_TOY]->(:Toy{name:'Banana'})
////

.Query
[source, cypher]
----
WITH 2000 AS minYear
MATCH (a:Person)-[r:KNOWS*1..3 WHERE r.since > b.yearOfBirth]->(b:Person)
RETURN r.since
----

// Error

======


Putting predicates inside a relationship pattern can help with readability.
Please note that it is strictly equivalent to using a standalone `WHERE` sub-clause.


.+WHERE+
======

////
CREATE (andy:Swedish:Person {name: 'Andy', age: 36, belt: 'white'}),
(timothy:Person {name: 'Timothy', age: 25, address: 'Sweden/Malmo'}),
(peter:Person {name: 'Peter', age: 35, email: 'peter_n@example.com'}),
(andy)-[:KNOWS {since: 2012}]->(timothy),
(andy)-[:KNOWS {since: 1999}]->(peter),
(andy)-[:HAS_DOG {since: 2016}]->(:Dog {name:'Andy'}),
(fido:Dog {name:'Fido'})<-[:HAS_DOG {since: 2010}]-(peter)-[:HAS_DOG {since: 2018}]->(:Dog {name:'Ozzy'}),
(fido)-[:HAS_TOY]->(:Toy{name:'Banana'})
////

.Query
[source, cypher]
----
WITH 2000 AS minYear
MATCH (a:Person)-[r:KNOWS]->(b:Person)
WHERE r.since < minYear
RETURN r.since
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +r.since+
| +1999+
1+d|Rows: 1
|===

======


Relationship pattern predicates can also be used inside pattern comprehensions, where the same caveats apply.


.+WHERE+
======

////
CREATE (andy:Swedish:Person {name: 'Andy', age: 36, belt: 'white'}),
(timothy:Person {name: 'Timothy', age: 25, address: 'Sweden/Malmo'}),
(peter:Person {name: 'Peter', age: 35, email: 'peter_n@example.com'}),
(andy)-[:KNOWS {since: 2012}]->(timothy),
(andy)-[:KNOWS {since: 1999}]->(peter),
(andy)-[:HAS_DOG {since: 2016}]->(:Dog {name:'Andy'}),
(fido:Dog {name:'Fido'})<-[:HAS_DOG {since: 2010}]-(peter)-[:HAS_DOG {since: 2018}]->(:Dog {name:'Ozzy'}),
(fido)-[:HAS_TOY]->(:Toy{name:'Banana'})
////

.Query
[source, cypher]
----
WITH 2000 AS minYear
MATCH (a:Person {name: 'Andy'})
RETURN [(a)-[r:KNOWS WHERE r.since < minYear]->(b:Person) | r.since] AS years
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +years+
| +[1999]+
1+d|Rows: 1
|===

======

