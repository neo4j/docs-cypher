:description: The `MATCH` clause is used to search for the pattern described in it.

[[query-match]]
= MATCH

[abstract]
--
The `MATCH` clause is used to search for the pattern described in it.
--

* xref::clauses/match.adoc#match-introduction[Introduction]
* xref::clauses/match.adoc#basic-node-finding[Basic node finding]
 ** xref::clauses/match.adoc#get-all-nodes[Get all nodes]
 ** xref::clauses/match.adoc#get-all-nodes-with-label[Get all nodes with a label]
 ** xref::clauses/match.adoc#related-nodes[Related nodes]
 ** xref::clauses/match.adoc#match-with-labels[Match with labels]
 ** xref::clauses/match.adoc#label-expression-match-or-expression[Match with a label expression for the node labels]
* xref::clauses/match.adoc#relationship-basics[Relationship basics]
 ** xref::clauses/match.adoc#outgoing-relationships[Outgoing relationships]
 ** xref::clauses/match.adoc#directed-rels-and-variable[Directed relationships and variable]
 ** xref::clauses/match.adoc#match-on-rel-type[Match on relationship type]
 ** xref::clauses/match.adoc#match-on-multiple-rel-types[Match on multiple relationship types]
 ** xref::clauses/match.adoc#match-on-rel-type-use-variable[Match on relationship type and use a variable]
* xref::clauses/match.adoc#relationships-in-depth[Relationships in depth]
 ** xref::clauses/match.adoc#rel-types-with-uncommon-chars[Relationship types with uncommon characters]
 ** xref::clauses/match.adoc#multiple-rels[Multiple relationships]
 ** xref::clauses/match.adoc#varlength-rels[Variable length relationships]
 ** xref::clauses/match.adoc#varlength-rels-multiple-types[Variable length relationships with multiple relationship types]
 ** xref::clauses/match.adoc#rel-variable-in-varlength-rels[Relationship variable in variable length relationships]
 ** xref::clauses/match.adoc#match-props-on-varlength-path[Match with properties on a variable length path]
 ** xref::clauses/match.adoc#zero-length-paths[Zero length paths]
 ** xref::clauses/match.adoc#named-paths[Named paths]
 ** xref::clauses/match.adoc#match-on-bound-rel[Matching on a bound relationship]
* xref::clauses/match.adoc#query-shortest-path[Shortest path]
 ** xref::clauses/match.adoc#single-shortest-path[Single shortest path]
 ** xref::clauses/match.adoc#single-shortest-path-with-predicates[Single shortest path with predicates]
 ** xref::clauses/match.adoc#all-shortest-paths[All shortest paths]
* xref::clauses/match.adoc#get-node-rel-by-id[Get node or relationship by ID]
 ** xref::clauses/match.adoc#match-node-by-id[Node by ID]
 ** xref::clauses/match.adoc#match-rel-by-id[Relationship by ID]
 ** xref::clauses/match.adoc#match-multiple-nodes-by-id[Multiple nodes by ID]


[[match-introduction]]
== Introduction

The `MATCH` clause allows you to specify the patterns Neo4j will search for in the database.
This is the primary way of getting data into the current set of bindings.
It is worth reading up more on the specification of the patterns themselves in xref::syntax/patterns.adoc[Patterns].

`MATCH` is often coupled to a `WHERE` part which adds restrictions, or predicates, to the `MATCH` patterns, making them more specific.
The predicates are part of the pattern description, and should not be considered a filter applied only after the matching is done.
_This means that `WHERE` should always be put together with the `MATCH` clause it belongs to._

`MATCH` can occur at the beginning of the query or later, possibly after a `WITH`.
If it is the first clause, nothing will have been bound yet, and Neo4j will design a search to find the results matching the clause and any associated predicates specified in any `WHERE` part.
This could involve a scan of the database, a search for nodes having a certain label, or a search of an index to find starting points for the pattern matching.
Nodes and relationships found by this search are available as _bound pattern elements,_ and can be used for pattern matching of paths.
They can also be used in any further `MATCH` clauses, where Neo4j will use the known elements, and from there find further unknown elements.

Cypher is declarative, and so usually the query itself does not specify the algorithm to use to perform the search.
Neo4j will automatically work out the best approach to finding start nodes and matching patterns.
Predicates in `WHERE` parts can be evaluated before pattern matching, during pattern matching, or after finding matches.
However, there are cases where you can influence the decisions taken by the query compiler.
Read more about indexes in xref::indexes-for-search-performance.adoc[], and more about specifying hints to force Neo4j to solve a query in a specific way in xref::query-tuning/using.adoc[Planner hints and the USING keyword].

[TIP]
====
To understand more about the patterns used in the `MATCH` clause, read xref::syntax/patterns.adoc[Patterns]
====

The following graph is used for the examples below:

image:graph_match_clause.svg[]

////
CREATE
  (charlie:Person {name: 'Charlie Sheen'}),
  (martin:Person {name: 'Martin Sheen'}),
  (michael:Person {name: 'Michael Douglas'}),
  (oliver:Person {name: 'Oliver Stone'}),
  (rob:Person {name: 'Rob Reiner'}),
  (wallStreet:Movie {title: 'Wall Street'}),
  (charlie)-[:ACTED_IN {role: 'Bud Fox'}]->(wallStreet),
  (martin)-[:ACTED_IN {role: 'Carl Fox'}]->(wallStreet),
  (michael)-[:ACTED_IN {role: 'Gordon Gekko'}]->(wallStreet),
  (oliver)-[:DIRECTED]->(wallStreet),
  (thePresident:Movie {title: 'The American President'}),
  (martin)-[:ACTED_IN {role: 'A.J. MacInerney'}]->(thePresident),
  (michael)-[:ACTED_IN {role: 'President Andrew Shepherd'}]->(thePresident),
  (rob)-[:DIRECTED]->(thePresident)
////

[[basic-node-finding]]
== Basic node finding

[[get-all-nodes]]
=== Get all nodes

By just specifying a pattern with a single node and no labels, all nodes in the graph will be returned.

======
// test:9c50f265-f7e9-4a9a-a4c2-871b02ae22e6["example", “match-clause"]
.Query
[source, cypher, indent=0]
----
MATCH (n)
RETURN n
----
// test:9c50f265-f7e9-4a9a-a4c2-871b02ae22e6["example", “match-clause"]

Returns all the nodes in the database.
// test:9c50f265-f7e9-4a9a-a4c2-871b02ae22e6["result", “match-clause"]
.Result
[role="queryresult",options="header,footer",cols="1*<m"]
----
Rows: 7
+------------------------------------------------+
| n                                              |
+------------------------------------------------+
| Node(:Person {name: 'Charlie Sheen'})          |
| Node(:Person {name: 'Martin Sheen'})           |
| Node(:Person {name: 'Michael Douglas'})        |
| Node(:Person {name: 'Oliver Stone'})           |
| Node(:Person {name: 'Rob Reiner'})             |
| Node(:Movie {title: 'Wall Street'})            |
| Node(:Movie {title: 'The American President'}) |
+------------------------------------------------+
----
// test:9c50f265-f7e9-4a9a-a4c2-871b02ae22e6["result", “match-clause"]
======


[[get-all-nodes-with-label]]
=== Get all nodes with a label

Getting all nodes with a label on them is done with a single node pattern where the node has a label on it.

.Query
[source, cypher, indent=0]
----
MATCH (movie:Movie)
RETURN movie.title
----

Returns all the movies in the database.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +movie.title+
| +"Wall Street"+
| +"The American President"+
1+d|Rows: 2
|===


[[related-nodes]]
=== Related nodes

The symbol `--` means _related to,_ without regard to type or direction of the relationship.

.Query
[source, cypher, indent=0]
----
MATCH (director {name: 'Oliver Stone'})--(movie)
RETURN movie.title
----

Returns all the movies directed by *'Oliver Stone'*.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +movie.title+
| +"Wall Street"+
1+d|Rows: 1
|===


[[match-with-labels]]
=== Match with labels

To constrain your pattern with labels on nodes, you add it to your pattern nodes, using the label syntax.

.Query
[source, cypher, indent=0]
----
MATCH (:Person {name: 'Oliver Stone'})--(movie:Movie)
RETURN movie.title
----

Returns any nodes connected with the `Person` *'Oliver'* that are labeled `Movie`.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +movie.title+
| +"Wall Street"+
1+d|Rows: 1
|===


[[label-expression-match-or-expression]]
=== Match with a label expression for the node labels

A match with an `OR` expression for the node label returns the nodes that contains both the specified labels.

////
CREATE
  (charlie:Person {name: 'Charlie Sheen'}),
  (martin:Person {name: 'Martin Sheen'}),
  (michael:Person {name: 'Michael Douglas'}),
  (oliver:Person {name: 'Oliver Stone'}),
  (rob:Person {name: 'Rob Reiner'}),
  (wallStreet:Movie {title: 'Wall Street'}),
  (charlie)-[:ACTED_IN {role: 'Bud Fox'}]->(wallStreet),
  (martin)-[:ACTED_IN {role: 'Carl Fox'}]->(wallStreet),
  (michael)-[:ACTED_IN {role: 'Gordon Gekko'}]->(wallStreet),
  (oliver)-[:DIRECTED]->(wallStreet),
  (thePresident:Movie {title: 'The American President'}),
  (martin)-[:ACTED_IN {role: 'A.J. MacInerney'}]->(thePresident),
  (michael)-[:ACTED_IN {role: 'President Andrew Shepherd'}]->(thePresident),
  (rob)-[:DIRECTED]->(thePresident)
////

.Query
[source, cypher]
----
MATCH (n:Movie|Person)
RETURN n.name AS name, n.title AS title
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +name+ | +title+
| +"Charlie Sheen"+ | +<null>+
| +"Martin Sheen"+ | +<null>+
| +"Michael Douglas"+ | +<null>+
| +"Oliver Stone"+ | +<null>+
| +"Rob Reiner"+ | +<null>+
| +<null>+ | +"Wall Street"+
| +<null>+ | +"The American President"+
2+d|Rows: 7
|===


[[relationship-basics]]
== Relationship basics

[[outgoing-relationships]]
=== Outgoing relationships

When the direction of a relationship is of interest, it is shown by using `+-->+` or `+<--+`, like this:

.Query
[source, cypher, indent=0]
----
MATCH (:Person {name: 'Oliver Stone'})-->(movie)
RETURN movie.title
----

Returns any nodes connected with the `Person` *'Oliver'* by an outgoing relationship.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +movie.title+
| +"Wall Street"+
1+d|Rows: 1
|===


[[directed-rels-and-variable]]
=== Directed relationships and variable

If a variable is required, either for filtering on properties of the relationship, or to return the relationship, this is how you introduce the variable.

.Query
[source, cypher, indent=0]
----
MATCH (:Person {name: 'Oliver Stone'})-[r]->(movie)
RETURN type(r)
----

Returns the type of each outgoing relationship from *'Oliver'*.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +type(r)+
| +"DIRECTED"+
1+d|Rows: 1
|===


[[match-on-rel-type]]
=== Match on relationship type

When you know the relationship type you want to match on, you can specify it by using a colon together with the relationship type.

.Query
[source, cypher, indent=0]
----
MATCH (wallstreet:Movie {title: 'Wall Street'})<-[:ACTED_IN]-(actor)
RETURN actor.name
----

Returns all actors that `ACTED_IN` *'Wall Street'*.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +actor.name+
| +"Michael Douglas"+
| +"Martin Sheen"+
| +"Charlie Sheen"+
1+d|Rows: 3
|===


[[match-on-multiple-rel-types]]
=== Match on multiple relationship types

To match on one of multiple types, you can specify this by chaining them together with the pipe symbol `|`.

.Query
[source, cypher, indent=0]
----
MATCH (wallstreet {title: 'Wall Street'})<-[:ACTED_IN|DIRECTED]-(person)
RETURN person.name
----

Returns nodes with an `ACTED_IN` or `DIRECTED` relationship to *'Wall Street'*.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +person.name+
| +"Oliver Stone"+
| +"Michael Douglas"+
| +"Martin Sheen"+
| +"Charlie Sheen"+
1+d|Rows: 4
|===


[[match-on-rel-type-use-variable]]
=== Match on relationship type and use a variable

If you both want to introduce an variable to hold the relationship, and specify the relationship type you want, just add them both, like this:

.Query
[source, cypher, indent=0]
----
MATCH (wallstreet {title: 'Wall Street'})<-[r:ACTED_IN]-(actor)
RETURN r.role
----

Returns `ACTED_IN` roles for *'Wall Street'*.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +r.role+
| +"Gordon Gekko"+
| +"Carl Fox"+
| +"Bud Fox"+
1+d|Rows: 3
|===


[[relationships-in-depth]]
== Relationships in depth

[NOTE]
====
Inside a single pattern, relationships will only be matched once. You can read more about this in xref::introduction/uniqueness.adoc[].
====

[[rel-types-with-uncommon-chars]]
=== Relationship types with uncommon characters

Sometimes your database will have types with non-letter characters, or with spaces in them.
Use ``` (backtick) to quote these.
To demonstrate this we can add an additional relationship between *'Charlie Sheen'* and *'Rob Reiner'*:

.Query
[source, cypher, indent=0]
----
MATCH
  (charlie:Person {name: 'Charlie Sheen'}),
  (rob:Person {name: 'Rob Reiner'})
CREATE (rob)-[:`TYPE INCLUDING A SPACE`]->(charlie)
----

Which leads to the following graph:

image:graph_match_clause_backtick.svg[]

////
CREATE
  (charlie:Person {name: 'Charlie Sheen'}),
  (martin:Person {name: 'Martin Sheen'}),
  (michael:Person {name: 'Michael Douglas'}),
  (oliver:Person {name: 'Oliver Stone'}),
  (rob:Person {name: 'Rob Reiner'}),
  (wallStreet:Movie {title: 'Wall Street'}),
  (charlie)-[:ACTED_IN {role: 'Bud Fox'}]->(wallStreet),
  (martin)-[:ACTED_IN {role: 'Carl Fox'}]->(wallStreet),
  (michael)-[:ACTED_IN {role: 'Gordon Gekko'}]->(wallStreet),
  (oliver)-[:DIRECTED]->(wallStreet),
  (thePresident:Movie {title: 'The American President'}),
  (martin)-[:ACTED_IN {role: 'A.J. MacInerney'}]->(thePresident),
  (michael)-[:ACTED_IN {role: 'President Andrew Shepherd'}]->(thePresident),
  (rob)-[:DIRECTED]->(thePresident)
MATCH
  (charlie:Person {name: 'Charlie Sheen'}),
  (rob:Person {name: 'Rob Reiner'})
CREATE (rob)-[:`TYPE INCLUDING A SPACE`]->(charlie)
////

.Query
[source, cypher, indent=0]
----
MATCH (n {name: 'Rob Reiner'})-[r:`TYPE INCLUDING A SPACE`]->()
RETURN type(r)
----

Returns a relationship type with spaces in it.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +type(r)+
| +"TYPE INCLUDING A SPACE"+
1+d|Rows: 1
|===


[[multiple-rels]]
=== Multiple relationships

Relationships can be expressed by using multiple statements in the form of `()--()`, or they can be strung together, like this:

////
CREATE
  (charlie:Person {name: 'Charlie Sheen'}),
  (martin:Person {name: 'Martin Sheen'}),
  (michael:Person {name: 'Michael Douglas'}),
  (oliver:Person {name: 'Oliver Stone'}),
  (rob:Person {name: 'Rob Reiner'}),
  (wallStreet:Movie {title: 'Wall Street'}),
  (charlie)-[:ACTED_IN {role: 'Bud Fox'}]->(wallStreet),
  (martin)-[:ACTED_IN {role: 'Carl Fox'}]->(wallStreet),
  (michael)-[:ACTED_IN {role: 'Gordon Gekko'}]->(wallStreet),
  (oliver)-[:DIRECTED]->(wallStreet),
  (thePresident:Movie {title: 'The American President'}),
  (martin)-[:ACTED_IN {role: 'A.J. MacInerney'}]->(thePresident),
  (michael)-[:ACTED_IN {role: 'President Andrew Shepherd'}]->(thePresident),
  (rob)-[:DIRECTED]->(thePresident)
////

.Query
[source, cypher, indent=0]
----
MATCH (charlie {name: 'Charlie Sheen'})-[:ACTED_IN]->(movie)<-[:DIRECTED]-(director)
RETURN movie.title, director.name
----

Returns the movie *'Charlie Sheen'* acted in and its director.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +movie.title+ | +director.name+
| +"Wall Street"+ | +"Oliver Stone"+
2+d|Rows: 1
|===


[[varlength-rels]]
=== Variable length relationships

Nodes that are a variable number of `+relationship->node+` hops away can be found using the following syntax:
`+-[:TYPE*minHops..maxHops]->+`.
`minHops` and `maxHops` are optional and default to 1 and infinity respectively.
When no bounds are given the dots may be omitted.
The dots may also be omitted when setting only one bound and this implies a fixed length pattern.

[NOTE]
====
Variable length relationships can be planned with an optimisation under certain circumstances, see xref::execution-plans/operators.adoc#query-plan-varlength-expand-pruning[VarLength Expand Pruning] query plan.
====


.+Variable length relationships+
======

////
CREATE
  (charlie:Person {name: 'Charlie Sheen'}),
  (martin:Person {name: 'Martin Sheen'}),
  (michael:Person {name: 'Michael Douglas'}),
  (oliver:Person {name: 'Oliver Stone'}),
  (rob:Person {name: 'Rob Reiner'}),
  (wallStreet:Movie {title: 'Wall Street'}),
  (charlie)-[:ACTED_IN {role: 'Bud Fox'}]->(wallStreet),
  (martin)-[:ACTED_IN {role: 'Carl Fox'}]->(wallStreet),
  (michael)-[:ACTED_IN {role: 'Gordon Gekko'}]->(wallStreet),
  (oliver)-[:DIRECTED]->(wallStreet),
  (thePresident:Movie {title: 'The American President'}),
  (martin)-[:ACTED_IN {role: 'A.J. MacInerney'}]->(thePresident),
  (michael)-[:ACTED_IN {role: 'President Andrew Shepherd'}]->(thePresident),
  (rob)-[:DIRECTED]->(thePresident)
////

.Query
[source, cypher, indent=0]
----
MATCH (charlie {name: 'Charlie Sheen'})-[:ACTED_IN*1..3]-(movie:Movie)
RETURN movie.title
----

Returns all movies related to `Charlie Sheen` by 1 to 3 hops:

* `Wall Street` is found through the direct connection, whereas the other two results are found via `Michael Douglas` and `Martin Sheen` respectively.
* As one can see from this example, variable length relationships do not impose any requirements on the intermediate nodes.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +movie.title+
| +"Wall Street"+
| +"The American President"+
| +"The American President"+
1+d|Rows: 3
|===

======


[[varlength-rels-multiple-types]]
=== Variable length relationships with multiple relationship types

Variable length relationships can be combined with multiple relationship types. In this case the `*minHops..maxHops` applies to all relationship types as well as any combination of them.

////
CREATE
  (charlie:Person {name: 'Charlie Sheen'}),
  (martin:Person {name: 'Martin Sheen'}),
  (michael:Person {name: 'Michael Douglas'}),
  (oliver:Person {name: 'Oliver Stone'}),
  (rob:Person {name: 'Rob Reiner'}),
  (wallStreet:Movie {title: 'Wall Street'}),
  (charlie)-[:ACTED_IN {role: 'Bud Fox'}]->(wallStreet),
  (martin)-[:ACTED_IN {role: 'Carl Fox'}]->(wallStreet),
  (michael)-[:ACTED_IN {role: 'Gordon Gekko'}]->(wallStreet),
  (oliver)-[:DIRECTED]->(wallStreet),
  (thePresident:Movie {title: 'The American President'}),
  (martin)-[:ACTED_IN {role: 'A.J. MacInerney'}]->(thePresident),
  (michael)-[:ACTED_IN {role: 'President Andrew Shepherd'}]->(thePresident),
  (rob)-[:DIRECTED]->(thePresident)
////

.Query
[source, cypher, indent=0]
----
MATCH (charlie {name: 'Charlie Sheen'})-[:ACTED_IN|DIRECTED*2]-(person:Person)
RETURN person.name
----

Returns all people related to *'Charlie Sheen'* by 2 hops with any combination of the relationship types `ACTED_IN` and `DIRECTED`.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +person.name+
| +"Oliver Stone"+
| +"Michael Douglas"+
| +"Martin Sheen"+
1+d|Rows: 3
|===


[[rel-variable-in-varlength-rels]]
=== Relationship variable in variable length relationships

When the connection between two nodes is of variable length, the list of relationships comprising the connection can be returned using the following syntax:

////
CREATE
  (charlie:Person {name: 'Charlie Sheen'}),
  (martin:Person {name: 'Martin Sheen'}),
  (michael:Person {name: 'Michael Douglas'}),
  (oliver:Person {name: 'Oliver Stone'}),
  (rob:Person {name: 'Rob Reiner'}),
  (wallStreet:Movie {title: 'Wall Street'}),
  (charlie)-[:ACTED_IN {role: 'Bud Fox'}]->(wallStreet),
  (martin)-[:ACTED_IN {role: 'Carl Fox'}]->(wallStreet),
  (michael)-[:ACTED_IN {role: 'Gordon Gekko'}]->(wallStreet),
  (oliver)-[:DIRECTED]->(wallStreet),
  (thePresident:Movie {title: 'The American President'}),
  (martin)-[:ACTED_IN {role: 'A.J. MacInerney'}]->(thePresident),
  (michael)-[:ACTED_IN {role: 'President Andrew Shepherd'}]->(thePresident),
  (rob)-[:DIRECTED]->(thePresident)
////

.Query
[source, cypher, indent=0]
----
MATCH p = (actor {name: 'Charlie Sheen'})-[:ACTED_IN*2]-(co_actor)
RETURN relationships(p)
----

Returns a list of relationships.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +relationships(p)+
| +[:ACTED_IN[0]{role:"Bud Fox"},:ACTED_IN[2]{role:"Gordon Gekko"}]+
| +[:ACTED_IN[0]{role:"Bud Fox"},:ACTED_IN[1]{role:"Carl Fox"}]+
1+d|Rows: 2
|===


[[match-props-on-varlength-path]]
=== Match with properties on a variable length path

A variable length relationship with properties defined on in it means that all relationships in the path must have the property set to the given value.
In this query, there are two paths between *'Charlie Sheen'* and his father *'Martin Sheen'*.
One of them includes a *'blocked'* relationship and the other does not.
In this case we first alter the original graph by using the following query to add `BLOCKED` and `UNBLOCKED` relationships:

////
CREATE
  (charlie:Person {name: 'Charlie Sheen'}),
  (martin:Person {name: 'Martin Sheen'}),
  (michael:Person {name: 'Michael Douglas'}),
  (oliver:Person {name: 'Oliver Stone'}),
  (rob:Person {name: 'Rob Reiner'}),
  (wallStreet:Movie {title: 'Wall Street'}),
  (charlie)-[:ACTED_IN {role: 'Bud Fox'}]->(wallStreet),
  (martin)-[:ACTED_IN {role: 'Carl Fox'}]->(wallStreet),
  (michael)-[:ACTED_IN {role: 'Gordon Gekko'}]->(wallStreet),
  (oliver)-[:DIRECTED]->(wallStreet),
  (thePresident:Movie {title: 'The American President'}),
  (martin)-[:ACTED_IN {role: 'A.J. MacInerney'}]->(thePresident),
  (michael)-[:ACTED_IN {role: 'President Andrew Shepherd'}]->(thePresident),
  (rob)-[:DIRECTED]->(thePresident)
////

.Query
[source, cypher, indent=0]
----
MATCH
  (charlie:Person {name: 'Charlie Sheen'}),
  (martin:Person {name: 'Martin Sheen'})
CREATE (charlie)-[:X {blocked: false}]->(:UNBLOCKED)<-[:X {blocked: false}]-(martin)
CREATE (charlie)-[:X {blocked: true}]->(:BLOCKED)<-[:X {blocked: false}]-(martin)
----

This means that we are starting out with the following graph:

image:graph_match_clause_variable_length.svg[]

////
CREATE
  (charlie:Person {name: 'Charlie Sheen'}),
  (martin:Person {name: 'Martin Sheen'}),
  (michael:Person {name: 'Michael Douglas'}),
  (oliver:Person {name: 'Oliver Stone'}),
  (rob:Person {name: 'Rob Reiner'}),
  (wallStreet:Movie {title: 'Wall Street'}),
  (charlie)-[:ACTED_IN {role: 'Bud Fox'}]->(wallStreet),
  (martin)-[:ACTED_IN {role: 'Carl Fox'}]->(wallStreet),
  (michael)-[:ACTED_IN {role: 'Gordon Gekko'}]->(wallStreet),
  (oliver)-[:DIRECTED]->(wallStreet),
  (thePresident:Movie {title: 'The American President'}),
  (martin)-[:ACTED_IN {role: 'A.J. MacInerney'}]->(thePresident),
  (michael)-[:ACTED_IN {role: 'President Andrew Shepherd'}]->(thePresident),
  (rob)-[:DIRECTED]->(thePresident)
MATCH
  (charlie:Person {name: 'Charlie Sheen'}),
  (martin:Person {name: 'Martin Sheen'})
CREATE (charlie)-[:X {blocked: false}]->(:UNBLOCKED)<-[:X {blocked: false}]-(martin)
CREATE (charlie)-[:X {blocked: true}]->(:BLOCKED)<-[:X {blocked: false}]-(martin)
////

.Query
[source, cypher, indent=0]
----
MATCH p = (charlie:Person)-[* {blocked:false}]-(martin:Person)
WHERE charlie.name = 'Charlie Sheen' AND martin.name = 'Martin Sheen'
RETURN p
----

Returns the paths between *'Charlie Sheen'* and *'Martin Sheen'* where all relationships have the `blocked` property set to `false`.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +p+
| +(0)-[X,7]->(7)<-[X,8]-(1)+
1+d|Rows: 1
|===


[[zero-length-paths]]
=== Zero length paths

Using variable length paths that have the lower bound zero means that two variables can point to the same node.
If the path length between two nodes is zero, they are by definition the same node.
Note that when matching zero length paths the result may contain a match even when matching on a relationship type not in use.

////
CREATE
  (charlie:Person {name: 'Charlie Sheen'}),
  (martin:Person {name: 'Martin Sheen'}),
  (michael:Person {name: 'Michael Douglas'}),
  (oliver:Person {name: 'Oliver Stone'}),
  (rob:Person {name: 'Rob Reiner'}),
  (wallStreet:Movie {title: 'Wall Street'}),
  (charlie)-[:ACTED_IN {role: 'Bud Fox'}]->(wallStreet),
  (martin)-[:ACTED_IN {role: 'Carl Fox'}]->(wallStreet),
  (michael)-[:ACTED_IN {role: 'Gordon Gekko'}]->(wallStreet),
  (oliver)-[:DIRECTED]->(wallStreet),
  (thePresident:Movie {title: 'The American President'}),
  (martin)-[:ACTED_IN {role: 'A.J. MacInerney'}]->(thePresident),
  (michael)-[:ACTED_IN {role: 'President Andrew Shepherd'}]->(thePresident),
  (rob)-[:DIRECTED]->(thePresident)
////

.Query
[source, cypher, indent=0]
----
MATCH (wallstreet:Movie {title: 'Wall Street'})-[*0..1]-(x)
RETURN x
----

Returns the movie itself as well as actors and directors one relationship away

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +x+
| +Node[5]{title:"Wall Street"}+
| +Node[3]{name:"Oliver Stone"}+
| +Node[2]{name:"Michael Douglas"}+
| +Node[1]{name:"Martin Sheen"}+
| +Node[0]{name:"Charlie Sheen"}+
1+d|Rows: 5
|===


[[named-paths]]
=== Named paths

If you want to return or filter on a path in your pattern graph, you can a introduce a named path.

////
CREATE
  (charlie:Person {name: 'Charlie Sheen'}),
  (martin:Person {name: 'Martin Sheen'}),
  (michael:Person {name: 'Michael Douglas'}),
  (oliver:Person {name: 'Oliver Stone'}),
  (rob:Person {name: 'Rob Reiner'}),
  (wallStreet:Movie {title: 'Wall Street'}),
  (charlie)-[:ACTED_IN {role: 'Bud Fox'}]->(wallStreet),
  (martin)-[:ACTED_IN {role: 'Carl Fox'}]->(wallStreet),
  (michael)-[:ACTED_IN {role: 'Gordon Gekko'}]->(wallStreet),
  (oliver)-[:DIRECTED]->(wallStreet),
  (thePresident:Movie {title: 'The American President'}),
  (martin)-[:ACTED_IN {role: 'A.J. MacInerney'}]->(thePresident),
  (michael)-[:ACTED_IN {role: 'President Andrew Shepherd'}]->(thePresident),
  (rob)-[:DIRECTED]->(thePresident)
////

.Query
[source, cypher, indent=0]
----
MATCH p = (michael {name: 'Michael Douglas'})-->()
RETURN p
----

Returns the two paths starting from *'Michael Douglas'*

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +p+
| +(2)-[ACTED_IN,2]->(5)+
| +(2)-[ACTED_IN,5]->(6)+
1+d|Rows: 2
|===


[[match-on-bound-rel]]
=== Matching on a bound relationship

When your pattern contains a bound relationship, and that relationship pattern does not specify direction, Cypher will try to match the relationship in both directions.

.Query
[source, cypher, indent=0]
----
MATCH (a)-[r]-(b)
WHERE elementId(r) = 0
RETURN a, b
----

This returns the two connected nodes, once as the start node, and once as the end node

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +a+ | +b+
| +Node[0]{name:"Charlie Sheen"}+ | +Node[5]{title:"Wall Street"}+
| +Node[5]{title:"Wall Street"}+ | +Node[0]{name:"Charlie Sheen"}+
2+d|Rows: 2
|===


[[query-shortest-path]]
== Shortest path

[[single-shortest-path]]
=== Single shortest path

Finding a single shortest path between two nodes is as easy as using the `shortestPath` function. It is done like this:

.Query
[source, cypher, indent=0]
----
MATCH
  (martin:Person {name: 'Martin Sheen'}),
  (oliver:Person {name: 'Oliver Stone'}),
  p = shortestPath((martin)-[*..15]-(oliver))
RETURN p
----

This means: find a single shortest path between two nodes, as long as the path is max 15 relationships long.
Within the parentheses you define a single link of a path -- the starting node, the connecting relationship and the end node.
Characteristics describing the relationship like relationship type, max hops and direction are all used when finding the shortest path.
If there is a `WHERE` clause following the match of a `shortestPath`, relevant predicates will be included in the `shortestPath`.
If the predicate is a `none()` or `all()` on the relationship elements of the path, it will be used during the search to improve performance (see xref::execution-plans/shortestpath-planning.adoc[Shortest path planning]).

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +p+
| +(1)-[ACTED_IN,1]->(5)<-[DIRECTED,3]-(3)+
1+d|Rows: 1
|===


[[single-shortest-path-with-predicates]]
=== Single shortest path with predicates

Predicates used in the `WHERE` clause that apply to the shortest path pattern are evaluated before deciding what the shortest matching path is.

.Query
[source, cypher, indent=0]
----
MATCH
  (charlie:Person {name: 'Charlie Sheen'}),
  (martin:Person {name: 'Martin Sheen'}),
  p = shortestPath((charlie)-[*]-(martin))
WHERE none(r IN relationships(p) WHERE type(r) = 'FATHER')
RETURN p
----

This query will find the shortest path between *'Charlie Sheen'* and *'Martin Sheen'*, and the `WHERE` predicate will ensure that we do not consider the father/son relationship between the two.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +p+
| +(0)-[ACTED_IN,0]->(5)<-[ACTED_IN,1]-(1)+
1+d|Rows: 1
|===


[[all-shortest-paths]]
=== All shortest paths

Finds all the shortest paths between two nodes.

.Query
[source, cypher, indent=0]
----
MATCH
  (martin:Person {name: 'Martin Sheen'} ),
  (michael:Person {name: 'Michael Douglas'}),
  p = allShortestPaths((martin)-[*]-(michael))
RETURN p
----

Finds the two shortest paths between *'Martin Sheen'* and *'Michael Douglas'*.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +p+
| +(1)-[ACTED_IN,1]->(5)<-[ACTED_IN,2]-(2)+
| +(1)-[ACTED_IN,4]->(6)<-[ACTED_IN,5]-(2)+
1+d|Rows: 2
|===


[[get-node-rel-by-id]]
== Get node or relationship by ID

[[match-node-by-id]]
=== Node by ID

Searching for nodes by ID can be done with the `elementId()` function in a predicate.

[NOTE]
====
Neo4j reuses its internal IDs when nodes and relationships are deleted.
This means that applications using, and relying on internal Neo4j IDs, are brittle or at risk of making mistakes.
It is therefore recommended to rather use application-generated IDs.
====

.Query
[source, cypher, indent=0]
----
MATCH (n)
WHERE elementId(n) = 0
RETURN n
----

The corresponding node is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +n+
| +Node[0]{name:"Charlie Sheen"}+
1+d|Rows: 1
|===


[[match-rel-by-id]]
=== Relationship by ID

Search for relationships by ID can be done with the `elementId()` function in a predicate.

This is not the recommended practice.
See xref::clauses/match.adoc#match-node-by-id[Node by ID] for more information on the use of Neo4j IDs.

.Query
[source, cypher, indent=0]
----
MATCH ()-[r]->()
WHERE elementId(r) = 0
RETURN r
----

The relationship with ID `0` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +r+
| +:ACTED_IN[0]{role:"Bud Fox"}+
1+d|Rows: 1
|===


[[match-multiple-nodes-by-id]]
=== Multiple nodes by ID

Multiple nodes are selected by specifying them in an `IN`-clause.

.Query
[source, cypher, indent=0]
----
MATCH (n)
WHERE elementId(n) IN [0, 3, 5]
RETURN n
----

This returns the nodes listed in the `IN`-expression.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +n+
| +Node[0]{name:"Charlie Sheen"}+
| +Node[3]{name:"Oliver Stone"}+
| +Node[5]{title:"Wall Street"}+
1+d|Rows: 3
|===

