:description: The `FINISH` clause defines a query to have no result.

[[query-finish]]
= FINISH

[[finish-introduction]]
== Introduction
The `FINISH` clause defines a query to have no result.

A query ending in `FINISH` still executes all its side effects.

[[finish-top-level]]
== Query ending with FINISH

If you want to read from the graph and execute procedure without returning any data, you can use `FINISH`.
Suppose you have a procedure `sendNotification(email :: STRING)` that sends email notifications to the given `email` address.

.Query
[source, cypher]
----
MATCH (p:Person)
CALL sendNotification(p.email)
FINISH
----

This query matches all nodes with label `Person` and executes the procedure `sendNotification` with each person `email` property.
The query has no result.
Nevertheless, the procedure is guaranteed to be executed for `Person` nodes found in the graph.

Any reading or updating query can end with `FINISH` instead of `RETURN`.

[[finish-in-subquery]]
== Subquery ending with FINISH

You can also use `FINISH` in subqueries.
Subqueries with `FINISH` do not influence the number of row in the driving table of the outer query.

.Query
[source, cypher]
----
MATCH (c:Company)
CALL {
  WITH c
  MATCH (p:Person)-[:WORKS_FOR]->(c)
  CALL sendNotification(p.handle + "@" + c.domain)
  FINISH
}
RETURN c.name AS company
----

This query matches all nodes with label `Company`.
The subquery for each company `c` matches all employees of `c` and executes the procedure `sendNotification` with the employee's email concatenated from the employee's `handle` and the companies `domain`.
The subquery has no result.
However, the procedure is guaranteed to be executed for all employees found by the subquery and the outer query returns the `name` of all companies found by the first `MATCH`.

.Result
[options="header,footer",cols="1*<m"]
|===
| company
| "ACME"
| "Neo4j"
d|Rows: 2
|===
