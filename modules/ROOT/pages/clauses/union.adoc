:description: The `UNION` clause is used to combine the result of multiple queries.

[[query-union]]
= UNION

`UNION` combines the results of two or more queries into a single result set that includes all the rows that belong to any queries in the union.

The number and the names of the columns must be identical in all queries combined by using `UNION`.

To keep all the result rows, use `UNION ALL`.
Using just `UNION` (or `UNION DISTINCT`) will combine and remove duplicates from the result set.

[NOTE]
====
If any of the queries in a UNION contain updates, the order of queries in the UNION is relevant.

Any clause before the UNION cannot observe writes made by a clause after the UNION.
Any clause after UNION can observe all writes made by a clause before the UNION.

For details see xref::clauses/clause_composition.adoc#cypher-clause-composition-union-queries[clause composition in queries with `UNION`] for details.
====

image:graph_union_clause.svg[]

////
[source, cypher, role=test-setup]
----
CREATE
  (ah:Actor {name: 'Anthony Hopkins'}),
  (hm:Actor {name: 'Helen Mirren'}),
  (hitchcock:Actor {name: 'Hitchcock'}),
  (hitchcockMovie:Movie {title: 'Hitchcock'}),
  (ah)-[:KNOWS]->(hm),
  (ah)-[:ACTS_IN]->(hitchcockMovie),
  (hm)-[:ACTS_IN]->(hitchcockMovie)
----
////


[[union-combine-queries-retain-duplicates]]
== Combine two queries and retain duplicates

Combining the results from two queries is done using `UNION ALL`.

.Query
[source, cypher]
----
MATCH (n:Actor)
RETURN n.name AS name
UNION ALL
MATCH (n:Movie)
RETURN n.title AS name
----

The combined result is returned, including duplicates.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| name
| "Anthony Hopkins"
| "Helen Mirren"
| "Hitchcock"
| "Hitchcock"
|Rows: 4
|===


[[union-combine-queries-remove-duplicates]]
== Combine two queries and remove duplicates

By not including `ALL` in the `UNION`, duplicates are removed from the combined result set.

.Query
[source, cypher]
----
MATCH (n:Actor)
RETURN n.name AS name
UNION
MATCH (n:Movie)
RETURN n.title AS name
----

The combined result is returned, without duplicates.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| name
| "Anthony Hopkins"
| "Helen Mirren"
| "Hitchcock"
|Rows: 3
|===

Removal of duplicates can also be accomplished by explicitly including `DISTINCT` in the `UNION`.

.Query
[source, cypher]
----
MATCH (n:Actor)
RETURN n.name AS name
UNION DISTINCT
MATCH (n:Movie)
RETURN n.title AS name
----

The combined result is returned, without duplicates.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| name
| "Anthony Hopkins"
| "Helen Mirren"
| "Hitchcock"
|Rows: 3
|===
