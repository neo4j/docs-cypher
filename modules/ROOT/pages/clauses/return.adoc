:description: The `RETURN` clause defines what to include in the query result set.

[[query-return]]
= RETURN

[[return-introduction]]
== Introduction
The `RETURN` clause defines the parts of a pattern (nodes, relationships, and/or properties) to be included in the query result. 

[[return-example-graph]]
== Example graph

The following graph is used for the examples below:

image::graph_return_clause.svg[width="600",role="middle"]

To recreate the graph, run the following query in an empty Neo4j database.

[source, cypher, role=test-setup]
----
CREATE
  (keanu:Person {name: 'Keanu Reeves', bornIn: 'Beirut', nationality: 'Canadian'}),
  (taiChi:Movie {title: 'Man of Tai Chi', released: 2013}),
  (keanu)-[:ACTED_IN]->(taiChi),
  (keanu)-[:DIRECTED]->(taiChi)
----


[[return-nodes]]
== Return nodes

To return a node, list it in the `RETURN` clause:

.Query
[source, cypher]
----
MATCH (p:Person {name: 'Keanu Reeves'})
RETURN p
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +p+
| +{"bornIn":"Beirut","nationality":"Canadian","name":"Keanu Reeves"}+
1+d|Rows: 1
|===


[[return-relationships]]
== Return relationships

To return a relationship type, list it in the `RETURN` clause:

.Query
[source, cypher]
----
MATCH (p:Person {name: 'Keanu Reeves'})-[r:ACTED_IN]->(m)
RETURN type(r)
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +type(r)+ 
| +:ACTED_IN+
1+d|Rows: 1
|===


[[return-property]]
== Return property

To return a specific property, use the dot separator:

.Query
[source, cypher]
----
MATCH (p:Person {name: 'Keanu Reeves'})
RETURN p.bornIn
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +p.bornIn+
| +"Beirut"+
1+d|Rows: 1
|===

[TIP]
====
To only return the value of a property, do not not return the full node/relationship.
This will improve performance.
====


[[return-all-elements]]
== Return all elements

To return all nodes, relationships and paths found in a query, use the `*` symbol:

.Query
[source, cypher]
----
MATCH p = (keanu:Person {name: 'Keanu Reeves'})-[r]->(m)
RETURN *
----

This returns the two nodes, and the two possible paths between them. 

.Result
[role="queryresult",options="header,footer",cols="4*<m"]
|===
| +keanu+ | +m+ | +p+ | +r+
| +{"bornIn":"Beirut","nationality":"Canadian","name":"Keanu Reeves"}+ | +{"title":"Man of Tai Chi","released":2013}+ | +[{"bornIn":"Beirut","nationality":"Canadian","name":"Keanu Reeves"},{},{"title":"Man of Tai Chi","released":2013}]+ | +{:ACTED_IN}+
| +{"bornIn":"Beirut","nationality":"Canadian","name":"Keanu Reeves"}+ | +{"title":"Man of Tai Chi","released":2013}+ | +[{"bornIn":"Beirut","nationality":"Canadian","name":"Keanu Reeves"},{},{"title":"Man of Tai Chi","released":2013}]+ | +{:DIRECTED}+
4+d|Rows: 1
|===


[[return-variable-with-uncommon-characters]]
== Variable with uncommon characters

To introduce a variable made up of characters not contained in the English alphabet, use ``` to enclose the variable:

.Query
[source, cypher]
----
MATCH (`/uncommon variable\`)
WHERE `/uncommon variable\`.name = 'Keanu Reeves'
RETURN `/uncommon variable\`.bornIn
----

The `bornIn` property of the node with the `name` property set to `'Keanu Reeves'` is returned:

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +`/uncommon variable\`.bornIn+
| +"Beirut"+
1+d|Rows: 1
|===


[[return-column-alias]]
== Column alias

Names of returned columns can be renamed using the `AS` operator:

.Query
[source, cypher]
----
MATCH (p:Person {name: 'Keanu Reeves'})
RETURN p.nationality AS citizenship
----

Returns the `nationality` property of `'Keanu Reeves'`, but the column is renamed to `citizenship`.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +citizenship+
| +"Canadian"+
1+d|Rows: 1
|===


[[return-optional-properties]]
== Optional properties

If the existence of a property is unknown, it can still be included in a `RETURN` clause.
It will be treated as `null` if it is missing.

.Query
[source, cypher]
----
MATCH (n)
RETURN n.bornIn
----

This example returns the `bornIn` properties for nodes that has that property, and `null` for  those nodes missing the property. 

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +n.bornIn+
| +"Beirut"+
| +<null>+
1+d|Rows: 2
|===


[[return-other-expressions]]
== Other expressions

Any expression can be used as a return item -- literals, predicates, properties, functions, and so on. 

.Query
[source, cypher]
----
MATCH (m:Movie {title: 'Man of Tai Chi'})
RETURN m.released < 2012, "I'm a literal",[p=(m)--() | p] AS `(m)--()`
----

Returns a predicate, a literal and function call with a pattern expression parameter:

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===
| +m.released < 2012+ | +"I'm a literal"+ | +(m)--()+
| +false+ | +"I'm a literal"+ | +[[{"title":"Man of Tai Chi","released":2013},{},{"bornIn":"Beirut","nationality":"Canadian","name":"Keanu Reeves"}],[{"title":"Man of Tai Chi","released":2013},{},{"bornIn":"Beirut","nationality":"Canadian","name":"Keanu Reeves"}]]+
3+d|Rows: 1
|===


[[return-unique-results]]
== Unique results

`DISTINCT` retrieves only unique rows for the columns that have been selected for output.

.Query
[source, cypher]
----
MATCH (p:Person {name: 'Keanu Reeves'})-->(m)
RETURN DISTINCT m
----

The `Movie` node `'Man of Tai Chi'` is returned by the query, but only once (without the `DISTINCT` operator it would have been returned twice because there are two relationships going to it from `'Keanu Reeves'`):

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +m+
| {"title":"Man of Tai Chi","released":2013}+
1+d|Rows: 1
|===

