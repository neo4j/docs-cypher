:description: `SKIP` (and its synonym `OFFSET) defines from which row to start including the rows in the output.

= SKIP

`SKIP` (and its synonym `OFFSET`) defines from which row to start including the rows in the output.

By using `SKIP`, the result set will get trimmed from the top.

[NOTE]
Neo4j does not guarantee the results generated by `SKIP`/`OFFSET`.
The only clause that guarantees a specific row order is xref:clauses/order-by.adoc[`ORDER BY`].

`SKIP` accepts any expression that evaluates to a positive `INTEGER` and does not refer to nodes or relationships.

[[example-graph]]
== Example graph

The following graph is used for the examples below:

image::graph_skip_clause.svg[width="600", role="middle"]

To recreate it, run the following query against an empty Neo4j database:

[source, cypher, role=test-setup]
----
CREATE
  (andy: Person {name: 'Andy'}),
  (bernard: Person {name: 'Bernard'}),
  (charlotte: Person {name: 'Charlotte'}),
  (david: Person {name: 'David'}),
  (erika: Person {name: 'Erika'}),
  (andy)-[:KNOWS]->(bernard),
  (andy)-[:KNOWS]->(charlotte),
  (andy)-[:KNOWS]->(david),
  (andy)-[:KNOWS]->(erika)
----

[[examples]]
== Examples

.Skip the first three rows
====

The following query returns a subset of the result, starting from the fourth result.

.Query
[source, cypher]
----
MATCH (n)
RETURN n.name
ORDER BY n.name
SKIP 3
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| n.name
| "David"
| "Erika"
d|Rows: 2
|===

====

.Return the middle two rows
====

The following query returns the middle two rows, with `SKIP` skipping the first and xref:clauses/limit.adoc[`LIMIT`] removing the final two.

.Query
[source, cypher]
----
MATCH (n)
RETURN n.name
ORDER BY n.name
SKIP 1
LIMIT 2
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| n.name
| "Bernard"
| "Charlotte"
d|Rows: 2
|===

====

.Using an expression with `SKIP` to return a subset of the rows
====

`SKIP` accepts any expression that evaluates to a positive `INTEGER`, as long as it can be statically calculated (i.e. calculated before the query is run).

This query skips the first row and then xref:functions/mathematical-numeric.adoc#functions-rand[randomly] skips an additional 0, 1, or 2 rows, resulting in skipping a total of 1, 2, or 3 rows before returning the remaining names.

.Query
[source, cypher]
----
MATCH (n)
RETURN n.name
ORDER BY n.name
SKIP 1 + toInteger(3 * rand())
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| n.name
| "Bernard"
| "Charlotte"
| "David"
| "Erika"
d|Rows: 4
|===

====


[role=label--new-5.24]
[[skip-standalone-clause]]
== Using `SKIP` as a standalone clause

`SKIP` can be used as a standalone clause, or in conjunction with xref:clauses/order-by.adoc[`ORDER BY`] or xref:clauses/limit.adoc[`LIMIT`].

.Standalone use of `SKIP`
[source, cypher]
----
MATCH (n)
SKIP 2
RETURN collect(n.name) AS names
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| names
| ["Charlotte", "David", "Erika"]
|Rows: 1
|===

The following query orders all nodes by `name`, skips the two first rows and limits the results to two rows.
It then xref:functions/aggregating.adoc#functions-collect[collects] the results in a list.

.`SKIP` used in conjunction with `ORDER BY` and `LIMIT`
[source, cypher]
----
MATCH (n)
ORDER BY n.name
SKIP 2
LIMIT 2
RETURN collect(n.name) AS names
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| names
| ["Charlotte", "David"]
|Rows: 1
|===

[role=label--new-5.24]
[[offset-synonym]]
== `OFFSET` as a synonym to `SKIP`

`OFFSET` was introduced as part of Cypher's xref:appendix/gql-conformance/index.adoc[] and can be used as a synonym to `SKIP`.

.Query
[source, cypher]
----
MATCH (n)
ORDER BY n.name
OFFSET 2
LIMIT 2
RETURN collect(n.name) AS names
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| names
| ["Charlotte", "David"]
|Rows: 1
|===