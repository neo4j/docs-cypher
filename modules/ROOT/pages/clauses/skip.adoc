:description: `SKIP` defines from which row to start including the rows in the output.

[[query-skip]]
= SKIP

[abstract]
--
`SKIP` defines from which row to start including the rows in the output.
--

By using `SKIP`, the result set will get trimmed from the top.
Please note that no guarantees are made on the order of the result unless the query specifies the `ORDER BY` clause.
`SKIP` accepts any expression that evaluates to a positive integer -- however the expression cannot refer to nodes or relationships.

image:graph_skip_clause.svg[]

////
[source, cypher, role=test-setup]
----
CREATE
  (a {name: 'A'}),
  (b {name: 'B'}),
  (c {name: 'C'}),
  (d {name: 'D'}),
  (e {name: 'E'}),
  (a)-[:KNOWS]->(b),
  (a)-[:KNOWS]->(c),
  (a)-[:KNOWS]->(d),
  (a)-[:KNOWS]->(e)
----
////


[[skip-first-three-rows]]
== Skip first three rows

To return a subset of the result, starting from the fourth result, use the following syntax:

.Query
[source, cypher, indent=0]
----
MATCH (n)
RETURN n.name
ORDER BY n.name
SKIP 3
----

The first three nodes are skipped, and only the last two are returned in the result.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +n.name+
| +"D"+
| +"E"+
1+d|Rows: 2
|===


[[skip-return-middle-rows]]
== Return middle two rows

To return a subset of the result, starting from somewhere in the middle, use this syntax:

.Query
[source, cypher, indent=0]
----
MATCH (n)
RETURN n.name
ORDER BY n.name
SKIP 1
LIMIT 2
----

Two nodes from the middle are returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +n.name+
| +"B"+
| +"C"+
1+d|Rows: 2
|===


[[skip-using-expression]]
== Using an expression with `SKIP` to return a subset of the rows

Skip accepts any expression that evaluates to a positive integer as long as it is not referring to any external variables:

.Query
[source, cypher, indent=0]
----
MATCH (n)
RETURN n.name
ORDER BY n.name
SKIP 1 + toInteger(3*rand())
----

Skip the firs row plus randomly 0, 1, or 2.
So randomly skip 1, 2, or 3 rows.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +n.name+
| +"B"+
| +"C"+
| +"D"+
| +"E"+
1+d|Rows: 4
|===

