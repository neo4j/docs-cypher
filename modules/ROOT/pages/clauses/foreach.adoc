:description: The `FOREACH` clause is used to update data within a collection whether components of a path, or result of aggregation.

[[query-foreach]]
= FOREACH

The `FOREACH` clause is used to update data within a collection whether components of a path, or result of aggregation.

Lists and paths are key concepts in Cypher.
The `FOREACH` clause can be used to update data, such as executing update commands on elements in a path, or on a list created by aggregation.

The variable context within the `FOREACH` parenthesis is separate from the one outside it.
This means that if you `CREATE` a node variable within a `FOREACH`, you will _not_ be able to use it outside of the foreach statement, unless you match to find it.

Within the `FOREACH` parentheses, you can do any of the updating commands -- `SET`, `REMOVE`, `CREATE`, `MERGE`, `DELETE`, and `FOREACH`.

[TIP]
====
If you want to execute an additional `MATCH` for each element in a list then the xref::clauses/unwind.adoc[`UNWIND`] clause would be a more appropriate command.
====

image:graph_foreach_clause.svg[]

////
CREATE
  (a:Person {name: 'A'}),
  (b:Person {name: 'B'}),
  (c:Person {name: 'C'}),
  (d:Person {name: 'D'}),
  (a)-[:KNOWS]->(b),
  (b)-[:KNOWS]->(c),
  (c)-[:KNOWS]->(d)
////


[[foreach-mark-all-nodes-along-a-path]]
== Mark all nodes along a path

This query will set the property `marked` to `true` on all nodes along a path.

.Query
[source, cypher, indent=0]
----
MATCH p=(start)-[*]->(finish)
WHERE start.name = 'A' AND finish.name = 'D'
FOREACH (n IN nodes(p) | SET n.marked = true)
----

.Result
[role="queryresult",options="footer",cols="1*<m"]
|===
|(empty result)
d|Rows: 0 +
Properties set: 4
|===

