:description: The `DELETE` clause is used to delete nodes, relationships or paths.

[[query-delete]]
= DELETE

[abstract]
--
The `DELETE` clause is used to delete nodes, relationships or paths.
--

For removing properties and labels, see xref::clauses/remove.adoc[REMOVE].
Remember that you cannot delete a node without also deleting relationships that start or end on said node.
Either explicitly delete the relationships, or use `DETACH DELETE`.

The examples start out with the following database:

image:graph_delete_clause.svg[]

////
CREATE
  (a:Person {name: 'Andy', age: 36}),
  (p:Person {name: 'Timothy', age: 25}),
  (t:Person {name: 'Peter', age: 34}),
  (z:Person {name: 'UNKNOWN'}),
  (a)-[:KNOWS]->(t),
  (a)-[:KNOWS]->(p)
////


[[delete-delete-single-node]]
== Delete single node

To delete a node, use the `DELETE` clause.

.Query
[source, cypher, indent=0]
----
MATCH (n:Person {name: 'UNKNOWN'})
DELETE n
----

.Result
[role="queryresult",options="footer",cols="1*<m"]
|===
1+|(empty result)
1+d|Rows: 0 +
Nodes deleted: 1
|===


[[delete-delete-all-nodes-and-relationships]]
== Delete all nodes and relationships

This query is not for deleting large amounts of data, but is useful when experimenting with small example data sets.

.Query
[source, cypher, indent=0]
----
MATCH (n)
DETACH DELETE n
----

.Result
[role="queryresult",options="footer",cols="1*<m"]
|===
1+|(empty result)
1+d|Rows: 0 +
Nodes deleted: 4 +
Relationships deleted: 2
|===


[[delete-delete-a-node-with-all-its-relationships]]
== Delete a node with all its relationships

When you want to delete a node and any relationship going to or from it, use `DETACH DELETE`.

.Query
[source, cypher, indent=0]
----
MATCH (n {name: 'Andy'})
DETACH DELETE n
----

.Result
[role="queryresult",options="footer",cols="1*<m"]
|===
1+|(empty result)
1+d|Rows: 0 +
Nodes deleted: 1 +
Relationships deleted: 2
|===

[NOTE]
====
For `DETACH DELETE` for users with restricted security privileges, see xref:4.4@operations-manual:ROOT:authentication-authorization/access-control/index.adoc#detach-delete-restricted-user[Operations Manual -> Fine-grained access control].
====


[[delete-delete-relationships-only]]
== Delete relationships only

It is also possible to delete relationships only, leaving the node(s) otherwise unaffected.

.Query
[source, cypher, indent=0]
----
MATCH (n {name: 'Andy'})-[r:KNOWS]->()
DELETE r
----

This deletes all outgoing `KNOWS` relationships from the node with the name *'Andy'*.

.Result
[role="queryresult",options="footer",cols="1*<m"]
|===
1+|(empty result)
1+d|Rows: 0 +
Relationships deleted: 2
|===

