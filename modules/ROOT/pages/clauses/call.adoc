:description: The `CALL` clause is used to call a procedure deployed in the database.
[[query-call]]
= CALL procedure

The `CALL` clause is used to call a procedure deployed in the database.

[NOTE]
The `CALL` clause is also used to evaluate a subquery.
For more information about the `CALL` clause in this context, refer to xref::subqueries/call-subquery.adoc[].

For information about how to list procedures, see xref:clauses/listing-procedures.adoc[].

[NOTE]
Neo4j comes with a number of built-in procedures.
For a list of these, see link:{neo4j-docs-base-uri}/operations-manual/{page-version}/reference/procedures[Operations Manual -> Procedures].
Users can also develop custom procedures and deploy to the database.
See link:{neo4j-docs-base-uri}/java-reference/{page-version}/extending-neo4j/procedures#extending-neo4j-procedures[Java Reference -> User-defined procedures] for details.

[[example-graph]]
== Example graph

The following graph is used for the examples below:

image::call_procedure.svg[width="400",role="middle"]

To recreate it, run the following query against an empty Neo4j database:

[source, cypher, role=test-setup]
----
CREATE (andy:Developer {name: 'Andy', born: 1991}),
       (beatrice:Developer {name: 'Beatrice', born: 1985}),
       (charlotte:Administrator {name: 'Charlotte', born: 1990}),
       (david:Administrator {name: 'David', born: 1994, nationality: 'Swedish'}),
       (andy)-[:KNOWS]->(beatrice),
       (beatrice)-[:KNOWS]->(charlotte),
       (andy)-[:KNOWS]->(david)
----

[[call-procedure-examples]]
== Examples

.`CALL` a procedure without arguments
====

This example calls the built-in procedure link:{neo4j-docs-base-uri}/operations-manual/{page-version}/reference/procedures/#procedure_db_labels[`db.labels()`], which lists all labels used in the database.

.Query
[source, cypher]
----
CALL db.labels()
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| label

| "Developer"
| "Administrator"

1+d|Rows: 2
|===

====

[NOTE]
It is best practice to use parentheses when calling procedures, although Cypher allows for their omission when calling procedures of arity-0 (no arguments).
Omission of parentheses is available only in a so-called standalone procedure call, when the whole query consists of a single `CALL` clause.


.`CALL` a procedure without arguments
====


This example calls the procedure link:{neo4j-docs-base-uri}/operations-manual/{page-version}/reference/procedures/#procedure_dbms_checkconfigvalue[`dbms.checkConfigValue()`], which checks the validity of a configuration setting value, using literal arguments.

.Query
[source, cypher]
----
CALL dbms.checkConfigValue('server.bolt.enabled', 'true')
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| "valid" | "message"
| true | "requires restart"

1+d|Rows: 2
|===

====

.`CALL` a procedure using parameters
====

This calls the example procedure `dbms.checkConfigValue()` using parameters as arguments.
Each procedure argument is taken to be the value of a corresponding statement parameter with the same name (or null if no such parameter has been given).

.Parameters
[source, parameters]
----
{
  "setting": "server.bolt.enabled",
  "value": "true"
}
----

.Query
[source, cypher]
----
CALL dbms.checkConfigValue($setting, $value)
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| "valid" | "message"
| true | "requires restart"

1+d|Rows: 2
|===

[NOTE]
Examples that use parameter arguments shows the given parameters in JSON format; the exact manner in which they are to be submitted depends upon the driver being used.
See xref::syntax/parameters.adoc[] for more about querying with parameters.

====


.`CALL` a procedure using both literal and parameter arguments
====

This calls the example procedure `dbms.checkConfigValue()` using both literal and parameter arguments.

.Parameters
[source, parameters]
----
{
  "setting": "server.bolt.enabled"
}
----

.Query
[source, cypher]
----
CALL dbms.checkConfigValue($setting, 'true')
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| "valid" | "message"
| true | "requires restart"

1+d|Rows: 2
|===

====


[[call-procedure-yield]]
== Using `YIELD`

The `YIELD` keyword is used to specify which columns of procedure metadata to return, allowing for the selection and filtering of the displayed information.

.`YIELD *`
====

Using `YIELD *` will return all available return columns for a procedure.

.Query
[source, cypher]
----
CALL db.labels() YIELD *
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| label

| "Administrator"
| "Developer"

1+d|Rows: 2
|===

If the procedure has deprecated return columns, those columns are also returned.

Note that `YIELD *` is only valid in standalone procedure calls.
For example, the following is not valid:

.Not allowed
[source, cypher, role=test-fail]
----
CALL db.labels() YIELD *
RETURN count(*) AS results
----

====

.`YIELD` specific procedure results and filter on them
====

`YIELD` can be used to filter for specific results.
This requires knowing the names of the arguments within a procedure's signature, which can either be found in the link:{neo4j-docs-base-uri}/operations-manual/{page-version}/reference/procedures/[Operations Manual -> Procedures] or returned by a `SHOW PROCEDURES` query.

.Find the argument names of `db.propertyKeys`
[source, cypher]
----
SHOW PROCEDURES YIELD name, signature
WHERE name = 'db.propertyKeys'
RETURN signature
----

.Result
[role="queryresult",options="header,footer",cols="1m"]
|===
| signature

| "db.propertyKeys() :: (propertyKey :: STRING)"

1+d|Rows: 1
|===

It is then possible to use these argument names for further query filtering.
Note that if the procedure call is part of a larger query, its output must be named explicitly.
In the below example, `propertyKey` is aliased as `prop` and then used later in the query to xref:functions/aggregating.adoc#functions-count[count] the occurrence of each property in the graph.

.Filter on specific argument returned by `YIELD`
[source, cypher]
----
CALL db.propertyKeys() YIELD propertyKey AS prop
MATCH (n)
WHERE n[prop] IS NOT NULL
RETURN prop, count(n) AS numNodes
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| prop | numNodes

| "name" | 4
| "born" | 4
| "nationality" | 1

2+d|Rows: 3
|===

====


=== Note on VOID procedures

Neo4j supports the notion of `VOID` procedures.
A `VOID` procedure is a procedure that does not declare any result fields and returns no result records.
`VOID` procedure only produces side-effects and does not allow for the use of `YIELD`.
Calling a `VOID` procedure in the middle of a larger query will simply pass on each input record (i.e., it acts like xref:clauses/with.adoc[`WITH *`] in terms of the record stream).

[role=label--new-5.24]
[[optional-call]]
== Optional procedure calls

`OPTIONAL CALL` allows for an optional procedure call.
Similar to xref:clauses/optional-match.adoc[`OPTIONAL MATCH`] any empty rows produced by the `OPTIONAL CALL` will return `null`.

.Difference between using `CALL` and `OPTIONAL CALL`
====

This query uses the link:{neo4j-docs-base-uri}/apoc/{page-version}/overview/apoc.neighbors/apoc.neighbors.tohop[`apoc.neighbors.tohop()`] procedure (part of Neo4j's link:{neo4j-docs-base-uri}/apoc/{page-version}/[APOC Core library]), which returns all nodes connected by the given relationship type within the specified distance (1 hop, in this case) and direction.

.Regular procedure `CALL`
[source, cypher]
----
MATCH (n)
CALL apoc.neighbors.tohop(n, "KNOWS>", 1)
YIELD node
RETURN n.name AS name, collect(node.name) AS connections
----

Note that the result does not include the nodes in the graph without any outgoing `KNOWS` relationships connected to them.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| name | connections

| "Andy"
| ["Beatrice", "David"]

| "Beatrice"
| ["Charlotte"]

2+d|Rows: 2
|===

The same query is used below, but `CALL` is replaced with `OPTIONAL CALL`.

.Optional procedure `CALL`
[source, cypher]
----
MATCH (n)
OPTIONAL CALL apoc.neighbors.tohop(n, "KNOWS>", 1)
YIELD node
RETURN n.name AS name, collect(node.name) AS connections
----

The result now includes the two nodes without any outgoing `KNOWS` relationships connected to them.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| name | connections

| "Andy"
| ["Beatrice, "David"]

| "Beatrice"
| ["Charlotte"]

| "Charlotte"
| []

| "David"
| []

2+d|Rows: 4
|===

====


