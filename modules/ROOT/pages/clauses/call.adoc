:description: The `CALL` clause is used to call a procedure deployed in the database.
[[query-call]]
= CALL procedure

The `CALL` clause is used to call a procedure deployed in the database.

[NOTE]
The `CALL` clause is also used to evaluate a subquery.
For more information about the `CALL` clause in this context, refer to xref::subqueries/call-subquery.adoc[].

For information about how to list procedures, see xref:clauses/listing-procedures.adoc[].

[NOTE]
====
Neo4j comes with a number of built-in procedures.
For a list of these, see link:{neo4j-docs-base-uri}/operations-manual/{page-version}/reference/procedures[Operations Manual -> Procedures].

Users can also develop custom procedures and deploy to the database.
See link:{neo4j-docs-base-uri}/java-reference/{page-version}/extending-neo4j/procedures#extending-neo4j-procedures[Java Reference -> User-defined procedures] for details.
====

== Example graph

The following graph is used for the examples below:

image::call_procedure.svg[width="600",role="middle"]

To recreate it, run the following query against an empty Neo4j database:

[source, cypher, role=test-setup]
----
CREATE (:User {name: 'Andy', born: 1991}),
       (:User {name: 'Beatrice', born: 1985}),
       (:Administrator {name: 'Charlotte', born: 1990}),
       ( {name: 'David', born: 1994, nationality: 'Swedish'})
----

[[call-procedure-examples]]
== Examples

.`CALL` a procedure without arguments
====

This example calls the built-in procedure link:{neo4j-docs-base-uri}/operations-manual/{page-version}/reference/procedures/#procedure_db_labels[`db.labels`], which lists all labels used in the database.

.Query
[source, cypher]
----
CALL db.labels()
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| label

| "User"
| "Administrator"

1+d|Rows: 2
|===

====

[NOTE]
It is best practice to use parentheses when calling procedures, although Cypher allows for their omission when calling procedures of arity-0 (no arguments).
Omission of parentheses is available only in a so-called standalone procedure call, when the whole query consists of a single `CALL` clause.


.`CALL` a procedure without arguments
====


This example calls the procedure link:{neo4j-docs-base-uri}/operations-manual/{page-version}/reference/procedures/#procedure_dbms_checkconfigvalue[`dbms.checkConfigValue`] using literal arguments.

.Query
[source, cypher]
----
CALL dbms.checkConfigValue('server.bolt.enabled', 'true')
----

.Result
[role="queryresult",options="header,footer",cols=""2*<m"]
|===
| "valid" | "message"
| true | "requires restart"

1+d|Rows: 2
|===

====

.`CALL` a procedure using parameters
====

This calls the example procedure `dbms.checkConfigValue` using parameters as arguments.
Each procedure argument is taken to be the value of a corresponding statement parameter with the same name (or null if no such parameter has been given).

.Parameters
[source, parameters]
----
{
  "setting": "server.bolt.enabled",
  "value": "true"
}
----

.Query
[source, cypher]
----
CALL dbms.checkConfigValue($setting, $value)
----

.Result
[role="queryresult",options="header,footer",cols=""2*<m"]
|===
| "valid" | "message"
| true | "requires restart"

1+d|Rows: 2
|===

[NOTE]
Examples that use parameter arguments shows the given parameters in JSON format; the exact manner in which they are to be submitted depends upon the driver being used.
See xref::syntax/parameters.adoc[] for more about querying with parameters.

====


.`CALL` a procedure using both literal and parameter arguments
====

This calls the example procedure `dbms.checkConfigValue` using both literal and parameter arguments.

.Parameters
[source, parameters]
----
{
  "setting": "server.bolt.enabled"
}
----

.Query
[source, cypher]
----
CALL dbms.checkConfigValue($setting, 'true')
----

.Result
[role="queryresult",options="header,footer",cols=""2*<m"]
|===
| "valid" | "message"
| true | "requires restart"

1+d|Rows: 2
|===

====


[[call-procedure-yield]]
== Using `YIELD`

The `YIELD` keyword is used to specify which columns of procedure metadata to return, allowing for the selection and filtering of the displayed information.

.`YIELD *`
====

Using `YIELD *` will return all available return columns for a procedure.

.Query
[source, cypher]
----
CALL db.labels() YIELD *
----

.Result
[role="queryresult",options="header,footer",cols=""1*<m"]
|===
| label

| "Administrator"
| "User"

1+d|Rows: 2
|===

If the procedure has deprecated return columns, those columns are also returned.

====

.`YIELD` specific argument values and filter on them
====

`YIELD` can be used to filter for specific argument values.
This requires knowing the names of arguments within a procedure's signature, which can either be found in the link:{neo4j-docs-base-uri}/operations-manual/{page-version}/reference/procedures/[Operations Manual -> Procedures] or returned by a `SHOW PROCEDURES` query.

.Find the argument names of `db.propertyKeys`
[source, cypher]
----
SHOW PROCEDURES YIELD name, signature
WHERE name = 'db.propertyKeys'
RETURN signature
----

.Result
[role="queryresult",options="header,footer",cols=""1m"]
|===
| signature

| "db.propertyKeys() :: (propertyKey :: STRING)"

1+d|Rows: 1
|===

It is then possible to use these argument names for further query filtering:

.Filter on specific argument returned by `YIELD`
[source, cypher]
----
CALL db.propertyKeys() YIELD propertyKey AS prop
MATCH (n)
WHERE n[prop] IS NOT NULL
RETURN prop, count(n) AS numNodes
----

.Result
[role="queryresult",options="header,footer",cols=""2*<m"]
|===
| prop | numNodes

| "name" | 4
| "born" | 4
| "nationality" | 1

2+d|Rows: 3
|===

====

.Filtering procedures using `WHERE`
====

This query uses a xref:clauses/where.adoc[`WHERE`] to further filter the returned procedure values.

.Query
[source, cypher]
----
CALL db.labels() YIELD label
WHERE label CONTAINS 'User'
RETURN count(label) AS numLabels
----

.Result
[role="queryresult",options="header,footer",cols=""1*<m"]
|===
| numLabels

| 1

1+d|Rows: 1
|===

====





=== Note on VOID procedures

Neo4j supports the notion of `VOID` procedures.
A `VOID` procedure is a procedure that does not declare any result fields and returns no result records and that has explicitly been declared as `VOID`.
`VOID` procedure only produces side-effects and does not allow for the use of `YIELD`

[role=label--new-5.24]
[[optional-call]]
== Optional procedure calls

`OPTIONAL CALL` allows for an optional procedure call.
Similar to xref:clauses/optional-match.adoc[`OPTIONAL MATCH`] any empty rows produced by the `OPTIONAL CALL` will return `null`.

.Difference between using `CALL` and `OPTIONAL CALL`
====

This query retrieves all node labels in the database, then for each label, it collects the xref:functions/scalar.adoc#elementid[elementId] of nodes that have that label.
xref:queries/case.adoc[`CASE`] is then used to determine the status message, indicating whether nodes with a specific label exist or if the label is `null`.

.Regular procedure `CALL`
[source, cypher]
----
CALL db.labels() YIELD label
WITH label
MATCH (n)
WHERE label IN labels(n)
WITH label, collect(elementId(n)) AS nodeIds
RETURN label, nodeIds,
CASE 
      WHEN label IS NULL THEN "These nodes have no labels" 
      ELSE "Nodes with this label exist" 
      END AS status
----

Note that the result does not include the graph in the graph without a label.

.Result
[role="queryresult",options="header,footer",cols=""3*<m"]
|===
| label | nodeIds | status

| "User"
| ["4:6d0cd2f7-bfd4-44ea-9247-d1d5d6a6571e:0", "4:6d0cd2f7-bfd4-44ea-9247-d1d5d6a6571e:9"]
| "Nodes with this label exist" 

| "Administrator"
| ["4:6d0cd2f7-bfd4-44ea-9247-d1d5d6a6571e:1"]
| "Nodes with this label exist"

3+d|Rows: 2
|===

The same query is used below, but `CALL` is replaced with `OPTIONAL CALL`.

.Optional procedure `CALL`
[source, cypher]
----
OPTIONAL CALL db.labels() YIELD label
WITH label
MATCH (n)
WHERE label IN labels(n)
WITH label, collect(elementId(n)) AS nodeIds
RETURN label, nodeIds,
CASE 
      WHEN label IS NULL THEN "These nodes have no labels" 
      ELSE "Nodes with this label exist" 
      END AS status
----

Now the result includes the node without any labels.

.Result
[role="queryresult",options="header,footer",cols=""3*<m"]
|===
| label | nodeIds | status

| null
| ["4:6d0cd2f7-bfd4-44ea-9247-d1d5d6a6571e:10"]
| "These nodes have no labels" 

| "User"
| ["4:6d0cd2f7-bfd4-44ea-9247-d1d5d6a6571e:0", "4:6d0cd2f7-bfd4-44ea-9247-d1d5d6a6571e:9"]
| "Nodes with this label exist" 

| "Administrator"
| ["4:6d0cd2f7-bfd4-44ea-9247-d1d5d6a6571e:1"]
| "Nodes with this label exist"

3+d|Rows: 3
|===

====


