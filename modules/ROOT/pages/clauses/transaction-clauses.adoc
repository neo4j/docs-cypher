:description: This section explains the `SHOW TRANSACTIONS` and `TERMINATE TRANSACTIONS` commands.

[[query-transaction-clauses]]
= Transaction commands

This page explains the `SHOW TRANSACTIONS` and `TERMINATE TRANSACTIONS` commands.

[[query-listing-transactions]]
== SHOW TRANSACTIONS

The `SHOW TRANSACTIONS` command is used to display running transactions within the instance.
This also includes fabric transactions.
For remote database aliases, transactions can be tracked by running `SHOW TRANSACTIONS` when connected to the remote database alias.

[NOTE]
====
The command `SHOW TRANSACTIONS` returns only the default output. For a full output use the optional `YIELD` command.
Full output: `SHOW TRANSACTIONS YIELD *`.
====

This command returns the following outputs:

.List transactions output
[options="header", cols="4,6,2"]
|===
| Column | Description | Type

m| database
a| The name of the database the transaction is executing against. label:default-output[]
m| STRING

m| transactionId
a| The transaction ID. label:default-output[]
m| STRING

m| currentQueryId
a| The ID of the query currently executing in this transaction, or an empty `STRING` if no query is currently executing. label:default-output[]
m| STRING

m| connectionId
a| The ID of the database connection attached to the transaction or an empty `STRING` for embedded connections. label:default-output[]
m| STRING

m| clientAddress
a| The client address of the connection issuing the transaction or an empty `STRING` if unavailable. label:default-output[]
m| STRING

m| username
a| The username of the user executing the transaction. label:default-output[]
m| STRING

m| currentQuery
a| The query text of the query currently executing in this transaction, or an empty `STRING` if no query is currently executing. label:default-output[]
m| STRING

m| startTime
a| The time at which the transaction was started. label:default-output[]
m| STRING

m| status
a| The current status of the transaction (`Terminated`, `Blocked`, `Closing`, or `Running`). label:default-output[]
m| STRING

m| elapsedTime
a| The time that has elapsed since the transaction was started. label:default-output[]
m| DURATION

m| outerTransactionId
a|
The ID of this transaction's outer transaction, if such exists, otherwise an empty `STRING`.
For details, see xref:subqueries/subqueries-in-transactions.adoc[`CALL { ... } IN TRANSACTIONS`].
m| STRING

m| metaData
a| Any metadata associated with the transaction, or an empty map if there is none.
m| MAP

m| parameters
a| A map containing all the parameters used by the query currently executing in this transaction, or an empty map if no query is currently executing.
m| MAP

m| planner
a|
The name of the Cypher planner used to plan the query currently executing in this transaction, or an empty `STRING` if no query is currently executing.
For details, see xref::planning-and-tuning/query-tuning.adoc#cypher-planner[Cypher planner].
m| STRING

m| runtime
a| The name of the Cypher runtime used by the query currently executing in this transaction, or an empty `STRING` if no query is currently executing. For details, see xref::planning-and-tuning/runtimes/index.adoc[Cypher runtime].
m| STRING

m| indexes
a| The indexes utilised by the query currently executing in this transaction, or an empty list if no query is currently executing.
m| LIST<MAP>

// New in 5.0
m| currentQueryStartTime
a| The time at which the query currently executing in this transaction was started, or an empty `STRING` if no query is currently executing.
m| STRING

m| protocol
a|
The protocol used by the connection issuing the transaction.
This is not necessarily an internet protocol, such as _http_, etc., although it could be.
It might also be "embedded", for example, if this connection represents an embedded session.
m| STRING

m| requestUri
a| The request URI used by the client connection issuing the transaction, or `null` if the URI is not available.
m| STRING

// New in 5.0
m| currentQueryStatus
a| The current status of the query currently executing in this transaction (`parsing`, `planning`, `planned`, `running`, or `waiting`), or an empty `STRING` if no query is currently executing.
m| STRING

m| statusDetails
a| Provide additional status details from the underlying transaction or an empty `STRING` if none is available.
m| STRING

m| resourceInformation
a| Information about any blocked transactions, or an empty map if there is none.
m| MAP

m| activeLockCount
a| Count of active locks held by the transaction.
m| INTEGER

// New in 5.0
m| currentQueryActiveLockCount
a| Count of active locks held by the query currently executing in this transaction.
m| INTEGER

m| cpuTime
a| CPU time that has been actively spent executing the transaction or `null` if unavailable.
m| DURATION

m| waitTime
a| Wait time that has been spent waiting to acquire locks.
m| DURATION

m| idleTime
a| Idle time for this transaction, or `null` if unavailable.
m| DURATION

// New in 5.0
m| currentQueryElapsedTime
a| The time that has elapsed since the query currently executing in this transaction was started, or `null` if no query is currently executing.
m| DURATION

// New in 5.0
m| currentQueryCpuTime
a| CPU time that has been actively spent executing the query currently executing in this transaction, or `null` if unavailable or no query is currently executing.
m| DURATION

// New in 5.0
m| currentQueryWaitTime
a| Wait time that has been spent waiting to acquire locks for the query currently executing in this transaction, or `null` if no query is currently executing.
m| DURATION

// New in 5.0
m| currentQueryIdleTime
a| Idle time for the query currently executing in this transaction, or `null` if unavailable or no query is currently executing.
m| DURATION

// New in 5.0
m| currentQueryAllocatedBytes
a| The number of bytes allocated on the heap so far by the query currently executing in this transaction, or `null` if unavailable or no query is currently executing.
m| INTEGER

m| allocatedDirectBytes
a| Amount of off-heap (native) memory allocated by the transaction in bytes or `null` if unavailable.
m| INTEGER

m| estimatedUsedHeapMemory
a| The estimated amount of used heap memory allocated by the transaction in bytes or `null` if unavailable.
m| INTEGER

m| pageHits
a| The total number of page cache hits that the transaction performed.
m| INTEGER

m| pageFaults
a| The total number of page cache faults that the transaction performed.
m| INTEGER

// New in 5.0
m| currentQueryPageHits
a| The total number of page cache hits that the query currently executing in this transaction performed.
m| INTEGER

// New in 5.0
m| currentQueryPageFaults
a| The total number of page cache faults that the query currently executing in this transaction performed.
m| INTEGER

m| initializationStackTrace
a| The initialization stacktrace for this transaction, or an empty `STRING` if unavailable.
m| STRING

|===


The `SHOW TRANSACTIONS` command can be combined with multiple `SHOW TRANSACTIONS` and `TERMINATE TRANSACTIONS`, see xref::clauses/transaction-clauses.adoc#query-combine-tx-commands[transaction commands combination].


=== Syntax

[NOTE]
====
More details about the syntax descriptions can be found link:{neo4j-docs-base-uri}/operations-manual/{page-version}/database-administration/syntax/#administration-syntax-reading[here].
====

List transactions on the current server::

[source, syntax, role="noheader"]
----
SHOW TRANSACTION[S] [transaction-id[,...]]
[YIELD { * | field[, ...] } [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
[WHERE expression]
[RETURN field[, ...] [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
----

The format of `transaction-id` is `<databaseName>-transaction-<id>`.
Transaction IDs must be supplied as one or more comma-separated quoted `STRING` values, or as an expression resolving to a `STRING` or a `LIST<STRING>`.

[NOTE]
====
When using the `RETURN` clause, the `YIELD` clause is mandatory and must not be omitted.
====

A user with the link:{neo4j-docs-base-uri}/operations-manual/{page-version}/authentication-authorization/database-administration/#access-control-database-administration-transaction[`SHOW TRANSACTION`] privilege can view the currently executing transactions in accordance with the privilege grants.
All users may view all of their own currently executing transactions.


=== Listing all transactions

To list all available transactions with the default outputs, use the `SHOW TRANSACTIONS` command.
If all outputs are required, use `SHOW TRANSACTIONS YIELD *`.

.Query
[source, cypher, role=test-result-skip]
----
SHOW TRANSACTIONS
----

.Result
[role="queryresult",options="header,footer",cols="10*<m"]
|===
| database | transactionId | currentQueryId | connectionId | clientAddress | username | currentQuery | startTime | status | elapsedTime

| "neo4j" | "neo4j-transaction-6" | "query-664" | "" | "" | "" | "SHOW TRANSACTIONS" | "2022-06-14T10:02:45.568Z" | "Running" | PT0.038S
| "neo4j" | "neo4j-transaction-4" | "query-663" | "" | "" | "" | "MATCH (n) RETURN n" | "2022-06-14T10:02:45.546Z" | "Running" | PT0.06S

10+d|Rows: 2
|===


=== Listing transactions with filtering on output

The listed transactions can be filtered by using the `WHERE` clause.
For example, getting the databases for all transactions where the currently executing query contains `'Mark'`:

.Query
[source, cypher, role=test-result-skip]
----
SHOW TRANSACTIONS YIELD database, currentQuery WHERE currentQuery contains 'Mark'
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| database | currentQuery

| "neo4j" | "MATCH (p:Person) WHERE p.name='Mark' RETURN p"
| "neo4j" | "SHOW TRANSACTIONS YIELD database, currentQuery WHERE currentQuery contains 'Mark'"

2+d|Rows: 2
|===

Several of the outputs have the `duration` type, which can be hard to read.
They can instead be returned in a more readable format:

.Query
[source, cypher, role=test-result-skip]
----
SHOW TRANSACTIONS
YIELD transactionId, elapsedTime, cpuTime, waitTime, idleTime,
  currentQueryElapsedTime, currentQueryCpuTime, currentQueryWaitTime, currentQueryIdleTime
RETURN
  transactionId AS txId,
  elapsedTime.milliseconds AS elapsedTimeMillis,
  cpuTime.milliseconds AS cpuTimeMillis,
  waitTime.milliseconds AS waitTimeMillis,
  idleTime.seconds AS idleTimeSeconds,
  currentQueryElapsedTime.milliseconds AS currentQueryElapsedTimeMillis,
  currentQueryCpuTime.milliseconds AS currentQueryCpuTimeMillis,
  currentQueryWaitTime.microseconds AS currentQueryWaitTimeMicros,
  currentQueryIdleTime.seconds AS currentQueryIdleTimeSeconds
----

.Result
[role="queryresult",options="header,footer",cols="9*<m"]
|===
| txId | elapsedTimeMillis | cpuTimeMillis | waitTimeMillis | idleTimeSeconds | currentQueryElapsedTimeMillis | currentQueryCpuTimeMillis | currentQueryWaitTimeMicros | currentQueryIdleTimeSeconds
| "neo4j-transaction-5" | 1055 | 767 | 0 | 0 | 1012 | 767 | 0 | 0
| "neo4j-transaction-9" | 156 | 155 | 0 | 0 | 97 | 97 | 0 | 0
| "neo4j-transaction-4" | 1082 | 17 | 0 | 1 | 1013 | 17 | 0 | 0
9+d|Rows: 3
|===


=== Listing specific transactions

It is possible to specify which transactions to return in the list by transaction ID.

.Query
[source, cypher, role=test-result-skip]
----
SHOW TRANSACTIONS "neo4j-transaction-3"
----

.Result
[role="queryresult",options="header,footer",cols="10*<m"]
|===
| database | transactionId | currentQueryId | connectionId | clientAddress | username | currentQuery | startTime | status | elapsedTime

| "neo4j" | "neo4j-transaction-3" | "query-1" | "" | "" | "" | "MATCH (n) RETURN n" | "2021-10-20T08:29:39.423Z" | "Running" | PT2.603S

10+d|Rows: 1
|===


[[query-terminate-transactions]]
== TERMINATE TRANSACTIONS

The `TERMINATE TRANSACTIONS` command is used to terminate running transactions by their IDs.

[NOTE]
====
The outputs for the `TERMINATE TRANSACTIONS` command there is no difference between the default output and full output (`YIELD *`), all outputs are default.
====

This command returns the following outputs:

.Terminate transactions output
[options="header", cols="4,6,2"]
|===
| Column | Description | Type

m| transactionId
a| The transaction ID.
m| STRING

m| username
a| The username of the user executing the transaction.
m| STRING

m| message
a| The result of the `TERMINATE TRANSACTION` command as applied to this transaction.
m| STRING

|===


The `TERMINATE TRANSACTIONS` command can be combined with multiple `SHOW TRANSACTIONS` and `TERMINATE TRANSACTIONS`, see xref::clauses/transaction-clauses.adoc#query-combine-tx-commands[transaction commands combination].


=== Syntax

[NOTE]
====
More details about the syntax descriptions can be found link:{neo4j-docs-base-uri}/operations-manual/{page-version}/database-administration/syntax/#administration-syntax-reading[here].
====

Terminate transactions by ID on the current server::

[source, syntax, role="noheader"]
----
TERMINATE TRANSACTION[S] transaction_id[, ...]
[YIELD { * \| field[, ...] }
  [ORDER BY field[, ...]]
  [SKIP n]
  [LIMIT n]
  [WHERE expression]
  [RETURN field[, ...] [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
]
----

The format of `transaction-id` is `<databaseName>-transaction-<id>`.
Transaction IDs must be supplied as one or more comma-separated quoted `STRING` values, or as an expression resolving to a `STRING` or a `LIST<STRING>`.

[NOTE]
====
When using the `WHERE` or `RETURN` clauses, the `YIELD` clause is mandatory and must not be omitted.
====

A user with the link:{neo4j-docs-base-uri}/operations-manual/{page-version}/authentication-authorization/database-administration/#access-control-database-administration-transaction[`TERMINATE TRANSACTION`] privilege can terminate transactions in accordance with the privilege grants.
All users may terminate their own currently executing transactions.


=== Terminate transactions

To end running transactions without waiting for them to complete on their own, use the `TERMINATE TRANSACTIONS` command.

.Query
[source, cypher, role=test-skip]
----
TERMINATE TRANSACTIONS "neo4j-transaction-1","neo4j-transaction-2"
----

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===
| transactionId | username | message

| "neo4j-transaction-1" | "neo4j" | "Transaction terminated."
| "neo4j-transaction-2" | null | "Transaction not found."

3+d|Rows: 2
|===


=== Terminate transactions with filtering on output

The output from the `TERMINATE TRANSACTIONS` command can be filtered using the `YIELD` and `WHERE` clauses.


.TERMINATE TRANSACTION YIELD
======
For example, returning the transaction IDs and message for the transactions that did not terminate.

.Query
[source, cypher, role=test-skip]
----
TERMINATE TRANSACTIONS "neo4j-transaction-1","neo4j-transaction-2"
YIELD transactionId, message
WHERE message <> "Transaction terminated."
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| transactionId | message
| "neo4j-transaction-2" | "Transaction not found."
2+d|Rows: 1
|===

======


.TERMINATE TRANSACTION error
======

In difference to `SHOW TRANSACTIONS`; the `TERMINATE TRANSACTIONS` does not allow `WHERE` without `YIELD`.

.Query
[source, cypher, role=test-fail]
----
TERMINATE TRANSACTIONS "neo4j-transaction-1","neo4j-transaction-2"
WHERE message <> "Transaction terminated."
----

.Error message
----
`WHERE` is not allowed by itself, please use `TERMINATE TRANSACTION ... YIELD ... WHERE ...`
----

======


[[query-combine-tx-commands]]
== Combining transaction commands

In difference to other show commands, the `SHOW` and `TERMINATE TRANSACTIONS` commands can be combined in the same query.

[NOTE]
====
When combining multiple commands the `YIELD` and `RETURN` clauses are mandatory and must not be omitted.
In addition, `YIELD *` is not permitted.
Instead, the `YIELD` clause needs to explicitly list the yielded columns.
====

[NOTE]
====
At this point in time, no other cypher clauses are allowed to be combined with the transaction commands.
====


=== Terminating all transactions by a given user

To terminate all transactions by a user, first find the transactions using `SHOW TRANSACTIONS`, then pass them onto `TERMINATE TRANSACTIONS`.


.TERMINATE TRANSACTIONS
======

.Query
[source, cypher, role=test-result-skip]
----
SHOW TRANSACTIONS
YIELD transactionId AS txId, username AS user
WHERE user = "Alice"
TERMINATE TRANSACTIONS txId
YIELD message
RETURN txId, message
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| txId | message
| "neo4j-transaction-1" | "Transaction terminated."
| "neo4j-transaction-2" | "Transaction terminated."
2+d|Rows: 2
|===

======


=== Terminating starving transactions

To terminate transactions that have been waiting for more than `30` minutes, first find the transactions using `SHOW TRANSACTIONS`, then pass them onto `TERMINATE TRANSACTIONS`.


.TERMINATE TRANSACTIONS
======

The following example shows a transaction that has been waiting for `40` minutes.

.Query
[source, cypher, role=test-result-skip]
----
SHOW TRANSACTIONS
YIELD transactionId, waitTime
WHERE waitTime > duration({minutes: 30})
TERMINATE TRANSACTIONS transactionId
YIELD username, message
RETURN *
----

.Result
[role="queryresult",options="header,footer",cols="4*<m"]
|===
| transactionId | waitTime | username | message
| "neo4j-transaction-1" | PT40M | "Alice" | "Transaction terminated."
4+d|Rows: 1
|===

======


=== Listing other transactions by the same user that were terminated

To list remaining transactions by users whose transactions were terminated, first terminate the transactions using `TERMINATE TRANSACTIONS`, then filter users through `SHOW TRANSACTIONS`.


.TERMINATE TRANSACTIONS
======

.Query
[source, cypher, role=test-result-skip]
----
TERMINATE TRANSACTION 'neo4j-transaction-1', 'neo4j-transaction-2'
YIELD username AS terminatedUser
SHOW TRANSACTIONS
YIELD username AS showUser, transactionId AS txId, database, currentQuery, status
WHERE showUser = terminatedUser AND NOT status STARTS WITH 'Terminated'
RETURN txId, showUser AS user, database, currentQuery
----

.Result
[role="queryresult",options="header,footer",cols="4*<m"]
|===
| txId | user | database | currentQuery
| "neo4j-transaction-3" | "Alice" | "neo4j" | "MATCH (n) RETURN n"
| "mydb-transaction-1" | "Bob" | "mydb" | "MATCH (n:Label) SET n.prop=false"
| "system-transaction-999" | "Bob" | "system" | "SHOW DATABASES"
4+d|Rows: 2
|===

======


=== Listing other transactions by the same user as a given transaction

To list other transactions by the same user as a given transaction, first find the transactions using `SHOW TRANSACTIONS`, then filter users through a second `SHOW TRANSACTIONS`.


.SHOW TRANSACTIONS
======

.Query
[source, cypher, role=test-result-skip]
----
SHOW TRANSACTION 'neo4j-transaction-1'
YIELD username AS originalUser, transactionId AS originalTxId
SHOW TRANSACTIONS
YIELD username AS newUser, transactionId AS txId, database, currentQuery, status
WHERE newUser = originalUser AND NOT txId = originalTxId
RETURN txId, newUser AS user, database, currentQuery, status
----

.Result
[role="queryresult",options="header,footer",cols="5*<m"]
|===
| txId | user | database | currentQuery | status
| "mydb-transaction-1" | "Bob" | "mydb" | "MATCH (n:Label) SET n.prop=false" | "Running"
| "system-transaction-999" | "Bob" | "system" | "SHOW DATABASES" | "Running"
5+d|Rows: 2
|===

======
