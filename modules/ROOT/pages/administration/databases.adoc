////
[source, cypher, role=test-setup]
----
CREATE DATABASE `movies`;
CREATE ALIAS `films` FOR DATABASE `movies`;
CREATE ALIAS `motion pictures` FOR DATABASE `movies`;
----
////

[[administration-databases]]
= Database management
:description: How to use Cypher to manage databases in Neo4j DBMS: creating, modifying, deleting, starting, and stopping individual databases within a single server.
:page-aliases: databases.adoc

This section explains how to use Cypher to manage databases in Neo4j DBMS.
It describes how to create, modify, delete, start, and stop individual databases within a single server.

Neo4j supports the management of multiple databases within the same DBMS.
The metadata for these databases, including the associated security model, is maintained in a special database called the `system` database.
All multi-database administrative commands must be run against the `system` database.
These administrative commands are automatically routed to the `system` database when connected to the DBMS over Bolt.

The syntax of the database management commands is as follows:

[NOTE]
====
More details about the syntax descriptions can be found xref:administration/index.adoc#administration-syntax[here].
====

.Database management command syntax
[options="header", width="100%", cols="1m,5a"]
|===
| Command | Syntax

| SHOW DATABASE
|
[source, syntax, role="noheader"]
----
SHOW { DATABASE[S] name \| DATABASE[S] \| DEFAULT DATABASE \| HOME DATABASE }
[WHERE expression]
----

[source, syntax, role="noheader"]
----
SHOW { DATABASE[S] name \| DATABASE[S] \| DEFAULT DATABASE \| HOME DATABASE }
YIELD { * \| field[, ...] } [ORDER BY field[, ...]] [SKIP n] [LIMIT n]
[WHERE expression]
[RETURN field[, ...] [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
----

| CREATE DATABASE
|
[source, syntax, role="noheader"]
----
CREATE DATABASE name [IF NOT EXISTS]
[TOPOLOGY n PRIMAR{Y\|IES} [m SECONDAR{Y\|IES}]]
[OPTIONS "{" option: value[, ...] "}"]
[WAIT [n [SEC[OND[S]]]]\|NOWAIT]
----

[source, syntax, role="noheader"]
----
CREATE OR REPLACE DATABASE name
[TOPOLOGY n PRIMAR{Y\|IES} [m SECONDAR{Y\|IES}]]
[OPTIONS "{" option: value[, ...] "}"]
[WAIT [n [SEC[OND[S]]]]\|NOWAIT]
----

| CREATE COMPOSITE DATABASE
|
[source, syntax, role="noheader"]
----
CREATE COMPOSITE DATABASE name [IF NOT EXISTS]
[OPTIONS "{" "}"]
[WAIT [n [SEC[OND[S]]]]\|NOWAIT]
----

[source, syntax, role="noheader"]
----
CREATE OR REPLACE COMPOSITE DATABASE name
[OPTIONS "{" "}"]
[WAIT [n [SEC[OND[S]]]]\|NOWAIT]
----

| ALTER DATABASE
|
[source, syntax, role="noheader"]
----
ALTER DATABASE name [IF EXISTS]
{
SET ACCESS {READ ONLY \| READ WRITE} \|
SET TOPOLOGY n PRIMAR{Y\|IES} [m SECONDAR{Y\|IES}] \|
SET OPTION option value
}
[WAIT [n [SEC[OND[S]]]]\|NOWAIT]
----

[source, syntax]
----
ALTER DATABASE name [IF EXISTS]
REMOVE OPTION option
[WAIT [n [SEC[OND[S]]]]\|NOWAIT]
----

[NOTE]
====
There can be multiple `SET OPTION` or `REMOVE OPTION` clauses for different option keys.
====

| STOP DATABASE
|
[source, syntax, role="noheader"]
----
STOP DATABASE name [WAIT [n [SEC[OND[S]]]]\|NOWAIT]
----

| START DATABASE
|
[source, syntax, role="noheader"]
----
START DATABASE name [WAIT [n [SEC[OND[S]]]]\|NOWAIT]
----

| DROP DATABASE
|
[source, syntax, role="noheader"]
----
DROP [COMPOSITE] DATABASE name [IF EXISTS] [{DUMP\|DESTROY} [DATA]] [WAIT [n [SEC[OND[S]]]]\|NOWAIT]
----

|===

[[administration-databases-show-databases]]
== Listing databases

There are four different commands for listing databases:

* Listing all databases.
* Listing a particular database.
* Listing the default database.
* Listing the home database.

These commands return the following columns:

.Listing databases output
[options="header", width="100%", cols="4m,6a,2m"]
|===
| Column | Description | Type

| name
| The name of the database. label:default-output[]
| STRING

| type
| The type of the database: `system`, `standard`, or `composite`. label:default-output[]
| STRING

| aliases
| The names of any aliases the database may have. label:default-output[]
| LIST<STRING>

| access
| The database access mode, either `read-write` or `read-only`. label:default-output[]

[NOTE]
====
A database may be described as read-only when using `ALTER DATABASE ... SET ACCESS READ ONLY`.
====
| STRING

| databaseID
| The database unique ID.
| STRING

| serverID
| The server instance ID.
| STRING

| address
|
Instance address in a clustered DBMS.
The default for a standalone database is `neo4j://localhost:7687`. label:default-output[]
| STRING

| role
| The current role of the database (`primary`, `secondary`, `unknown`). label:default-output[]
| STRING

| writer
|`true` for the instance that accepts writes for this database (this instance is the leader for this database in a cluster or this is a standalone instance). label:default-output[]
| BOOLEAN

| requestedStatus
| The expected status of the database.
The value can be either `online` or `offline`. label:default-output[]
| STRING

| currentStatus
| The actual status of the database. label:default-output[]

The value can be one of the following:

* `online` - the database is running
* `offline` - the database is not running
* `starting` - the database is not running, but is about to.
* `stopping` - the database is not running anymore, but still has not stopped completely.
Offline operations (e.g. `load`/`dump`) cannot be performed yet.
* `store copying` - the database is currently being updated from another instance of Neo4j
* For other states please refer to the link:{neo4j-docs-base-uri}/operations-manual/{page-version}/manage-databases/errors/#database-management-states[operation manual].

| STRING

| statusMessage
| A message explaining the status of the database, often explaining why it is not in the correct state. label:default-output[]
| STRING

| default
|
Show if this is the default database for the DBMS. label:default-output[]

[NOTE]
====
Not returned by `SHOW HOME DATABASE` or `SHOW DEFAULT DATABASE`.
====
| BOOLEAN

| home
|
Shown if this is the home database for the current user. label:default-output[]

[NOTE]
====
Not returned by `SHOW HOME DATABASE` or `SHOW DEFAULT DATABASE`.
====
| BOOLEAN

| `currentPrimariesCount`
| Number of primaries for this database reported as running currently.
It is the same as the number of rows where `role=primary` and `name=this database`.
| INTEGER

| `currentSecondariesCount`
| Number of secondaries for this database reported as running currently.
It is the same as the number of rows where `role=secondary` and `name=this database`.
| INTEGER

| `requestedPrimariesCount`
| The requested number of primaries for this database.
May be lower than current if the DBMS is currently reducing the number of copies of the database, or higher if it is currently increasing the number of copies.
| INTEGER

| `requestedSecondariesCount`
| The requested number of secondaries for this database.
May be lower than current if the DBMS is currently reducing the number of copies of the database, or higher if it is currently increasing the number of copies.
| INTEGER

| creationTime
| The date and time at which the database was created.
| ZONED DATETIME

| lastStartTime
| The date and time at which the database was last started.
| ZONED DATETIME

| lastStopTime
| The date and time at which the database was last stopped.
| ZONED DATETIME

| store
a|
Information about the storage engine and the store format.

The value is a string formatted as:

[source, syntax, role="noheader"]
----
{storage engine}-{store format}-{major version}.{minor version}
----
| STRING

| lastCommittedTxn
| The ID of the last transaction received.
| INTEGER

| replicationLag
|
Number of transactions the current database is behind compared to the database on the primary instance.
The lag is expressed in negative integers. In standalone environments, the value is always `0`.
| INTEGER

|constituents
|The names of any constituents the database may have. label:default-output[]
| LIST<STRING>

|options
|The map of options applied to the database.
| MAP

|===


.+SHOW DATABASES+
======

A summary of all available databases can be displayed using the command `SHOW DATABASES`.

.Query
[source, cypher]
----
SHOW DATABASES
----

.Result
[role="queryresult",options="header,footer",cols="13*<m"]
|===

| +name+ | +type+ | +aliases+ | +access+ | +address+ | +role+ | +writer+ | +requestedStatus+ | +currentStatus+ | +statusMessage+ | +default+ | +home+ | +constituents+
| +"movies"+ | +"standard"+ | +["films","motion pictures"]+ | +"read-write"+ | +"localhost:7687"+ | +"primary"+ | +true+ | +"online"+ | +"online"+ | +""+ | +false+ | +false+ | +[]+
| +"neo4j"+ | +"standard"+ | +[]+ | +"read-write"+ | +"localhost:7687"+ | +"primary"+ | +true+ | +"online"+ | +"online"+ | +""+ | +true+ | +true+ | +[]+
| +"system"+ | +"system"+ | +[]+ | +"read-write"+ | +"localhost:7687"+ | +"primary"+ | +true+ | +"online"+ | +"online"+ | +""+ | +false+ | +false+ | +[]+
13+d|Rows: 3

|===


[NOTE]
====
The results of this command are filtered according to the `ACCESS` privileges of the user.
However, some privileges enable users to see additional databases regardless of their `ACCESS` privileges:

* Users with `CREATE/DROP/ALTER DATABASE` or `SET DATABASE ACCESS` privileges can see all standard databases.
* Users with `CREATE/DROP COMPOSITE DATABASE` or `COMPOSITE DATABASE MANAGEMENT` privileges can see all composite databases.
* Users with `DATABASE MANAGEMENT` privilege can see all databases.

If a user has not been granted `ACCESS` privilege to any databases nor any of the above special cases, the command can still be executed but will only return the `system` database, which is always visible.
====
[NOTE]
====
As of Neo4j 5.3, databases hosted on servers that are offline are also returned by the `SHOW DATABASES` command.
For such databases, the `address` column displays `NULL`, the `currentStatus` column displays `unknown`, and the `statusMessage` displays `Server is unavailable`.
====

======


.+SHOW DATABASES+
======

In this example, the detailed information for a particular database can be displayed using the command `SHOW DATABASE name YIELD *`.
When a `YIELD` clause is provided, the full set of columns is returned.

.Query
[source, cypher, role=test-result-skip]
----
SHOW DATABASE movies YIELD *
----

// Limited result set.
// The output has been capped.

.Result
[role="queryresult",options="header,footer",cols="7*<m"]
|===
| +name+ | +aliases+ | +access+ | +databaseID+ | +serverID+ | +address+ | +...+
| +"movies"+ | +["films","motion pictures"]+ | +"read-write"+ | +"367221F9021C00CEBFCA25C8E2101F1DCF45C7DB9BF7D7A0949B87745E760EDD"+ | +"adc0a7bc-d9a6-4cc8-b394-91635fbb1137"+ | +"localhost:7687"+ | +...+
7+d|Rows: 1
|===

======


.+SHOW DATABASES+
======

The number of databases can be seen using a `count()` aggregation with `YIELD` and `RETURN`.

.Query
[source, cypher]
----
SHOW DATABASES YIELD *
RETURN count(*) AS count
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +count+
| +3+
1+d|Rows: 1

|===

======


.+SHOW DEFAULT DATABASE+
======

The default database can be seen using the command `SHOW DEFAULT DATABASE`.

.Query
[source, cypher]
----
SHOW DEFAULT DATABASE
----

.Result
[role="queryresult",options="header,footer",cols="11*<m"]
|===

| +name+ | +type+ | +aliases+ | +access+ | +address+ | +role+ | +writer+ | +requestedStatus+ | +currentStatus+ | +statusMessage+ | +constituents+
| +"neo4j"+ | +"standard"+ | +[]+ | +"read-write"+ | +"localhost:7687"+ | +"primary"+ | +true+ | +"online"+ | +"online"+ | +""+ | +[]+
11+d|Rows: 1

|===

======


.+SHOW HOME DATABASE+
======

The home database for the current user can be seen using the command `SHOW HOME DATABASE`.

.Query
[source, cypher]
----
SHOW HOME DATABASE
----

.Result
[role="queryresult",options="header,footer",cols="11*<m"]
|===

| +name+ | +type+ | +aliases+ | +access+ | +address+ | +role+ | +writer+ | +requestedStatus+ | +currentStatus+ | +statusMessage+ | +constituents+
| +"neo4j"+ | +"standard"+ | +[]+ | +"read-write"+ | +"localhost:7687"+ | +"primary"+ | +true+ | +"online"+ | +"online"+ | +""+ | +[]+
11+d|Rows: 1

|===

======


.+SHOW DATABASES+
======

It is also possible to filter and sort the results by using `YIELD`, `ORDER BY`, and `WHERE`.

.Query
[source, cypher]
----
SHOW DATABASES YIELD name, currentStatus, requestedStatus
ORDER BY currentStatus
WHERE name CONTAINS 'o'
----

In this example:

* The number of columns returned has been reduced with the `YIELD` clause.
* The order of the returned columns has been changed.
* The results have been filtered to only show database names containing `'o'`.
* The results are ordered by the `currentStatus` column using `ORDER BY`.

It is also possible to use `SKIP` and `LIMIT` to paginate the results.


.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===

| +name+ | +currentStatus+ | +requestedStatus+
| +"movies"+ | +"online"+ | +"online"+
| +"neo4j"+ | +"online"+ | +"online"+
3+d|Rows: 2

|===


[NOTE]
====
Note that for failed databases, the `currentStatus` and `requestedStatus` are different.
This often implies an error, but **does not always**.
For example, a database may take a while to transition from `offline` to `online` due to performing recovery.
Or, during normal operation a database's `currentStatus` may be transiently different from its `requestedStatus` due to a necessary automatic process, such as one Neo4j instance copying store files from another.
The possible statuses are `initial`, `online`, `offline`, `store copying` and `unknown`.
====

For composite databases the `constituents` column is particularly interesting as it lists the aliases that make up the composite database:

////
[source, cypher, role=test-setup]
----
CREATE COMPOSITE DATABASE `library`;
CREATE DATABASE `sci-fi`;
CREATE DATABASE `romance`;
CREATE ALIAS `library`.`sci-fi` FOR DATABASE `sci-fi`;
CREATE ALIAS `library`.`romance` FOR DATABASE `romance`;
----
////

.Query
[source, cypher]
----
SHOW DATABASE library YIELD name, constituents
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +name+ | +constituents+
| +"library"+ | +["library.sci-fi","library.romance"]+
2+d|Rows: 1
|===

======


[role=enterprise-edition not-on-aura]
[[administration-databases-create-database]]
== Creating databases

Databases can be created using `CREATE DATABASE`.

Database names are subject to the xref::syntax/naming.adoc[standard Cypher restrictions on valid identifiers].

The following naming rules apply:

* Database name length must be between 3 and 63 characters.
* The first character must be an ASCII alphabetic character.
* Subsequent characters can be ASCII alphabetic (`mydatabase`), numeric characters (`mydatabase2`), dots (`main.db`), and dashes (enclosed within backticks, e.g., `CREATE DATABASE ++`main-db`++`).
Using database names with dots without enclosing them in backticks is deprecated.
* Names cannot end with dots or dashes.
* Names that begin with an underscore or with the prefix `system` are reserved for internal use.

[NOTE]
====
Having dots (`.`) in the database names is not recommended.
This is due to the difficulty of determining if a dot is part of the database name or a delimiter for a database alias in a composite database.
====

.+CREATE DATABASE+
======

.Query
[source, cypher]
----
CREATE DATABASE customers
----

.Result
[source, result, role="noheader"]
----
System updates: 1
Rows: 0
----

======


.+SHOW DATABASES+
======

When a database has been created, it will show up in the listing provided by the command `SHOW DATABASES`.

.Query
[source, cypher]
----
SHOW DATABASES YIELD name
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +name+
| +"customers"+
| +"library"+
| +"movies"+
| +"neo4j"+
| +"romance"+
| +"sci-fi"+
| +"system"+
1+d|Rows: 7

|===

======


[role=enterprise-edition not-on-aura]
[[administration-databases-create-database-topology]]
=== Cluster topology

In a cluster environment, it may be desirable to control the number of servers used to host a database.
The number of primary and secondary servers can be specified using the following command.

.Query
[source, cypher]
----
CREATE DATABASE `topology-example` TOPOLOGY 1 PRIMARY 0 SECONDARIES
----

For more details on primary and secondary server roles, see link:{neo4j-docs-base-uri}/operations-manual/{page-version}/clustering#clustering-introduction-operational[Cluster overview].

[NOTE]
====
`TOPOLOGY` is only available for standard databases and not composite databases.
Composite databases are always available on all servers.
====

[NOTE]
====
If `TOPOLOGY` is not specified, the database is created according to `initial.dbms.default_primaries_count` and `initial.dbms.default_secondaries_count` specified in _neo4j.conf_.

Keep in mind that any setting with the `initial` prefix only applies on **first startup** and after this, these values can be overwritten using the `dbms.setDefaultAllocationNumbers` procedure.
====

[role=enterprise-edition not-on-aura]
[[administration-databases-create-composite-database]]
=== Creating composite databases

Composite databases do not contain data, but they reference to other databases that can be queried together through their constituent aliases.
For more information about composite databases, see link:{neo4j-docs-base-uri}/operations-manual/{page-version}/composite-databases/introduction[Operations Manual -> Composite database introduction].

Composite databases can be created using `CREATE COMPOSITE DATABASE`.

Composite database names are subject to the same rules as xref:administration-databases-create-database[standard databases].
One difference is however that the deprecated syntax using dots without enclosing the name in backticks is not available.
Both dots and dashes need to be enclosed within backticks when using composite databases.

[NOTE]
====
Having dots (`.`) in the composite database names is not recommended.
This is due to the difficulty of determining if a dot is part of the composite database name or a delimiter for a database alias in a composite database.
====

.Query
[source, cypher]
----
CREATE COMPOSITE DATABASE inventory
----

[role="statsonlyqueryresult"]
0 rows, System updates: 1

When a composite database has been created, it will show up in the listing provided by the command `SHOW DATABASES`.


.Query
[source, cypher]
----
SHOW DATABASES YIELD name, type, access, role, writer, constituents
----

.Result
[role="queryresult",options="header,footer",cols="6*<m"]
|===
| +name+ | +type+ | +access+ | +role+ | +writer+ | +constituents+
| +"customers"+ | +"standard"+ | +"read-write"+ | +"primary"+ | +true+ | +[]+
| +"inventory"+ | +"composite"+ | +"read-only"+ | +<null>+ | +false+ | +[]+
| +"library"+ | +"composite"+ | +"read-only"+ | +<null>+ | +false+ | +["library.sci-fi","library.romance"]+
| +"movies"+ | +"standard"+ | +"read-write"+ | +"primary"+ | +true+ | +[]+
| +"neo4j"+ | +"standard"+ | +"read-write"+ | +"primary"+ | +true+ | +[]+
| +"romance"+ | +"standard"+ | +"read-write"+ | +"primary"+ | +true+ | +[]+
| +"sci-fi"+ | +"standard"+ | +"read-write"+ | +"primary"+ | +true+ | +[]+
| +"system"+ | +"system"+ | +"read-write"+ | +"primary"+ | +true+ | +[]+
| +"topology-example"+ | +"standard"+ | +"read-write"+ | +"primary"+ | +true+ | +[]+
6+d|Rows: 8
|===

In order to create database aliases in the composite database, give the composite database as namespace for the alias.
For information about creating aliases in composite databases, see xref:administration/aliases.adoc#alias-management-create-composite-database-alias[here].


[role=enterprise-edition not-on-aura]
[[administration-databases-create-database-existing]]
=== Handling Existing Databases

These commands are optionally idempotent, with the default behavior to fail with an error if the database already exists.
Appending `IF NOT EXISTS` to the command ensures that no error is returned and nothing happens should the database already exist.
Adding `OR REPLACE` to the command will result in any existing database being deleted and a new one created.

These behavior flags apply to both standard and composite databases (e.g. a composite database may replace a standard one or another composite.)


.+CREATE DATABASE+
======

.Query
[source, cypher]
----
CREATE COMPOSITE DATABASE customers IF NOT EXISTS
----


======


.+CREATE OR REPLACE DATABASE+
======

.Query
[source, cypher]
----
CREATE OR REPLACE DATABASE customers
----

This is equivalent to running `DROP DATABASE customers IF EXISTS` followed by `CREATE DATABASE customers`.

[NOTE]
====
The `IF NOT EXISTS` and `OR REPLACE` parts of these commands cannot be used together.
====

======


[role=enterprise-edition not-on-aura]
[[administration-databases-create-database-options]]
=== Options

The `CREATE DATABASE` command can have a map of options, e.g. `OPTIONS {key: 'value'}`.

[NOTE]
====
There are no available `OPTIONS` values for composite databases.
====


[options="header"]
|===

| Key | Value | Description

| `existingData`
| `use`
|
Controls how the system handles existing data on disk when creating the database.
Currently this is only supported with `existingDataSeedInstance` and must be set to `use` which indicates the existing data files should be used for the new database.

| `existingDataSeedInstance`
| ID of the cluster server
|
Defines which server is used for seeding the data of the created database.
The server ID can be found in the `serverId` column after running `SHOW SERVERS`.

| `seedURI`
| URI to a backup or a dump from an existing database.
|
Defines an identical seed from an external source which will be used to seed all servers.

| `seedConfig`
| comma separated list of configuration values.
|
Defines additional configuration specified by comma separated `name=value` pairs that might be required by certain seed providers.

| `seedCredentials`
| credentials
|
Defines credentials that needs to be passed into certain seed providers.

| `txLogEnrichment`
| `FULL` \| `DIFF` \| `NONE`
|
Defines level of enrichment applied to transaction logs for Change Data Capture (CDC) purposes.
For details about enrichment mode, see link:{neo4j-docs-base-uri}/cdc/{page-version}/getting-started/enrichment-mode[Change Data Capture Manual -> Getting Started -> Enrichment mode].

|===


[NOTE]
====
The `existingData`, `existingDataSeedInstance`, `seedURI`, `seedConfig` and `seedCredentials` options cannot be combined with the `OR REPLACE` part of this command.
For details about the use of these seeding options, see link:{neo4j-docs-base-uri}/operations-manual/{page-version}/clustering/databases/#cluster-seed[Operations Manual -> Seed a cluster].
====


[role=enterprise-edition not-on-aura]
[[administration-databases-alter-database]]
== Altering databases

Standard databases can be modified using the command `ALTER DATABASE`.

[role=enterprise-edition not-on-aura]
[[administration-databases-alter-database-access]]
=== Access

By default, a database has read-write access mode on creation.
The database can be limited to read-only mode on creation using the configuration parameters `dbms.databases.default_to_read_only`, `dbms.databases.read_only`, and `dbms.database.writable`.
For details, see link:{neo4j-docs-base-uri}/operations-manual/{page-version}/manage-databases/configuration#manage_database_parameters[Configuration parameters].

A database that was created with read-write access mode can be changed to read-only.
To change it to read-only, you can use the `ALTER DATABASE` command with the sub-clause `SET ACCESS READ ONLY`.
Subsequently, the database access mode can be switched back to read-write using the sub-clause `SET ACCESS READ WRITE`.
Altering the database access mode is allowed at all times, whether a database is online or offline.

If conflicting modes are set by the `ALTER DATABASE` command and the configuration parameters, i.e. one says read-write and the other read-only, the database will be read-only and prevent write queries.

[NOTE]
====
Modifying access mode is only available to standard databases and not composite databases.
====


.+ALTER DATABASE+
======

.Query
[source, cypher]
----
ALTER DATABASE customers SET ACCESS READ ONLY
----

.Result
[source, result, role="noheader"]
----
System updates: 1
Rows: 0
----

======


.+SHOW DATABASES+
======

The database access mode can be seen in the `access` output column of the command `SHOW DATABASES`.

.Query
[source, cypher]
----
SHOW DATABASES yield name, access
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +name+ | +access+
| +"customers"+ | +"read-only"+
| +"inventory"+ | +"read-only"+
| +"library"+ | +"read-only"+
| +"movies"+ | +"read-write"+
| +"neo4j"+ | +"read-write"+
| +"romance"+ | +"read-write"+
| +"sci-fi"+ | +"read-write"+
| +"system"+ | +"read-write"+
| +"topology-example"+ | +"read-write"+
2+d|Rows: 9
|===

======


.+ALTER DATABASE+
======

`ALTER DATABASE` commands are optionally idempotent, with the default behavior to fail with an error if the database does not exist.
Appending `IF EXISTS` to the command ensures that no error is returned and nothing happens should the database not exist.

.Query
[source, cypher]
----
ALTER DATABASE nonExisting IF EXISTS
SET ACCESS READ WRITE
----

======

[role=enterprise-edition not-on-aura]
[[administration-databases-alter-database-topology]]
=== Topology

In a cluster environment, it may be desirable to change the number of servers used to host a database.
The number of primary and secondary servers can be specified using the following command:

.+ALTER DATABASE+
======

// can't test: can't go from 1 primary to multiple primaries, and cannot start with many primaries since we don't have a cluster

.Query
[source, cypher, test-skip]
----
ALTER DATABASE `topology-example`
SET TOPOLOGY 3 PRIMARY 0 SECONDARIES
----

======

[NOTE]
====
It is not possible to automatically transition *from* a topology with _multiple_ primary hosts *to* a topology with a _single_ primary host, but it is possible to increase the number of primaries from one to more.
See the link:{neo4j-docs-base-uri}/operations-manual/{page-version}/clustering/databases#_alter_topology[Operations Manual -> Alter topology] for more information.
====


.+SHOW DATABASE+
======

.Query
[source, cypher]
----
SHOW DATABASES yield name, currentPrimariesCount, currentSecondariesCount, requestedPrimariesCount, requestedSecondariesCount
----

======

For more details on primary and secondary server roles, see link:{neo4j-docs-base-uri}/operations-manual/{page-version}/clustering/introduction#clustering-introduction-operational[Operations Manual -> Clustering overview].

[NOTE]
====
Modifying database topology is only available to standard databases and not composite databases.
====

`ALTER DATABASE` commands are optionally idempotent, with the default behavior to fail with an error if the database does not exist.
Appending `IF EXISTS` to the command ensures that no error is returned and nothing happens should the database not exist.

.Query
[source, cypher]
----
ALTER DATABASE nonExisting IF EXISTS SET TOPOLOGY 1 PRIMARY 0 SECONDARY
----

[role="statsonlyqueryresult"]
0 rows

[role=enterprise-edition]
[[administration-databases-alter-database-options]]
=== Options

////
[source, cypher, role=test-setup]
----
CREATE DATABASE `options-example`;
----
////

The `ALTER DATABASE` command can be used to set or remove specific options, which are listed below.

[options="header"]
|===

| Key | Value | Description

| `txLogEnrichment`
| `FULL` \| `DIFF` \| `NONE`
|
Defines level of enrichment applied to transaction logs for Change Data Capture (CDC) purposes.
For details about enrichment mode, see link:{neo4j-docs-base-uri}/cdc/{page-version}/getting-started/enrichment-mode[Change Data Capture Manual -> Getting Started -> Enrichment mode].

|===

[NOTE]
====
There are no available `OPTIONS` values for composite databases.
====

.+ALTER DATABASE SET OPTION+
======

// skip test until we make the change_data_capture feature flag on by default.

.Query
[source, cypher, role="test-skip"]
----
ALTER DATABASE `options-example`
SET OPTION txLogEnrichment 'FULL'
----

======

.+SHOW DATABASE+
======

// skip test until we make the change_data_capture feature flag on by default.

.Query
[source, cypher, role="test-skip"]
----
SHOW DATABASES yield name, options
----

// Limited result set.
// The output has been capped.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +name+ | +options+
| +"options-example"+ | +{txLogEnrichment: "FULL"}+
| +"neo4j"+ | +{}+
| +"system"+ | +{}+
2+d|Rows: 3
|===

======

.+ALTER DATABASE REMOVE OPTION+
======

// skip test until we make the change_data_capture feature flag on by default.

.Query
[source, cypher, role="test-skip"]
----
ALTER DATABASE `options-example`
REMOVE OPTION txLogEnrichment
----

======


.+SHOW DATABASE+
======

// skip test until we make the change_data_capture feature flag on by default.

.Query
[source, cypher, role="test-skip"]
----
SHOW DATABASES YIELD name, options
----

// Limited result set.
// The output has been capped.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +name+ | +options+
| +"options-example"+ | +{}+
| +"neo4j"+ | +{}+
| +"system"+ | +{}+
2+d|Rows: 3
|===

======


[role=enterprise-edition not-on-aura]
[[administration-databases-stop-database]]
== Stopping databases

Databases can be stopped using the command `STOP DATABASE`.


.+STOP DATABASE+
======

.Query
[source, cypher]
----
STOP DATABASE customers
----

.Result
[source, result, role="noheader"]
----
System updates: 1
Rows: 0
----

[NOTE]
====
Both standard databases and composite databases can be stopped using this command.
====

======


.+SHOW DATABASE+
======

The status of the stopped database can be seen using the command `SHOW DATABASE name`.

.Query
[source, cypher]
----
SHOW DATABASE customers YIELD name, requestedStatus, currentStatus
----

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===
| +name+ | +requestedStatus+ | +currentStatus+
| +"customers"+ | +"offline"+ | +"offline"+
3+d|Rows: 1
|===

======


[role=enterprise-edition not-on-aura]
[[administration-databases-start-database]]
== Starting databases

Databases can be started using the command `START DATABASE`.


.+START DATABASE+
======

.Query
[source, cypher]
----
START DATABASE customers
----

.Result
[source, result, role="noheader"]
----
System updates: 1
Rows: 0
----

[NOTE]
====
Both standard databases and composite databases can be stopped using this command.
====

======


.+SHOW DATABASE+
======

The status of the started database can be seen using the command `SHOW DATABASE name`.

.Query
[source, cypher]
----
SHOW DATABASE customers YIELD name, requestedStatus, currentStatus
----

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===
| +name+ | +requestedStatus+ | +currentStatus+
| +"customers"+ | +"online"+ | +"online"+
3+d|Rows: 1
|===

======


[role=enterprise-edition not-on-aura]
[[administration-databases-drop-database]]
== Deleting databases

Standard and composite databases can be deleted by using the command `DROP DATABASE`.


.+DROP DATABASE+
======

.Query
[source, cypher]
----
DROP DATABASE customers
----

.Result
[source, result, role="noheader"]
----
System updates: 1
Rows: 0
----

It is also possible to ensure that only composite databases are dropped. A `DROP COMPOSITE` request would then fail if the targeted database is a standard database.

======


.+SHOW DATABASES+
======

When a database has been deleted, it will no longer show up in the listing provided by the command `SHOW DATABASES`.

.Query
[source, cypher]
----
SHOW DATABASES YIELD name
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +name+
| +"inventory"+
| +"library"+
| +"movies"+
| +"neo4j"+
| +"options-example"+
| +"romance"+
| +"sci-fi"+
| +"system"+
| +"topology-example"+
13+d|Rows: 9

|===

======


.+DROP DATABASE+
======

This command is optionally idempotent, with the default behavior to fail with an error if the database does not exist.
Appending `IF EXISTS` to the command ensures that no error is returned and nothing happens should the database not exist.
It will always return an error, if there is an existing alias that targets the database. In that case, the alias needs to be dropped before dropping the database.

.Query
[source, cypher]
----
DROP DATABASE customers IF EXISTS
----

The `DROP DATABASE` command will remove a database entirely.

======


.+DROP DATABASE+
======

You can request that a dump of the store files is produced first, and stored in the path configured using the `dbms.directories.dumps.root` setting (by default `<neo4j-home>/data/dumps`).
This can be achieved by appending `DUMP DATA` to the command (or `DESTROY DATA` to explicitly request the default behavior).
These dumps are equivalent to those produced by `neo4j-admin dump` and can be similarly restored using `neo4j-admin load`.

.Query
[source, cypher]
----
DROP DATABASE `topology-example` DUMP DATA
----

The options `IF EXISTS` and  `DUMP DATA`/ `DESTROY DATA` can also be combined.
An example could look like this:

.Query
[source, cypher]
----
DROP DATABASE customers IF EXISTS DUMP DATA
----

======

It is also possible to ensure that only composite databases are dropped. A `DROP COMPOSITE` request would then fail if the targeted database is a standard database.

.+DROP COMPOSITE DATABASE+
======

.Query
[source, cypher]
----
DROP COMPOSITE DATABASE inventory
----

[role="statsonlyqueryresult"]
0 rows, System updates: 1

To ensure the database to be dropped is standard and not composite, the user first needs to check the `type` column of `SHOW DATABASES` manually.

======


[role=enterprise-edition not-on-aura]
[[administration-wait-nowait]]
== Wait options

_The_ `WAIT` _subclause was added as an option to the_ `ALTER DATABASE` _command in Neo4j 5.7._

Aside from `SHOW DATABASES`, all database management commands accept an optional `WAIT`/`NOWAIT` clause.
The `WAIT`/`NOWAIT` clause allows you to specify a time limit in which the command must complete and return.

The options are:

* `WAIT n SECONDS` - Return once completed or when the specified time limit of `n` seconds is up.
* `WAIT` - Return once completed or when the default time limit of 300 seconds is up.
* `NOWAIT` - Return immediately.

A command using a `WAIT` clause will automatically commit the current transaction when it executes successfully, as the command needs to run immediately for it to be possible to `WAIT` for it to complete.
Any subsequent commands executed will therefore be performed in a new transaction.
This is different to the usual transactional behavior, and for this reason it is recommended that these commands be run in their own transaction.
The default behavior is `NOWAIT`, so if no clause is specified the transaction will behave normally and the action is performed in the background post-commit.

[NOTE]
====
A command with a `WAIT` clause may be interrupted whilst it is waiting to complete.
In this event the command will continue to execute in the background and will not be aborted.
====


.+CREATE DATABASE+
======

.Query
[source, cypher]
----
CREATE DATABASE slow WAIT 5 SECONDS
----

.Result
[role="queryresult",options="header,footer",cols="4*<m"]
|===
| +address+ | +state+ | +message+ | +success+
| +"localhost:7687"+ | +"CaughtUp"+ | +"caught up"+ | +true+
4+d|Rows: 1
|===

The `success` column provides an aggregate status of whether or not the command is considered successful and thus every row will have the same value.
The intention of this column is to make it easy to determine, for example in a script, whether or not the command completed successfully without timing out.

A command with a `WAIT` clause may be interrupted whilst it is waiting to complete.
In this event the command will continue to execute in the background and will not be aborted.

======
