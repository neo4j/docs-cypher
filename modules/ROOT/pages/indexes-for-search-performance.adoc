:description: This section explains how to manage indexes used for search performance.

[[indexes-search-performance]]
= Indexes for search performance

[abstract]
--
This section explains how to manage indexes used for search performance.
--

For query performance purposes, it is important to also understand how the indexes are used by the Cypher planner.
Refer to xref::query-tuning/index.adoc[] for examples and in-depth discussions on how query plans result from different index and query scenarios.
See specifically xref::query-tuning/indexes.adoc[The use of indexes] for examples of how various index scenarios result in different query plans.

For information on index configuration and limitations, refer to link:{neo4j-docs-base-uri}/operations-manual/{page-version}/performance/index-configuration[Operations Manual -> Index configuration].


[[indexes-types-and-limitations]]
== Indexes (types and limitations)

A database index is a redundant copy of some of the data in the database for the purpose of making searches of related data more efficient.
This comes at the cost of additional storage space and slower writes, so deciding what to index and what not to index is an important and often non-trivial task.

Once an index has been created, it will be managed and kept up to date by the DBMS.
Neo4j will automatically pick up and start using the index once it has been created and brought online.

There are multiple index types available:

* Range index.
* Lookup index.
* Text index.
* Point index.
* Full-text index.

See xref::indexes-for-full-text-search.adoc[Full-text search index] for more information about full-text indexes.
Lookup indexes contain nodes with one or more labels or relationship types, without regard for any properties.

Cypher enables the creation of range indexes on one or more properties for all nodes or relationships with a given label or relationship type:

* An index created on a single property for any given label or relationship type is called a _single-property index_.
* An index created on more than one property for any given label or relationship type is called a _composite index_.

Differences in the usage patterns between composite and single-property indexes are described in xref::indexes-for-search-performance.adoc#indexes-single-vs-composite-index[].

Additionally, text and point indexes are a kind of single-property indexes, with the limitation that they only recognize properties with string and point values, respectively.
Nodes or relationships with the indexed label or relationship type where the indexed property is of another value type are not included in the index.

The following is true for indexes:

* Best practice is to give the index a name when it is created.
If the index is not explicitly named, it gets an auto-generated name.
* The index name must be unique among both indexes and constraints.
* Index creation is by default not idempotent, and an error will be thrown if you attempt to create the same index twice.
Using the keyword `IF NOT EXISTS` makes the command idempotent, and no error will be thrown if you attempt to create the same index twice.


[[indexes-syntax]]
== Syntax

[IMPORTANT]
====
The index name must be unique among both indexes and constraints.
====

[NOTE]
====
Best practice is to give the index a name when it is created.
If the index is not explicitly named, it gets an auto-generated name.
====

[NOTE]
====
The `+CREATE ... INDEX ...+` command is optionally idempotent. This mean that its default behavior is to throw an error if an attempt is made to create the same index twice.
With `IF NOT EXISTS`, no error is thrown and nothing happens should an index with the same name or same schema and index type already exist.
It may still throw an error if conflicting constraints exist, such as constraints with the same name or schema and backing index type.
====

[NOTE]
====
More details about the syntax descriptions can be found xref:administration/index.adoc#administration-syntax[here].
====


.+Create a range index on nodes+
[options="noheader", width="100%", cols="2, 8a"]
|===

| Syntax
|
[source, syntax, role="noheader"]
----
CREATE [RANGE] INDEX [index_name] [IF NOT EXISTS]
FOR (n:LabelName)
ON (n.propertyName_1[,
    n.propertyName_2,
    ...
    n.propertyName_n])
[OPTIONS "{" option: value[, ...] "}"]
----

| Description
|
Create a range index on nodes, either on a single property or composite.

Index provider can be specified using the `OPTIONS` clause.

|===


.+Create a range index on relationships+
[options="noheader", width="100%", cols="2, 8a"]
|===

| Syntax
|
[source, syntax, role="noheader"]
----
CREATE [RANGE] INDEX [index_name] [IF NOT EXISTS]
FOR ()-"["r:TYPE_NAME"]"-()
ON (r.propertyName_1[,
    r.propertyName_2,
    ...
    r.propertyName_n])
[OPTIONS "{" option: value[, ...] "}"]
----

| Description
|
Create a range index on relationships, either on a single property or composite.

Index provider can be specified using the `OPTIONS` clause.

|===


.+Create a node label lookup index+
[options="noheader", width="100%", cols="2, 8a"]
|===

| Syntax
|
[source, syntax, role="noheader"]
----
CREATE LOOKUP INDEX [index_name] [IF NOT EXISTS]
FOR (n)
ON EACH labels(n)
[OPTIONS "{" option: value[, ...] "}"]
----

| Description
|
Create a node label lookup index.

Index provider can be specified using the `OPTIONS` clause.

|===


.+Create a relationship type lookup index+
[options="noheader", width="100%", cols="2, 8a"]
|===

| Syntax
|
[source, syntax, role="noheader"]
----
CREATE LOOKUP INDEX [index_name] [IF NOT EXISTS]
FOR ()-"["r"]"-()
ON [EACH] type(r)
[OPTIONS "{" option: value[, ...] "}"]
----

| Description
|
Create a relationship type lookup index.

Index provider can be specified using the `OPTIONS` clause.

|===


.+Create a text index on nodes+
[options="noheader", width="100%", cols="2, 8a"]
|===

| Syntax
|
[source, syntax, role="noheader"]
----
CREATE TEXT INDEX [index_name] [IF NOT EXISTS]
FOR (n:LabelName)
ON (n.propertyName)
[OPTIONS "{" option: value[, ...] "}"]
----

| Description
|
Create a text index on nodes where the property has a string value.

Index provider can be specified using the `OPTIONS` clause.

|===


.+Create a text index on relationships+
[options="noheader", width="100%", cols="2, 8a"]
|===

| Syntax
|
[source, syntax, role="noheader"]
----
CREATE TEXT INDEX [index_name] [IF NOT EXISTS]
FOR ()-"["r:TYPE_NAME"]"-()
ON (r.propertyName)
[OPTIONS "{" option: value[, ...] "}"]
----

| Description
|
Create a text index on relationships where the property has a string value.

Index provider can be specified using the `OPTIONS` clause.

|===


.+Create a point index on nodes+
[options="noheader", width="100%", cols="2, 8a"]
|===

| Syntax
|
[source, syntax, role="noheader"]
----
CREATE POINT INDEX [index_name] [IF NOT EXISTS]
FOR (n:LabelName)
ON (n.propertyName)
[OPTIONS "{" option: value[, ...] "}"]
----

| Description
|
Create a point index on nodes where the property has a point value.

Index provider and configuration can be specified using the `OPTIONS` clause.

|===


.+Create a point index on relationships+
[options="noheader", width="100%", cols="2, 8a"]
|===

| Syntax
|
[source, syntax, role="noheader"]
----
CREATE POINT INDEX [index_name] [IF NOT EXISTS]
FOR ()-"["r:TYPE_NAME"]"-()
ON (r.propertyName)
[OPTIONS "{" option: value[, ...] "}"]
----

| Description
|
Create a point index on relationships where the property has a point value.

Index provider and configuration can be specified using the `OPTIONS` clause.

|===


.+Drop an index+
[options="noheader", width="100%", cols="2, 8a"]
|===

| Syntax
|
[source, syntax, role="noheader"]
----
DROP INDEX index_name [IF EXISTS]
----

| Description
| Drop an index of any index type.

| Note
|
The command is optionally idempotent. This means that its default behavior is to throw an error if an attempt is made to drop the same index twice.
With `IF EXISTS`, no error is thrown and nothing happens should the index not exist.

|===


.List indexes
[options="noheader", width="100%", cols="2, 8a"]
|===

| Syntax
|
[source, syntax, role="noheader"]
----
SHOW [ALL \| FULLTEXT \| LOOKUP \| POINT \| RANGE \| TEXT] INDEX[ES]
  [YIELD { * \| field[, ...] } [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
  [WHERE expression]
  [RETURN field[, ...] [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
----

| Description
| List indexes in the database, either all or filtered on index type.

| Note
| When using the `RETURN` clause, the `YIELD` clause is mandatory and must not be omitted.

|===


Creating an index requires xref::administration/access-control/database-administration.adoc#access-control-database-administration-index[the `CREATE INDEX` privilege],
while dropping an index requires xref::administration/access-control/database-administration.adoc#access-control-database-administration-index[the `DROP INDEX` privilege] and
listing indexes require xref::administration/access-control/database-administration.adoc#access-control-database-administration-index[the `SHOW INDEX` privilege].

xref:query-tuning/using.adoc[Planner hints and the USING keyword] describes how to make the Cypher planner use specific indexes (especially in cases where the planner would not necessarily have used them).


[[indexes-single-vs-composite-index]]
== Composite index limitations

Like single-property range indexes, composite range indexes support all predicates:

* equality check: `n.prop = value`
* list membership check: `n.prop IN list`
* existence check: `n.prop IS NOT NULL`
* range search: `n.prop > value`
* prefix search: `STARTS WITH`

[NOTE]
====
For details about each operator, see xref::syntax/operators.adoc[Operators].
====

However, predicates might be planned as existence check and a filter.
For most predicates, this can be avoided by following these restrictions:

* If there is any `equality check` and `list membership check` predicates,
they need to be for the first properties defined by the index.
* There can be up to one `range search` or `prefix search` predicate.
* There can be any number of `existence check` predicates.
* Any predicate after a `range search`, `prefix search` or `existence check` predicate has to be an `existence check` predicate.

[NOTE]
====
The `suffix search` (`ENDS WITH`) and `substring search` (`CONTAINS`) predicates can utilize the index as well.
However, they are always planned as an existence check and a filter and any predicates following after will therefore also be planned as such.
====

For example, an index on nodes with `:Label(prop1,prop2,prop3,prop4,prop5,prop6)` and predicates:

[source, cypher, role=test-skip]
----
WHERE n.prop1 = 'x' AND n.prop2 = 1 AND n.prop3 > 5 AND n.prop4 < 'e' AND n.prop5 = true AND n.prop6 IS NOT NULL
----

will be planned as:

[source, cypher, role=test-skip]
----
WHERE n.prop1 = 'x' AND n.prop2 = 1 AND n.prop3 > 5 AND n.prop4 IS NOT NULL AND n.prop5 IS NOT NULL AND n.prop6 IS NOT NULL
----

with filters on `n.prop4 < 'e'` and `n.prop5 = true`, since `n.prop3` has a `range search` predicate.

And an index on nodes with `:Label(prop1,prop2)` with predicates:

[source, cypher, role=test-skip]
----
WHERE n.prop1 ENDS WITH 'x' AND n.prop2 = false
----

will be planned as:

[source, cypher, role=test-skip]
----
WHERE n.prop1 IS NOT NULL AND n.prop2 IS NOT NULL
----

with filters on `n.prop1 ENDS WITH 'x'` and `n.prop2 = false`, since `n.prop1` has a `suffix search` predicate.

Composite indexes require predicates on all properties indexed.
If there are predicates on only a subset of the indexed properties, it will not be possible to use the composite index.
To get this kind of fallback behavior, it is necessary to create additional indexes on the relevant sub-set of properties or on single properties.


[[indexes-create-indexes]]
== +CREATE INDEX+

Creating an index is done with the `+CREATE ... INDEX ...+` command.
If no index type is specified in the create command a range index will be created.

[IMPORTANT]
====
The index name must be unique among both indexes and constraints.
====

[NOTE]
====
Best practice is to give the index a name when it is created.
If the index is not explicitly named, it gets an auto-generated name.
====

[NOTE]
====
The `+CREATE ... INDEX ...+` command is optionally idempotent. This mean that its default behavior is to throw an error if an attempt is made to create the same index twice.
With `IF NOT EXISTS`, no error is thrown and nothing happens should an index with the same name or same schema and index type already exist.
It may still throw an error if conflicting constraints exist, such as constraints with the same name or schema and backing index type.
====

Creating an index requires xref::administration/access-control/database-administration.adoc#access-control-database-administration-index[the `CREATE INDEX` privilege].

[NOTE]
====
The new index is not immediately available, but is created in the background.
====

*Examples:*

* xref::indexes-for-search-performance.adoc#indexes-create-range-index[]
** xref::indexes-for-search-performance.adoc#indexes-create-a-single-property-range-index-for-nodes[]
** xref::indexes-for-search-performance.adoc#indexes-create-a-single-property-range-index-for-relationships[]
** xref::indexes-for-search-performance.adoc#indexes-create-a-composite-range-index-for-nodes[]
** xref::indexes-for-search-performance.adoc#indexes-create-a-composite-range-index-for-relationships[]
** xref::indexes-for-search-performance.adoc#indexes-create-a-range-index-only-if-it-does-not-already-exist[]
* xref::indexes-for-search-performance.adoc#indexes-create-token-index[]
** xref::indexes-for-search-performance.adoc#indexes-create-a-node-label-lookup-index[]
** xref::indexes-for-search-performance.adoc#indexes-create-a-relationship-type-lookup-index[]
** xref::indexes-for-search-performance.adoc#indexes-create-a-lookup-index-only-if-it-does-not-already-exist[]
* xref::indexes-for-search-performance.adoc#indexes-create-point-index[]
** xref::indexes-for-search-performance.adoc#indexes-create-a-node-point-index[]
** xref::indexes-for-search-performance.adoc#indexes-create-a-relationship-point-index[]
** xref::indexes-for-search-performance.adoc#indexes-create-a-point-index-only-if-it-does-not-already-exist[]
** xref::indexes-for-search-performance.adoc#indexes-create-a-point-index-specifying-the-index-configuration[]
** xref::indexes-for-search-performance.adoc#indexes-create-a-point-index-specifying-both-the-index-provider-and-configuration[]
* xref::indexes-for-search-performance.adoc#indexes-create-text-index[]
** xref::indexes-for-search-performance.adoc#indexes-create-a-node-text-index[]
** xref::indexes-for-search-performance.adoc#indexes-create-a-relationship-text-index[]
** xref::indexes-for-search-performance.adoc#indexes-create-a-text-index-only-if-it-does-not-already-exist[]
** xref::indexes-for-search-performance.adoc#indexes-create-a-text-index-specifying-the-index-provider[]
* xref::indexes-for-search-performance.adoc#indexes-create-conflicting-index[]
** xref::indexes-for-search-performance.adoc#indexes-failure-to-create-an-already-existing-index[]
** xref::indexes-for-search-performance.adoc#indexes-failure-to-create-an-index-with-the-same-name-as-an-already-existing-index[]
** xref::indexes-for-search-performance.adoc#indexes-failure-to-create-an-index-when-a-constraint-already-exists[]
** xref::indexes-for-search-performance.adoc#indexes-failure-to-create-an-index-with-the-same-name-as-an-already-existing-constraint[]


[[indexes-create-range-index]]
=== Creating a range index

Creating a range index can be done with the `CREATE INDEX` command.

Range indexes have only one index provider available, `range-1.0`, and no supported index configuration.

Note that the index is not immediately available, but is created in the background.

*Examples:*

* xref::indexes-for-search-performance.adoc#indexes-create-a-single-property-range-index-for-nodes[]
* xref::indexes-for-search-performance.adoc#indexes-create-a-single-property-range-index-for-relationships[]
* xref::indexes-for-search-performance.adoc#indexes-create-a-composite-range-index-for-nodes[]
* xref::indexes-for-search-performance.adoc#indexes-create-a-composite-range-index-for-relationships[]
* xref::indexes-for-search-performance.adoc#indexes-create-a-range-index-only-if-it-does-not-already-exist[]

[discrete]
[[indexes-create-a-single-property-range-index-for-nodes]]
==== Create a single-property range index for nodes

.+Creating a named range index on a single property for all nodes with a particular label+
======
The following statement will create a named range index on all nodes labeled with `Person` and which have the `surname` property:

.Query
[source, cypher]
----
CREATE INDEX node_range_index_name FOR (n:Person) ON (n.surname)
----

[NOTE]
====
The index name must be unique.
====

.Result
[queryresult]
----
Added 1 index.
----

======

[discrete]
[[indexes-create-a-single-property-range-index-for-relationships]]
==== Create a single-property range index for relationships

.+Creating a named range index on a single property for all relationships with a particular relationship type+
======
The following statement will create a named range index on all relationships with relationship type `KNOWS` and property `since`:

.Query
[source, cypher]
----
CREATE INDEX rel_range_index_name FOR ()-[r:KNOWS]-() ON (r.since)
----

[NOTE]
====
The index name must be unique.
====

.Result
[queryresult]
----
Added 1 index.
----

======

[discrete]
[[indexes-create-a-composite-range-index-for-nodes]]
==== Create a composite range index for nodes

A named range index on multiple properties for all nodes with a particular label -- i.e. a composite index -- can be created with:

[source, syntax, role="noheader"]
----
CREATE INDEX index_name FOR (n:Label) ON (n.prop1, ..., n.propN)
----

Only nodes with the specified label and that contain all the properties in the index definition will be added to the index.


.+Creating a named range index on multiple properties for all nodes with a particular label+
======

The following statement will create a named composite range index on all nodes labeled with `Person` and which have both an `age` and `country` property:

.Query
[source, cypher]
----
CREATE INDEX composite_range_node_index_name FOR (n:Person) ON (n.age, n.country)
----

[NOTE]
====
The index name must be unique.
====

.Result
[queryresult]
----
Added 1 index.
----

======

[discrete]
[[indexes-create-a-composite-range-index-for-relationships]]
==== Create a composite range index for relationships

A named range index on multiple properties for all relationships with a particular relationship type -- i.e. a composite index -- can be created with:

[source, syntax, role="noheader"]
----
CREATE INDEX index_name FOR ()-[r:TYPE]-() ON (r.prop1, ..., r.propN)
----

Only relationships with the specified type and that contain all the properties in the index definition will be added to the index.


.+Creating a named range index on multiple properties for all relationships with a particular relationship type+
======

The following statement will create a named composite range index on all relationships labeled with `PURCHASED` and which have both a `date` and `amount` property:

.Query
[source, cypher]
----
CREATE INDEX composite_range_rel_index_name FOR ()-[r:PURCHASED]-() ON (r.date, r.amount)
----

[NOTE]
====
The index name must be unique.
====

.Result
[queryresult]
----
Added 1 index.
----

======

[discrete]
[[indexes-create-a-range-index-only-if-it-does-not-already-exist]]
==== Create a range index only if it does not already exist

If it is not known whether an index exists or not, add `IF NOT EXISTS` to ensure it does.

.+Creating a range index with `IF NOT EXISTS`+
======
The following statement will attempt to create a named range index on all nodes labeled with `Person` and which have the `surname` property:

.Query
[source, cypher]
----
CREATE INDEX node_range_index_name IF NOT EXISTS
FOR (n:Person) ON (n.surname)
----

[NOTE]
====
The index will not be created if there already exists an index with the same schema and type, same name or both.
====

.Result
[queryresult]
----
(no changes, no records)
----

======


[[indexes-create-token-index]]
=== Creating a token lookup index

Creating a token lookup index can be done with the `CREATE LOOKUP INDEX` command.

Token lookup indexes (node label and relationship type lookup indexes) have only one index provider available, `token-lookup-1.0`, and no supported index configuration.

Note that the index is not immediately available, but is created in the background.

*Examples:*

* xref::indexes-for-search-performance.adoc#indexes-create-a-node-label-lookup-index[]
* xref::indexes-for-search-performance.adoc#indexes-create-a-relationship-type-lookup-index[]
* xref::indexes-for-search-performance.adoc#indexes-create-a-lookup-index-only-if-it-does-not-already-exist[]

[discrete]
[[indexes-create-a-node-label-lookup-index]]
==== Create a node label lookup index

[NOTE]
====
Only one node label lookup index can exist at the time.
====

.+Creating a named node label lookup index for all nodes with one or more labels+
======
The following statement will create a named node label lookup index on all nodes with labels:

// Lookup indexes exist by default, recreating them would raise an error
.Query
[source, cypher, role=test-skip]
----
CREATE LOOKUP INDEX node_label_lookup_index FOR (n) ON EACH labels(n)
----

[NOTE]
====
The index name must be unique.
====

.Result
[queryresult]
----
Added 1 index.
----

======

[discrete]
[[indexes-create-a-relationship-type-lookup-index]]
==== Create a relationship type lookup index

[NOTE]
====
Only one relationship type lookup index can exist at the time.
====

.+Creating a named relationship type lookup index for all relationships with any relationship type+
======
The following statement will create a named relationship type lookup index on all relationships:

// Lookup indexes exist by default, recreating them would raise an error
.Query
[source, cypher, role=test-skip]
----
CREATE LOOKUP INDEX rel_type_lookup_index FOR ()-[r]-() ON EACH type(r)
----

[NOTE]
====
The index name must be unique.
====

.Result
[queryresult]
----
Added 1 index.
----

======

[discrete]
[[indexes-create-a-lookup-index-only-if-it-does-not-already-exist]]
==== Create a token lookup index only if it does not already exist

If it is not known whether an index exists or not, add `IF NOT EXISTS` to ensure it does.

.+Creating a node label index with `IF NOT EXISTS`+
======
The following statement will attempt to create a named node label lookup index on all nodes with labels:

.Query
[source, cypher]
----
CREATE LOOKUP INDEX node_label_lookup IF NOT EXISTS FOR (n) ON EACH labels(n)
----

[NOTE]
====
The index will not be created if there already exists an index with the same schema and type, same name or both.
====

.Result
[queryresult]
----
(no changes, no records)
----

======


[[indexes-create-point-index]]
=== Creating a point index

Creating a point index can be done with the `CREATE POINT INDEX` command.

Point indexes have only one index provider available, `point-1.0`, but it does have supported index configuration, see the last examples.

[NOTE]
====
Note that point indexes only recognize point values and do not support multiple properties.
====

Note that the index is not immediately available, but is created in the background.

*Examples:*

* xref::indexes-for-search-performance.adoc#indexes-create-a-node-point-index[]
* xref::indexes-for-search-performance.adoc#indexes-create-a-relationship-point-index[]
* xref::indexes-for-search-performance.adoc#indexes-create-a-point-index-only-if-it-does-not-already-exist[]
* xref::indexes-for-search-performance.adoc#indexes-create-a-point-index-specifying-the-index-configuration[]
* xref::indexes-for-search-performance.adoc#indexes-create-a-point-index-specifying-both-the-index-provider-and-configuration[]

[discrete]
[[indexes-create-a-node-point-index]]
==== Create a node point index

.+Creating a named point index on a single property for all nodes with a particular label+
======
The following statement will create a named point index on all nodes labeled with `Person` and which have the `sublocation` property:

.Query
[source, cypher]
----
CREATE POINT INDEX node_index_name FOR (n:Person) ON (n.sublocation)
----

[NOTE]
====
The index name must be unique.
====

.Result
[queryresult]
----
Added 1 index.
----

======

[discrete]
[[indexes-create-a-relationship-point-index]]
==== Create a relationship point index

.+Creating a named point index on a single property for all relationships with a particular relationship type+
======
The following statement will create a named point index on all relationships with relationship type `STREET` and property `intersection`:

.Query
[source, cypher]
----
CREATE POINT INDEX rel_index_name FOR ()-[r:STREET]-() ON (r.intersection)
----

[NOTE]
====
The index name must be unique.
====

.Result
[queryresult]
----
Added 1 index.
----

======

[discrete]
[[indexes-create-a-point-index-only-if-it-does-not-already-exist]]
==== Create a point index only if it does not already exist

If it is not known whether an index exists or not, add `IF NOT EXISTS` to ensure it does.


.+Creating a point index with `IF NOT EXISTS`+
======

.Query
[source, cypher]
----
CREATE POINT INDEX node_index_name IF NOT EXISTS
FOR (n:Person) ON (n.sublocation)
----

[NOTE]
====
Note that the index will not be created if there already exists an index with the same schema and type, same name or both.
====

.Result
[queryresult]
----
(no changes, no records)
----

======

[discrete]
[[indexes-create-a-point-index-specifying-the-index-configuration]]
==== Create a point index specifying the index configuration

To create a point index with a specific index configuration, the `OPTIONS` clause is used.

The valid configuration settings are:

* `spatial.cartesian.min`
* `spatial.cartesian.max`
* `spatial.cartesian-3d.min`
* `spatial.cartesian-3d.max`
* `spatial.wgs-84.min`
* `spatial.wgs-84.max`
* `spatial.wgs-84-3d.min`
* `spatial.wgs-84-3d.max`

Non-specified settings have their respective default values.


.+Creating a point index with index configuration+
======

.Query
[source, cypher]
----
CREATE POINT INDEX index_with_config
FOR (n:Label) ON (n.prop2)
OPTIONS {
  indexConfig: {
    `spatial.cartesian.min`: [-100.0, -100.0],
    `spatial.cartesian.max`: [100.0, 100.0]
  }
}
----

.Result
[queryresult]
----
Added 1 index.
----

======

Specifying the index configuration can be combined with specifying index provider.
Though only one valid value exists for the index provider, `point-1.0`, which is the default value.

[discrete]
[[indexes-create-a-point-index-specifying-both-the-index-provider-and-configuration]]
==== Create a point index specifying both the index provider and configuration

To create a point index with a specific index provider and configuration, the `OPTIONS` clause is used.
Only one valid value exists for the index provider, `point-1.0`, which is the default value.

The valid configuration settings are:

* `spatial.cartesian.min`
* `spatial.cartesian.max`
* `spatial.cartesian-3d.min`
* `spatial.cartesian-3d.max`
* `spatial.wgs-84.min`
* `spatial.wgs-84.max`
* `spatial.wgs-84-3d.min`
* `spatial.wgs-84-3d.max`

Non-specified settings have their respective default values.

.+Creating a point index with index provider and configuration+
======

.Query
[source, cypher]
----
CREATE POINT INDEX index_with_options
FOR ()-[r:TYPE]-() ON (r.prop1)
OPTIONS {
 indexProvider: 'point-1.0',
 indexConfig: {
   `spatial.wgs-84.min`: [-100.0, -80.0],
   `spatial.wgs-84.max`: [100.0, 80.0]
  }
}
----

.Result
[queryresult]
----
Added 1 index.
----

======

Index provider and configuration can also be specified separately.


[[indexes-create-text-index]]
=== Creating a text index

Creating a text index can be done with the `CREATE TEXT INDEX` command.

Text indexes have two index providers available, `text-2.0` (default) and `text-1.0` (deprecated), and no supported index configuration.

[NOTE]
====
Text indexes only recognize string values and do not support multiple properties.
====

Note that the index is not immediately available, but is created in the background.

*Examples:*

* xref::indexes-for-search-performance.adoc#indexes-create-a-node-text-index[]
* xref::indexes-for-search-performance.adoc#indexes-create-a-relationship-text-index[]
* xref::indexes-for-search-performance.adoc#indexes-create-a-text-index-only-if-it-does-not-already-exist[]
* xref::indexes-for-search-performance.adoc#indexes-create-a-text-index-specifying-the-index-provider[]

[discrete]
[[indexes-create-a-node-text-index]]
==== Create a node text index

.+Creating a named text index on a single property for all nodes with a particular label+
======
The following statement will create a named text index on all nodes labeled with `Person` and which have the `nickname` property:

.Query
[source, cypher]
----
CREATE TEXT INDEX node_index_nickname FOR (n:Person) ON (n.nickname)
----

[NOTE]
====
The index name must be unique.
====

.Result
[queryresult]
----
Added 1 index.
----

======

[discrete]
[[indexes-create-a-relationship-text-index]]
==== Create a relationship text index

.+Creating a named text index on a single property for all relationships with a particular relationship type+
======
The following statement will create a named text index on all relationships with relationship type `KNOWS` and property `interest`:

.Query
[source, cypher]
----
CREATE TEXT INDEX relprop_index_name FOR ()-[r:KNOWS]-() ON (r.interest)
----

[NOTE]
====
The index name must be unique.
====

.Result
[queryresult]
----
Added 1 index.
----

======

[discrete]
[[indexes-create-a-text-index-only-if-it-does-not-already-exist]]
==== Create a text index only if it does not already exist

If it is not known whether an index exists or not, add `IF NOT EXISTS` to ensure it does.


.+Creating a text index with `IF NOT EXISTS`+
======
The following statement will attempt to create a named text index on all nodes labeled with `Person` and which have the `nickname` property:

.Query
[source, cypher]
----
CREATE TEXT INDEX node_index_name IF NOT EXISTS FOR (n:Person) ON (n.nickname)
----

[NOTE]
====
Note that the index will not be created if there already exists an index with the same schema and type, same name or both.
====

.Result
[queryresult]
----
(no changes, no records)
----

======

[discrete]
[[indexes-create-a-text-index-specifying-the-index-provider]]
==== Create a text index specifying the index provider

To create a text index with a specific index provider, the `OPTIONS` clause is used.
The valid values for the index provider are `text-2.0` and `text-1.0` (deprecated).
The default provider is `text-2.0`.

.+Creating a text index with index provider+
======

.Query
[source, cypher]
----
CREATE TEXT INDEX index_with_indexprovider FOR ()-[r:TYPE]-() ON (r.prop1)
OPTIONS {indexProvider: 'text-2.0'}
----

.Result
[queryresult]
----
Added 1 index.
----

======

There is no supported index configuration for text indexes.


[[indexes-create-conflicting-index]]
=== Creating an index when a conflicting index or constraint exists

*Examples:*

* xref::indexes-for-search-performance.adoc#indexes-failure-to-create-an-already-existing-index[]
* xref::indexes-for-search-performance.adoc#indexes-failure-to-create-an-index-with-the-same-name-as-an-already-existing-index[]
* xref::indexes-for-search-performance.adoc#indexes-failure-to-create-an-index-when-a-constraint-already-exists[]
* xref::indexes-for-search-performance.adoc#indexes-failure-to-create-an-index-with-the-same-name-as-an-already-existing-constraint[]

[discrete]
[[indexes-failure-to-create-an-already-existing-index]]
==== Failure to create an already existing index

Create an index on the property `title` on nodes with the `Book` label, when that index already exists.

.+Creating a duplicated index+
======

////
[source, cypher, role=test-setup]
----
CREATE INDEX example_index FOR (n:Book) ON (n.title)
----
////

.Query
[source, cypher, role=test-fail]
----
CREATE INDEX bookTitleIndex FOR (book:Book) ON (book.title)
----

In this case the index can not be created because it already exists.

.Error message
[source, error]
----
There already exists an index (:Book {title}).
----

======

[discrete]
[[indexes-failure-to-create-an-index-with-the-same-name-as-an-already-existing-index]]
==== Failure to create an index with the same name as an already existing index

Create a named index on the property `numberOfPages` on nodes with the `Book` label, when an index with the given name already exists.
The index type of the existing index does not matter.


.+Creating an index with a duplicated name+
======

////
[source, cypher, role=test-setup]
----
CREATE TEXT INDEX indexOnBooks FOR (b:Label1) ON (b.prop1)
----
////

.Query
[source, cypher, role=test-fail]
----
CREATE INDEX indexOnBooks FOR (book:Book) ON (book.numberOfPages)
----

In this case the index can't be created because there already exists an index with the given name.

.Error message
[source, error]
----
There already exists an index called 'indexOnBooks'.
----

======

[discrete]
[[indexes-failure-to-create-an-index-when-a-constraint-already-exists]]
==== Failure to create an index when a constraint already exists

Create an index on the property `isbn` on nodes with the `Book` label, when an index-backed constraint already exists on that schema.
This is only relevant for range indexes.

.+Creating a range index on same schema as existing index-backed constraint+
======

////
[source, cypher, role=test-setup]
----
CREATE CONSTRAINT FOR (book:Book) REQUIRE (book.isbn) IS UNIQUE
----
////

.Query
[source, cypher, role=test-fail]
----
CREATE INDEX bookIsbnIndex FOR (book:Book) ON (book.isbn)
----

In this case the index can not be created because an index-backed constraint already exists on that label and property combination.

.Error message
[source, error]
----
There is a uniqueness constraint on (:Book {isbn}), so an index is already created that matches this.
----

======

[discrete]
[[indexes-failure-to-create-an-index-with-the-same-name-as-an-already-existing-constraint]]
==== Failure to create an index with the same name as an already existing constraint

Create a named index on the property `numberOfPages` on nodes with the `Book` label, when a constraint with the given name already exists.


.+Creating an index with same name as an existing constraint+
======

////
[source, cypher, role=test-setup]
----
CREATE CONSTRAINT bookRecommendations FOR (book:Book) REQUIRE (book.recommend) IS NOT NULL
----
////

.Query
[source, cypher, role=test-fail]
----
CREATE INDEX bookRecommendations FOR (book:Book) ON (book.recommendations)
----

In this case the index can not be created because there already exists a constraint with the given name.

.Error message
[source, error]
----
There already exists a constraint called 'bookRecommendations'.
----

======


[[indexes-list-indexes]]
== +SHOW INDEXES+

Listing indexes can be done with `SHOW INDEXES`, which will produce a table with the following columns:

.List indexes output
[options="header", cols="4,6,2"]
|===
| Column | Description | Type

| `id`
| The id of the index. label:default-output[]
| `INTEGER`

| `name`
| Name of the index (explicitly set by the user or automatically assigned). label:default-output[]
| `STRING`

| `state`
| Current state of the index. label:default-output[]
| `STRING`

| `populationPercent`
| % of index population. label:default-output[]
| `FLOAT`

| `type`
| The IndexType of this index (`FULLTEXT`, `LOOKUP`, `POINT`, `RANGE`, or `TEXT`). label:default-output[]
| `STRING`

| `entityType`
| Type of entities this index represents (nodes or relationship). label:default-output[]
| `STRING`

| `labelsOrTypes`
| The labels or relationship types of this index. label:default-output[]
| `LIST OF STRING`

| `properties`
| The properties of this index. label:default-output[]
| `LIST OF STRING`

| `indexProvider`
| The index provider for this index. label:default-output[]
| `STRING`

// New in 5.0
| `owningConstraint`
| The name of the constraint the index is associated with or `null` if the index is not associated with any constraint. label:default-output[]
| `STRING`

| `options`
| The options passed to `CREATE` command.
| `MAP`

| `failureMessage`
| The failure description of a failed index.
| `STRING`

| `createStatement`
| Statement used to create the index.
| `STRING`

|===

[NOTE]
====
The command `SHOW INDEXES` returns only the default output.
For a full output use the optional `YIELD` command.
Full output: `+SHOW INDEXES YIELD *+`.
====

Listing indexes also allows for `WHERE` and `YIELD` clauses to filter the returned rows and columns.

Listing indexes require xref::administration/access-control/database-administration.adoc#access-control-database-administration-index[the `SHOW INDEX` privilege].

*Examples:*

* xref::indexes-for-search-performance.adoc#indexes-listing-all-indexes[]
* xref::indexes-for-search-performance.adoc#indexes-listing-indexes-with-filtering[]


[discrete]
[[indexes-listing-all-indexes]]
=== Listing all indexes

To list all indexes with the default output columns, the `SHOW INDEXES` command can be used.
If all columns are required, use `SHOW INDEXES YIELD *`.


.+Showing all indexes+
======

.Query
[source, cypher, role=test-result-skip]
----
SHOW INDEXES
----

One of the output columns from `SHOW INDEXES` is the name of the index.
This can be used to drop the index with the xref::indexes-for-search-performance.adoc#indexes-drop-an-index[`DROP INDEX` command].

// SHOW INDEXES default outputs
// 4.4: id, name, state, populationPercent, uniqueness, type, entityType, labelsOrTypes, properties, indexProvider
// 5.0: id, name, state, populationPercent, type, entityType, labelsOrTypes, properties, indexProvider, owningConstraint

.Result
[queryresult]
----
+-------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------+
| id | name                              | state    | populationPercent | type     | entityType     | labelsOrTypes | properties         | indexProvider      | owningConstraint      |
+-------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------+
| 20 | "composite_range_node_index_name" | "ONLINE" | 100.0             | "RANGE"  | "NODE"         | ["Person"]    | ["age", "country"] | "range-1.0"        | NULL                  |
| 2  | "composite_range_rel_index_name"  | "ONLINE" | 100.0             | "RANGE"  | "RELATIONSHIP" | ["PURCHASED"] | ["date", "amount"] | "range-1.0"        | NULL                  |
| 14 | "constraint_1bc95fcb"             | "ONLINE" | 100.0             | "RANGE"  | "NODE"         | ["Book"]      | ["isbn"]           | "range-1.0"        | "constraint_1bc95fcb" |
| 4  | "example_index"                   | "ONLINE" | 100.0             | "RANGE"  | "NODE"         | ["Book"]      | ["title"]          | "range-1.0"        | NULL                  |
| 7  | "indexOnBooks"                    | "ONLINE" | 100.0             | "RANGE"  | "NODE"         | ["Label1"]    | ["prop1"]          | "range-1.0"        | NULL                  |
| 10 | "index_with_config"               | "ONLINE" | 100.0             | "POINT"  | "NODE"         | ["Label"]     | ["prop2"]          | "point-1.0"        | NULL                  |
| 6  | "index_with_indexprovider"        | "ONLINE" | 100.0             | "TEXT"   | "RELATIONSHIP" | ["TYPE"]      | ["prop1"]          | "text-2.0"         | NULL                  |
| 9  | "index_with_options"              | "ONLINE" | 100.0             | "POINT"  | "RELATIONSHIP" | ["TYPE"]      | ["prop1"]          | "point-1.0"        | NULL                  |
| 8  | "index_with_provider"             | "ONLINE" | 100.0             | "POINT"  | "NODE"         | ["Label"]     | ["prop1"]          | "point-1.0"        | NULL                  |
| 15 | "node_index_name"                 | "ONLINE" | 100.0             | "POINT"  | "NODE"         | ["Person"]    | ["sublocation"]    | "point-1.0"        | NULL                  |
| 3  | "node_index_nickname"             | "ONLINE" | 100.0             | "TEXT"   | "NODE"         | ["Person"]    | ["nickname"]       | "text-2.0"         | NULL                  |
| 11 | "node_label_lookup_index_2"       | "ONLINE" | 100.0             | "LOOKUP" | "NODE"         | NULL          | NULL               | "token-lookup-1.0" | NULL                  |
| 16 | "node_range_index_name"           | "ONLINE" | 100.0             | "RANGE"  | "NODE"         | ["Person"]    | ["surname"]        | "range-1.0"        | NULL                  |
| 19 | "range_index_with_provider"       | "ONLINE" | 100.0             | "RANGE"  | "RELATIONSHIP" | ["TYPE"]      | ["prop1"]          | "range-1.0"        | NULL                  |
| 13 | "rel_index_name"                  | "ONLINE" | 100.0             | "POINT"  | "RELATIONSHIP" | ["STREET"]    | ["intersection"]   | "point-1.0"        | NULL                  |
| 18 | "rel_range_index_name"            | "ONLINE" | 100.0             | "RANGE"  | "RELATIONSHIP" | ["KNOWS"]     | ["since"]          | "range-1.0"        | NULL                  |
| 17 | "rel_type_lookup_index"           | "ONLINE" | 100.0             | "LOOKUP" | "RELATIONSHIP" | NULL          | NULL               | "token-lookup-1.0" | NULL                  |
| 1  | "relprop_index_name"              | "ONLINE" | 100.0             | "TEXT"   | "RELATIONSHIP" | ["KNOWS"]     | ["interest"]       | "text-2.0"         | NULL                  |
+-------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------+
18 rows
----

======


[discrete]
[[indexes-listing-indexes-with-filtering]]
=== Listing indexes with filtering

One way of filtering the output from `SHOW INDEXES` by index type is the use of type keywords, listed in the xref::indexes-for-search-performance.adoc#indexes-syntax[syntax table].

For example, to show only range indexes, use `SHOW RANGE INDEXES`.

Another more flexible way of filtering the output is to use the `WHERE` clause.
An example is to only show indexes not belonging to constraints.


.+Showing range indexes+
======

.Query
[source, cypher, role=test-result-skip]
----
SHOW RANGE INDEXES WHERE owningConstraint IS NULL
----

This will only return the default output columns.

To get all columns, use:

[source, syntax, role="noheader"]
----
SHOW INDEXES YIELD * WHERE ...
----

.Result
[queryresult]
----
+--------------------------------------------------------------------------------------------------------------------------------------------------------------------------+
| id | name                              | state    | populationPercent | type    | entityType     | labelsOrTypes | properties         | indexProvider | owningConstraint |
+--------------------------------------------------------------------------------------------------------------------------------------------------------------------------+
| 20 | "composite_range_node_index_name" | "ONLINE" | 100.0             | "RANGE" | "NODE"         | ["Person"]    | ["age", "country"] | "range-1.0"   | NULL             |
| 2  | "composite_range_rel_index_name"  | "ONLINE" | 100.0             | "RANGE" | "RELATIONSHIP" | ["PURCHASED"] | ["date", "amount"] | "range-1.0"   | NULL             |
| 4  | "example_index"                   | "ONLINE" | 100.0             | "RANGE" | "NODE"         | ["Book"]      | ["title"]          | "range-1.0"   | NULL             |
| 7  | "indexOnBooks"                    | "ONLINE" | 100.0             | "RANGE" | "NODE"         | ["Label1"]    | ["prop1"]          | "range-1.0"   | NULL             |
| 16 | "node_range_index_name"           | "ONLINE" | 100.0             | "RANGE" | "NODE"         | ["Person"]    | ["surname"]        | "range-1.0"   | NULL             |
| 19 | "range_index_with_provider"       | "ONLINE" | 100.0             | "RANGE" | "RELATIONSHIP" | ["TYPE"]      | ["prop1"]          | "range-1.0"   | NULL             |
| 18 | "rel_range_index_name"            | "ONLINE" | 100.0             | "RANGE" | "RELATIONSHIP" | ["KNOWS"]     | ["since"]          | "range-1.0"   | NULL             |
+--------------------------------------------------------------------------------------------------------------------------------------------------------------------------+
7 rows
----

======


[[indexes-drop-indexes]]
== +DROP INDEX+

An index can be dropped (removed) using the name with the `DROP INDEX index_name` command.
This command can drop indexes of any type, except those backing constraints.
The name of the index can be found using the xref::indexes-for-search-performance.adoc#indexes-list-indexes[`SHOW INDEXES` command], given in the output column `name`.

[NOTE]
====
The `DROP INDEX` command is optionally idempotent.
This means that its default behavior is to throw an error if an attempt is made to drop the same index twice.
With `IF EXISTS`, no error is thrown and nothing happens should the index not exist.
====

Dropping an index requires xref::administration/access-control/database-administration.adoc#access-control-database-administration-index[the `DROP INDEX` privilege].

*Examples:*

* xref::indexes-for-search-performance.adoc#indexes-drop-an-index[]
* xref::indexes-for-search-performance.adoc#indexes-drop-a-non-existing-index[]


[discrete]
[[indexes-drop-an-index]]
=== Drop an index


.+Dropping an index+
======

.Query
[source, cypher]
----
DROP INDEX example_index
----

.Result
[queryresult]
----
Removed 1 index.
----

======


[discrete]
[[indexes-drop-a-non-existing-index]]
=== Drop a non-existing index

If it is uncertain if an index exists and you want to drop it if it does but not get an error should it not, use `IF EXISTS`.


.+Dropping an index with `IF NOT EXISTS`+
======

.Query
[source, cypher]
----
DROP INDEX missing_index_name IF EXISTS
----

.Result
[queryresult]
----
(no changes, no records)
----

======

