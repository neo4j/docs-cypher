:description: Functions that operate on numeric expressions only, and will return an error if used on any other values.
:table-caption!:

[[query-functions-numeric]]
= Mathematical functions - numeric

Numeric mathematical functions operate on numeric expressions only, and will return an error if used on any other values.
See also xref::syntax/operators.adoc#query-operators-mathematical[Mathematical operators].

[[example-graph]]
== Example graph

The following graph is used for the examples below:

image::graph_numeric_functions.svg[role="middle", width="700"]

To recreate the graph, run the following query against an empty Neo4j database:

[source, cypher, role=test-setup]
----
CREATE
  (alice:Developer {name:'Alice', age: 38, eyes: 'Brown'}),
  (bob:Administrator {name: 'Bob', age: 25, eyes: 'Blue'}),
  (charlie:Administrator {name: 'Charlie', age: 53, eyes: 'Green'}),
  (daniel:Adminstrator {name: 'Daniel', age: 54, eyes: 'Brown'}),
  (eskil:Designer {name: 'Eskil', age: 41, eyes: 'blue', likedColors: ['Pink', 'Yellow', 'Black']}),
  (alice)-[:KNOWS]->(bob),
  (alice)-[:KNOWS]->(charlie),
  (bob)-[:KNOWS]->(daniel),
  (charlie)-[:KNOWS]->(daniel),
  (bob)-[:MARRIED]->(eskil)
----

[[functions-abs]]
== abs()

.Details
|===
| *Syntax* 3+| `abs(input)`
| *Description* 3+| Returns the absolute value of an `INTEGER` or `FLOAT`.
.2+| *Arguments* | *Name* | *Type* | *Description*
| `input` | `INTEGER \| FLOAT` | A numeric value from which the absolute number will be returned.
| *Returns* 3+| `INTEGER \| FLOAT`
|===

.Considerations
|===

| `abs(null)` returns `null`.
| If `input` is negative, `-(input)` (i.e. the _negation_ of `input`) is returned.

|===

.+abs()+
======

.Query
// tag::functions_mathematical_numeric_abs[]
[source, cypher, indent=0]
----
MATCH (a), (e) WHERE a.name = 'Alice' AND e.name = 'Eskil'
RETURN a.age, e.age, abs(a.age - e.age)
----
// end::functions_mathematical_numeric_abs[]

The absolute value of the age difference is returned.

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===

| a.age | e.age | abs(a.age - e.age)
| 38 | 41 | 3
3+d|Rows: 1

|===

======


[[functions-ceil]]
== ceil()

.Details
|===
| *Syntax* 3+| `ceil(input)`
| *Description* 3+| Returns the smallest `FLOAT` that is greater than or equal to a number and equal to an `INTEGER`.
.2+| *Arguments* | *Name* | *Type* | *Description*
| `input` | `FLOAT` | A value to be rounded to the nearest higher integer.
| *Returns* 3+| `FLOAT`
|===

.Considerations
|===

| `ceil(null)` returns `null`.

|===


.+ceil()+
======

.Query
// tag::functions_mathematical_numeric_ceil[]
[source, cypher, indent=0]
----
RETURN ceil(0.1)
----
// end::functions_mathematical_numeric_ceil[]

The ceil of `0.1` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| ceil(0.1)
| 1.0
1+d|Rows: 1

|===

======


[[functions-floor]]
== floor()

.Details
|===
| *Syntax* 3+| `floor(input)`
| *Description* 3+| Returns the largest `FLOAT` that is less than or equal to a number and equal to an `INTEGER`.
.2+| *Arguments* | *Name* | *Type* | *Description*
| `input` | `FLOAT` | A value to be rounded to the nearest lower integer.
| *Returns* 3+| `FLOAT`
|===

.Considerations
|===

| `floor(null)` returns `null`.

|===


.+floor()+
======

.Query
// tag::functions_mathematical_numeric_floor[]
[source, cypher, indent=0]
----
RETURN floor(0.9)
----
// end::functions_mathematical_numeric_floor[]

The floor of `0.9` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| floor(0.9)
| 0.0
1+d|Rows: 1
|===

======


[[functions-isnan]]
== isNaN()

.Details
|===
| *Syntax* 3+| `isNaN(input)`
| *Description* 3+| Returns whether the given `INTEGER` or `FLOAT` is NaN.
.2+| *Arguments* | *Name* | *Type* | *Description*
| `input` | `INTEGER \| FLOAT` | A numeric value to be compared against `NaN`.
| *Returns* 3+| `BOOLEAN`
|===

.Considerations
|===

| `isNaN(null)` returns `null`.

|===


.+isNaN()+
======

.Query
// tag::functions_mathematical_numeric_is_nan[]
[source, cypher]
----
RETURN isNaN(0/0.0)
----
// end::functions_mathematical_numeric_is_nan[]

`true` is returned since the value is `NaN`.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| isNaN(0/0.0)
| true
1+d|Rows: 1

|===

======


[[functions-rand]]
== rand()

.Details
|===
| *Syntax* 3+| `rand()`
| *Description* 3+| Returns a random `FLOAT` in the range from 0 (inclusive) to 1 (exclusive).
| *Returns* 3+| `FLOAT`
|===

.+rand()+
======

.Query
// tag::functions_mathematical_numeric_rand[]
[source, cypher, indent=0]
----
RETURN rand()
----
// end::functions_mathematical_numeric_rand[]

A random number is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| rand()
| 0.5460251846326871
1+d|Rows: 1

|===

======


[[functions-round]]
== round()

.Details
|===
| *Syntax* 3+| `round(value[, precision, mode])`
| *Description* 3+| Returns the value of a rounded number, optionally using a specified precision and rounding mode.
.4+| *Arguments* | *Name* | *Type* | *Description*
| `value` | `FLOAT` | A value to be rounded.
| `precision` | `INTEGER \| FLOAT` | The rounding precision.
| `mode` | `STRING` | A precision rounding mode (`UP`, `DOWN`, `CEILING`, `FLOOR`, `HALF_UP`, `HALF_DOWN`, `HALF_EVEN`).
| *Returns* 3+| `FLOAT`
|===

.Modes
[options="header"]
|===
| `mode` | Description

| `UP`
| Round away from zero.

| `DOWN`
| Round towards zero.

| `CEILING`
| Round towards positive infinity.

| `FLOOR`
| Round towards negative infinity.

| `HALF_UP`
| Round towards closest value of given precision, with ties always being rounded away from zero.

| `HALF_DOWN`
| Round towards closest value of given precision, with ties always being rounded towards zero.

| `HALF_EVEN`
| Round towards closest value of given precision, with ties always being rounded to the even neighbor.

|===

.Considerations
|===

| For the rounding modes, a tie means that the two closest values of the given precision are at the same distance from the given value.
E.g. for precision 1, 2.15 is a tie as it has equal distance to 2.1 and 2.2, while 2.151 is not a tie, as it is closer to 2.2.
|  `round()` returns `null` if any of its input parameters are `null`.

|===


.+round()+
======

.Query
// tag::functions_mathematical_numeric_round[]
[source, cypher, indent=0]
----
RETURN round(3.141592)
----
// end::functions_mathematical_numeric_round[]

`3.0` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| round(3.141592)
| 3.0
1+d|Rows: 1
|===

======

.+round() of negative number with tie+
======

.Query
[source, cypher, indent=0]
----
RETURN round(-1.5)
----

Ties are rounded towards positive infinity, therefore `-1.0` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| round(-1.5)
| -1.0
1+d|Rows: 1
|===

======

[[functions-round2]]
=== round() with precision

.+round() with precision+
======

.Query
// tag::functions_mathematical_numeric_round_with_precision[]
[source, cypher, indent=0]
----
RETURN round(3.141592, 3)
----
// end::functions_mathematical_numeric_round_with_precision[]

`3.142` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| round(3.141592, 3)
| 3.142
1+d|Rows: 1

|===

======

.+round() with precision 0 and tie+
======

.Query
[source, cypher, indent=0]
----
RETURN round(-1.5, 0)
----

To align with `round(-1.5)`, `-1.0` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| round(-1.5, 0)
| -1.0
1+d|Rows: 1

|===

======

.+round() with precision 1 and tie+
======

.Query
[source, cypher, indent=0]
----
RETURN round(-1.55, 1)
----

The default is to round away from zero when there is a tie, therefore `-1.6` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| round(-1.55, 1)
| -1.6
1+d|Rows: 1

|===

======

[[functions-round3]]
=== round() with precision and rounding mode

.+round() with precision and UP rounding mode+
======

.Query
// tag::functions_mathematical_numeric_round_with_precision_and_rounding_mode[]
[source, cypher, indent=0]
----
RETURN round(1.249, 1, 'UP') AS positive,
round(-1.251, 1, 'UP') AS negative,
round(1.25, 1, 'UP') AS positiveTie,
round(-1.35, 1, 'UP') AS negativeTie
----
// end::functions_mathematical_numeric_round_with_precision_and_rounding_mode[]

The rounded values using precision 1 and rounding mode `UP` are returned.

.Result
[role="queryresult",options="header,footer",cols="4*<m"]
|===

| positive | negative | positiveTie | negativeTie
| 1.3 | -1.3 | 1.3 | -1.4
4+d|Rows: 1

|===

======

.+round() with precision and DOWN rounding mode+
======

.Query
[source, cypher, indent=0]
----
RETURN round(1.249, 1, 'DOWN') AS positive,
round(-1.251, 1, 'DOWN') AS negative,
round(1.25, 1, 'DOWN') AS positiveTie,
round(-1.35, 1, 'DOWN') AS negativeTie
----

The rounded values using precision 1 and rounding mode `DOWN` are returned.

.Result
[role="queryresult",options="header,footer",cols="4*<m"]
|===

| positive | negative | positiveTie | negativeTie
| 1.2 | -1.2 | 1.2 | +-1.3
4+d|Rows: 1

|===

======

.+round() with precision and CEILING rounding mode+
======

.Query
[source, cypher, indent=0]
----
RETURN round(1.249, 1, 'CEILING') AS positive,
round(-1.251, 1, 'CEILING') AS negative,
round(1.25, 1, 'CEILING') AS positiveTie,
round(-1.35, 1, 'CEILING') AS negativeTie
----

The rounded values using precision 1 and rounding mode `CEILING` are returned.

.Result
[role="queryresult",options="header,footer",cols="4*<m"]
|===

| positive | negative | positiveTie | negativeTie
| 1.3 | -1.2 | 1.3 | -1.3
4+d|Rows: 1

|===

======

.+round() with precision and FLOOR rounding mode+
======

.Query
[source, cypher, indent=0]
----
RETURN round(1.249, 1, 'FLOOR') AS positive,
round(-1.251, 1, 'FLOOR') AS negative,
round(1.25, 1, 'FLOOR') AS positiveTie,
round(-1.35, 1, 'FLOOR') AS negativeTie
----

The rounded values using precision 1 and rounding mode `FLOOR` are returned.

.Result
[role="queryresult",options="header,footer",cols="4*<m"]
|===

| positive | negative | positiveTie | negativeTie
| 1.2 | -1.3 | 1.2 | -1.4
4+d|Rows: 1

|===

======

.+round() with precision and HALF_UP rounding mode+
======

.Query
[source, cypher, indent=0]
----
RETURN round(1.249, 1, 'HALF_UP') AS positive,
round(-1.251, 1, 'HALF_UP') AS negative,
round(1.25, 1, 'HALF_UP') AS positiveTie,
round(-1.35, 1, 'HALF_UP') AS negativeTie
----

The rounded values using precision 1 and rounding mode `HALF_UP` are returned.

.Result
[role="queryresult",options="header,footer",cols="4*<m"]
|===

| positive | negative | positiveTie | negativeTie
| 1.2 | -1.3 | 1.3 | -1.4
4+d|Rows: 1

|===

======
.+round() with precision and HALF_DOWN rounding mode+
======

.Query
[source, cypher, indent=0]
----
RETURN round(1.249, 1, 'HALF_DOWN') AS positive,
round(-1.251, 1, 'HALF_DOWN') AS negative,
round(1.25, 1, 'HALF_DOWN') AS positiveTie,
round(-1.35, 1, 'HALF_DOWN') AS negativeTie
----

The rounded values using precision 1 and rounding mode `HALF_DOWN` are returned.

.Result
[role="queryresult",options="header,footer",cols="4*<m"]
|===

| positive | negative | positiveTie | negativeTie
| 1.2 | -1.3 | 1.2 | -1.3
4+d|Rows: 1

|===

======

.+round() with precision and HALF_EVEN rounding mode+
======

.Query
[source, cypher, indent=0]
----
RETURN round(1.249, 1, 'HALF_EVEN') AS positive,
round(-1.251, 1, 'HALF_EVEN') AS negative,
round(1.25, 1, 'HALF_EVEN') AS positiveTie,
round(-1.35, 1, 'HALF_EVEN') AS negativeTie
----

The rounded values using precision 1 and rounding mode `HALF_EVEN` are returned.

.Result
[role="queryresult",options="header,footer",cols="4*<m"]
|===

| positive | negative | positiveTie | negativeTie
| 1.2 | -1.3 | 1.2 | -1.4
4+d|Rows: 1

|===

======

[[functions-sign]]
== sign()

.Details
|===
| *Syntax* 3+| `sign(input)`
| *Description* 3+| Returns the signum of an `INTEGER` or `FLOAT`: 0 if the number is 0, -1 for any negative number, and 1 for any positive number.
.2+| *Arguments* | *Name* | *Type* | *Description*
| `input` | `INTEGER \| FLOAT` | A positive or negative number.
| *Returns* 3+| `INTEGER`
|===

.Considerations
|===

| `sign(null)` returns `null`.

|===


.+sign()+
======

.Query
// tag::functions_mathematical_numeric_sign[]
[source, cypher, indent=0]
----
RETURN sign(-17), sign(0.1)
----
// end::functions_mathematical_numeric_sign[]

The signs of `-17` and `0.1` are returned.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===

| sign(-17) | sign(0.1)
| -1 | 1
2+d|Rows: 1

|===

======

