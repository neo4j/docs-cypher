:description: Functions that operate on numeric expressions only, and will return an error if used on any other values.

[[query-functions-numeric]]
= Mathematical functions - numeric

[abstract]
--
These functions all operate on numeric expressions only, and will return an error if used on any other values.
See also xref::syntax/operators.adoc#query-operators-mathematical[Mathematical operators].
--

The following graph is used for the examples below:

image:graph_numeric_functions.svg[]

////
[source, cypher, role=test-setup]
----
CREATE
  (alice:A {name:'Alice', age: 38, eyes: 'brown'}),
  (bob:B {name: 'Bob', age: 25, eyes: 'blue'}),
  (charlie:C {name: 'Charlie', age: 53, eyes: 'green'}),
  (daniel:D {name: 'Daniel', age: 54, eyes: 'brown'}),
  (eskil:E {name: 'Eskil', age: 41, eyes: 'blue', array: ['one', 'two', 'three']}),
  (alice)-[:KNOWS]->(bob),
  (alice)-[:KNOWS]->(charlie),
  (bob)-[:KNOWS]->(daniel),
  (charlie)-[:KNOWS]->(daniel),
  (bob)-[:MARRIED]->(eskil)
----
////


[[functions-abs]]
== abs()

`abs()` returns the absolute value of the given number.

*Syntax:*

[source, syntax, role="noheader"]
----
abs(expression)
----

*Returns:*

|===

| The type of the value returned will be that of `expression`.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| A numeric expression.

|===

*Considerations:*

|===

| `abs(null)` returns `null`.
| If `expression` is negative, `-(expression)` (i.e. the _negation_ of `expression`) is returned.

|===

.+abs()+
======

.Query
[source, cypher, indent=0]
----
MATCH (a), (e) WHERE a.name = 'Alice' AND e.name = 'Eskil' RETURN a.age, e.age, abs(a.age - e.age)
----

The absolute value of the age difference is returned.

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===

| +a.age+ | +e.age+ | +abs(a.age - e.age)+
| +38+ | +41+ | +3+
3+d|Rows: 1

|===

======


[[functions-ceil]]
== ceil()

`ceil()` returns the smallest `FLOAT` that is greater than or equal to the given number and equal to an `INTEGER`.

*Syntax:*

[source, syntax, role="noheader"]
----
ceil(expression)
----

*Returns:*

|===

| `FLOAT`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| A numeric expression.

|===

*Considerations:*

|===

| `ceil(null)` returns `null`.

|===


.+ceil()+
======

.Query
[source, cypher, indent=0]
----
RETURN ceil(0.1)
----

The ceil of `0.1` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +ceil(0.1)+
| +1.0+
1+d|Rows: 1

|===

======


[[functions-floor]]
== floor()

`floor()` returns the largest `FLOAT` that is less than or equal to the given number and equal to an `INTEGER`.

*Syntax:*

[source, syntax, role="noheader"]
----
floor(expression)
----

*Returns:*

|===

| `FLOAT`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| A numeric expression.

|===

*Considerations:*

|===

| `floor(null)` returns `null`.

|===


.+floor()+
======

.Query
[source, cypher, indent=0]
----
RETURN floor(0.9)
----

The floor of `0.9` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +floor(0.9)+
| +0.0+
1+d|Rows: 1
|===

======


[[functions-isnan]]
== isNaN()

`isNaN()` returns `true` if the given numeric value is `NaN` (Not a Number).

*Syntax:*

[source, syntax, role="noheader"]
----
isNaN(expression)
----

*Returns:*

|===

| `BOOLEAN`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| A numeric expression.

|===

*Considerations:*

|===

| `isNaN(null)` returns `null`.

|===


.+isNaN()+
======

.Query
[source, cypher]
----
RETURN isNaN(0/0.0)
----

`true` is returned since the value is `NaN`.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +isNaN(0/0.0)+
| +true+
1+d|Rows: 1

|===

======


[[functions-rand]]
== rand()

`rand()` returns a random `FLOAT` in the range from 0 (inclusive) to 1 (exclusive); i.e. `[0,1)`. The numbers returned follow an approximate uniform distribution.

*Syntax:*

[source, syntax, role="noheader"]
----
rand()
----

*Returns:*

|===

| `FLOAT`

|===


.+rand()+
======

.Query
[source, cypher, indent=0]
----
RETURN rand()
----

A random number is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +rand()+
| +0.5460251846326871+
1+d|Rows: 1

|===

======


[[functions-round]]
== round()

`round()` returns the value of the given number rounded to the nearest `INTEGER`, with ties always rounded towards positive infinity.

*Syntax:*

[source, syntax, role="noheader"]
----
round(expression)
----

*Returns:*

|===

| `FLOAT`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| A numeric expression to be rounded.

|===

*Considerations:*

|===

| `round(null)` returns `null`.

|===


.+round()+
======

.Query
[source, cypher, indent=0]
----
RETURN round(3.141592)
----

`3.0` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +round(3.141592)+
| +3.0+
1+d|Rows: 1
|===

======

.+round() of negative number with tie+
======

.Query
[source, cypher, indent=0]
----
RETURN round(-1.5)
----

Ties are rounded towards positive infinity, therefore `-1.0` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +round(-1.5)+
| +-1.0+
1+d|Rows: 1
|===

======

[[functions-round2]]
== round(), with precision
`round()` returns the value of the given number rounded to the closest value of given precision, with ties always being rounded away from zero (using rounding mode `HALF_UP`).
The exception is for precision 0, where ties are rounded towards positive infinity to align with <<functions-round>> without precision.

*Syntax:*

[source, syntax, role="noheader"]
----
round(expression, precision)
----

*Returns:*
|===

| `FLOAT`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| A numeric expression to be rounded.

| `precision`
| A numeric expression specifying precision.

|===

*Considerations:*

|===

| `round()` returns `null` if any of its input parameters are `null`.

|===


.+round() with precision+
======

.Query
[source, cypher, indent=0]
----
RETURN round(3.141592, 3)
----

`3.142` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +round(3.141592, 3)+
| +3.142+
1+d|Rows: 1

|===

======

.+round() with precision 0 and tie+
======

.Query
[source, cypher, indent=0]
----
RETURN round(-1.5, 0)
----

To align with `round(-1.5)`, `-1.0` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +round(-1.5, 0)+
| +-1.0+
1+d|Rows: 1

|===

======

.+round() with precision 1 and tie+
======

.Query
[source, cypher, indent=0]
----
RETURN round(-1.55, 1)
----

The default is to round away from zero when there is a tie, therefore `-1.6` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +round(-1.55, 1)+
| +-1.6+
1+d|Rows: 1

|===

======


[[functions-round3]]
== round(), with precision and rounding mode

`round()` returns the value of the given number rounded with the specified precision and the specified rounding mode.

*Syntax:*

[source, syntax, role="noheader"]
----
round(expression, precision, mode)
----

*Returns:*

|===

| `FLOAT`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| A numeric expression to be rounded.

| `precision`
| A numeric expression specifying precision.

| `mode`
| A string expression specifying rounding mode.

|===

*Modes:*
[options="header"]
|===
| `Mode` | Description

| `UP`
| Round away from zero.

| `DOWN`
| Round towards zero.

| `CEILING`
| Round towards positive infinity.

| `FLOOR`
| Round towards negative infinity.

| `HALF_UP`
| Round towards closest value of given precision, with ties always being rounded away from zero.

| `HALF_DOWN`
| Round towards closest value of given precision, with ties always being rounded towards zero.

| `HALF_EVEN`
| Round towards closest value of given precision, with ties always being rounded to the even neighbor.

|===

*Considerations:*
|===

| For the rounding modes, a tie means that the two closest values of the given precision are at the same distance from the given value.
E.g. for precision 1, 2.15 is a tie as it has equal distance to 2.1 and 2.2, while 2.151 is not a tie, as it is closer to 2.2.

|===

|===

|  `round()` returns `null` if any of its input parameters are `null`.

|===

.+round() with precision and UP rounding mode+
======

.Query
[source, cypher, indent=0]
----
RETURN round(1.249, 1, 'UP') AS positive,
round(-1.251, 1, 'UP') AS negative,
round(1.25, 1, 'UP') AS positiveTie,
round(-1.35, 1, 'UP') AS negativeTie
----

The rounded values using precision 1 and rounding mode `UP` are returned.

.Result
[role="queryresult",options="header,footer",cols="4*<m"]
|===

| +positive+ | +negative+ | +positiveTie+ | +negativeTie+
| +1.3+ | +-1.3+ | +1.3+ | +-1.4+
4+d|Rows: 1

|===

======

.+round() with precision and DOWN rounding mode+
======

.Query
[source, cypher, indent=0]
----
RETURN round(1.249, 1, 'DOWN') AS positive,
round(-1.251, 1, 'DOWN') AS negative,
round(1.25, 1, 'DOWN') AS positiveTie,
round(-1.35, 1, 'DOWN') AS negativeTie
----

The rounded values using precision 1 and rounding mode `DOWN` are returned.

.Result
[role="queryresult",options="header,footer",cols="4*<m"]
|===

| +positive+ | +negative+ | +positiveTie+ | +negativeTie+
| +1.2+ | +-1.2+ | +1.2+ | +-1.3+
4+d|Rows: 1

|===

======

.+round() with precision and CEILING rounding mode+
======

.Query
[source, cypher, indent=0]
----
RETURN round(1.249, 1, 'CEILING') AS positive,
round(-1.251, 1, 'CEILING') AS negative,
round(1.25, 1, 'CEILING') AS positiveTie,
round(-1.35, 1, 'CEILING') AS negativeTie
----

The rounded values using precision 1 and rounding mode `CEILING` are returned.

.Result
[role="queryresult",options="header,footer",cols="4*<m"]
|===

| +positive+ | +negative+ | +positiveTie+ | +negativeTie+
| +1.3+ | +-1.2+ | +1.3+ | +-1.3+
4+d|Rows: 1

|===

======

.+round() with precision and FLOOR rounding mode+
======

.Query
[source, cypher, indent=0]
----
RETURN round(1.249, 1, 'FLOOR') AS positive,
round(-1.251, 1, 'FLOOR') AS negative,
round(1.25, 1, 'FLOOR') AS positiveTie,
round(-1.35, 1, 'FLOOR') AS negativeTie
----

The rounded values using precision 1 and rounding mode `FLOOR` are returned.

.Result
[role="queryresult",options="header,footer",cols="4*<m"]
|===

| +positive+ | +negative+ | +positiveTie+ | +negativeTie+
| +1.2+ | +-1.3+ | +1.2+ | +-1.4+
4+d|Rows: 1

|===

======

.+round() with precision and HALF_UP rounding mode+
======

.Query
[source, cypher, indent=0]
----
RETURN round(1.249, 1, 'HALF_UP') AS positive,
round(-1.251, 1, 'HALF_UP') AS negative,
round(1.25, 1, 'HALF_UP') AS positiveTie,
round(-1.35, 1, 'HALF_UP') AS negativeTie
----

The rounded values using precision 1 and rounding mode `HALF_UP` are returned.

.Result
[role="queryresult",options="header,footer",cols="4*<m"]
|===

| +positive+ | +negative+ | +positiveTie+ | +negativeTie+
| +1.2+ | +-1.3+ | +1.3+ | +-1.4+
4+d|Rows: 1

|===

======
.+round() with precision and HALF_DOWN rounding mode+
======

.Query
[source, cypher, indent=0]
----
RETURN round(1.249, 1, 'HALF_DOWN') AS positive,
round(-1.251, 1, 'HALF_DOWN') AS negative,
round(1.25, 1, 'HALF_DOWN') AS positiveTie,
round(-1.35, 1, 'HALF_DOWN') AS negativeTie
----

The rounded values using precision 1 and rounding mode `HALF_DOWN` are returned.

.Result
[role="queryresult",options="header,footer",cols="4*<m"]
|===

| +positive+ | +negative+ | +positiveTie+ | +negativeTie+
| +1.2+ | +-1.3+ | +1.2+ | +-1.3+
4+d|Rows: 1

|===

======

.+round() with precision and HALF_EVEN rounding mode+
======

.Query
[source, cypher, indent=0]
----
RETURN round(1.249, 1, 'HALF_EVEN') AS positive,
round(-1.251, 1, 'HALF_EVEN') AS negative,
round(1.25, 1, 'HALF_EVEN') AS positiveTie,
round(-1.35, 1, 'HALF_EVEN') AS negativeTie
----

The rounded values using precision 1 and rounding mode `HALF_EVEN` are returned.

.Result
[role="queryresult",options="header,footer",cols="4*<m"]
|===

| +positive+ | +negative+ | +positiveTie+ | +negativeTie+
| +1.2+ | +-1.3+ | +1.2+ | +-1.4+
4+d|Rows: 1

|===

======

[[functions-sign]]
== sign()

`sign()` returns the signum of the given number: `0` if the number is `0`, `-1` for any negative number, and `1` for any positive number.

*Syntax:*

[source, syntax, role="noheader"]
----
sign(expression)
----

*Returns:*

|===

| `INTEGER`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| A numeric expression.

|===

*Considerations:*

|===

| `sign(null)` returns `null`.

|===


.+sign()+
======

.Query
[source, cypher, indent=0]
----
RETURN sign(-17), sign(0.1)
----

The signs of `-17` and `0.1` are returned.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===

| +sign(-17)+ | +sign(0.1)+
| +-1+ | +1+
2+d|Rows: 1

|===

======

