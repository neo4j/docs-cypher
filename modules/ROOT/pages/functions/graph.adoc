[[query-functions-graph]]
= Graph functions
:description: Graph functions provide information about the constituent graphs in composite databases
:test-skip: true
:table-caption!:

[[functions-graph-names]]
== graph.names()

.Details
|===
| *Syntax* 3+| `graph.names()`
| *Description* 3+| Lists the names of graphs in the current database.
| *Returns* 3+| `LIST<STRING>`
|===

.Considerations
|===
| `graph.names()` is only supported on link:{neo4j-docs-base-uri}/operations-manual/{page-version}/composite-databases[composite databases].
|===

.+graph.names()+
======
.Setup
[source, cypher, indent=0]
----
CREATE DATABASE dba;
CREATE DATABASE dbb;
CREATE DATABASE dbc;
CREATE COMPOSITE DATABASE composite;
CREATE ALIAS composite.first FOR DATABASE dba;
CREATE ALIAS composite.second FOR DATABASE dbb;
CREATE ALIAS composite.third FOR DATABASE dbc;
----

.Query
// tag::functions_graph_names[]
[source, cypher, indent=0]
----
RETURN graph.names() AS name
----
// end::functions_graph_names[]

The names of all graphs on the current composite database are returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +name+
| "composite.first"
| "composite.second"
| "composite.third"
1+d|Rows: 3

|===

======

[[functions-graph-propertiesByName]]
== graph.propertiesByName()

.Details
|===
| *Syntax* 3+| `graph.propertiesByName(graphName)`
| *Description* 3+| Returns the `MAP` of properties associated with a graph.
.2+| *Arguments* | *Name* | *Type* | *Description*
| `graphName` | `STRING` | The name of the graph from which all associated properties will be returned.
| *Returns* 3+| `MAP`
|===

.Considerations
|===
| `graph.propertiesByName()` is only supported on link:{neo4j-docs-base-uri}/operations-manual/{page-version}/composite-databases[composite databases].
| The properties in the returned `MAP` are set on the link:{neo4j-docs-base-uri}/operations-manual/{page-version}/database-administration/aliases/manage-aliases-standard-databases/[alias]that adds the graph as a constituent of a composite database.
|===

.+graph.propertiesByName()+
======
.Setup
[source, cypher, indent=0]
----
CREATE DATABASE dba;
CREATE DATABASE dbb;
CREATE DATABASE dbc;
CREATE COMPOSITE DATABASE composite;
CREATE ALIAS composite.first FOR DATABASE dba
  PROPERTIES {number: 1, tags: ['A', 'B']};
CREATE ALIAS composite.second FOR DATABASE dbb
  PROPERTIES {number: 0, tags: ['A']};
CREATE ALIAS composite.third FOR DATABASE dbc
  PROPERTIES {number: 2, tags: ['B', 'C']};
----

.Query
// tag::functions_graph_properties_by_name[]
[source, cypher, indent=0]
----
UNWIND graph.names() AS name
RETURN name, graph.propertiesByName(name) AS props
----
// end::functions_graph_properties_by_name[]

Properties for all graphs on the current composite database are returned.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===

| +name+ | +props+
| "composite.first"  | {number: 1, tags: ["A", "B"]}
| "composite.second" | {number: 0, tags: ["A"]}
| "composite.third"  | {number: 2, tags: ["B", "C"]}
2+d|Rows: 3

|===

.Query
[source, cypher, indent=0]
----
UNWIND graph.names() AS name
WITH name, graph.propertiesByName(name) AS props
WHERE "A" IN props.tags
CALL {
  USE graph.byName(name)
  MATCH (n)
  RETURN n
}
RETURN n
----

Returns all nodes from a subset of graphs that have a `tags` property containing `"A"`.

======

[[functions-graph-byname]]
== graph.byName()

.Details
|===
| *Syntax* 3+| `graph.byName(name)`
| *Description* 3+| Returns the graph reference of the given name. It is only supported in the `USE` clause, on composite databases.
.2+| *Arguments* | *Name* | *Type* | *Description*
| `name` | `STRING` | The name of the graph to be resolved.
| *Returns* 3+| `GRAPH`
|===

.+graph.byName()+
======

.Query
// tag::functions_graph_by_name[]
[source, cypher, role=noplay]
----
UNWIND graph.names() AS graphName
CALL {
  USE graph.byName(graphName)
  MATCH (n)
  RETURN n
}
RETURN n
----
// end::functions_graph_by_name[]

Returns all nodes from all graphs on the current composite database.

======

[role=label--new-5.13]
[[functions-graph-by-elementid]]
== graph.byElementId()

.Details
|===
| *Syntax* 3+| `graph.byElementId(elementId)`
| *Description* 3+| Returns the graph reference with the given element id. It is only supported in the `USE` clause, on composite databases.
.2+| *Arguments* | *Name* | *Type* | *Description*
| `elementId` | `STRING` | An element id of a node or relationship.
| *Returns* 3+| `GRAPH`
|===

.Considerations
|===
| If the constituent database is not a standard database in the DBMS, an error will be thrown.
|===

.+graph.byElementId()+
======

In this example, it is assumed that the DBMS contains a composite database constituent, which contains the element id `4:c0a65d96-4993-4b0c-b036-e7ebd9174905:0`.

.Query
// tag::functions_graph_by_element_id[]
[source, cypher, role=test-skip]
----
USE graph.byElementId("4:c0a65d96-4993-4b0c-b036-e7ebd9174905:0")
MATCH (n) RETURN n
----
// end::functions_graph_by_element_id[]

======
