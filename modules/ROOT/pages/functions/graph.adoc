[[query-functions-graph]]
= Graph functions
:description: Graph functions provide information about the constituent graphs in composite databases
:test-skip: true

[[functions-graph-names]]
== graph.names()

Returns a list containing the names of all graphs on the current composite database.
It is only supported on link:{neo4j-docs-base-uri}/operations-manual/{page-version}/composite-databases[composite databases].

.+graph.names()+
======
.Setup
[source, cypher, indent=0]
----
CREATE DATABASE dba;
CREATE DATABASE dbb;
CREATE DATABASE dbc;
CREATE COMPOSITE DATABASE composite;
CREATE ALIAS composite.first FOR DATABASE dba;
CREATE ALIAS composite.second FOR DATABASE dbb;
CREATE ALIAS composite.third FOR DATABASE dbc;
----

.Query
[source, cypher, indent=0]
----
RETURN graph.names() AS name
----

The names of all graphs on the current composite database are returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +name+
| "composite.first"
| "composite.second"
| "composite.third"
1+d|Rows: 3

|===

======

[[functions-graph-propertiesByName]]
== graph.propertiesByName()

Returns a map containing the properties associated with the given graph. The properties are set on the link:{neo4j-docs-base-uri}/operations-manual/{page-version}/database-administration/aliases/manage-aliases-standard-databases/[alias]that adds the graph as a constituent of a composite database.
It is only supported on link:{neo4j-docs-base-uri}/operations-manual/{page-version}/composite-databases[composite databases].

.Syntax
[source, syntax, role="noheader"]
----
graph.propertiesByName(name)
----

*Returns:*

|===

| `MAP`

|===

*Arguments:*

[options="header", cols="m,m,2a"]
|===
| Name | Type | Description

| name
| STRING
| The name of the graph from which all associated properties will be returned.

|===

.+graph.propertiesByName()+
======
.Setup
[source, cypher, indent=0]
----
CREATE DATABASE dba;
CREATE DATABASE dbb;
CREATE DATABASE dbc;
CREATE COMPOSITE DATABASE composite;
CREATE ALIAS composite.first FOR DATABASE dba
  PROPERTIES {number: 1, tags: ['A', 'B']};
CREATE ALIAS composite.second FOR DATABASE dbb
  PROPERTIES {number: 0, tags: ['A']};
CREATE ALIAS composite.third FOR DATABASE dbc
  PROPERTIES {number: 2, tags: ['B', 'C']};
----

.Query
[source, cypher, indent=0]
----
UNWIND graph.names() AS name
RETURN name, graph.propertiesByName(name) AS props
----

Properties for all graphs on the current composite database are returned.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===

| +name+ | +props+
| "composite.first"  | {number: 1, tags: ["A", "B"]}
| "composite.second" | {number: 0, tags: ["A"]}
| "composite.third"  | {number: 2, tags: ["B", "C"]}
2+d|Rows: 3

|===

.Query
[source, cypher, indent=0]
----
UNWIND graph.names() AS name
WITH name, graph.propertiesByName(name) AS props
WHERE "A" IN props.tags
CALL {
  USE graph.byName(name)
  MATCH (n)
  RETURN n
}
RETURN n
----

Returns all nodes from a subset of graphs that have a `tags` property containing `"A"`.

======

[[functions-graph-byname]]
== graph.byName()

Resolves a constituent graph by name.
It is only supported in the xref:clauses/use.adoc[`USE` clause], on link:{neo4j-docs-base-uri}/operations-manual/{page-version}/composite-databases[composite databases].

.Syntax
[source, syntax, role="noheader"]
----
USE graph.byName(name :: STRING)
----

*Returns:*

|===

| `GRAPH`

|===

*Arguments:*

[options="header", cols="m,m,2a"]
|===
| Name | Type | Description

| name
| STRING
| The name of the graph to be resolved.

|===

.+graph.byName()+
======

.Query
[source, cypher, indent=0]
----
UNWIND graph.names() AS graphName
CALL {
  USE graph.byName(graphName)
  MATCH (n)
  RETURN n
}
RETURN n
----

Returns all nodes from all graphs on the current composite database.

======

[role=label--new-5.13]
[[functions-graph-by-elementid]]
== graph.byElementId()

The `graph.byElementId()` function is used in the xref:clauses/use.adoc[] clause to resolve a constituent graph to which a given element id belongs.
If the constituent database is not a standard database in the DBMS, an error will be thrown.

.Syntax
[source, syntax, role="noheader"]
----
USE graph.byElementId(elementId)
----

*Returns:*

|===

| `GRAPH`

|===

*Arguments:*


[options="header", cols="m,m,2a"]
|===
| Name | Type | Description

| elementId
| STRING
| An element id of a node or relationship.

|===

.+graph.byElementId()+
======

In this example, it is assumed that the DBMS contains a composite database constituent, which contains the element id `4:c0a65d96-4993-4b0c-b036-e7ebd9174905:0`.

.Query
[source, cypher, role=test-skip]
----
USE graph.byElementId("4:c0a65d96-4993-4b0c-b036-e7ebd9174905:0")
MATCH (n) RETURN n
----

======
