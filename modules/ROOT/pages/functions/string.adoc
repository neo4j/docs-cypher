:description: String functions all operate on string expressions only, and will return an error if used on any other values.
:table-caption!:

[[query-functions-string]]
= String functions

String functions operate on string expressions only, and will return an error if used on any other values.
The exception to this rule is `toString()`, which also accepts numbers, booleans and temporal values (i.e. `DATE`, `ZONED TIME`` `LOCAL TIME`, `ZONED DATETIME`, `LOCAL DATETIME`  or `DURATION` values).

Functions taking a `STRING` as input all operate on _Unicode characters_ rather than on a standard `char[]`.
For example, the `size()` function applied to any _Unicode character_ will return `1`, even if the character does not fit in the 16 bits of one `char`.

[NOTE]
====
When `toString()` is applied to a temporal value, it returns a `STRING` representation suitable for parsing by the corresponding xref::functions/temporal/index.adoc[temporal functions].
This `STRING` will therefore be formatted according to the https://en.wikipedia.org/wiki/ISO_8601[ISO 8601] format.
====

See also xref::syntax/operators.adoc#query-operators-string[String operators].

[role=label--new-5.20]
[[functions-btrim]]
== btrim()

.Details
|===
| *Syntax* 3+| `btrim(input[, trimCharacterString])`
| *Description* 3+| Returns the given `STRING` with leading and trailing whitespace removed, optionally specifying a `trimCharacterString` to remove.
.3+| *Arguments* | *Name* | *Type* | *Description*
| `input` | `STRING` | A value from which the leading and trailing trim character will be removed.
| `trimCharacterString` | `STRING` | A character to be removed from the start and end of the given string.
| *Returns* 3+| `STRING`
|===

.Considerations
|===

| `btrim(null)` returns `null`.
| `btrim(null, null)` returns `null`.
| `btrim("hello", null)` returns `null`.
| `btrim(null, ' ')` returns `null`.
| If `trimCharacterString` is not specified then all leading and trailing whitespace will be removed.

|===


.btrim()
======

.Query
[source, cypher, indent=0]
----
RETURN btrim('   hello    '), btrim('xxyyhelloxyxy', 'xy')
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===

| btrim('   hello') | btrim('xxyyhelloxyxy', 'xy')
| "hello"           | "hello"
2+d|Rows: 1

|===

======

[[functions-left]]
== left()

.Details
|===
| *Syntax* 3+| `left(original, length)`
| *Description* 3+| Returns a `STRING` containing the specified number (`INTEGER`) of leftmost characters in the given `STRING`.
.3+| *Arguments* | *Name* | *Type* | *Description*
| `original` | `STRING` | A string value whose rightmost characters will be trimmed.
| `length` | `INTEGER` | The length of the leftmost characters to be returned.
| *Returns* 3+| `STRING`
|===

.Considerations
|===

| `left(null, length)` return `null`.
| `left(null, null)` return `null`.
| `left(original, null)` will raise an error.
// Should be: If `length` is a negative integer, an error is raised.
| If `length` is not a positive `INTEGER`, an error is raised.
| If `length` exceeds the size of `original`, `original` is returned.

|===

.+left()+
======

.Query
[source, cypher, indent=0]
----
RETURN left('hello', 3)
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| left('hello', 3)
| "hel"
1+d|Rows: 1

|===

======

[role=label--new-5.21]
[[functions-lower]]
== lower()

.Details
|===
| *Syntax* 3+| `lower(input)`
| *Description* 3+| Returns the given `STRING` in lowercase.
.2+| *Arguments* | *Name* | *Type* | *Description*
| `input` | `STRING` | A string to be converted into lowercase.
| *Returns* 3+| `STRING`
|===

This function is an alias to the xref:functions/string.adoc#functions-tolower[`toLower()`] function, and it was introduced as part of Cypher's xref:appendix/gql-conformance/index.adoc[].

.Considerations
|===

| `lower(null)` returns `null`.

|===


.+lower()+
======

.Query
[source, cypher, indent=0]
----
RETURN lower('HELLO')
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| lower('HELLO')
| "hello"
1+d|Rows: 1
|===

======


[[functions-ltrim]]
== ltrim()

.Details
|===
| *Syntax* 3+| `ltrim(input[, trimCharacterString])`
| *Description* 3+| Returns the given `STRING` with leading whitespace removed, optionally specifying a `trimCharacterString` to remove.
.3+| *Arguments* | *Name* | *Type* | *Description*
| `input` | `STRING` | A value from which the leading trim character will be removed.
| `trimCharacterString` | `STRING` | A character to be removed from the start of the given string.
| *Returns* 3+| `STRING`
|===

.Considerations
|===

| `ltrim(null)` returns `null`.
| `ltrim(null, null)` returns `null`.
| `ltrim("hello", null)` returns `null`.
| `ltrim(null, ' ')` returns `null`.
| As of Neo4j 5.20, a `trimCharacterString` can be specified.
If this is not specified all leading whitespace will be removed.

|===


.+ltrim()+
======

.Query
[source, cypher, indent=0]
----
RETURN ltrim('   hello'), ltrim('xxyyhelloxyxy', 'xy')
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===

| ltrim('   hello') | ltrim('xxyyhelloxyxy', 'xy')
| "hello"           | "helloxyxy"
2+d|Rows: 1

|===

======


[role=label--new-5.17]
[[functions-normalize]]
== normalize()

.Details
|===
| *Syntax* 3+| `normalize(input[, normalForm])`
| *Description* 3+| Normalize a `STRING`, optionally specifying a normalization form.
.3+| *Arguments* | *Name* | *Type* | *Description*
| `input` | `STRING` | A value to be normalized.
| `normalForm` | `[NFC, NFD, NFKC, NFKD]` | A keyword specifying any of the normal forms; NFC, NFD, NFKC or NFKD.
| *Returns* 3+| `STRING`
|===

[NOTE]
====
Unicode normalization is a process that transforms different representations of the same string into a standardized form.
For more information, see the documentation for link:https://unicode.org/reports/tr15/#Norm_Forms[Unicode normalization forms].
====

The `normalize()` function is useful for converting `STRING` values into comparable forms.
When comparing two `STRING` values, it is their Unicode codepoints that are compared.
In Unicode, a codepoint for a character that looks the same may be represented by two, or more, different codepoints.
For example, the character `<` can be represented as `\uFE64` (﹤) or `\u003C` (<).
To the human eye, the characters may appear identical.
However,  if compared, Cypher will return false as `\uFE64` does not equal `\u003C`.
Using the `normalize()` function, it is possible to
normalize the codepoint `\uFE64` to `\u003C`, creating a single codepoint representation, allowing them to be successfully compared.

.Considerations
|===

| `normalize(null)` returns `null`.

|===


.+normalize()+
======

.Query
[source, cypher, indent=0]
----
RETURN normalize('\u212B') = '\u00C5' AS result
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| result
| true
1+d|Rows: 1

|===

======

To check if a `STRING` is normalized, use the xref:syntax/operators.adoc#match-string-is-normalized[`IS NORMALIZED`] operator.

[[functions-normalize-with-normal-form]]
=== normalize() with specified normal form

There are two main types of normalization forms:

*  *Canonical equivalence*: The `NFC` (default) and `NFD` are forms of canonical equivalence.
This means that codepoints that represent the same abstract character will
be normalized to the same codepoint (and have the same appearance and behavior).
The `NFC` form will always give the *composed* canonical form (in which the combined codes are replaced with a single representation, if possible).
The`NFD` form gives the *decomposed* form (the opposite of the composed form, which converts the combined codepoints into a split form if possible).

* *Compatability normalization*: `NFKC` and `NFKD` are forms of compatibility normalization.
All canonically equivalent sequences are compatible, but not all compatible sequences are canonical.
This means that a character normalized in `NFC` or `NFD` should also be normalized in `NFKC` and `NFKD`.
Other characters with only slight differences in appearance should be compatibly equivalent.

For example, the Greek Upsilon with Acute and Hook Symbol `ϓ` can be represented by the Unicode codepoint: `\u03D3`.

* Normalized in `NFC`: `\u03D3` Greek Upsilon with Acute and Hook Symbol (ϓ)
* Normalized in `NFD`: `\u03D2\u0301` Greek Upsilon with Hook Symbol + Combining Acute Accent (ϓ)
* Normalized in `NFKC`: `\u038E` Greek Capital Letter Upsilon with Tonos (Ύ)
* Normalized in `NFKD`: `\u03A5\u0301` Greek Capital Letter Upsilon + Combining Acute Accent (Ύ)

In the compatibility normalization forms (`NFKC` and `NFKD`) the character is visibly different as it no longer contains the hook symbol.

.+normalize()+ with specified normalization form
======

.Query
[source, cypher, indent=0]
----
RETURN normalize('\uFE64', NFKC) = '\u003C' AS result
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| result
| true
1+d|Rows: 1

|===

======


[[functions-replace]]
== replace()

.Details
|===
| *Syntax* 3+| `replace(original, search, replace)`
| *Description* 3+| Returns a `STRING` in which all occurrences of a specified search `STRING` in the given `STRING` have been replaced by another (specified) replacement `STRING`.
.4+| *Arguments* | *Name* | *Type* | *Description*
| `original` | `STRING` | The string to be modified.
| `search` | `STRING` | The value to replace in the original string.
| `replace` | `STRING` | The value to be inserted in the original string.
| *Returns* 3+| `STRING`
|===

.Considerations
|===

| If any argument is `null`, `null` will be returned.
| If `search` is not found in `original`, `original` will be returned.

|===


.+replace()+
======

.Query
[source, cypher, indent=0]
----
RETURN replace("hello", "l", "w")
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| replace("hello", "l", "w")
| "hewwo"
1+d|Rows: 1

|===

======


[[functions-reverse]]
== reverse()

.Details
|===
| *Syntax* 3+| `reverse(input)`
| *Description* 3+| Returns a `STRING` or `LIST<ANY>` in which the order of all characters or elements in the given `STRING` or `LIST<ANY>` have been reversed.
.2+| *Arguments* | *Name* | *Type* | *Description*
| `input` | `STRING \| LIST<ANY>` | The string or list to be reversed.
| *Returns* 3+| `STRING \| LIST<ANY>`
|===

.Considerations
|===

| `reverse(null)` returns `null`.
| See also xref:functions/list.adoc#functions-reverse[List functions -> reverse].

|===


.+reverse+
======

.Query
[source, cypher, indent=0]
----
RETURN reverse('anagram')
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| reverse('anagram')
| "margana"
1+d|Rows: 1

|===

======


[[functions-right]]
== right()

.Details
|===
| *Syntax* 3+| `right(original, length)`
| *Description* 3+| Returns a `STRING` containing the specified number of rightmost characters in the given `STRING`.
.3+| *Arguments* | *Name* | *Type* | *Description*
| `original` | `STRING` | A string value whose leftmost characters will be trimmed.
| `length` | `INTEGER` | The length of the rightmost characters to be returned.
| *Returns* 3+| `STRING`
|===

.Considerations
|===

| `right(null, length)` return `null`.
| `right(null, null)` return `null`.
| `right(original, null)` will raise an error.
// Should be: If `length` is a negative integer, an error is raised.
| If `length` is not a positive `INTEGER`, an error is raised.
| If `length` exceeds the size of `original`, `original` is returned.

|===


.+right()+
======

.Query
[source, cypher, indent=0]
----
RETURN right('hello', 3)
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| right('hello', 3)
| "llo"
1+d|Rows: 1

|===

======


[[functions-rtrim]]
== rtrim()

.Details
|===
| *Syntax* 3+| `rtrim(input[, trimCharacterString])`
| *Description* 3+| Returns the given `STRING` with trailing whitespace characters to remove, optionally specifying a `trimCharacterString` of characters to remove.
.3+| *Arguments* | *Name* | *Type* | *Description*
| `input` | `STRING` | A value from which the leading and trailing trim character will be removed.
| `trimCharacterString` | `STRING` | A character to be removed from the start and end of the given string.
| *Returns* 3+| `STRING`
|===

.Considerations
|===


| `rtrim(null)` returns `null`.
| `rtrim(null, null)` returns `null`.
| `rtrim("hello", null)` returns `null`.
| `rtrim(null, ' ')` returns `null`.
| As of Neo4j 5.20, a `trimCharacterString` can be specified.
If this is not specified all trailing whitespace will be removed.

|===


.+rtrim()+
======

.Query
[source, cypher, indent=0]
----
RETURN rtrim('hello   '), rtrim('xxyyhelloxyxy', 'xy')
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===

| rtrim('hello   ')  | rtrim('xxyyhelloxyxy', 'xy')
| "hello"            | "xxyyhello"
2+d|Rows: 1

|===

======


[[functions-split]]
== split()

.Details
|===
| *Syntax* 3+| `split(original, splitDelimiters)`
| *Description* 3+| Returns a `LIST<STRING>` resulting from the splitting of the given `STRING` around matches of the given delimiter(s).
.3+| *Arguments* | *Name* | *Type* | *Description*
| `original` | `STRING` | The string to be split.
| `splitDelimiters` | `STRING \| LIST<STRING>` | The string with which to split the original string.
| *Returns* 3+| `LIST<STRING>`
|===

.Considerations
|===

| `split(null, splitDelimiter)` return `null`.
| `split(original, null)` return `null`

|===


.+split()+
======

.Query
[source, cypher, indent=0]
----
RETURN split('one,two', ',')
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| split('one,two', ',')
| ["one","two"]
1+d|Rows: 1

|===

======


[[functions-substring]]
== substring()

.Details
|===
| *Syntax* 3+| `substring(original, start, length)`
| *Description* 3+| Returns a substring of a given `length` from the given `STRING`, beginning with a 0-based index start.
.4+| *Arguments* | *Name* | *Type* | *Description*
| `original` | `STRING` | The string to be shortened.
| `start` | `INTEGER` | The start position of the new string.
| `length` | `INTEGER` | The length of the new string.
| *Returns* 3+| `STRING`
|===

.Considerations
|===

| `start` uses a zero-based index.
| If `length` is omitted, the function returns the substring starting at the position given by `start` and extending to the end of `original`.
| If `original` is `null`, `null` is returned.
| If either `start` or `length` is `null` or a negative integer, an error is raised.
| If `start` is `0`, the substring will start at the beginning of `original`.
| If `length` is `0`, the empty `STRING` will be returned.

|===


.+substring()+
======

.Query
[source, cypher, indent=0]
----
RETURN substring('hello', 1, 3), substring('hello', 2)
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===

| substring('hello', 1, 3) | substring('hello', 2)
| "ell" | "llo"
2+d|Rows: 1

|===

======


[[functions-tolower]]
== toLower()

.Details
|===
| *Syntax* 3+| `toLower(input)`
| *Description* 3+| Returns the given `STRING` in lowercase.
.2+| *Arguments* | *Name* | *Type* | *Description*
| `input` | `STRING` | A string to be converted into lowercase.
| *Returns* 3+| `STRING`
|===

.Considerations
|===

| `toLower(null)` returns `null`.

|===


.+toLower()+
======

.Query
[source, cypher, indent=0]
----
RETURN toLower('HELLO')
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| toLower('HELLO')
| "hello"
1+d|Rows: 1
|===

======


[[functions-tostring]]
== toString()

.Details
|===
| *Syntax* 3+| `toString(input)`
| *Description* 3+| Converts an `INTEGER`, `FLOAT`, `BOOLEAN`, `POINT` or temporal type (i.e. `DATE`, `ZONED TIME`, `LOCAL TIME`, `ZONED DATETIME`, `LOCAL DATETIME` or `DURATION`) value to a `STRING`.
.2+| *Arguments* | *Name* | *Type* | *Description*
| `input` | `ANY` | A value to be converted into a string.
| *Returns* 3+| `STRING`
|===

.Considerations
|===

| `toString(null)` returns `null`.
| If `input` is a `STRING`, it will be returned unchanged.
| This function will return an error if provided with an expression that is not an `INTEGER`, `FLOAT`, `BOOLEAN`, `STRING`, `POINT`, `DURATION`, `DATE`, `ZONED TIME`, `LOCAL TIME`, `LOCAL DATETIME` or `ZONED DATETIME` value.

|===


.+toString()+
======

.Query
[source, cypher, indent=0]
----
RETURN
  toString(11.5),
  toString('already a string'),
  toString(true),
  toString(date({year: 1984, month: 10, day: 11})) AS dateString,
  toString(datetime({year: 1984, month: 10, day: 11, hour: 12, minute: 31, second: 14, millisecond: 341, timezone: 'Europe/Stockholm'})) AS datetimeString,
  toString(duration({minutes: 12, seconds: -60})) AS durationString
----

.Result
[role="queryresult",options="header,footer",cols="6*<m"]
|===

| toString(11.5) | toString('already a string') | toString(true) | dateString | datetimeString | durationString
| "11.5" | "already a string" | "true" | "1984-10-11" | "1984-10-11T12:31:14.341+01:00[Europe/Stockholm]" | "PT11M"
6+d|Rows: 1

|===

======


[[functions-tostringornull]]
== toStringOrNull()

.Details
|===
| *Syntax* 3+| `toStringOrNull(input)`
| *Description* 3+| Converts an `INTEGER`, `FLOAT`, `BOOLEAN`, `POINT` or temporal type (i.e. `DATE`, `ZONED TIME`, `LOCAL TIME`, `ZONED DATETIME`, `LOCAL DATETIME` or `DURATION`) value to a `STRING`, or null if the value cannot be converted.
.2+| *Arguments* | *Name* | *Type* | *Description*
| `input` | `ANY` | A value to be converted into a string or null.
| *Returns* 3+| `STRING`
|===

.Considerations
|===
| `toStringOrNull(null)` returns `null`.
| If the `expression` is not an `INTEGER`, `FLOAT`, `BOOLEAN`, `STRING`, `POINT`, `DURATION`, `DATE`, `ZONED TIME`, `LOCAL TIME`, `LOCAL DATETIME` or `ZONED DATETIME` value, `null` will be returned.
|===


.+toStringOrNull()+
======

.Query
[source, cypher, indent=0]
----
RETURN toStringOrNull(11.5),
toStringOrNull('already a string'),
toStringOrNull(true),
toStringOrNull(date({year: 1984, month: 10, day: 11})) AS dateString,
toStringOrNull(datetime({year: 1984, month: 10, day: 11, hour: 12, minute: 31, second: 14, millisecond: 341, timezone: 'Europe/Stockholm'})) AS datetimeString,
toStringOrNull(duration({minutes: 12, seconds: -60})) AS durationString,
toStringOrNull(['A', 'B', 'C']) AS list
----

.Result
[role="queryresult",options="header,footer",cols="7*<m"]
|===

| toStringOrNull(11.5) | toStringOrNull('already a string') | toStringOrNull(true) | dateString | datetimeString | durationString | list
| "11.5" | "already a string" | "true" | "1984-10-11" | "1984-10-11T12:31:14.341+01:00[Europe/Stockholm]" | "PT11M" | <null>
7+d|Rows: 1

|===

======


[[functions-toupper]]
== toUpper()

.Details
|===
| *Syntax* 3+| `toUpper(input)`
| *Description* 3+| Returns the given `STRING` in uppercase.
.2+| *Arguments* | *Name* | *Type* | *Description*
| `input` | `STRING` | A string to be converted into uppercase.
| *Returns* 3+| `STRING`
|===

.Considerations
|===

| `toUpper(null)` returns `null`.

|===


.+toUpper()+
======

.Query
[source, cypher, indent=0]
----
RETURN toUpper('hello')
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| toUpper('hello')
| "HELLO"
1+d|Rows: 1

|===

======


[[functions-trim]]
== trim()

.Details
|===
| *Syntax* 3+| `trim(trimSpecification, trimCharacterString, input)`
| *Description* 3+| Returns the given `STRING` with leading and/or trailing `trimCharacterString` removed.
.4+| *Arguments* | *Name* | *Type* | *Description*
| `trimSpecification` | `[LEADING, TRAILING, BOTH]` | The parts of the string to trim; LEADING, TRAILING, BOTH
| `trimCharacterString` | `STRING` | The characters to be removed from the start and/or end of the given string.
| `input` | `STRING` | A value from which all leading and/or trailing trim characters will be removed.
| *Returns* 3+| `STRING`
|===

.Considerations
|===

| `trim(null)` returns `null`.
| `trim(null FROM "hello")` returns `null`.
| `trim(" " FROM null)` returns `null`.
| `trim(BOTH null FROM null)` returns `null`.
| As of Neo4j 5.20, a `trimCharacterString` can be specified.
If this is not specified all leading and/or trailing whitespace will be removed.

|===


.+trim()+
======

.Query
[source, cypher, indent=0]
----
RETURN trim('   hello   '), trim(BOTH 'x' FROM 'xxxhelloxxx')
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===

| trim('   hello   ') | trim(BOTH 'x' FROM 'xxxhelloxxx')
| "hello"             | "hello"
2+d|Rows: 1

|===

======

[role=label--new-5.21]
[[functions-upper]]
== upper()

.Details
|===
| *Syntax* 3+| `upper(input)`
| *Description* 3+| Returns the given `STRING` in uppercase.
.2+| *Arguments* | *Name* | *Type* | *Description*
| `input` | `STRING` | A string to be converted into uppercase.
| *Returns* 3+| `STRING`
|===

This function is an alias to the xref:functions/string.adoc#functions-toupper[`toUpper()`] function, and it was introduced as part of Cypher's xref:appendix/gql-conformance/index.adoc[].


.Considerations
|===

| `upper(null)` returns `null`.

|===


.+upper()+
======

.Query
[source, cypher, indent=0]
----
RETURN upper('hello')
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| upper('hello')
| "HELLO"
1+d|Rows: 1

|===

======

