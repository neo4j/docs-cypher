:description: String functions all operate on string expressions only, and will return an error if used on any other values.

[[query-functions-string]]
= String functions


String functions operate on string expressions only, and will return an error if used on any other values.
The exception to this rule is `toString()`, which also accepts numbers, booleans and temporal values (i.e. `DATE`, `ZONED TIME`` `LOCAL TIME`, `ZONED DATETIME`, `LOCAL DATETIME`  or `DURATION` values).

Functions taking a `STRING` as input all operate on _Unicode characters_ rather than on a standard `char[]`.
For example, the `size()` function applied to any _Unicode character_ will return `1`, even if the character does not fit in the 16 bits of one `char`.

[NOTE]
====
When `toString()` is applied to a temporal value, it returns a `STRING` representation suitable for parsing by the corresponding xref::functions/temporal/index.adoc[temporal functions].
This `STRING` will therefore be formatted according to the https://en.wikipedia.org/wiki/ISO_8601[ISO 8601] format.
====

See also xref::syntax/operators.adoc#query-operators-string[String operators].

[role=label--new-5.19]
[[functions-btrim]]
== btrim()

`btrim()` returns the original `STRING` with leading and trailing `trimCharacterString` characters removed.
If `trimCharacterString` is not specified then all leading and trailing whitespace will be removed.

*Syntax:*

[source, syntax, role="noheader"]
----
btrim(original [, trimCharacterString])
----

*Returns:*

|===

| `STRING`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `original`
| An expression that returns a `STRING`.

| `trimCharacterString`
| An expression that returns a `STRING`.

|===

*Considerations:*

|===

| `btrim(null)` returns `null`.
| `btrim(null, null)` returns `null`.
| `btrim("hello", null)` returns `null`.
| `btrim(null, ' ')` returns `null`.

|===


.btrim()
======

.Query
[source, cypher, indent=0]
----
RETURN btrim('   hello    '), btrim('xxyyhelloxyxy', 'xy')
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===

| btrim('   hello') | btrim('xxyyhelloxyxy', 'xy')
| "hello"           | "hello"
2+d|Rows: 1

|===

======

[[functions-left]]
== left()

`left()` returns a `STRING` containing the specified number of leftmost characters of the given `STRING`.

*Syntax:*

[source, syntax, role="noheader"]
----
left(original, length)
----

*Returns:*

|===

| `STRING`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `original`
| An expression that returns a `STRING`.

| `length`
| An expression that returns a positive `INTEGER`.

|===

*Considerations:*

|===

| `left(null, length)` return `null`.
| `left(null, null)` return `null`.
| `left(original, null)` will raise an error.
// Should be: If `length` is a negative integer, an error is raised.
| If `length` is not a positive `INTEGER`, an error is raised.
| If `length` exceeds the size of `original`, `original` is returned.

|===

.+left()+
======

.Query
[source, cypher, indent=0]
----
RETURN left('hello', 3)
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +left('hello', 3)+
| +"hel"+
1+d|Rows: 1

|===

======


[[functions-ltrim]]
== ltrim()

`ltrim()` returns the original `STRING` with leading `trimCharacterString` characters removed.
As of Neo4j 5.19, a `trimCharacterString` can be specified.
If this is not specified all leading whitespace will be removed.

*Syntax:*

[source, syntax, role="noheader"]
----
ltrim(original [, trimCharacterString])
----

*Returns:*

|===

| `STRING`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `original`
| An expression that returns a `STRING`.

| `trimCharacterString`
| An expression that returns a `STRING`.

|===

*Considerations:*

|===

| `ltrim(null)` returns `null`.
| `ltrim(null, null)` returns `null`.
| `ltrim("hello", null)` returns `null`.
| `ltrim(null, ' ')` returns `null`.

|===


.+ltrim()+
======

.Query
[source, cypher, indent=0]
----
RETURN ltrim('   hello'), ltrim('xxyyhelloxyxy', 'xy')
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===

| ltrim('   hello') | ltrim('xxyyhelloxyxy', 'xy')
| "hello"           | "helloxyxy"
2+d|Rows: 1

|===

======


[role=label--new-5.17]
[[functions-normalize]]
== normalize()

`normalize()` returns the given `STRING` normalized using the `NFC` Unicode normalization form.

[NOTE]
====
Unicode normalization is a process that transforms different representations of the same string into a standardized form.
For more information, see the documentation for link:https://unicode.org/reports/tr15/#Norm_Forms[Unicode normalization forms].
====

The `normalize()` function is useful for converting `STRING` values into comparable forms.
When comparing two `STRING` values, it is their Unicode codepoints that are compared.
In Unicode, a codepoint for a character that looks the same may be represented by two, or more, different codepoints.
For example, the character `<` can be represented as `\uFE64` (﹤) or `\u003C` (<).
To the human eye, the characters may appear identical.
However,  if compared, Cypher will return false as `\uFE64` does not equal `\u003C`.
Using the `normalize()` function, it is possible to
normalize the codepoint `\uFE64` to `\u003C`, creating a single codepoint representation, allowing them to be successfully compared.

*Syntax:*

[source, syntax, role="noheader"]
----
normalize(input)
----

*Returns:*

|===

| `STRING`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `input`
| An expression that returns a `STRING`.

|===

*Considerations:*

|===

| `normalize(null)` returns `null`.

|===


.+normalize()+
======

.Query
[source, cypher, indent=0]
----
RETURN normalize('\u212B') = '\u00C5' AS result
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +result+
| +true+
1+d|Rows: 1

|===

======

To check if a `STRING` is normalized, use the xref:syntax/operators.adoc#match-string-is-normalized[`IS NORMALIZED`] operator.

[role=label--new-5.17]
[[functions-normalize-with-normal-form]]
== normalize(), with specified normal form

`normalize()` returns the given `STRING` normalized using the specified normalization form.
The normalization form can be of type `NFC`, `NFD`, `NFKC` or `NFKD`.

There are two main types of normalization forms:

*  *Canonical equivalence*: The `NFC` (default) and `NFD` are forms of canonical equivalence.
This means that codepoints that represent the same abstract character will
be normalized to the same codepoint (and have the same appearance and behavior).
The `NFC` form will always give the *composed* canonical form (in which the combined codes are replaced with a single representation, if possible).
The`NFD` form gives the *decomposed* form (the opposite of the composed form, which converts the combined codepoints into a split form if possible).

* *Compatability normalization*: `NFKC` and `NFKD` are forms of compatibility normalization.
All canonically equivalent sequences are compatible, but not all compatible sequences are canonical.
This means that a character normalized in `NFC` or `NFD` should also be normalized in `NFKC` and `NFKD`.
Other characters with only slight differences in appearance should be compatibly equivalent.

For example, the Greek Upsilon with Acute and Hook Symbol `ϓ` can be represented by the Unicode codepoint: `\u03D3`.

* Normalized in `NFC`: `\u03D3` Greek Upsilon with Acute and Hook Symbol (ϓ)
* Normalized in `NFD`: `\u03D2\u0301` Greek Upsilon with Hook Symbol + Combining Acute Accent (ϓ)
* Normalized in `NFKC`: `\u038E` Greek Capital Letter Upsilon with Tonos (Ύ)
* Normalized in `NFKD`: `\u03A5\u0301` Greek Capital Letter Upsilon + Combining Acute Accent (Ύ)

In the compatibility normalization forms (`NFKC` and `NFKD`) the character is visibly different as it no longer contains the hook symbol.

*Syntax:*

[source, syntax, role="noheader"]
----
normalize(input, normalForm)
----

*Returns:*

|===

| `STRING`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `input`
| An expression that returns a `STRING`.


| `normalForm`
| A keyword specifying the normal form, can be `NFC`, `NFD`, `NFKC` or `NFKD`.

|===

*Considerations:*

|===

| `normalize(null, NFC)` returns `null`.

|===


.+normalize()+
======

.Query
[source, cypher, indent=0]
----
RETURN normalize('\uFE64', NFKC) = '\u003C' AS result
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +result+
| +true+
1+d|Rows: 1

|===

======

To check if a `STRING` is normalized in a specific Unicode normal form, use the xref:syntax/operators.adoc#match-string-is-normalized-specified-normal-form[`IS NORMALIZED`] operator with a specified normalization form.

[[functions-replace]]
== replace()

`replace()` returns a `STRING` in which all occurrences of a specified `STRING` in the given `STRING` have been replaced by another (specified) replacement `STRING`.

*Syntax:*

[source, syntax, role="noheader"]
----
replace(original, search, replace)
----

*Returns:*

|===

| `STRING`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `original`
| An expression that returns a `STRING`.

| `search`
| An expression that specifies the `STRING` to be replaced in `original`.

| `replace`
| An expression that specifies the replacement `STRING`.

|===

*Considerations:*

|===

| If any argument is `null`, `null` will be returned.
| If `search` is not found in `original`, `original` will be returned.

|===


.+replace()+
======

.Query
[source, cypher, indent=0]
----
RETURN replace("hello", "l", "w")
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +replace("hello", "l", "w")+
| +"hewwo"+
1+d|Rows: 1

|===

======


[[functions-reverse]]
== reverse()

`reverse()` returns a `STRING` in which the order of all characters in the given `STRING` have been reversed.

*Syntax:*

[source, syntax, role="noheader"]
----
reverse(original)
----

*Returns:*

|===

| `STRING`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `original`
| An expression that returns a `STRING`.

|===

*Considerations:*

|===

| `reverse(null)` returns `null`.

|===


.+reverse+
======

.Query
[source, cypher, indent=0]
----
RETURN reverse('anagram')
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +reverse('anagram')+
| +"margana"+
1+d|Rows: 1

|===

======


[[functions-right]]
== right()

`right()` returns a `STRING` containing the specified number of rightmost characters in the given `STRING`.

*Syntax:*

[source, syntax, role="noheader"]
----
right(original, length)
----

*Returns:*

|===

| `STRING`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `original`
| An expression that returns a `STRING`.

| `length`
| An expression that returns a positive `INTEGER`.

|===

*Considerations:*

|===

| `right(null, length)` return `null`.
| `right(null, null)` return `null`.
| `right(original, null)` will raise an error.
// Should be: If `length` is a negative integer, an error is raised.
| If `length` is not a positive `INTEGER`, an error is raised.
| If `length` exceeds the size of `original`, `original` is returned.

|===


.+right()+
======

.Query
[source, cypher, indent=0]
----
RETURN right('hello', 3)
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +right('hello', 3)+
| +"llo"+
1+d|Rows: 1

|===

======


[[functions-rtrim]]
== rtrim()

`rtrim()` returns the original `STRING` with trailing `trimCharacterString` characters removed.
As of Neo4j 5.19, a `trimCharacterString` can be specified.
If this is not specified all trailing whitespace will be removed.

*Syntax:*

[source, syntax, role="noheader"]
----
rtrim(original [, trimCharacterString])
----

*Returns:*

|===

| `STRING`

|===

*Arguments:*
[options="header"]
|===
| Name | Description

| `original`
| An expression that returns a `STRING`.

| `trimCharacterString`
| An expression that returns a `STRING`.

|===

*Considerations:*

|===


| `rtrim(null)` returns `null`.
| `rtrim(null, null)` returns `null`.
| `rtrim("hello", null)` returns `null`.
| `rtrim(null, ' ')` returns `null`.

|===


.+rtrim()+
======

.Query
[source, cypher, indent=0]
----
RETURN rtrim('hello   '), rtrim('xxyyhelloxyxy', 'xy')
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===

| rtrim('hello   ')  | rtrim('xxyyhelloxyxy', 'xy')
| "hello"            | "xxyyhello"
2+d|Rows: 1

|===

======


[[functions-split]]
== split()

`split()` returns a `LIST<STRING>` resulting from the splitting of the given `STRING` around matches of the given delimiter.

*Syntax:*

[source, syntax, role="noheader"]
----
split(original, splitDelimiter)
----

*Returns:*

|===

| `LIST<STRING>`

|===

*Arguments:*
[options="header"]
|===
| Name | Description

| `original`
| An expression that returns a `STRING`.

| `splitDelimiter`
| The `STRING` with which to split `original`.

|===

*Considerations:*

|===

| `split(null, splitDelimiter)` return `null`.
| `split(original, null)` return `null`

|===


.+split()+
======

.Query
[source, cypher, indent=0]
----
RETURN split('one,two', ',')
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +split('one,two', ',')+
| +["one","two"]+
1+d|Rows: 1

|===

======


[[functions-substring]]
== substring()

`substring()` returns a substring of the given `STRING`, beginning with a zero-based index start and length.

*Syntax:*

[source, syntax, role="noheader"]
----
substring(original, start [, length])
----

*Returns:*

|===

| `STRING`

|===

*Arguments:*
[options="header"]
|===
| Name | Description

| `original`
| An expression that returns a `STRING`.

| `start`
| An expression that returns a positive `INTEGER`, denoting the position at which the substring will begin.

| `length`
| An expression that returns a positive `INTEGER`, denoting how many characters of `original` will be returned.

|===

*Considerations:*
|===

| `start` uses a zero-based index.
| If `length` is omitted, the function returns the substring starting at the position given by `start` and extending to the end of `original`.
| If `original` is `null`, `null` is returned.
| If either `start` or `length` is `null` or a negative integer, an error is raised.
| If `start` is `0`, the substring will start at the beginning of `original`.
| If `length` is `0`, the empty `STRING` will be returned.

|===


.+substring()+
======

.Query
[source, cypher, indent=0]
----
RETURN substring('hello', 1, 3), substring('hello', 2)
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===

| +substring('hello', 1, 3)+ | +substring('hello', 2)+
| +"ell"+ | +"llo"+
2+d|Rows: 1

|===

======


[[functions-tolower]]
== toLower()

`toLower()` returns the given `STRING` in lowercase.

*Syntax:*

[source, syntax, role="noheader"]
----
toLower(original)
----

*Returns:*

|===

| `STRING`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `original`
| An expression that returns a `STRING`.

|===

*Considerations:*
|===

| `toLower(null)` returns `null`.

|===


.+toLower()+
======

.Query
[source, cypher, indent=0]
----
RETURN toLower('HELLO')
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +toLower('HELLO')+
| +"hello"+
1+d|Rows: 1
|===

======


[[functions-tostring]]
== toString()

`toString()` converts an `INTEGER`, `FLOAT`, `BOOLEAN`, `STRING`, `POINT`, `DURATION`, `DATE`, `ZONED TIME`, `LOCAL TIME`, `LOCAL DATETIME` or `ZONED DATETIME` value to a `STRING`.

*Syntax:*

[source, syntax, role="noheader"]
----
toString(expression)
----

*Returns:*

|===

| `STRING`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| An expression that returns an `INTEGER`, `FLOAT`, `BOOLEAN`, `STRING`, `POINT`, `DURATION`, `DATE`, `ZONED TIME`, `LOCAL TIME`, `LOCAL DATETIME` or `ZONED DATETIME` value.

|===

*Considerations:*

|===

| `toString(null)` returns `null`.
| If `expression` is a `STRING`, it will be returned unchanged.
| This function will return an error if provided with an expression that is not an `INTEGER`, `FLOAT`, `BOOLEAN`, `STRING`, `POINT`, `DURATION`, `DATE`, `ZONED TIME`, `LOCAL TIME`, `LOCAL DATETIME` or `ZONED DATETIME` value.

|===


.+toString()+
======

.Query
[source, cypher, indent=0]
----
RETURN
  toString(11.5),
  toString('already a string'),
  toString(true),
  toString(date({year: 1984, month: 10, day: 11})) AS dateString,
  toString(datetime({year: 1984, month: 10, day: 11, hour: 12, minute: 31, second: 14, millisecond: 341, timezone: 'Europe/Stockholm'})) AS datetimeString,
  toString(duration({minutes: 12, seconds: -60})) AS durationString
----

.Result
[role="queryresult",options="header,footer",cols="6*<m"]
|===

| +toString(11.5)+ | +toString('already a string')+ | +toString(true)+ | +dateString+ | +datetimeString+ | +durationString+
| +"11.5"+ | +"already a string"+ | +"true"+ | +"1984-10-11"+ | +"1984-10-11T12:31:14.341+01:00[Europe/Stockholm]"+ | +"PT11M"+
6+d|Rows: 1

|===

======


[[functions-tostringornull]]
== toStringOrNull()

The function `toStringOrNull()` converts an `INTEGER`, `FLOAT`, `BOOLEAN`, `STRING`, `POINT`, `DURATION`, `DATE`, `ZONED TIME`, `LOCAL TIME`, `LOCAL DATETIME` or `ZONED DATETIME` value to a `STRING`.

*Syntax:*

[source, syntax, role="noheader"]
----
toStringOrNull(expression)
----

*Returns:*

|===

| `STRING` or `null`.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| Any expression that returns a value.

|===

*Considerations:*
|===
| `toStringOrNull(null)` returns `null`.
| If the `expression` is not an `INTEGER`, `FLOAT`, `BOOLEAN`, `STRING`, `POINT`, `DURATION`, `DATE`, `ZONED TIME`, `LOCAL TIME`, `LOCAL DATETIME` or `ZONED DATETIME` value, `null` will be returned.
|===


.+toStringOrNull()+
======

.Query
[source, cypher, indent=0]
----
RETURN toStringOrNull(11.5),
toStringOrNull('already a string'),
toStringOrNull(true),
toStringOrNull(date({year: 1984, month: 10, day: 11})) AS dateString,
toStringOrNull(datetime({year: 1984, month: 10, day: 11, hour: 12, minute: 31, second: 14, millisecond: 341, timezone: 'Europe/Stockholm'})) AS datetimeString,
toStringOrNull(duration({minutes: 12, seconds: -60})) AS durationString,
toStringOrNull(['A', 'B', 'C']) AS list
----

.Result
[role="queryresult",options="header,footer",cols="7*<m"]
|===

| +toStringOrNull(11.5)+ | +toStringOrNull('already a string')+ | +toStringOrNull(true)+ | +dateString+ | +datetimeString+ | +durationString+ | +list+
| +"11.5"+ | +"already a string"+ | +"true"+ | +"1984-10-11"+ | +"1984-10-11T12:31:14.341+01:00[Europe/Stockholm]"+ | +"PT11M"+ | +<null>+
7+d|Rows: 1

|===

======


[[functions-toupper]]
== toUpper()

`toUpper()` returns the given `STRING` in uppercase.

*Syntax:*

[source, syntax, role="noheader"]
----
toUpper(original)
----

*Returns:*

|===

| `STRING`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `original`
| An expression that returns a `STRING`.

|===

*Considerations:*

|===

| `toUpper(null)` returns `null`.

|===


.+toUpper()+
======

.Query
[source, cypher, indent=0]
----
RETURN toUpper('hello')
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +toUpper('hello')+
| +"HELLO"+
1+d|Rows: 1

|===

======


[[functions-trim]]
== trim()

`trim()` returns the given `STRING` with the leading and/or trailing `trimCharacterString` character removed.
As of Neo4j 5.19, a `trimCharacterString` can be specified.
If this is not specified all leading and/or trailing whitespace will be removed.

*Syntax:*

[source, syntax, role="noheader"]
----
trim([[LEADING | TRAILING | BOTH] [trimCharacterString] FROM] original)
----

*Returns:*

|===

| `STRING`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `original`
| An expression that returns a `STRING`.

| `trimCharacterString`
| An expression that returns a single character `STRING`.

|===

*Considerations:*
|===

| `trim(null)` returns `null`.
| `trim(null FROM "hello")` returns `null`.
| `trim(" " FROM null)` returns `null`.
| `trim(BOTH null FROM null)` returns `null`.

|===


.+trim()+
======

.Query
[source, cypher, indent=0]
----
RETURN trim('   hello   '), trim(BOTH 'x' FROM 'xxxhelloxxx')
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===

| trim('   hello   ') | trim(BOTH 'x' FROM 'xxxhelloxxx')
| "hello"             | "hello"
2+d|Rows: 1

|===

======

