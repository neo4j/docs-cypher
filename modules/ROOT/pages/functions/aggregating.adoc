:description: Aggregating functions take a set of values and calculate an aggregated value over them.
include::https://raw.githubusercontent.com/neo4j-graphacademy/courses/main/asciidoc/courses/cypher-aggregation/ad.adoc[]

[[query-functions-aggregating]]
= Aggregating functions

An aggregating function performs a calculation over a set of values, returning a single value.
Aggregation can be computed over all the matching paths, or it can be further divided by introducing xref:functions/aggregating.adoc#grouping-keys[grouping keys].

[TIP]
====
To learn more about how Cypher handles aggregations performed on zero rows, refer to link:https://neo4j.com/developer/kb/understanding-aggregations-on-zero-rows//[Neo4j Knowledge Base -> Understanding aggregations on zero rows].
====

== Example graph

The following graph is used for the examples below:

image::graph_aggregating_functions.svg[]

To recreate the graph, run the following query against an empty Neo4j database:

[source, cypher, role=test-setup]
----
CREATE
  (keanu:Person {name: 'Keanu Reeves', age: 58}),
  (liam:Person {name: 'Liam Neeson', age: 70}),
  (carrie:Person {name: 'Carrie Anne Moss', age: 55}),
  (guy:Person {name: 'Guy Pearce', age: 55}),
  (kathryn:Person {name: 'Kathryn Bigelow', age: 71}),
  (speed:Movie {title: 'Speed'}),
  (keanu)-[:ACTED_IN]->(speed),
  (keanu)-[:KNOWS]->(carrie),
  (keanu)-[:KNOWS]->(liam),
  (keanu)-[:KNOWS]->(kathryn),
  (carrie)-[:KNOWS]->(guy),
  (liam)-[:KNOWS]->(guy)
----

[[functions-avg]]
== avg() - Numeric values

The function `avg()` returns the average of a set of `INTEGER` or `FLOAT` values.

[source, syntax]
----
avg(expression)
----

*Returns:*

|===

| Either an `INTEGER` or a `FLOAT`, depending on the values returned by `expression` and whether or not the calculation overflows.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| An expression returning a set of numeric values.

|===


*Considerations:*
|===

| Any `null` values are excluded from the calculation.
| `avg(null)` returns `null`.

|===


.+avg()+
======

.Query
[source, cypher]
----
MATCH (p:Person)
RETURN avg(p.age)
----

The average of all the values in the property `age` is returned:

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +avg(p.age)+
| +61.8+
1+d|Rows: 1

|===

======


[[functions-avg-duration]]
== avg() - Durations

The function `avg()` returns the average of a set of `DURATION` values.

[source, syntax]
----
avg(expression)
----

*Returns:*

|===

| `DURATION`

|===


*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| An expression returning a set of `DURATION` values.

|===


*Considerations:*

|===

| Any `null` values are excluded from the calculation.
| `avg(null)` returns `null`.

|===


.+avg()+
======

.Query
[source, cypher]
----
UNWIND [duration('P2DT3H'), duration('PT1H45S')] AS dur
RETURN avg(dur)
----

The average of the two supplied `DURATION` values is returned:

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +avg(dur)+
| +P1DT2H22.5S+
1+d|Rows: 1

|===

======


[[functions-collect]]
== collect()

The function `collect()` returns a single aggregated list containing the values returned by an expression.

[source, syntax]
----
collect(expression)
----

*Returns:*

|===

| A list containing heterogeneous elements; the types of the elements are determined by the values returned by `expression`.

|===


*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| An expression returning a set of values.

|===


*Considerations:*

|===

| Any `null` values are ignored and will not be added to the list.
| `collect(null)` returns an empty list.

|===


.+collect()+
======

.Query
[source, cypher]
----
MATCH (p:Person)
RETURN collect(p.age)
----

All the values are collected and returned in a single list:

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +collect(p.age)+
| +[58, 70, 55, 55, 71]+
1+d|Rows: 1

|===

======


[[functions-count]]
== count()

The function `count()` returns the number of values or rows, and appears in two variants:

`count(*)`:: returns the number of matching rows.
`count(expr)`:: returns the number of non-`null` values returned by an expression.

[source, syntax]
----
count(expression)
----

*Returns:*

|===

| `INTEGER`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| An expression.

|===

*Considerations:*

|===

| `count(*)` includes rows returning `null`.
| `count(expr)` ignores `null` values.
| `count(null)` returns `0`.

|===

[TIP]
====
Neo4j maintains a transactional count store for holding count metadata, which can significantly increase the speed of queries using the `count()` function.
For more information about the count store, refer to link:https://neo4j.com/developer/kb/fast-counts-using-the-count-store/[Neo4j Knowledge Base -> Fast counts using the count store].
====

=== Using `count(*)` to return the number of nodes

The function `count(*)` can be used to return the number of nodes; for example, the number of nodes connected to a node `n`.

.+count()+
======

.Query
[source, cypher]
----
MATCH (p:Person {name: 'Keanu Reeves'})-->(x)
RETURN labels(p), p.age, count(*)
----

The labels and `age` property of the start node `Keanu Reeves` and the number of nodes related to it are returned:

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===

| +labels(p)+ | +p.age+ | +count(*)+
| +["Person"]+ | +58+ | +4+
3+d|Rows: 1

|===

======


=== Using `count(*)` to group and count relationship types

The function `count(*)` can be used to group the type of matched relationships and return the number of types.

.+count()+
======

.Query
[source, cypher]
----
MATCH (p:Person {name: 'Keanu Reeves'})-[r]->()
RETURN type(r), count(*)
----

The type of matched relationships are grouped and the group count of relationship types is returned:

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===

| +type(r)+ | +count(*)+
| +"ACTED_IN"+ | +1+
| +"KNOWS"+ | +3+
2+d|Rows: 2

|===

======


=== Counting non-`null` values

Instead of simply returning the number of rows with `count(*)`, the function `count(expression)` can be used to return the number of non-`null` values returned by the expression.

.+count()+
======

.Query
[source, cypher]
----
MATCH (p:Person)
RETURN count(p.age)
----

The number of nodes with the label `Person` and a property `age` is returned:
(To calculate the sum, use `sum(n.age)`)

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +count(p.age)+
| +5+
1+d|Rows: 1

|===

======

[[counting_with_and_without_duplicates]]
=== Counting with and without duplicates

The default behavior of the `count` function is to count all matching results, including duplicates.
To avoid counting duplicates, use the `DISTINCT` keyword.

As of Neo4j 5.15, it is also possible to use the `ALL` keyword with aggregating functions.
This will count all results, including duplicates, and is functionally the same as not using the `DISTINCT` keyword.
The `ALL` keyword was introduced as part of Cypher's xref:appendix/gql-conformance/index.adoc[].

This example tries to find all friends of friends of `Keanu Reeves` and count them.
It shows the behavior of using both the `ALL` and the `DISTINCT` keywords:

.+count()+
======

.Query
[source, cypher]
----
MATCH (p:Person)-->(friend:Person)-->(friendOfFriend:Person)
WHERE p.name = 'Keanu Reeves'
RETURN friendOfFriend.name, count(friendOfFriend), count(ALL friendOfFriend), count(DISTINCT friendOfFriend)
----

The nodes `Carrie Anne Moss` and `Liam Neeson` both have an outgoing `KNOWS` relationship to `Guy Pearce`.
The `Guy Pearce` node will, therefore, get counted twice when not using `DISTINCT`.

.Result
[role="queryresult",options="header,footer",cols="4*<m"]
|===

| friendOfFriend.name  | count(friendOfFriend) | count(ALL friendOfFriend)    | count(DISTINCT friendOfFriend)
| "Guy Pearce" | 2 | 2 | 1
2+d|Rows: 1

|===

======


[[functions-max]]
== max()

The function `max()` returns the maximum value in a set of values.

[source, syntax]
----
max(expression)
----

*Returns:*

|===

| A xref::values-and-types/property-structural-constructed.adoc#property-types[property type], or a list, depending on the values returned by `expression`.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| An expression returning a set containing any combination of xref::values-and-types/property-structural-constructed.adoc#property-types[property types] and lists thereof.

|===

*Considerations:*

|===

| Any `null` values are excluded from the calculation.
| In a mixed set, any numeric value is always considered to be higher than any `STRING` value, and any `STRING` value is always considered to be higher than any `LIST<ANY>``.
| Lists are compared in dictionary order, i.e. list elements are compared pairwise in ascending order from the start of the list to the end.
| `max(null)` returns `null`.

|===


.+max()+
======

.Query
[source, cypher]
----
UNWIND [1, 'a', null, 0.2, 'b', '1', '99'] AS val
RETURN max(val)
----

The highest of all the values in the mixed set -- in this case, the numeric value `1` -- is returned:

[NOTE]
====
The value `'99'` (a `STRING`), is considered to be a lower value than `1` (an `INTEGER`), because `'99'` is a `STRING`.
====

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +max(val)+
| +1+
1+d|Rows: 1
|===

======


.+max()+
======

.Query
[source, cypher]
----
UNWIND [[1, 'a', 89], [1, 2]] AS val
RETURN max(val)
----

The highest of all the lists in the set -- in this case, the list `[1, 2]` -- is returned, as the number `2` is considered to be a higher value than the `STRING` `'a'`, even though the list `[1, 'a', 89]` contains more elements.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +max(val)+
| +[1,2]+
1+d|Rows: 1

|===

======


.+max()+
======

.Query
[source, cypher]
----
MATCH (p:Person)
RETURN max(p.age)
----

The highest of all the values in the property `age` is returned:

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +max(p.age)+
| +71+
1+d|Rows: 1

|===

======


[[functions-min]]
== min()

The function `min()` returns the minimum value in a set of values.

[source, syntax]
----
min(expression)
----

*Returns:*

|===

| A xref::values-and-types/property-structural-constructed.adoc#property-types[property type], or a list, depending on the values returned by `expression`.

|===

*Arguments:*

[options="header"]
|===

| Name | Description

| `expression`
| An expression returning a set containing any combination of xref::values-and-types/property-structural-constructed.adoc#property-types[property types] and lists thereof.

|===

*Considerations:*

|===

| Any `null` values are excluded from the calculation.
| In a mixed set, any `STRING` value is always considered to be lower than any numeric value, and any `LIST<ANY>` is always considered to be lower than any `STRING`.
| Lists are compared in dictionary order, i.e. list elements are compared pairwise in ascending order from the start of the list to the end.
| `min(null)` returns `null`.

|===


.+min()+
======

.Query
[source, cypher]
----
UNWIND [1, 'a', null, 0.2, 'b', '1', '99'] AS val
RETURN min(val)
----

The lowest of all the values in the mixed set -- in this case, the `STRING` value `"1"` -- is returned.
Note that the (numeric) value `0.2`, which may _appear_ at first glance to be the lowest value in the list, is considered to be a higher value than `"1"` as the latter is a `STRING`.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +min(val)+
| +"1"+
1+d|Rows: 1
|===

======


.+min()+
======

.Query
[source, cypher]
----
UNWIND ['d', [1, 2], ['a', 'c', 23]] AS val
RETURN min(val)
----

The lowest of all the values in the set -- in this case, the list `['a', 'c', 23]` -- is returned, as (i) the two lists are considered to be lower values than the `STRING` `"d"`, and (ii) the `STRING` `"a"` is considered to be a lower value than the numerical value `1`.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +min(val)+
| +["a","c",23]+
1+d|Rows: 1

|===

======


.+min()+
======

.Query
[source, cypher]
----
MATCH (p:Person)
RETURN min(p.age)
----

The lowest of all the values in the property `age` is returned:

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +min(p.age)+
| +55+
1+d|Rows: 1

|===

======


[[functions-percentilecont]]
== percentileCont()

The function `percentileCont()` returns the percentile of the given value over a group, with a percentile from `0.0` to `1.0`.
It uses a linear interpolation method, calculating a weighted average between two values if the desired percentile lies between them.
For nearest values using a rounding method, see `percentileDisc`.

[source, syntax]
----
percentileCont(expression, percentile)
----

*Returns:*

|===

| `FLOAT`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| A numeric expression.

| `percentile`
| A numeric value between `0.0` and `1.0`.

|===

*Considerations:*

|===

| Any `null` values are excluded from the calculation.
| `percentileCont(null, percentile)` returns `null`.

|===

.+percentileCont()+
======

.Query
[source, cypher]
----
MATCH (p:Person)
RETURN percentileCont(p.age, 0.4)
----

The 40th percentile of the values in the property `age` is returned, calculated with a weighted average:

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +percentileCont(p.age, 0.4)+
| +56.8+
1+d|Rows: 1

|===

======


[[functions-percentiledisc]]
== percentileDisc()

The function `percentileDisc()` returns the percentile of the given value over a group, with a percentile from `0.0` to `1.0`.
It uses a rounding method and calculates the nearest value to the percentile.
For interpolated values, see `percentileCont`.

[source, syntax]
----
percentileDisc(expression, percentile)
----

*Returns:*

|===

| Either an `INTEGER` or a `FLOAT`, depending on the values returned by `expression` and whether or not the calculation overflows.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| A numeric expression.

| `percentile`
| A numeric value between `0.0` and `1.0`.

|===

*Considerations:*

|===

| Any `null` values are excluded from the calculation.
| `percentileDisc(null, percentile)` returns `null`.

|===


.+percentileDisc()+
======

.Query
[source, cypher]
----
MATCH (p:Person)
RETURN percentileDisc(p.age, 0.5)
----

The 50th percentile of the values in the property `age` is returned:

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +percentileDisc(p.age, 0.5)+
| +58+
1+d|Rows: 1

|===

======


[[functions-stdev]]
== stDev()

The function `stDev()` returns the standard deviation for the given value over a group.
It uses a standard two-pass method, with `N - 1` as the denominator, and should be used when taking a sample of the population for an unbiased estimate.
When the standard variation of the entire population is being calculated, `stdDevP` should be used.

[source, syntax]
----
stDev(expression)
----

*Returns:*

|===

| `FLOAT`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| A numeric expression.

|===

*Considerations:*
|===

| Any `null` values are excluded from the calculation.
| `stDev(null)` returns `0`.

|===


.+stDev()+
======

.Query
[source, cypher]
----
MATCH (p:Person)
WHERE p.name IN ['Keanu Reeves', 'Liam Neeson', 'Carrie Anne Moss']
RETURN stDev(p.age)
----

The standard deviation of the values in the property `age` is returned:

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +stDev(p.age)+
| +7.937253933193772+
1+d|Rows: 1

|===

======


[[functions-stdevp]]
== stDevP()

The function `stDevP()` returns the standard deviation for the given value over a group.
It uses a standard two-pass method, with `N` as the denominator, and should be used when calculating the standard deviation for an entire population.
When the standard variation of only a sample of the population is being calculated, `stDev` should be used.

[source, syntax]
----
stDevP(expression)
----

*Returns:*

|===

| `FLOAT`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| A numeric expression.

|===

*Considerations:*

|===

| Any `null` values are excluded from the calculation.
| `stDevP(null)` returns `0`.

|===


.+stDevP()+
======

.Query
[source, cypher]
----
MATCH (p:Person)
WHERE p.name IN ['Keanu Reeves', 'Liam Neeson', 'Carrie Anne Moss']
RETURN stDevP(p.age)
----

The population standard deviation of the values in the property `age` is returned:

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +stDevP(p.age)+
| +6.48074069840786+
1+d|Rows: 1

|===

======


[[functions-sum]]
== sum() - Numeric values

The function `sum()` returns the sum of a set of numeric values.

[source, syntax]
----
sum(expression)
----

*Returns:*

|===

| Either an `INTEGER` or a `FLOAT`, depending on the values returned by `expression`.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| An expression returning a set of numeric values.

|===

*Considerations:*

|===

| Any `null` values are excluded from the calculation.
| `sum(null)` returns `0`.

|===


.+sum()+
======

.Query
[source, cypher]
----
MATCH (p:Person)
RETURN sum(p.age)
----

The sum of all the values in the property `age` is returned:

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +sum(p.age)+
| +309+
1+d|Rows: 1

|===

======


[[functions-sum-duration]]
== sum() - Durations

The function `sum()` returns the sum of a set of durations.

[source, syntax]
----
sum(expression)
----

*Returns:*

|===

| `DURATION`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| An expression returning a set of Durations.

|===

*Considerations:*

|===

| Any `null` values are excluded from the calculation.

|===

.+sum()+
======

.Query
[source, cypher]
----
UNWIND [duration('P2DT3H'), duration('PT1H45S')] AS dur
RETURN sum(dur)
----

The sum of the two supplied Durations is returned:

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +sum(dur)+
| +P2DT4H45S+
1+d|Rows: 1

|===

======


[[grouping-keys]]
== Aggregating expressions and grouping keys

*Aggregating expressions* are expressions which contain one or more aggregating functions.
A simple aggregating expression consists of a single aggregating function.
For instance, `sum(x.a)` is an aggregating expression that only consists of the aggregating function `sum( )` with `x.a` as its argument.
Aggregating expressions are also allowed to be more complex, where the result of one or more aggregating functions are input arguments to other expressions.
For instance, `0.1 * (sum(x.a) / count(x.b))` is an aggregating expression that contains two aggregating functions, `sum( )` with `x.a` as its argument and `count( )` with `x.b` as its argument.
Both are input arguments to the division expression.

*Grouping keys* are non-aggregating expressions that are used to group the values going into the aggregating functions.
For example, given the following query containing two return expressions, `n` and `+count(*)+`:

[source, cypher, role=test-skip]
----
RETURN n, count(*)
----

The first, `n` is not an aggregating function, so it will be the grouping key.
The latter, `count(*)` is an aggregating function.
The matching paths will be divided into different buckets, depending on the grouping key.
The aggregating function will then be run on these buckets, calculating an aggregate value per bucket.

The input expression of an aggregating function can contain any expression, including expressions that are not grouping keys.
However, not all expressions can be composed with aggregating functions.
The example below will throw an error since `n.x`, which is not a grouping key, is combined with the aggregating function `count(*)`.

[source, cypher, role=test-skip]
----
RETURN n.x + count(*)
----

To sort the result set using aggregating functions, the aggregation must be included in the `ORDER BY` sub-clause following the `RETURN` clause.

[[grouping-key-examples]]
=== Examples

.Simple aggregation without any grouping keys
======

.Query
[source, cypher]
----
MATCH (p:Person)
RETURN max(p.age)
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +max(p.age)+
| +71+
1+d|Rows: 1
|===

======


.Addition of an aggregation and a constant, without any grouping keys
======

.Query
[source, cypher]
----
MATCH (p:Person)
RETURN max(p.age) + 1
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +max(p.age) + 1+
| +72+
1+d|Rows: 1
|===

======


.Subtraction of a property access and an aggregation
======

Note that `p` is a grouping key:

.Query
[source, cypher]
----
MATCH (p:Person{name:'Keanu Reeves'})-[:KNOWS]-(f:Person)
RETURN p, p.age - max(f.age)
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +p+ | +p.age - max(f.age)+
| +{{"name":"Keanu Reeves","age":58}}+ | +-13+
2+d|Rows: 1
|===

======


.Subtraction of a property access and an aggregation.
======

Note that `p.age` is a grouping key:

.Query
[source, cypher]
----
MATCH (p:Person {name:'Keanu Reeves'})-[:KNOWS]-(f:Person)
RETURN p.age, p.age - max(f.age)
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +p.age+ | +p.age - max(f.age)+
| +58+ | +-13+
2+d|Rows: 1
|===



Grouping keys themselves can be complex expressions.
For better query readability, Cypher only recognizes a sub-expression in aggregating expressions as a grouping key if the grouping key is either:

- A variable - e.g. the `p` in `RETURN p, p.age - max(f.age)`.
- A property access - e.g. the `p.age` in `RETURN p.age, p.age - max(f.age)`.
- A map access - e.g. the `p.age` in `WITH {name:'Keanu Reeves', age:58} AS p RETURN p.age, p.age - max(p.age)`.

If more complex grouping keys are needed as operands in aggregating expression, it is always possible to project them in advance using `WITH`.

Using the property `p.age` will throw an exception, since `p.age` is not a grouping key.
Therefore, it cannot be used in the expressions which contain the aggregating function.
The below two queries would consequently return the same error message:

.Query
[source, cypher, role=test-fail]
----
MATCH (p:Person {name:'Keanu Reeves'})-[:KNOWS]-(f:Person)
RETURN p.age - max(f.age)
----

.Query
[source, cypher, role=test-fail]
----
MATCH (p:Person {name:'Keanu Reeves'})-[:KNOWS]-(f:Person)
RETURN p.age + p.age, p.age + p.age - max(f.age)
----

.Error message
[source, error]
----
Aggregation column contains implicit grouping expressions. For example, in 'RETURN n.a, n.a + n.b + count(*)' the aggregation expression 'n.a + n.b + count(*)' includes the implicit grouping key 'n.b'. It may be possible to rewrite the query by extracting these grouping/aggregation expressions into a preceding WITH clause. Illegal expression(s): n.age
----

However, the latter query would work if rewritten to:

.Query
[source, cypher]
----
MATCH (p:Person {name:'Keanu Reeves'})-[:KNOWS]-(f:Person)
WITH p.age + p.age AS groupingKey, f 
RETURN groupingKey, groupingKey - max(f.age)
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +groupingKey+ | +groupingKey - max(f.age)+
| +116+ | +45+
2+d|Rows: 1
|===
======

=== Rules for aggregating expressions

For aggregating expressions to be correctly computable for the buckets formed by the grouping key(s), they have to fulfill some requirements.
Specifically, each sub-expression in an aggregating expression has to be either:

* an aggregating function, e.g. `sum(x.a)`.
* a constant, e.g. `0.1`.
* a parameter, e.g. `$param`.
* a grouping key, e.g. the `a` in `RETURN a, count(*)`.
* a local variable, e.g. the `x` in  `count(*) + size([ x IN range(1, 10) | x ])`.
* a sub-expression, all operands of which have to be allowed in an aggregating expression.
