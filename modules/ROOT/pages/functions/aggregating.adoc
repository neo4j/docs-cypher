:description: Aggregating functions take a set of values and calculate an aggregated value over them.

[[query-functions-aggregating]]
= Aggregating functions

[abstract]
--
Aggregating functions take a set of values and calculate an aggregated value over them.
--

Functions:

* xref::functions/aggregating.adoc#functions-avg[avg() - Numeric values]
* xref::functions/aggregating.adoc#functions-avg-duration[avg() - Durations]
* xref::functions/aggregating.adoc#functions-collect[collect()]
* xref::functions/aggregating.adoc#functions-count[count()]
* xref::functions/aggregating.adoc#functions-max[max()]
* xref::functions/aggregating.adoc#functions-min[min()]
* xref::functions/aggregating.adoc#functions-percentilecont[percentileCont()]
* xref::functions/aggregating.adoc#functions-percentiledisc[percentileDisc()]
* xref::functions/aggregating.adoc#functions-stdev[stDev()]
* xref::functions/aggregating.adoc#functions-stdevp[stDevP()]
* xref::functions/aggregating.adoc#functions-sum[sum() - Numeric values]
* xref::functions/aggregating.adoc#functions-sum-duration[sum() - Durations]

Aggregation can be computed over all the matching paths, or it can be further divided by introducing grouping keys.
Grouping keys are non-aggregate expressions, that are used to group the values going into the aggregate functions.

Assume we have the following return statement:

[source, cypher, role=test-fail]
----
RETURN n, count(*)
----

We have two return expressions: `n`, and `+count(*)+`.
The first, `n`, is not an aggregate function, so it will be the grouping key.
The latter, `+count(*)+` is an aggregate expression.
The matching paths will be divided into different buckets, depending on the grouping key.
The aggregate function will then be run on these buckets, calculating an aggregate value per bucket.

The input expression of an aggregation function can contain any expression, including expressions that are not grouping keys.
However, not all expressions can be composed with aggregation functions.
The example below will throw an error since we compose `n.x`, which is not a grouping key, with the aggregation expression `+count(*)+`.
For more information see xref:functions/aggregating.adoc#grouping-keys[Grouping keys].

[source, cypher, role=test-skip]
----
RETURN n.x + count(*)
----

.Error message
[source, error]
----
Aggregation column contains implicit grouping expressions. For example, in 'RETURN n.a, n.a + n.b + count(*)' the aggregation expression 'n.a + n.b + count(*)' includes the implicit grouping key 'n.b'. It may be possible to rewrite the query by extracting these grouping/aggregation expressions into a preceding WITH clause. Illegal expression(s): n.x
----

To use aggregations to sort the result set, the aggregation must be included in the `RETURN` to be used in the `ORDER BY`.

The `DISTINCT` operator works in conjunction with aggregation.
It is used to make all values unique before running them through an aggregate function.
More information about `DISTINCT` may be found in xref::syntax/operators.adoc#query-operators-aggregation[Syntax -> Aggregation operators].

The following graph is used for the examples below:

image:graph_aggregating_functions.svg[]

To recreate the graph, run the following query in an empty Neo4j database:

[source, cypher, role=test-setup]
----
CREATE
  (a:Person {name: 'A', age: 13}),
  (b:Person {name: 'B', age: 33, eyes: 'blue'}),
  (c:Person {name: 'C', age: 44, eyes: 'blue'}),
  (d1:Person {name: 'D', eyes: 'brown'}),
  (d2:Person {name: 'D'}),
  (book:Book {name: 'Cypher'}),
  (a)-[:READS]->(book),
  (a)-[:KNOWS]->(d1),
  (a)-[:KNOWS]->(c),
  (a)-[:KNOWS]->(b),
  (c)-[:KNOWS]->(d2),
  (b)-[:KNOWS]->(d2)
----

[[functions-avg]]
== avg() - Numeric values

The function `avg()` returns the average of a set of numeric values.

*Syntax:*

[source, syntax]
----
avg(expression)
----

*Returns:*

|===

| Either an Integer or a Float, depending on the values returned by `expression` and whether or not the calculation overflows.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| An expression returning a set of numeric values.

|===


*Considerations:*
|===

| Any `null` values are excluded from the calculation.
| `avg(null)` returns `null`.

|===


.+avg()+
======

.Query
[source, cypher]
----
MATCH (n:Person)
RETURN avg(n.age)
----

The average of all the values in the property `age` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +avg(n.age)+
| +30.0+
1+d|Rows: 1

|===

======


[[functions-avg-duration]]
== avg() - Durations

The function `avg()` returns the average of a set of Durations.

*Syntax:*

[source, syntax]
----
avg(expression)
----

*Returns:*

|===

| A Duration.

|===


*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| An expression returning a set of Durations.

|===


*Considerations:*

|===

| Any `null` values are excluded from the calculation.
| `avg(null)` returns `null`.

|===


.+avg()+
======

.Query
[source, cypher]
----
UNWIND [duration('P2DT3H'), duration('PT1H45S')] AS dur
RETURN avg(dur)
----

The average of the two supplied Durations is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +avg(dur)+
| +P1DT2H22.5S+
1+d|Rows: 1

|===

======


[[functions-collect]]
== collect()

The function `collect()` returns a single aggregated list containing the values returned by an expression.

*Syntax:*

[source, syntax]
----
collect(expression)
----

*Returns:*

|===

| A list containing heterogeneous elements; the types of the elements are determined by the values returned by `expression`.

|===


*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| An expression returning a set of values.

|===


*Considerations:*

|===

| Any `null` values are ignored and will not be added to the list.
| `collect(null)` returns an empty list.

|===


.+collect()+
======

.Query
[source, cypher]
----
MATCH (n:Person)
RETURN collect(n.age)
----

All the values are collected and returned in a single list.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +collect(n.age)+
| +[13,33,44]+
1+d|Rows: 1

|===

======


[[functions-count]]
== count()

The function `count()` returns the number of values or rows, and appears in two variants:

`count(*)`:: returns the number of matching rows.
`count(expr)`:: returns the number of non-`null` values returned by an expression.

*Syntax:*

[source, syntax]
----
count(expression)
----

*Returns:*

|===

| An Integer.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| An expression.

|===

*Considerations:*

|===

| `count(*)` includes rows returning `null`.
| `count(expr)` ignores `null` values.
| `count(null)` returns `0`.

|===


=== Using `count(*)` to return the number of nodes

The function `count(*)` can be used to return the number of nodes; for example, the number of nodes connected to some node `n`.

.+count()+
======

.Query
[source, cypher]
----
MATCH (n {name: 'A'})-->(x)
RETURN labels(n), n.age, count(*)
----

The labels and `age` property of the start node `n` and the number of nodes related to `n` are returned.

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===

| +labels(n)+ | +n.age+ | +count(*)+
| +["Person"]+ | +13+ | +4+
3+d|Rows: 1

|===

======


=== Using `count(*)` to group and count relationship types

The function `count(*)` can be used to group the type of matched relationships and return the number.

.+count()+
======

.Query
[source, cypher]
----
MATCH (n {name: 'A'})-[r]->()
RETURN type(r), count(*)
----

The type of matched relationships are grouped and the group count are returned.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===

| +type(r)+ | +count(*)+
| +"READS"+ | +1+
| +"KNOWS"+ | +3+
2+d|Rows: 2

|===

======


=== Counting non-`null` values

Instead of simply returning the number of rows with `count(*)`, the function `count(expression)` can be used to return the number of non-`null` values returned by the expression.

.+count()+
======

.Query
[source, cypher]
----
MATCH (n:Person)
RETURN count(n.age)
----

The number of nodes with the label `Person` and a property `age` is returned. (If you want the sum, use `sum(n.age)`)

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +count(n.age)+
| +3+
1+d|Rows: 1

|===

======


=== Counting with and without duplicates

In this example we are trying to find all our friends of friends, and count them:

`count(DISTINCT friend_of_friend)`:: Will only count a `friend_of_friend` once, as `DISTINCT` removes the duplicates.
`count(friend_of_friend)`:: Will consider the same `friend_of_friend` multiple times.

.+count()+
======

.Query
[source, cypher]
----
MATCH (me:Person)-->(friend:Person)-->(friend_of_friend:Person)
WHERE me.name = 'A'
RETURN count(DISTINCT friend_of_friend), count(friend_of_friend)
----

Both `B` and `C` know `D` and thus `D` will get counted twice when not using `DISTINCT`.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===

| +count(DISTINCT friend_of_friend)+ | +count(friend_of_friend)+
| +1+ | +2+
2+d|Rows: 1

|===

======


[[functions-max]]
== max()

The function `max()` returns the maximum value in a set of values.

*Syntax:*

[source, syntax]
----
max(expression)
----

*Returns:*

|===

| A xref::syntax/values.adoc#property-types[property type], or a list, depending on the values returned by `expression`.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| An expression returning a set containing any combination of xref::syntax/values.adoc#property-types[property types] and lists thereof.

|===

*Considerations:*

|===

| Any `null` values are excluded from the calculation.
| In a mixed set, any numeric value is always considered to be higher than any string value, and any string value is always considered to be higher than any list.
| Lists are compared in dictionary order, i.e. list elements are compared pairwise in ascending order from the start of the list to the end.
| `max(null)` returns `null`.

|===


.+max()+
======

.Query
[source, cypher]
----
UNWIND [1, 'a', null, 0.2, 'b', '1', '99'] AS val
RETURN max(val)
----

The highest of all the values in the mixed set -- in this case, the numeric value `1` -- is returned.

[NOTE]
====
The value `'99'` (a string), is considered to be a lower value than `1` (an integer), because `'99'` is a string.
====

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +max(val)+
| +1+
1+d|Rows: 1
|===

======


.+max()+
======

.Query
[source, cypher]
----
UNWIND [[1, 'a', 89], [1, 2]] AS val
RETURN max(val)
----

The highest of all the lists in the set -- in this case, the list `[1, 2]` -- is returned, as the number `2` is considered to be a higher value than the string `'a'`, even though the list `[1, 'a', 89]` contains more elements.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +max(val)+
| +[1,2]+
1+d|Rows: 1

|===

======


.+max()+
======

.Query
[source, cypher]
----
MATCH (n:Person)
RETURN max(n.age)
----

The highest of all the values in the property `age` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +max(n.age)+
| +44+
1+d|Rows: 1

|===

======


[[functions-min]]
== min()

The function `min()` returns the minimum value in a set of values.

*Syntax:*

[source, syntax]
----
min(expression)
----

*Returns:*

|===

| A xref::syntax/values.adoc#property-types[property type], or a list, depending on the values returned by `expression`.

|===

*Arguments:*

[options="header"]
|===

| Name | Description

| `expression`
| An expression returning a set containing any combination of xref::syntax/values.adoc#property-types[property types] and lists thereof.

|===

*Considerations:*

|===

| Any `null` values are excluded from the calculation.
| In a mixed set, any string value is always considered to be lower than any numeric value, and any list is always considered to be lower than any string.
| Lists are compared in dictionary order, i.e. list elements are compared pairwise in ascending order from the start of the list to the end.
| `min(null)` returns `null`.

|===


.+min()+
======

.Query
[source, cypher]
----
UNWIND [1, 'a', null, 0.2, 'b', '1', '99'] AS val
RETURN min(val)
----

The lowest of all the values in the mixed set -- in this case, the string value `"1"` -- is returned.
Note that the (numeric) value `0.2`, which may _appear_ at first glance to be the lowest value in the list, is considered to be a higher value than `"1"` as the latter is a string.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +min(val)+
| +"1"+
1+d|Rows: 1
|===

======


.+min()+
======

.Query
[source, cypher]
----
UNWIND ['d', [1, 2], ['a', 'c', 23]] AS val
RETURN min(val)
----

The lowest of all the values in the set -- in this case, the list `['a', 'c', 23]` -- is returned, as (i) the two lists are considered to be lower values than the string `"d"`, and (ii) the string `"a"` is considered to be a lower value than the numerical value `1`.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +min(val)+
| +["a","c",23]+
1+d|Rows: 1

|===

======


.+min()+
======

.Query
[source, cypher]
----
MATCH (n:Person)
RETURN min(n.age)
----

The lowest of all the values in the property `age` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +min(n.age)+
| +13+
1+d|Rows: 1

|===

======


[[functions-percentilecont]]
== percentileCont()

The function `percentileCont()` returns the percentile of the given value over a group, with a percentile from `0.0` to `1.0`.
It uses a linear interpolation method, calculating a weighted average between two values if the desired percentile lies between them.
For nearest values using a rounding method, see `percentileDisc`.

*Syntax:*

[source, syntax]
----
percentileCont(expression, percentile)
----

*Returns:*

|===

| A Float.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| A numeric expression.

| `percentile`
| A numeric value between `0.0` and `1.0`.

|===

*Considerations:*

|===

| Any `null` values are excluded from the calculation.
| `percentileCont(null, percentile)` returns `null`.

|===

.+percentileCont()+
======

.Query
[source, cypher]
----
MATCH (n:Person)
RETURN percentileCont(n.age, 0.4)
----

The 40th percentile of the values in the property `age` is returned, calculated with a weighted average.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +percentileCont(n.age, 0.4)+
| +29.0+
1+d|Rows: 1

|===

======


[[functions-percentiledisc]]
== percentileDisc()

The function `percentileDisc()` returns the percentile of the given value over a group, with a percentile from `0.0` to `1.0`.
It uses a rounding method and calculates the nearest value to the percentile.
For interpolated values, see `percentileCont`.

*Syntax:*

[source, syntax]
----
percentileDisc(expression, percentile)
----

*Returns:*

|===

| Either an Integer or a Float, depending on the values returned by `expression` and whether or not the calculation overflows.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| A numeric expression.

| `percentile`
| A numeric value between `0.0` and `1.0`.

|===

*Considerations:*

|===

| Any `null` values are excluded from the calculation.
| `percentileDisc(null, percentile)` returns `null`.

|===


.+percentileDisc()+
======

.Query
[source, cypher]
----
MATCH (n:Person)
RETURN percentileDisc(n.age, 0.5)
----

The 50th percentile of the values in the property `age` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +percentileDisc(n.age, 0.5)+
| +33+
1+d|Rows: 1

|===

======


[[functions-stdev]]
== stDev()

The function `stDev()` returns the standard deviation for the given value over a group.
It uses a standard two-pass method, with `N - 1` as the denominator, and should be used when taking a sample of the population for an unbiased estimate.
When the standard variation of the entire population is being calculated, `stdDevP` should be used.

*Syntax:*

[source, syntax]
----
stDev(expression)
----

*Returns:*

|===

| A Float.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| A numeric expression.

|===

*Considerations:*
|===

| Any `null` values are excluded from the calculation.
| `stDev(null)` returns `0`.

|===


.+stDev()+
======

.Query
[source, cypher]
----
MATCH (n)
WHERE n.name IN ['A', 'B', 'C']
RETURN stDev(n.age)
----

The standard deviation of the values in the property `age` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +stDev(n.age)+
| +15.716233645501712+
1+d|Rows: 1

|===

======


[[functions-stdevp]]
== stDevP()

The function `stDevP()` returns the standard deviation for the given value over a group.
It uses a standard two-pass method, with `N` as the denominator, and should be used when calculating the standard deviation for an entire population.
When the standard variation of only a sample of the population is being calculated, `stDev` should be used.

*Syntax:*

[source, syntax]
----
stDevP(expression)
----

*Returns:*

|===

| A Float.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| A numeric expression.

|===

*Considerations:*

|===

| Any `null` values are excluded from the calculation.
| `stDevP(null)` returns `0`.

|===


.+stDevP()+
======

.Query
[source, cypher]
----
MATCH (n)
WHERE n.name IN ['A', 'B', 'C']
RETURN stDevP(n.age)
----

The population standard deviation of the values in the property `age` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +stDevP(n.age)+
| +12.832251036613439+
1+d|Rows: 1

|===

======


[[functions-sum]]
== sum() - Numeric values

The function `sum()` returns the sum of a set of numeric values.

*Syntax:*

[source, syntax]
----
sum(expression)
----

*Returns:*

|===

| Either an Integer or a Float, depending on the values returned by `expression`.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| An expression returning a set of numeric values.

|===

*Considerations:*

|===

| Any `null` values are excluded from the calculation.
| `sum(null)` returns `0`.

|===


.+sum()+
======

.Query
[source, cypher]
----
MATCH (n:Person)
RETURN sum(n.age)
----

The sum of all the values in the property `age` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +sum(n.age)+
| +90+
1+d|Rows: 1

|===

======


[[functions-sum-duration]]
== sum() - Durations

The function `sum()` returns the sum of a set of durations.

*Syntax:*

[source, syntax]
----
sum(expression)
----

*Returns:*

|===

| A Duration.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| An expression returning a set of Durations.

|===

*Considerations:*

|===

| Any `null` values are excluded from the calculation.

|===

.+sum()+
======

.Query
[source, cypher]
----
UNWIND [duration('P2DT3H'), duration('PT1H45S')] AS dur
RETURN sum(dur)
----

The sum of the two supplied Durations is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +sum(dur)+
| +P2DT4H45S+
1+d|Rows: 1

|===

======


[[grouping-keys]]
== Grouping keys

Aggregation expressions are expressions which contain one or more aggregation functions.
A simple aggregation expression consists of a single aggregation function.
For instance, `SUM(x.a)` is an aggregation expression that only consists of the aggregation function `SUM( )` with `x.a` as its argument.
Aggregation expressions are also allowed to be more complex, where the result of one or more aggregation functions
are input arguments to other expressions.
For instance, `0.1 * (SUM(x.a) / COUNT(x.b))` is an aggregation expression that contains two aggregation functions,
`SUM( )` with `x.a` as its argument and `COUNT( )` with `x.b` as its argument.
Both are input arguments to the division expression.


For aggregation expressions to be correctly computable for the buckets formed by the grouping key(s), they have to fulfill some requirements.
Specifically, each sub expression in an aggregation expression has to be either:

* an aggregation function, e.g. `SUM(x.a)`,
* a constant, e.g. `0.1`,
* a parameter, e.g. `$param`,
* a grouping key, e.g. the `a` in `RETURN a, count(*)`
* a local variable, e.g. the `x` in  `count(*) + size([ x IN range(1, 10) | x ])`, or
* a subexpression, all whose operands are operands allowed in an aggregation expression.


[[grouping-key-examples]]
=== Examples of aggregation expressions.


.Simple aggregation without any grouping keys:
======

.Query
[source, cypher]
----
MATCH (p: Person) RETURN max(p.age)
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +max(p.age)+
| +44+
1+d|Rows: 1
|===

======


.Addition of an aggregation and a constant, without any grouping keys:
======

.Query
[source, cypher]
----
MATCH (p: Person)
RETURN max(p.age) + 1
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +max(p.age) + 1+
| +45+
1+d|Rows: 1
|===

======


.Subtraction of a property access and an aggregation.
======

Note that `n` is a grouping key:

.Query
[source, cypher]
----
MATCH (n: Person{name:"A"})-[:KNOWS]-(f:Person)
RETURN n, n.age - max(f.age)
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +n+ | +n.age - max(f.age)+
| +{"name":"A","age":13}+ | +-31+
2+d|Rows: 1
|===

======


.Subtraction of a property access and an aggregation.
======

Note that `n.age` is a grouping key:

.Query
[source, cypher]
----
MATCH (n: Person{name:"A"})-[:KNOWS]-(f:Person)
RETURN n.age, n.age - max(f.age)
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +n.age+ | +n.age - max(f.age)+
| +13+ | +-31+
2+d|Rows: 1
|===



Grouping keys themselves can be complex expressions.
For better query readability, Cypher only recognizes a sub-expression in aggregation expressions as a grouping key if the grouping key is either:

- A variable - e.g. the `n` in `RETURN n, n.age - max(f.age)`
- A property access - e.g. the `n.age` in `RETURN n.age, n.age - max(f.age)`
- A map access - e.g. the `n.age` in `WITH {age: 34, name:Chris} AS n RETURN n.age, n.age - max(n.age)`

If more complex grouping keys are needed as operands in aggregation expression, it is always possible to project them in advance with `WITH`.

Using the property `n.age` will throw an exception, since `n.age` is not a grouping key. 
It can, therefore, not be used in the expressions which contain the aggregating function. 
The below two queries would consequently return the same error message: 

.Query
[source, cypher, role=test-fail]
----
MATCH (n: Person{name:"A"})-[:KNOWS]-(f:Person)
RETURN n.age - max(f.age)
----

.Query
[source, cypher, role=test-fail]
----
MATCH (n: Person{name:"A"})-[:KNOWS]-(f:Person)
RETURN n.age + n.age, n.age + n.age - max(f.age)
----

.Error message
[source, error]
----
Aggregation column contains implicit grouping expressions. For example, in 'RETURN n.a, n.a + n.b + count(*)' the aggregation expression 'n.a + n.b + count(*)' includes the implicit grouping key 'n.b'. It may be possible to rewrite the query by extracting these grouping/aggregation expressions into a preceding WITH clause. Illegal expression(s): n.age
----

The latter query would, however, work if rewritten to:

.Query
[source, cypher]
----
MATCH (n: Person{name:"A"})-[:KNOWS]-(f:Person)
WITH n.age + n.age AS groupingKey, f 
RETURN groupingKey, groupingKey - max(f.age)
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +groupingKey+ | +groupingKey - max(f.age)+
| +26+ | +-18+
2+d|Rows: 1
|===
======