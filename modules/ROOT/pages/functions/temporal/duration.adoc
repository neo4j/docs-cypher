:description: Cypher provides functions allowing for the creation and manipulation of temporal `DURATION` values.

[[query-functions-temporal-duration]]
= Temporal functions - duration

Duration functions allow for the creation and manipulation of temporal `DURATION` values.


[NOTE]
====
See also xref::values-and-types/temporal.adoc[Temporal values] and xref::syntax/operators.adoc#query-operators-temporal[Temporal operators].
====

[[functions-duration]]
== Creating a `DURATION` from duration components

`duration()` can construct a `DURATION` from a `MAP` of its components in the same way as the temporal instant types.

* `years`
* `quarters`
* `months`
* `weeks`
* `days`
* `hours`
* `minutes`
* `seconds`
* `milliseconds`
* `microseconds`
* `nanoseconds`

*Syntax:*

[source, syntax, role="noheader"]
----
duration([ {years, quarters, months, weeks, days, hours, minutes, seconds, milliseconds, microseconds, nanoseconds} ])
----

*Returns:*

|===

| `DURATION`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `A single map consisting of the following:`
|

| `years`
| A numeric expression.

| `quarters`
| A numeric expression.

| `months`
| A numeric expression.

| `weeks`
| A numeric expression.

| `days`
| A numeric expression.

| `hours`
| A numeric expression.

| `minutes`
| A numeric expression.

| `seconds`
| A numeric expression.

| `milliseconds`
| A numeric expression.

| `microseconds`
| A numeric expression.

| `nanoseconds`
| A numeric expression.

|===

*Considerations:*

|===

| At least one parameter must be provided (`duration()` and `+duration({})+` are invalid).
| There is no constraint on how many of the parameters are provided.
| It is possible to have a `DURATION` where the amount of a smaller unit (e.g. `seconds`) exceeds the threshold of a larger unit (e.g. `days`).
| The values of the parameters may be expressed as decimal fractions.
| The values of the parameters may be arbitrarily large.
| The values of the parameters may be negative.
| The xref:values-and-types/temporal.adoc#cypher-temporal-accessing-components-durations[components of `DURATION` objects] are individually accessible.

|===

.+duration()+
======

.Query
[source, cypher, indent=0]
----
UNWIND [
duration({days: 14, hours:16, minutes: 12}),
duration({months: 5, days: 1.5}),
duration({months: 0.75}),
duration({weeks: 2.5}),
duration({minutes: 1.5, seconds: 1, milliseconds: 123, microseconds: 456, nanoseconds: 789}),
duration({minutes: 1.5, seconds: 1, nanoseconds: 123456789})
] AS aDuration
RETURN aDuration
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +aDuration+
| +P14DT16H12M+
| +P5M1DT12H+
| +P22DT19H51M49.5S+
| +P17DT12H+
| +PT1M31.123456789S+
| +PT1M31.123456789S+
1+d|Rows: 6

|===

======


[[functions-duration-create-string]]
== Creating a `DURATION` from a `STRING`

`duration()` returns the `DURATION` value obtained by parsing a `STRING` representation of a temporal amount.

*Syntax:*

[source, syntax, role="noheader"]
----
duration(temporalAmount)
----

*Returns:*

|===

| `DURATION`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `temporalAmount`
| A `STRING` representing a temporal amount.

|===

*Considerations:*

|===

| `temporalAmount` must comply with either the xref::values-and-types/temporal.adoc#cypher-temporal-specifying-durations[unit based form or date-and-time based form defined for `DURATION` values].

|===


.+duration()+
======

.Query
[source, cypher, indent=0]
----
UNWIND [
duration("P14DT16H12M"),
duration("P5M1.5D"),
duration("P0.75M"),
duration("PT0.75M"),
duration("P2012-02-02T14:37:21.545")
] AS aDuration
RETURN aDuration
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +aDuration+
| +P14DT16H12M+
| +P5M1DT12H+
| +P22DT19H51M49.5S+
| +PT45S+
| +P2012Y2M2DT14H37M21.545S+
1+d|Rows: 5

|===

======


[[functions-duration-computing]]
== Computing the `DURATION` between two temporal instants

`duration()` has sub-functions which compute the _logical difference_ (in days, months, etc) between two temporal instant values:

* `duration.between(a, b)`: Computes the difference in multiple components between instant `a` and instant `b`. This captures month, days, seconds and sub-seconds differences separately.
* `duration.inMonths(a, b)`: Computes the difference in whole months (or quarters or years) between instant `a` and instant `b`. This captures the difference as the total number of months. Any difference smaller than a whole month is disregarded.
* `duration.inDays(a, b)`: Computes the difference in whole days (or weeks) between instant `a` and instant `b`. This captures the difference as the total number of days.  Any difference smaller than a whole day is disregarded.
* `duration.inSeconds(a, b)`: Computes the difference in seconds (and fractions of seconds, or minutes or hours) between instant `a` and instant `b`. This captures the difference as the total number of seconds.


[[functions-duration-between]]
=== duration.between()

`duration.between()` returns the `DURATION` value equal to the difference between the two given instants.

*Syntax:*

[source, syntax, role="noheader"]
----
duration.between(instant1, instant2)
----

*Returns:*

|===

| `DURATION`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `instant1`
| An expression returning any temporal instant type (`DATE` etc.) that represents the starting instant.

| `instant2`
| An expression returning any temporal instant type (`DATE` etc.) that represents the ending instant.

|===

*Considerations:*

|===

| If `instant2` occurs earlier than `instant1`, the resulting `DURATION` will be negative.
| If `instant1` has a time component and `instant2` does not, the time component of `instant2` is assumed to be midnight, and vice versa.
| If `instant1` has a time zone component and `instant2` does not, the time zone component of `instant2` is assumed to be the same as that of `instant1`, and vice versa.
| If `instant1` has a date component and `instant2` does not, the date component of `instant2` is assumed to be the same as that of `instant1`, and vice versa.

|===


.+duration.between()+
======

.Query
[source, cypher, indent=0]
----
UNWIND [
duration.between(date("1984-10-11"), date("1985-11-25")),
duration.between(date("1985-11-25"), date("1984-10-11")),
duration.between(date("1984-10-11"), datetime("1984-10-12T21:40:32.142+0100")),
duration.between(date("2015-06-24"), localtime("14:30")),
duration.between(localtime("14:30"), time("16:30+0100")),
duration.between(localdatetime("2015-07-21T21:40:32.142"), localdatetime("2016-07-21T21:45:22.142")),
duration.between(datetime({year: 2017, month: 10, day: 29, hour: 0, timezone: 'Europe/Stockholm'}), datetime({year: 2017, month: 10, day: 29, hour: 0, timezone: 'Europe/London'}))
] AS aDuration
RETURN aDuration
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +aDuration+
| +P1Y1M14D+
| +P-1Y-1M-14D+
| +P1DT21H40M32.142S+
| +PT14H30M+
| +PT2H+
| +P1YT4M50S+
| +PT1H+
1+d|Rows: 7

|===

======


[[functions-duration-inmonths]]
=== duration.inMonths()

`duration.inMonths()` returns the `DURATION` value equal to the difference in whole months, quarters or years between the two given instants.

*Syntax:*

[source, syntax, role="noheader"]
----
duration.inMonths(instant1, instant2)
----

*Returns:*

|===

| `DURATION`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `instant1`
| An expression returning any temporal instant type (`DATE` etc.) that represents the starting instant.

| `instant2`
| An expression returning any temporal instant type (`DATE` etc.) that represents the ending instant.

|===

*Considerations:*
|===

| If `instant2` occurs earlier than `instant1`, the resulting `DURATION` will be negative.
| If `instant1` has a time component and `instant2` does not, the time component of `instant2` is assumed to be midnight, and vice versa.
| If `instant1` has a time zone component and `instant2` does not, the time zone component of `instant2` is assumed to be the same as that of `instant1`, and vice versa.
| If `instant1` has a date component and `instant2` does not, the date component of `instant2` is assumed to be the same as that of `instant1`, and vice versa.
| Any difference smaller than a whole month is disregarded.
| Call the months component of the returned `DURATION` object via `.months` to get the duration total number of months, see xref:values-and-types/temporal.adoc#cypher-temporal-accessing-components-durations[Components of durations].

|===


.+duration.inMonths()+
======

.Query
[source, cypher, indent=0]
----
UNWIND [
duration.inMonths(date("1984-10-11"), date("1985-11-25")),
duration.inMonths(date("1985-11-25"), date("1984-10-11")),
duration.inMonths(date("1984-10-11"), datetime("1984-10-12T21:40:32.142+0100")),
duration.inMonths(date("2015-06-24"), localtime("14:30")),
duration.inMonths(localdatetime("2015-07-21T21:40:32.142"), localdatetime("2016-07-21T21:45:22.142")),
duration.inMonths(datetime({year: 2017, month: 10, day: 29, hour: 0, timezone: 'Europe/Stockholm'}), datetime({year: 2017, month: 10, day: 29, hour: 0, timezone: 'Europe/London'}))
] AS aDuration
RETURN aDuration
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +aDuration+
| +P1Y1M+
| +P-1Y-1M+
| +PT0S+
| +PT0S+
| +P1Y+
| +PT0S+
1+d|Rows: 6

|===

======


[[functions-duration-indays]]
=== duration.inDays()

`duration.inDays()` returns the `DURATION` value equal to the difference in whole days or weeks between the two given instants.

*Syntax:*

[source, syntax, role="noheader"]
----
duration.inDays(instant1, instant2)
----

*Returns:*

|===

| `DURATION`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `instant1`
| An expression returning any temporal instant type (`DATE` etc.) that represents the starting instant.

| `instant2`
| An expression returning any temporal instant type (`DATE` etc.) that represents the ending instant.

|===

*Considerations:*

|===

| If `instant2` occurs earlier than `instant1`, the resulting `DURATION` will be negative.
| If `instant1` has a time component and `instant2` does not, the time component of `instant2` is assumed to be midnight, and vice versa.
| If `instant1` has a time zone component and `instant2` does not, the time zone component of `instant2` is assumed to be the same as that of `instant1`, and vice versa.
| If `instant1` has a date component and `instant2` does not, the date component of `instant2` is assumed to be the same as that of `instant1`, and vice versa.
| Any difference smaller than a whole day is disregarded.
| Call the days component of the returned `DURATION` object via `.days` to get the duration total number of days, see xref:values-and-types/temporal.adoc#cypher-temporal-accessing-components-durations[Components of durations].

|===


.+duration.inDays()+
======

.Query
[source, cypher, indent=0]
----
UNWIND [
duration.inDays(date("1984-10-11"), date("1985-11-25")),
duration.inDays(date("1985-11-25"), date("1984-10-11")),
duration.inDays(date("1984-10-11"), datetime("1984-10-12T21:40:32.142+0100")),
duration.inDays(date("2015-06-24"), localtime("14:30")),
duration.inDays(localdatetime("2015-07-21T21:40:32.142"), localdatetime("2016-07-21T21:45:22.142")),
duration.inDays(datetime({year: 2017, month: 10, day: 29, hour: 0, timezone: 'Europe/Stockholm'}), datetime({year: 2017, month: 10, day: 29, hour: 0, timezone: 'Europe/London'}))
] AS aDuration
RETURN aDuration
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +aDuration+
| +P410D+
| +P-410D+
| +P1D+
| +PT0S+
| +P366D+
| +PT0S+
1+d|Rows: 6

|===

======


[[functions-duration-inseconds]]
=== duration.inSeconds()

`duration.inSeconds()` returns the `DURATION` value equal to the difference in seconds and fractions of seconds, or minutes or hours, between the two given instants.

*Syntax:*

[source, syntax, role="noheader"]
----
duration.inSeconds(instant1, instant2)
----

*Returns:*

|===

| `DURATION`

|===

*Arguments:*
[options="header"]
|===
| Name | Description

| `instant1`
| An expression returning any temporal instant type (`DATE` etc.) that represents the starting instant.

| `instant2`
| An expression returning any temporal instant type (`DATE` etc.) that represents the ending instant.

|===

*Considerations:*

|===

| If `instant2` occurs earlier than `instant1`, the resulting `DURATION` will be negative.
| If `instant1` has a time component and `instant2` does not, the time component of `instant2` is assumed to be midnight, and vice versa.
| If `instant1` has a time zone component and `instant2` does not, the time zone component of `instant2` is assumed to be the same as that of `instant1`, and vice versa.
| If `instant1` has a date component and `instant2` does not, the date component of `instant2` is assumed to be the same as that of `instant1`, and vice versa.
| Call the seconds component of the returned `DURATION` object via `.seconds` to get the duration total number of seconds, see xref:values-and-types/temporal.adoc#cypher-temporal-accessing-components-durations[Components of durations].

|===


.+duration.inSeconds()+
======

.Query
[source, cypher, indent=0]
----
UNWIND [
duration.inSeconds(date("1984-10-11"), date("1984-10-12")),
duration.inSeconds(date("1984-10-12"), date("1984-10-11")),
duration.inSeconds(date("1984-10-11"), datetime("1984-10-12T01:00:32.142+0100")),
duration.inSeconds(date("2015-06-24"), localtime("14:30")),
duration.inSeconds(datetime({year: 2017, month: 10, day: 29, hour: 0, timezone: 'Europe/Stockholm'}), datetime({year: 2017, month: 10, day: 29, hour: 0, timezone: 'Europe/London'}))
] AS aDuration
RETURN aDuration
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +aDuration+
| +PT24H+
| +PT-24H+
| +PT25H32.142S+
| +PT14H30M+
| +PT1H+
1+d|Rows: 5

|===

======

