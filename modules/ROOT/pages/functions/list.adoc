:description: List functions return lists of things -- nodes in a path, and so on.

[[query-functions-list]]
= List functions

[abstract]
--
List functions return lists of things -- nodes in a path, and so on.
--

Further details and examples of lists may be found in xref::values-and-types/lists.adoc[Lists] and xref::syntax/operators.adoc#query-operators-list[List operators].

== Example graph

The following graph is used for the examples below:

image:graph_list_functions.svg[]

To recreate the graph, run the following query in an empty Neo4j database: 

[source, cypher, role=test-setup]
----
CREATE
  (alice:Person:Developer {name:'Alice', age: 38, eyes: 'brown'}),
  (bob {name: 'Bob', age: 25, eyes: 'blue'}),
  (charlie {name: 'Charlie', age: 53, eyes: 'green'}),
  (daniel {name: 'Daniel', age: 54, eyes: 'brown'}),
  (eskil {name: 'Eskil', age: 41, eyes: 'blue', array: ['one', 'two', 'three']}),
  (alice)-[:KNOWS]->(bob),
  (alice)-[:KNOWS]->(charlie),
  (bob)-[:KNOWS]->(daniel),
  (charlie)-[:KNOWS]->(daniel),
  (bob)-[:MARRIED]->(eskil)
----

[[functions-keys]]
== keys()

`keys` returns a list containing the string representations for all the property names of a node, relationship, or map.

*Syntax:*

[source, syntax, role="noheader"]
----
keys(expression)
----

*Returns:*

|===

| A list containing String elements.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| An expression that returns a node, a relationship, or a map.

|===

*Considerations:*

|===

| `keys(null)` returns `null`.

|===


.+keys()+
======

.Query
[source, cypher]
----
MATCH (a) WHERE a.name = 'Alice'
RETURN keys(a)
----

A list containing the names of all the properties on the node bound to `a` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +keys(a)+
| +["eyes","name","age"]+
1+d|Rows: 1

|===

======


[[functions-labels]]
== labels()

`labels` returns a list containing the string representations for all the labels of a node.

*Syntax:*

[source, syntax, role="noheader"]
----
labels(node)
----

*Returns:*

|===

| A list containing String elements.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `node`
| An expression that returns a single node.

|===

*Considerations:*

|===

| `labels(null)` returns `null`.

|===


.+labels()+
======

.Query
[source, cypher]
----
MATCH (a) WHERE a.name = 'Alice'
RETURN labels(a)
----

A list containing all the labels of the node bound to `a` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +labels(a)+
| +["Person","Developer"]+
1+d|Rows: 1

|===

======


[[functions-nodes]]
== nodes()

`nodes()` returns a list containing all the nodes in a path.

*Syntax:*

[source, syntax, role="noheader"]
----
nodes(path)
----

*Returns:*

|===

| A list containing Node elements.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `path`
| An expression that returns a path.

|===

*Considerations:*

|===

| `nodes(null)` returns `null`.

|===


.+nodes()+
======

.Query
[source, cypher]
----
MATCH p = (a)-->(b)-->(c)
WHERE a.name = 'Alice' AND c.name = 'Eskil'
RETURN nodes(p)
----

A list containing all the nodes in the path `p` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +nodes(p)+
| +[(:Person:Developer {name: "Alice",eyes: "brown",age: 38}), ({name: "Bob",eyes: "blue",age: 25}), ({array: ['one', 'two', 'three'],name: "Eskil",eyes: "blue",age: 41})]+
1+d|Rows: 1

|===

======


[[functions-range]]
== range()

`range()` returns a list comprising all integer values within a range bounded by a start value `start` and end value `end`, where the difference `step` between any two consecutive values is constant; i.e. an arithmetic progression.
To create ranges with decreasing integer values, use a negative value `step`.
The range is inclusive for non-empty ranges, and the arithmetic progression will therefore always contain `start` and -- depending on the values of `start`, `step` and `end` -- `end`.
The only exception where the range does not contain `start` are empty ranges.
An empty range will be returned if the value `step` is negative and `start - end` is positive, or vice versa, e.g. `range(0, 5, -1)`.

*Syntax:*

[source, syntax, role="noheader"]
----
range(start, end [, step])
----

*Returns:*

|===

| A list of Integer elements.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `start`
| An expression that returns an integer value.

| `end`
| An expression that returns an integer value.

| `step`
| A numeric expression defining the difference between any two consecutive values, with a default of `1`.

|===


.+range()+
======

.Query
[source, cypher]
----
RETURN range(0, 10), range(2, 18, 3), range(0, 5, -1)
----

Three lists of numbers in the given ranges are returned.

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===

| +range(0, 10)+ | +range(2, 18, 3)+ | +range(0, 5, -1)+
| +[0,1,2,3,4,5,6,7,8,9,10]+ | +[2,5,8,11,14,17]+ | +[]+
3+d|Rows: 1

|===

======


[[functions-reduce]]
== reduce()

`reduce()` returns the value resulting from the application of an expression on each successive element in a list in conjunction with the result of the computation thus far.
This function will iterate through each element `e` in the given list, run the expression on `e` -- taking into account the current partial result -- and store the new partial result in the accumulator.
This function is analogous to the `fold` or `reduce` method in functional languages such as Lisp and Scala.

*Syntax:*

[source, syntax, role="noheader"]
----
reduce(accumulator = initial, variable IN list | expression)
----

*Returns:*

|===

| The type of the value returned depends on the arguments provided, along with the semantics of `expression`.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `accumulator`
| A variable that will hold the result and the partial results as the list is iterated.

| `initial`
| An expression that runs once to give a starting value to the accumulator.

| `list`
| An expression that returns a list.

| `variable`
| The closure will have a variable introduced in its context. We decide here which variable to use.

| `expression`
| This expression will run once per value in the list, and produce the result value.

|===


.+reduce()+
======

.Query
[source, cypher]
----
MATCH p = (a)-->(b)-->(c)
WHERE a.name = 'Alice' AND b.name = 'Bob' AND c.name = 'Daniel'
RETURN reduce(totalAge = 0, n IN nodes(p) | totalAge + n.age) AS reduction
----

The `age` property of all nodes in the path are summed and returned as a single value.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +reduction+
| +117+
1+d|Rows: 1

|===

======


[[functions-relationships]]
== relationships()

`relationships()` returns a list containing all the relationships in a path.

*Syntax:*

[source, syntax, role="noheader"]
----
relationships(path)
----

*Returns:*

|===

| A list containing Relationship elements.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `path`
| An expression that returns a path.

|===

*Considerations:*

|===

| `relationships(null)` returns `null`.

|===


.+relationships()+
======

.Query
[source, cypher]
----
MATCH p = (a)-->(b)-->(c)
WHERE a.name = 'Alice' AND c.name = 'Eskil'
RETURN relationships(p)
----

A list containing all the relationships in the path `p` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +relationships(p)+
| +[:KNOWS[0]{},:MARRIED[4]{}]+
1+d|Rows: 1

|===

======


[[functions-reverse-list]]
== reverse()

`reverse()` returns a list in which the order of all elements in the original list have been reversed.

*Syntax:*

[source, syntax, role="noheader"]
----
reverse(original)
----

*Returns:*

|===

| A list containing homogeneous or heterogeneous elements; the types of the elements are determined by the elements within `original`.

|===

*Arguments:*
[options="header"]
|===
| Name | Description

| `original`
| An expression that returns a list.

|===

*Considerations:*

|===

| Any `null` element in `original` is preserved.

|===


.+reverse()+
======

.Query
[source, cypher]
----
WITH [4923,'abc',521, null, 487] AS ids
RETURN reverse(ids)
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +reverse(ids)+
| +[487,<null>,521,"abc",4923]+
1+d|Rows: 1

|===

======


[[functions-tail]]
== tail()

`tail()` returns a list `l~result~` containing all the elements, excluding the first one, from a list `list`.

*Syntax:*

[source, syntax, role="noheader"]
----
tail(list)
----

*Returns:*

|===

| A list containing heterogeneous elements; the types of the elements are determined by the elements in `list`.

|===

*Arguments:*
[options="header"]
|===
| Name | Description

| `list`
| An expression that returns a list.

|===


.+tail()+
======

.Query
[source, cypher]
----
MATCH (a) WHERE a.name = 'Eskil'
RETURN a.array, tail(a.array)
----

The property named `array` and a list comprising all but the first element of the `array` property are returned.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===

| +a.array+ | +tail(a.array)+
| +["one","two","three"]+ | +["two","three"]+
2+d|Rows: 1

|===

======


[[functions-tobooleanlist]]
== toBooleanList()

`toBooleanList()` converts a list of values and returns a list of boolean values.
If any values are not convertible to boolean they will be null in the list returned.

*Syntax:*

[source, syntax, role="noheader"]
----
toBooleanList(list)
----

*Returns:*

|===

| A list containing the converted elements; depending on the input value a converted value is either a boolean value or `null`.

|===

*Arguments:*
[options="header"]
|===
| Name | Description

| `list`
| An expression that returns a list.

|===

*Considerations:*

|===

| Any `null` element in `list` is preserved.
| Any boolean value in `list` is preserved.
| If the `list` is `null`, `null` will be returned.
| If the `list` is not a list, an error will be returned.
| The conversion for each value in `list` is done according to the xref::functions/scalar.adoc#functions-tobooleanornull[`toBooleanOrNull()` function].

|===


.+toBooleanList()+
======

.Query
[source, cypher, indent=0]
----
RETURN toBooleanList(null) as noList,
toBooleanList([null, null]) as nullsInList,
toBooleanList(['a string', true, 'false', null, ['A','B']]) as mixedList
----

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===

| +noList+ | +nullsInList+ | +mixedList+
| +<null>+ | +[<null>,<null>]+ | +[<null>,true,false,<null>,<null>]+
3+d|Rows: 1

|===

======


[[functions-tofloatlist]]
== toFloatList()

`toFloatList()` converts a list of values and returns a list of floating point values.
If any values are not convertible to floating point they will be `null` in the list returned.

*Syntax:*

[source, syntax, role="noheader"]
----
toFloatList(list)
----

*Returns:*

|===

| A list containing the converted elements; depending on the input value a converted value is either a floating point value or `null`.

|===

*Arguments:*
[options="header"]
|===
| Name | Description

| `list`
| An expression that returns a list.

|===

*Considerations:*

|===

| Any `null` element in `list` is preserved.
| Any floating point value in `list` is preserved.
| If the `list` is `null`, `null` will be returned.
| If the `list` is not a list, an error will be returned.
| The conversion for each value in `list` is done according to the xref::functions/scalar.adoc#functions-tofloatornull[`toFloatOrNull()` function].

|===


.+toFloatList()+
======

.Query
[source, cypher]
----
RETURN toFloatList(null) as noList,
toFloatList([null, null]) as nullsInList,
toFloatList(['a string', 2.5, '3.14159', null, ['A','B']]) as mixedList
----

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===

| +noList+ | +nullsInList+ | +mixedList+
| +<null>+ | +[<null>,<null>]+ | +[<null>,2.5,3.14159,<null>,<null>]+
3+d|Rows: 1

|===

======


[[functions-tointegerlist]]
== toIntegerList()

`toIntegerList()` converts a list of values and returns a list of integer values.
If any values are not convertible to integer they will be `null` in the list returned.

*Syntax:*

[source, syntax, role="noheader"]
----
toIntegerList(list)
----

*Returns:*

|===

| A list containing the converted elements; depending on the input value a converted value is either a integer value or `null`.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `list`
| An expression that returns a list.

|===

*Considerations:*
|===

| Any `null` element in `list` is preserved.
| Any integer value in `list` is preserved.
| If the `list` is `null`, `null` will be returned.
| If the `list` is not a list, an error will be returned.
| The conversion for each value in `list` is done according to the xref::functions/scalar.adoc#functions-tointegerornull[`toIntegerOrNull()` function].

|===


.+toIntegerList()+
======

.Query
[source, cypher]
----
RETURN toIntegerList(null) as noList,
toIntegerList([null, null]) as nullsInList,
toIntegerList(['a string', 2, '5', null, ['A','B']]) as mixedList
----

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===

| +noList+ | +nullsInList+ | +mixedList+
| +<null>+ | +[<null>,<null>]+ | +[<null>,2,5,<null>,<null>]+
3+d|Rows: 1

|===

======


[[functions-tostringlist]]
== toStringList()

`toStringList()` converts a list of values and returns a list of string values.
If any values are not convertible to string they will be `null` in the list returned.

*Syntax:*

[source, syntax, role="noheader"]
----
toStringList(list)
----

*Returns:*

|===

| A list containing the converted elements; depending on the input value a converted value is either a string value or `null`.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `list`
| An expression that returns a list.

|===

*Considerations:*

|===

| Any `null` element in `list` is preserved.
| Any string value in `list` is preserved.
| If the `list` is `null`, `null` will be returned.
| If the `list` is not a list, an error will be returned.
| The conversion for each value in `list` is done according to the xref::functions/string.adoc#functions-tostringornull[`toStringOrNull()` function].

|===


.+toStringList()+
======

.Query
[source, cypher]
----
RETURN toStringList(null) as noList,
toStringList([null, null]) as nullsInList,
toStringList(['already a string', 2, date({year:1955, month:11, day:5}), null, ['A','B']]) as mixedList
----

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===

| +noList+ | +nullsInList+ | +mixedList+
| +<null>+ | +[<null>,<null>]+ | +["already a string","2","1955-11-05",<null>,<null>]+
3+d|Rows: 1

|===

======

