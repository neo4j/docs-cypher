:description: Predicates are boolean functions that return `true` or `false` for a given set of non-`null` input.

[[query-functions-predicate]]
= Predicate functions

== Introduction

Predicates are boolean functions that return `true` or `false` for a given set of non-`null` input.
They are most commonly used to filter out paths in the `WHERE` part of a query.

== Example graph

The following graph is used for the examples below:

image::graph_predicate_functions.svg[[width="600",role="middle"]

To recreate it, run the following query against an empty Neo4j database:

[source, cypher, role=test-setup]
----
CREATE
  (keanu:Person {name:'Keanu Reeves', age:58, nationality:'Canadian'}),
  (carrie:Person {name:'Carrie Anne Moss', age:55, nationality:'American'}),
  (liam:Person {name:'Liam Neeson', age:70, nationality:'Northern Irish'}),
  (guy:Person {name:'Guy Pearce', age:55, nationality:'Australian'}),
  (kathryn:Person {name:'Kathryn Bigelow', age:71, nationality:'American'}),
  (jessica:Person {name:'Jessica Chastain', age:45, address:''}),
  (theMatrix:Movie {title:'The Matrix'}),
  (keanu)-[:KNOWS]->(carrie),
  (keanu)-[:KNOWS]->(liam),
  (keanu)-[:KNOWS]->(kathryn),
  (kathryn)-[:KNOWS]->(jessica),
  (carrie)-[:KNOWS]->(guy),
  (liam)-[:KNOWS]->(guy),
  (keanu)-[:ACTED_IN]->(theMatrix),
  (carrie)-[:ACTED_IN]->(theMatrix)
----

[[functions-all]]
== all()

The function `all()` returns `true` if the predicate holds for all elements in the given list.

`null` is returned if the list is `null` or if the predicate evaluates to `null` for at least one element and does not evaluate to `false` for any other element.

*Syntax:*

[source, syntax, role="noheader"]
----
all(variable IN list WHERE predicate)
----

*Returns:*

|===

| `BOOLEAN`

|===

*Arguments:*

[options="header"]
|===

| Name | Description

| `list`
a|
An expression that returns a list.
A single element cannot be explicitly passed as a literal in the cypher statement.
However, an implicit conversion will happen for single elements when passing node properties during cypher execution.

| `variable`
| A variable that can be used from within the predicate.

| `predicate`
| A predicate that is tested against all items in the list.

|===


.+all()+
======

.Query
[source, cypher, indent=0]
----
MATCH p = (a)-[*]->(b)
WHERE
  a.name = 'Keanu Reeves'
  AND b.name = 'Guy Pearce'
  AND all(x IN nodes(p) WHERE x.age < 60)
RETURN p
----

All nodes in the returned paths will have a property `age` with a value lower than `60`:

image::predicate_function_example.svg[width="300",role="middle"]

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +p+
| +(:Person {nationality: "Canadian",name: "Keanu Reeves",age: 58})-[:KNOWS]->(:Person {nationality: "American",name: "Carrie Anne Moss",age: 55})-[:KNOWS]->(:Person {nationality: "Australian",name: "Guy Pearce",age: 55})+
1+d|Rows: 1

|===

======


[[functions-any]]
== any()

The function `any()` returns `true` if the predicate holds for at least one element in the given list.

`null` is returned if the list is `null`, or if the predicate evaluates to `null` for at least one element and does not evaluate to `true` for any other element.

*Syntax:*

[source, syntax, role="noheader"]
----
any(variable IN list WHERE predicate)
----

*Returns:*

|===

| `BOOLEAN`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `list`
a|
An expression that returns a list.
A single element cannot be explicitly passed as a literal in the cypher statement.
However, an implicit conversion will happen for single elements when passing node properties during cypher execution.

| `variable`
| A variable that can be used from within the predicate.

| `predicate`
| A predicate that is tested against all items in the list.

|===


.+any()+
======

.Query
[source, cypher, indent=0]
----
MATCH (p:Person)
WHERE any(nationality IN p.nationality WHERE nationality = 'American')
RETURN p
----

The query returns the `Person` nodes with the `nationality` property value `American`:

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +p+
| +{"nationality":"American","name":"Carrie Anne Moss","age":55}+
| +{"nationality":"American","name":"Kathryn Bigelow","age":71}+

1+d|Rows: 2

|===

======


[[functions-exists]]
== exists()

The function `exists()` returns `true` if a match for the given pattern exists in the graph.

`null` is returned if the input argument is `null`.

[NOTE]
====
To check if a property is not `null` use the xref::syntax/operators.adoc#cypher-comparison[`IS NOT NULL` predicate].
====

*Syntax:*

[source, syntax, role="noheader"]
----
exists(pattern)
----

*Returns:*

|===

| `BOOLEAN`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `pattern`
| A pattern.

|===


.+exists()+
======

.Query
[source, cypher, indent=0]
----
MATCH (p:Person)
RETURN
  p.name AS name,
  exists((p)-[:ACTED_IN]->()) AS has_acted_in_rel
----

This query returns the `name` property of every `Person` node, along with a boolean (`true` or `false`) indicating if those nodes have an `ACTED_IN` relationship in the graph.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===

| +name+ | +has_acted_in_rel+
| +"Carrie Anne Moss"+ | +true+
| +"Keanu Reeves"+ | +true+
| +"Liam Neeson"+ | +false+
| +"Guy Pearce"+ | +false+
| +"Kathryn Bigelow"+ | +false+
| +"Jessica Chastain"+ | +false+
2+d|Rows: 6

|===

======

[NOTE]
====
The *function* `exists()` looks very similar to the *expression* `+EXISTS { ... }+`, but they are not related.

See xref::subqueries/existential.adoc[Using EXISTS subqueries] for more information.
====


[[functions-isempty]]
== isEmpty()

The function `isEmpty()` returns `true` if the given list or map contains no elements, or if the given `STRING` contains no characters.

*Syntax:*

[source, syntax, role="noheader"]
----
isEmpty(list)
----

*Returns:*

|===

| `BOOLEAN`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `list`
| An expression that returns a list.

|===


.+isEmpty(list)+
======

.Query
[source, cypher]
----
MATCH (p:Person)
WHERE NOT isEmpty(p.nationality)
RETURN p.name, p.nationality
----

This query returns every `Person` node in the graph with a set `nationality` property value (i.e., all `Person` nodes except for `Jessica Chastain`):

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| p.name | p.nationality
| "Keanu Reeves" | "Canadian"
| "Carrie Anne Moss" | "American"
| "Liam Neeson" | "Northern Irish"
| "Guy Pearce" | "Australian"
| "Kathryn Bigelow" | "American"
2+d|Rows: 5
|===

======


*Syntax:*

[source, syntax, role="noheader"]
----
isEmpty(map)
----

*Returns:*

|===

| `BOOLEAN`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `map`
| An expression that returns a map.

|===


.+isEmpty(map)+
======

.Query
[source, cypher, indent=0]
----
MATCH (n)
WHERE isEmpty(properties(n))
RETURN n
----

Because the example graph contains no empty nodes, nothing is returned:

.Result
----
(no changes, no records)
----

======


*Syntax:*

[source, syntax, role="noheader"]
----
isEmpty(string)
----

*Returns:*
|===

| `BOOLEAN`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `string`
| An expression that returns a `STRING`.

|===


.+isEmpty(string)+
======

.Query
[source, cypher, indent=0]
----
MATCH (p:Person)
WHERE isEmpty(p.address)
RETURN p.name AS name
----

The `name` property of each node that has an empty `STRING` `address` property is returned:

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +name+
| +"Jessica Chastain"+
1+d|Rows: 1

|===

======

[NOTE]
====
The function `isEmpty()`, like most other Cypher functions, returns `null` if `null` is passed in to the function.
That means that a predicate `isEmpty(n.address)` will filter out all nodes where the `address` property is not set.
Thus, `isEmpty()` is not suited to test for `null`-values.
`IS NULL` or `IS NOT NULL` should be used for that purpose.
====


[[functions-none]]
== none()

The function `none()` returns `true` if the predicate does _not_ hold for any element in the given list.

`null` is returned if the list is `null`, or if the predicate evaluates to `null` for at least one element and does not evaluate to `true` for any other element.

*Syntax:*

[source, syntax, role="noheader"]
----
none(variable IN list WHERE predicate)
----

*Returns:*
|===

| `BOOLEAN`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `list`
a|
An expression that returns a list.
A single element cannot be explicitly passed as a literal in the cypher statement.
However, an implicit conversion will happen for single elements when passing node properties during cypher execution.

| `variable`
| A variable that can be used from within the predicate.

| `predicate`
| A predicate that is tested against all items in the list.

|===


.+none()+
======

.Query
[source, cypher, indent=0]
----
MATCH p = (n)-[*]->(b)
WHERE
  n.name = 'Keanu Reeves'
  AND none(x IN nodes(p) WHERE x.age > 60)
RETURN p
----

No node in the returned path has an `age` property with a greater value than `60`:

image::predicate_function_example.svg[width="300",role="middle"]


.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +p+
| +(:Person {nationality: "Canadian",name: "Keanu Reeves",age: 58})-[:KNOWS]->(:Person {nationality: "American",name: "Carrie Anne Moss",age: 55})+
| +(:Person {nationality: "Canadian",name: "Keanu Reeves",age: 58})-[:KNOWS]->(:Person {nationality: "American",name: "Carrie Anne Moss",age: 55})-[:KNOWS]->(:Person {nationality: "Australian",name: "Guy Pearce",age: 55})+
1+d|Rows: 2

|===

======


[[functions-single]]
== single()

The function `single()` returns `true` if the predicate holds for exactly _one_ of the elements in the given list.

`null` is returned if the list is `null`, or if the predicate evaluates to `null` for at least one element and `true` for max one element.

*Syntax:*

[source, syntax, role="noheader"]
----
single(variable IN list WHERE predicate)
----

*Returns:*
|===

| `BOOLEAN`

|===

*Arguments:*
[options="header"]
|===
| Name | Description

| `list`
| An expression that returns a list.

| `variable`
| A variable that can be used from within the predicate.

| `predicate`
| A predicate that is tested against all items in the list.

|===


.+single()+
======

.Query
[source, cypher, indent=0]
----
MATCH p = (n)-->(b)
WHERE
  n.name = 'Keanu Reeves'
  AND single(x IN nodes(p) WHERE x.nationality = 'Northern Irish')
RETURN p
----

In every returned path there is exactly one node which the `nationality` property value `Northern Irish`:

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +p+
| +(:Person {nationality: "Canadian",name: "Keanu Reeves",age: 58})-[:KNOWS]->(:Person {nationality: "Northern Irish",name: "Liam Neeson",age: 70})+
1+d|Rows: 1

|===

======
