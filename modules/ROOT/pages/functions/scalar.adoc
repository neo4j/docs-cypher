:description: Scalar functions return a single value.

[[query-functions-scalar]]
= Scalar functions

Scalar functions return a single value.


[IMPORTANT]
====
The `length()` and `size()` functions are quite similar, and so it is important to take note of the difference.

Function `length()`:: Only works for xref::functions/scalar.adoc#functions-length[paths].
Function `size()`:: Only works for the three types: xref::functions/scalar.adoc#functions-size-of-string[strings], xref::functions/scalar.adoc#functions-size[lists], xref::functions/scalar.adoc#functions-size-of-pattern-comprehension[pattern comprehension].
====

image:graph_scalar_functions.svg[]

////
[source, cypher, role=test-setup]
----
CREATE
  (alice:Developer {name:'Alice', age: 38, eyes: 'brown'}),
  (bob {name: 'Bob', age: 25, eyes: 'blue'}),
  (charlie {name: 'Charlie', age: 53, eyes: 'green'}),
  (daniel {name: 'Daniel', age: 54, eyes: 'brown'}),
  (eskil {name: 'Eskil', age: 41, eyes: 'blue', liked_colors: ['pink', 'yellow', 'black']}),
  (alice)-[:KNOWS]->(bob),
  (alice)-[:KNOWS]->(charlie),
  (bob)-[:KNOWS]->(daniel),
  (charlie)-[:KNOWS]->(daniel),
  (bob)-[:MARRIED]->(eskil)
----
////


[[functions-char_length]]
== char_length()

_This feature was introduced in Neo4j 5.13._

The function `char_length()` returns the number of Unicode characters in a `STRING`.
This function is an alias of the xref::functions/scalar.adoc#functions-size[`size()`] function.

*Syntax:*

[source, syntax, role="noheader"]
----
char_length(string)
----

*Returns:*

|===

| `INTEGER`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `string`
| An expression that returns a `STRING`.

|===

*Considerations:*
|===

| `char_length(null)` returns `null`.

|===


.+char_length()+
======

.Query
[source, cypher, indent=0]
----
RETURN char_length('Alice')
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +char_length('Alice')+
| +5+
1+d|Rows: 1

|===

The number of Unicode characters in the string is returned.

======

[[functions-character_length]]
== character_length()

_This feature was introduced in Neo4j 5.13._

The function `character_length()` returns the number of Unicode characters in a `STRING`.
This function is an alias of the xref::functions/scalar.adoc#functions-size[`size()`] function.

*Syntax:*

[source, syntax, role="noheader"]
----
character_length(string)
----

*Returns:*

|===

| `INTEGER`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `string`
| An expression that returns a `STRING`.

|===

*Considerations:*
|===

| `character_length(null)` returns `null`.

|===


.+character_length()+
======

.Query
[source, cypher, indent=0]
----
RETURN character_length('Alice')
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +character_length('Alice')+
| +5+
1+d|Rows: 1

|===

The number of Unicode characters in the string is returned.

======

[[functions-coalesce]]
== coalesce()

The function `coalesce()` returns the first non-`null` value in the given list of expressions.

*Syntax:*

[source, syntax, role="noheader"]
----
coalesce(expression [, expression]*)
----

*Returns:*

|===

| The type of the value returned will be that of the first non-`null` expression.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| An expression that may return `null`.

|===

*Considerations:*

|===

| `null` will be returned if all the arguments are `null`.

|===


.+coalesce()+
======

.Query
[source, cypher, indent=0]
----
MATCH (a)
WHERE a.name = 'Alice'
RETURN coalesce(a.hairColor, a.eyes)
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +coalesce(a.hairColor, a.eyes)+
| +"brown"+
1+d|Rows: 1

|===

======


[[functions-elementid]]
== elementId()

The function `elementId()` returns a node or relationship identifier, unique within a specific transaction and DBMS.

There are important considerations to bear in mind when using `elementId()`:

. Every node and relationship is guaranteed an element ID.
This ID is unique among both nodes and relationships across all databases in the same DBMS within the scope of a single transaction.
However, no guarantees are given regarding the order of the returned ID values or the length of the ID string values.
Outside of the scope of a single transaction, no guarantees are given about the mapping between ID values and elements.

. Neo4j reuses its internal IDs when nodes and relationships are deleted.
This means that applications using, and relying on internal Neo4j IDs, are brittle or at risk of making mistakes.
It is therefore recommended to rather use application-generated IDs.

*Syntax:*

[source, syntax, role="noheader"]
----
elementId(expression)
----

*Returns:*

|===

| `STRING`

|===


*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| An expression that returns a `NODE` or a `RELATIONSHIP`.

|===


*Considerations:*

|===

| `elementId(null)` returns `null`.
| `elementId` on values other than a `NODE`, `RELATIONSHIP`, or null will fail the query.

|===


.+elementId()+
======

////
CREATE
  (alice:Developer {name:'Alice', age: 38, eyes: 'brown'}),
  (bob {name: 'Bob', age: 25, eyes: 'blue'}),
  (charlie {name: 'Charlie', age: 53, eyes: 'green'}),
  (daniel {name: 'Daniel', age: 54, eyes: 'brown'}),
  (eskil {name: 'Eskil', age: 41, eyes: 'blue', liked_colors: ['pink', 'yellow', 'black']}),
  (alice)-[:KNOWS]->(bob),
  (alice)-[:KNOWS]->(charlie),
  (bob)-[:KNOWS]->(daniel),
  (charlie)-[:KNOWS]->(daniel),
  (bob)-[:MARRIED]->(eskil)
////

.Query
[source, cypher]
----
MATCH (a)
RETURN elementId(a)
----

The node identifier for each of the nodes is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +elementId(a)+
| +"4:c0a65d96-4993-4b0c-b036-e7ebd9174905:0"+
| +"4:c0a65d96-4993-4b0c-b036-e7ebd9174905:1"+
| +"4:c0a65d96-4993-4b0c-b036-e7ebd9174905:2"+
| +"4:c0a65d96-4993-4b0c-b036-e7ebd9174905:3"+
| +"4:c0a65d96-4993-4b0c-b036-e7ebd9174905:4"+
1+d|Rows: 5
|===

======


[[functions-endnode]]
== endNode()

The function `endNode()` returns the end `NODE` of a `RELATIONSHIP`.

*Syntax:*

[source, syntax, role="noheader"]
----
endNode(relationship)
----

*Returns:*

|===

| `NODE`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `relationship`
| An expression that returns a `RELATIONSHIP`.

|===

*Considerations:*

|===

| `endNode(null)` returns `null`.

|===


.+endNode()+
======

.Query
[source, cypher, indent=0]
----
MATCH (x:Developer)-[r]-()
RETURN endNode(r)
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +endNode(r)+
| +{name:"Bob",age:25,eyes:"blue"}+
| +{name:"Charlie",age:53,eyes:"green"}+
1+d|Rows: 2
|===

======


[[functions-head]]
== head()

The function `head()` returns the first element in a list.

*Syntax:*

[source, syntax, role="noheader"]
----
head(expression)
----

*Returns:*

|===

| The type of the value returned will be that of the first element of the list.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| An expression that returns a list.

|===

*Considerations:*
|===

| `head(null)` returns `null`.
| `head([])` returns `null`.
| If the first element in `list` is `null`, `head(list)` will return `null`.

|===


.+head()+
======

.Query
[source, cypher, indent=0]
----
MATCH (a)
WHERE a.name = 'Eskil'
RETURN a.liked_colors, head(a.liked_colors)
----

The first element in the list is returned.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===

| +a.liked_colors+ | +head(a.liked_colors)+
| +["pink","yellow","black"]+ | +"pink"+
2+d|Rows: 1

|===

======

[[functions-id]]
== id() label:deprecated[]

The function `id()` returns a node or a relationship identifier, unique by an object type and a database.
Therefore, it is perfectly allowable for `id()` to return the same value for both nodes and relationships in the same database.

[IMPORTANT]
====
The function `id` is deprecated.
Use the function `elementId` instead.
====

[NOTE]
====
Neo4j implements the ID so that:

*Node*

Every node in a database has an identifier.
The identifier for a node is guaranteed to be unique among other nodes' identifiers in the same database, within the scope of a single transaction.

*Relationship*

Every relationship in a database has an identifier.
The identifier for a relationship is guaranteed to be unique among other relationships' identifiers in the same database, within the scope of a single transaction.
====

[NOTE]
====
On a link:{neo4j-docs-base-uri}/operations-manual/{page-version}/database-administration/composite-databases/concepts/[composite database], the `id()` function should be used with caution.
It is recommended to use xref:functions/scalar.adoc#functions-elementid[`elementId()`] instead. 

When called in database-specific subqueries, the resulting id value for a node or relationship is local to that database.
The local id for nodes or relationships from different databases may be the same.

When called from the root context of a query, the resulting value is an extended id for the node or relationship.
The extended id is likely different from the local id for the same node or relationship.
====

*Syntax:*

[source, syntax, role="noheader"]
----
id(expression)
----

*Returns:*

|===

| `INTEGER`

|===

*Arguments:*
[options="header"]
|===
| Name | Description

| `expression`
| An expression that returns a `NODE` or a `RELATIONSHIP`.

|===

*Considerations:*
|===

| `id(null)` returns `null`.

|===


.+id()+
======

.Query
[source, cypher, indent=0]
----
MATCH (a)
RETURN id(a)
----

The node identifier for each of the nodes is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +id(a)+
| +0+
| +1+
| +2+
| +3+
| +4+
1+d|Rows: 5

|===

======


[[functions-last]]
== last()

The function `last()` returns the last element in a list.

*Syntax:*

[source, syntax, role="noheader"]
----
last(expression)
----

*Returns:*

|===

| The type of the value returned will be that of the last element of the list.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| An expression that returns a list.

|===

*Considerations:*

|===

| `last(null)` returns `null`.
| `last([])` returns `null`.
| If the last element in `list` is `null`, `last(list)` will return `null`.

|===


.+last()+
======

.Query
[source, cypher, indent=0]
----
MATCH (a)
WHERE a.name = 'Eskil'
RETURN a.liked_colors, last(a.liked_colors)
----

The last element in the list is returned.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===

| +a.liked_colors+ | +last(a.liked_colors)+
| +["pink","yellow","black"]+ | +"black"+
2+d|Rows: 1

|===

======


[[functions-length]]
== length()

The function `length()` returns the length of a `PATH`.

*Syntax:*

[source, syntax, role="noheader"]
----
length(path)
----

*Returns:*

|===

| `INTEGER`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `path`
| An expression that returns a `PATH`.

|===

*Considerations:*

|===

| `length(null)` returns `null`.

|===


.+length()+
======

.Query
[source, cypher, indent=0]
----
MATCH p = (a)-->(b)-->(c)
WHERE a.name = 'Alice'
RETURN length(p)
----

The length of the path `p` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +length(p)+
| +2+
| +2+
| +2+
1+d|Rows: 3

|===

======


[[functions-nullIf]]
== nullIf()

The function `nullIf()` returns null if the two given parameters are equivalent, otherwise returns the value of the first parameter.
This function is the opposite of the xref:functions/scalar.adoc#functions-coalesce[coalesce()] function, which returns a default value if the given value is null.

*Syntax:*

[source, syntax, role="noheader"]
----
nullIf(v1, v2)
----

*Returns:*

|===

| `ANY`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `v1`
| An expression that returns `ANY`.

| `v2`
| An expression that returns `ANY`.

|===

.+nullIf()+
======

.Query
[source, cypher, indent=0]
----
RETURN nullIf(4, 4)
----

The null value is returned as the two parameters are equivalent.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +nullIf(4, 4)+
| +null+
1+d|Rows: 1

|===

======

.+nullIf()+
======

.Query
[source, cypher, indent=0]
----
RETURN nullIf("abc", "def")
----

The first parameter, "abc", is returned, as the two parameters are not equivalent.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +nullIf("abc", "def")+
| +"abc"+
1+d|Rows: 1

|===

======

.+nullIf()+
======

The `nullIf()` function can be used in conjunction with the `coalesce()` function for transforming one data value into another value:

.Query
[source, cypher, indent=0]
----
MATCH (a)
RETURN a.name AS name, coalesce(nullIf(a.eyes, "brown"), "hazel") AS eyeColor
----


.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===

| +name+      | +eyeColor+
| +"Alice"+   | +"hazel"+
| +"Bob"+     | +"blue"+
| +"Charlie"+ | +"green"+
| +"Daniel"+  | +"hazel"+
| +"Eskil"+   | +"blue"+
2+d|Rows: 5

|===

======


[[functions-properties]]
== properties()

The function `properties()` returns a `MAP` containing all the properties; the function can be utilized for a relationship or a node.
If the argument is already a `MAP`, it is returned unchanged.

*Syntax:*

[source, syntax, role="noheader"]
----
properties(expression)
----

*Returns:*

|===

| `MAP`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| An expression that returns a `RELATIONSHIP`, a `NODE`, or a `MAP`.

|===

*Considerations:*

|===

| `properties(null)` returns `null`.

|===


.+properties()+
======

.Query
[source, cypher, indent=0]
----
CREATE (p:Person {name: 'Stefan', city: 'Berlin'})
RETURN properties(p)
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +properties(p)+
| +{"city":"Berlin","name":"Stefan"}+
1+d|Rows: 1 +
Nodes created: 1 +
Properties set: 2 +
Labels added: 1

|===

======


[[functions-randomuuid]]
== randomUUID()

The function `randomUUID()` returns a randomly-generated Universally Unique Identifier (UUID), also known as a Globally Unique Identifier (GUID).
This is a 128-bit value with strong guarantees of uniqueness.

*Syntax:*

[source, syntax, role="noheader"]
----
randomUUID()
----

*Returns:*

|===

| `STRING`

|===


.+randomUUID()+
======

.Query
[source, cypher, indent=0]
----
RETURN randomUUID() AS uuid
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +uuid+
| +"9f4c297d-309a-4743-a196-4525b96135c1"+
1+d|Rows: 1
|===

A randomly-generated UUID is returned.

======


[[functions-size]]
== size()

The function `size()` returns the number of elements in a list.

*Syntax:*

[source, syntax, role="noheader"]
----
size(list)
----

*Returns:*

|===

| `INTEGER`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `list`
| An expression that returns a list.

|===

*Considerations:*
|===

| `size(null)` returns `null`.

|===


.+size()+
======

.Query
[source, cypher, indent=0]
----
RETURN size(['Alice', 'Bob'])
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +size(['Alice', 'Bob'])+
| +2+
1+d|Rows: 1

|===

The number of elements in the list is returned.

======


[[functions-size-of-pattern-comprehension]]
== size() applied to pattern comprehension

This is the same function `size()` as described above, but you pass in a pattern comprehension.
The function size will then calculate on a `LIST<PATH>`.

*Syntax:*

[source, syntax, role="noheader"]
----
size(pattern comprehension)
----

*Arguments:*

[options="header"]
|===
| Name | Description

| `pattern comprehension`
| A pattern comprehension that returns a list.

|===


.+size()+
======

.Query
[source, cypher, indent=0]
----
MATCH (a)
WHERE a.name = 'Alice'
RETURN size([p=(a)-->()-->() | p]) AS fof
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +fof+
| +3+
1+d|Rows: 1

|===

The number of paths matching the pattern expression is returned. (The size of the list of paths).

======


[[functions-size-of-string]]
== size() applied to string

The function `size()` returns the number of Unicode characters in a `STRING`.

*Syntax:*

[source, syntax, role="noheader"]
----
size(string)
----

*Returns:*

|===

| `INTEGER`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `string`
| An expression that returns a `STRING` value.

|===

*Considerations:*

|===

| `size(null)` returns `null`.

|===


.+size()+
======

.Query
[source, cypher, indent=0]
----
MATCH (a)
WHERE size(a.name) > 6
RETURN size(a.name)
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +size(a.name)+
| +7+
1+d|Rows: 1

|===

The number of characters in the string `'Charlie'` is returned.

======


[[functions-startnode]]
== startNode()

The function `startNode()` returns the start `NODE` of a `RELATIONSHIP`.

*Syntax:*

[source, syntax, role="noheader"]
----
startNode(relationship)
----

*Returns:*

|===

| `NODE`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `relationship`
| An expression that returns a `RELATIONSHIP`.

|===

*Considerations:*

|===

| `startNode(null)` returns `null`.

|===


.+startNode()+
======

.Query
[source, cypher, indent=0]
----
MATCH (x:Developer)-[r]-()
RETURN startNode(r)
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +startNode(r)+
| +{name:"Alice",age:38,eyes:"brown"}+
| +{name:"Alice",age:38,eyes:"brown"}+
1+d|Rows: 2

|===

======


[[functions-timestamp]]
== timestamp()

The function `timestamp()` returns the difference, measured in milliseconds, between the current time and midnight, January 1, 1970 UTC.

[NOTE]
====
It is the equivalent of `datetime().epochMillis`.
====

*Syntax:*

[source, syntax, role="noheader"]
----
timestamp()
----

*Returns:*

|===

| `INTEGER`

|===

*Considerations:*

|===

|`timestamp()` will return the same value during one entire query, even for long-running queries.

|===


.+timestamp()+
======

.Query
[source, cypher, indent=0]
----
RETURN timestamp()
----

The time in milliseconds is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +timestamp()+
| +1655201331965+
1+d|Rows: 1

|===

======


[[functions-toboolean]]
== toBoolean()

The function `toBoolean()` converts a `STRING`, `INTEGER` or `BOOLEAN` value to a `BOOLEAN` value.

*Syntax:*

[source, syntax, role="noheader"]
----
toBoolean(expression)
----

*Returns:*

|===

| `BOOLEAN`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| An expression that returns a `BOOLEAN`, `STRING` or `INTEGER` value.

|===

*Considerations:*

|===

| `toBoolean(null)` returns `null`.
| If `expression` is a `BOOLEAN` value, it will be returned unchanged.
| If the parsing fails, `null` will be returned.
| If `expression` is the `INTEGER` value `0`, `false` will be returned. For any other `INTEGER` value `true` will be returned.
| This function will return an error if provided with an expression that is not a `STRING`, `INTEGER` or `BOOLEAN` value.

|===

.+toBoolean()+
======

.Query
[source, cypher, indent=0]
----
RETURN toBoolean('true'), toBoolean('not a boolean'), toBoolean(0)
----

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===

| +toBoolean('true')+ | +toBoolean('not a boolean')+ | +toBoolean(0)+
| +true+ | +<null>+ | +false+
3+d|Rows: 1

|===

======


[[functions-tobooleanornull]]
== toBooleanOrNull()

The function `toBooleanOrNull()` converts a `STRING`, `INTEGER` or `BOOLEAN` value to a `BOOLEAN` value. For any other input value, `null` will be returned.

*Syntax:*

[source, syntax, role="noheader"]
----
toBooleanOrNull(expression)
----

*Returns:*

|===

| `BOOLEAN` or `null`.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| Any expression that returns a value.

|===

*Considerations:*

|===

| `toBooleanOrNull(null)` returns `null`.
| If `expression` is a `BOOLEAN` value, it will be returned unchanged.
| If the parsing fails, `null` will be returned.
| If `expression` is the `INTEGER` value `0`, `false` will be returned. For any other `INTEGER` value `true` will be returned.
| If the `expression` is not a `STRING`, `INTEGER` or `BOOLEAN` value, `null` will be returned.

|===

.+toBooleanOrNull()+
======

.Query
[source, cypher, indent=0]
----
RETURN toBooleanOrNull('true'), toBooleanOrNull('not a boolean'), toBooleanOrNull(0), toBooleanOrNull(1.5)
----

.Result
[role="queryresult",options="header,footer",cols="4*<m"]
|===
| +toBooleanOrNull('true')+ | +toBooleanOrNull('not a boolean')+ | +toBooleanOrNull(0)+ | +toBooleanOrNull(1.5)+
| +true+ | +<null>+ | +false+ | +<null>+
4+d|Rows: 1
|===

======


[[functions-tofloat]]
== toFloat()

The function `toFloat()` converts an `INTEGER`, `FLOAT` or a `STRING` value to a `FLOAT`.

*Syntax:*

[source, syntax, role="noheader"]
----
toFloat(expression)
----

*Returns:*
|===

| `FLOAT`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| An expression that returns an `INTEGER`, `FLOAT` or a `STRING` value.

|===

*Considerations:*

|===

| `toFloat(null)` returns `null`.
| If `expression` is a `FLOAT`, it will be returned unchanged.
| If the parsing fails, `null` will be returned.
| This function will return an error if provided with an expression that is not an `INTEGER`, `FLOAT` or a `STRING` value.

|===


.+toFloat()+
======

.Query
[source, cypher, indent=0]
----
RETURN toFloat('11.5'), toFloat('not a number')
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]

|===
| +toFloat('11.5')+ | +toFloat('not a number')+
| +11.5+ | +<null>+
2+d|Rows: 1

|===

======

[[functions-tofloatornull]]
== toFloatOrNull()

The function `toFloatOrNull()` converts an `INTEGER`, `FLOAT` or a `STRING` value to a `FLOAT`.
For any other input value, `null` will be returned.

*Syntax:*

[source, syntax, role="noheader"]
----
toFloatOrNull(expression)
----

*Returns:*
|===

| `FLOAT` or `null`.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| Any expression that returns a value.

|===

*Considerations:*

|===

|`toFloatOrNull(null)` returns `null`.
|If `expression` is a `FLOAT`, it will be returned unchanged.
|If the parsing fails, `null` will be returned.
|If the `expression` is not an `INTEGER`, `FLOAT` or a `STRING` value, `null` will be returned.

|===


.+toFloatOrNull()+
======

.Query
[source, cypher, indent=0]
----
RETURN toFloatOrNull('11.5'), toFloatOrNull('not a number'), toFloatOrNull(true)
----

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===

| +toFloatOrNull('11.5')+ | +toFloatOrNull('not a number')+ | +toFloatOrNull(true)+
| +11.5+ | +<null>+ | +<null>+
3+d|Rows: 1

|===

======


[[functions-tointeger]]
== toInteger()

The function `toInteger()` converts a `BOOLEAN`, `INTEGER`, `FLOAT` or a `STRING` value to an `INTEGER` value.

*Syntax:*

[source, syntax, role="noheader"]
----
toInteger(expression)
----

*Returns:*

|===

| `INTEGER`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression`
| An expression that returns a `BOOLEAN`, `FLOAT`, `INTEGER` or a `STRING` value.

|===

*Considerations:*

|===

| `toInteger(null)` returns `null`.
| If `expression` is an integer value, it will be returned unchanged.
| If the parsing fails, `null` will be returned.
| If `expression` is the boolean value `false`, `0` will be returned.
| If `expression` is the boolean value `true`, `1` will be returned.
| This function will return an error if provided with an expression that is not a boolean, floating point, integer or a string value.

|===


.+toInteger()+
======

.Query
[source, cypher, indent=0]
----
RETURN toInteger('42'), toInteger('not a number'), toInteger(true)
----

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===

| +toInteger('42')+ | +toInteger('not a number')+ | +toInteger(true)+
| +42+ | +<null>+ | +1+
3+d|Rows: 1

|===

======


[[functions-tointegerornull]]
== toIntegerOrNull()

The function `toIntegerOrNull()` converts a `BOOLEAN`, `INTEGER`, `FLOAT` or a `STRING` value to an `INTEGER` value. For any other input value, `null` will be returned.

*Syntax:*

[source, syntax, role="noheader"]
----
toIntegerOrNull(expression)
----

*Returns:*
|===

| `INTEGER` or `null`.

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression` | Any expression that returns a value.

|===

*Considerations:*

|===

| `toIntegerOrNull(null)` returns `null`.
| If `expression` is an integer value, it will be returned unchanged.
| If the parsing fails, `null` will be returned.
| If `expression` is the `BOOLEAN` value `false`, `0` will be returned.
| If `expression` is the `BOOLEAN` value `true`, `1` will be returned.
| If the `expression` is not a `BOOLEAN`, `FLOAT`, `INTEGER` or a `STRING` value, `null` will be returned.

|===


.+toIntegerOrNull()+
======

.Query
[source, cypher, indent=0]
----
RETURN toIntegerOrNull('42'), toIntegerOrNull('not a number'), toIntegerOrNull(true), toIntegerOrNull(['A', 'B', 'C'])
----

.Result
[role="queryresult",options="header,footer",cols="4*<m"]
|===

| +toIntegerOrNull('42')+ | +toIntegerOrNull('not a number')+ | +toIntegerOrNull(true)+ | +toIntegerOrNull(['A', 'B', 'C'])+
| +42+ | +<null>+ | +1+ | +<null>+
4+d|Rows: 1

|===

======


[[functions-type]]
== type()

The function `type()` returns the `STRING` representation of the `RELATIONSHIP` type.

*Syntax:*

[source, syntax, role="noheader"]
----
type(relationship)
----

*Returns:*

|===

| `STRING`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `relationship`
| An expression that returns a `RELATIONSHIP`.

|===

*Considerations:*

|===

| `type(null)` returns `null`.

|===


.+type()+
======

.Query
[source, cypher, indent=0]
----
MATCH (n)-[r]->()
WHERE n.name = 'Alice'
RETURN type(r)
----

The relationship type of `r` is returned.

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +type(r)+
| +"KNOWS"+
| +"KNOWS"+
1+d|Rows: 2

|===

======

[[functions-valueType]]
== valueType()

_This feature was introduced in Neo4j 5.13._

The function `valueType()` returns a `STRING` representation of the most precise value type that the given expression evaluates to.
The output is deterministic and makes use of xref::values-and-types/property-structural-constructed.adoc#type-normalization[Type Normalization].

*Syntax:*

[source, syntax, role="noheader"]
----
valueType(expression)
----

*Returns:*

|===

| `STRING`

|===

*Arguments:*

[options="header"]
|===
| Name | Description

| `expression` | Any expression that returns a value.

|===

*Considerations:*

It is possible that future releases of Cypher will include updates to the current type system.
This can include the introduction of new types and subtypes of already supported types.
If a new type is introduced, it will be returned by the `valueType()` function as soon as it is released.
However, if a more precise subtype of a previously supported type is introduced, it would be considered a breaking change.
As a result, any new subtypes introduced after the release of Neo4j 5.13 will not be returned by the `valueType()` function until the following major release (Neo4j 6.0).

For example, the function currently returns `"FLOAT"`, but if a more specific `FLOAT` type was added, e.g. `FLOAT32`, this would be considered more specific and not be returned until Neo4j 6.0.
As a result,`"FLOAT"` would continue to be returned for any `FLOAT32` values until the release of Neo4j 6.0.

With this in mind, the below list contains all supported types (as of Neo4j 5.13) displayed by the `valueType()` function until the release of Neo4j 6.0:

*  Predefined types
** `NOTHING`
** `NULL`
** `BOOLEAN`
** `STRING`
** `INTEGER`
** `FLOAT`
** `DATE`
** `LOCAL TIME`
** `ZONED TIME`
** `LOCAL DATETIME`
** `ZONED DATETIME`
** `DURATION`
** `POINT`
** `NODE`
** `RELATIONSHIP`
* Constructed types
** `MAP`
** `LIST<INNER_TYPE>` (ordered by the inner type)
** `PATH`
* Dynamic union types
** `INNER_TYPE_1 \| INNER_TYPE_2...` (ordered by specific rules for closed dynamic union type)
** `ANY`

This should be taken into account when relying on the output of the `valueType()` function.

See the xref::values-and-types/type-predicate.adoc[type predicate expression] for an alternative way of testing type values.


.+valueType()+
======

.Query
[source, cypher, indent=0]
----
UNWIND ["abc", 1, 2.0, true, [date()]] AS value
RETURN valueType(value) AS result
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +result+
| +"STRING NOT NULL"+
| +"INTEGER NOT NULL"+
| +"FLOAT NOT NULL"+
| +"BOOLEAN NOT NULL"+
| +"LIST<DATE NOT NULL> NOT NULL"+
1+d|Rows: 5

|===

======

