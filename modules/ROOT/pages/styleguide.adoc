:description: The recommended style when writing Cypher queries.

[appendix]
[[cypher-styleguide]]
= Cypher styleguide

[abstract]
--
The recommended style when writing Cypher queries.
--

This appendix contains the following:

* xref::styleguide.adoc#cypher-styleguide-general-recommendations[General recommendations]
* xref::styleguide.adoc#cypher-styleguide-indentation-and-line-breaks[Indentations and line breaks]
* xref::styleguide.adoc#cypher-styleguide-casing[Casing]
* xref::styleguide.adoc#cypher-styleguide-spacing[Spacing]
* xref::styleguide.adoc#cypher-styleguide-patterns[Patterns]
* xref::styleguide.adoc#cypher-styleguide-meta-characters[Meta characters]

The purpose of the styleguide is to make the code as easy to read as possible, and thereby contributing to lower cost of maintenance.

For rules and recommendations for naming of labels, relationship types and properties, please see the xref::syntax/naming.adoc[Naming rules and recommendations].


[[cypher-styleguide-general-recommendations]]
== General recommendations

* When using Cypher language constructs in prose, use a `monospaced` font and follow the styling rules.
* When referring to labels and relationship types, the colon should be included as follows: `:Label`, `:REL_TYPE`.
* When referring to functions, use lower camel case and parentheses should be used as follows: `shortestPath()`.
Arguments should normally not be included.
* If you are storing Cypher statements in a separate file, use the file extension `.cypher`.


[[cypher-styleguide-indentation-and-line-breaks]]
== Indentation and line breaks

* Start a new clause on a new line.
+
.Bad
[source, cypher]
----
MATCH (n) WHERE n.name CONTAINS 's' RETURN n.name
----
+
.Good
[source, cypher]
----
MATCH (n)
WHERE n.name CONTAINS 's'
RETURN n.name
----

* Indent `ON CREATE` and `ON MATCH` with two spaces.
Put `ON CREATE` before `ON MATCH` if both are present.
+
// https://github.com/neo-technology/neo4j/pull/19075
// fix haven't go inside 5.x
.Bad
[source, cypher, role="test-skip"]
----
MERGE (n) ON CREATE SET n.prop = 0
MERGE (a:A)-[:T]-(b:B)
ON MATCH SET b.name = 'you'
ON CREATE SET a.name = 'me'
RETURN a.prop
----
+
.Good
[source, cypher]
----
MERGE (n)
  ON CREATE SET n.prop = 0
MERGE (a:A)-[:T]-(b:B)
  ON CREATE SET a.name = 'me'
  ON MATCH SET b.name = 'you'
RETURN a.prop
----

* Start a subquery on a new line after the opening brace, indented with two (additional) spaces.
Leave the closing brace on its own line.
+
.Bad
[source, cypher]
----
MATCH (a:A)
WHERE
  EXISTS { MATCH (a)-->(b:B) WHERE b.prop = $param }
RETURN a.foo
----
+
.Also bad
[source, cypher]
----
MATCH (a:A)
WHERE EXISTS
{MATCH (a)-->(b:B)
WHERE b.prop = $param}
RETURN a.foo
----
+
.Good
[source, cypher]
----
MATCH (a:A)
WHERE EXISTS {
  MATCH (a)-->(b:B)
  WHERE b.prop = $param
}
RETURN a.foo
----

* Do not break the line if the simplified subquery form is used.
+
.Bad
[source, cypher]
----
MATCH (a:A)
WHERE EXISTS {
  (a)-->(b:B)
}
RETURN a.prop
----
+
.Good
[source, cypher]
----
MATCH (a:A)
WHERE EXISTS { (a)-->(b:B) }
RETURN a.prop
----


[[cypher-styleguide-casing]]
== Casing

* Write keywords in upper case.
+
.Bad
[source, cypher]
----
match (p:Person)
where p.name starts with 'Ma'
return p.name
----
+
.Good
[source, cypher]
----
MATCH (p:Person)
WHERE p.name STARTS WITH 'Ma'
RETURN p.name
----

* Write the value `null` in lower case.
+
.Bad
[source, cypher]
----
WITH NULL AS n1, Null AS n2
RETURN n1 IS NULL AND n2 IS NOT NULL
----
+
.Good
[source, cypher]
----
WITH null AS n1, null AS n2
RETURN n1 IS NULL AND n2 IS NOT NULL
----

* Write boolean literals (`true` and `false`) in lower case.
+
.Bad
[source, cypher]
----
WITH TRUE AS b1, False AS b2
RETURN b1 AND b2
----
+
.Good
[source, cypher]
----
WITH true AS b1, false AS b2
RETURN b1 AND b2
----

* Use camel case, starting with a lower-case character, for:
** functions
** properties
** variables
** parameters
+
.Bad
[source, cypher]
----
CREATE (N {Prop: 0})
WITH RAND() AS Rand, $pArAm AS MAP
RETURN Rand, MAP.property_key, Count(N)
----
+
.Good
[source, cypher]
----
CREATE (n {prop: 0})
WITH rand() AS rand, $param AS map
RETURN rand, map.propertyKey, count(n)
----


[[cypher-styleguide-spacing]]
== Spacing

* For literal maps:
** No space between the opening brace and the first key
** No space between key and colon
** One space between colon and value
** No space between value and comma
** One space between comma and next key
** No space between the last value and the closing brace
+
.Bad
[source, cypher]
----
WITH { key1 :'value' ,key2  :  42 } AS map
RETURN map
----
+
.Good
[source, cypher]
----
WITH {key1: 'value', key2: 42} AS map
RETURN map
----

* One space between label/type predicates and property predicates in patterns.
+
.Bad
[source, cypher]
----
MATCH (p:Person{property: -1})-[:KNOWS   {since: 2016}]->()
RETURN p.name
----
+
.Good
[source, cypher]
----
MATCH (p:Person {property: -1})-[:KNOWS {since: 2016}]->()
RETURN p.name
----

* No space in patterns.
+
.Bad
[source, cypher]
----
MATCH (:Person) --> (:Vehicle)
RETURN count(*)
----
+
.Good
[source, cypher]
----
MATCH (:Person)-->(:Vehicle)
RETURN count(*)
----

* Use a wrapping space around operators.
+
.Bad
[source, cypher]
----
MATCH p=(s)-->(e)
WHERE s.name<>e.name
RETURN length(p)
----
+
.Good
[source, cypher]
----
MATCH p = (s)-->(e)
WHERE s.name <> e.name
RETURN length(p)
----

* No space in label predicates.
+
.Bad
[source, cypher]
----
MATCH (person    : Person  :  Owner  )
RETURN person.name
----
+
.Good
[source, cypher]
----
MATCH (person:Person:Owner)
RETURN person.name
----

* Use a space after each comma in lists and enumerations.
+
.Bad
[source, cypher]
----
MATCH (),()
WITH ['a','b',3.14] AS list
RETURN list,2,3,4
----
+
.Good
[source, cypher]
----
MATCH (), ()
WITH ['a', 'b', 3.14] AS list
RETURN list, 2, 3, 4
----

* No padding space within function call parentheses.
+
.Bad
[source, cypher]
----
RETURN split( 'original', 'i' )
----
+
.Good
[source, cypher]
----
RETURN split('original', 'i')
----

* Use padding space within simple subquery expressions.
+
.Bad
[source, cypher]
----
MATCH (a:A)
WHERE EXISTS {(a)-->(b:B)}
RETURN a.prop
----
+
.Good
[source, cypher]
----
MATCH (a:A)
WHERE EXISTS { (a)-->(b:B) }
RETURN a.prop
----


[[cypher-styleguide-patterns]]
== Patterns

* When patterns wrap lines, break after arrows, not before.
+
.Bad
[source, cypher]
----
MATCH (:Person)-->(vehicle:Car)-->(:Company)
      <--(:Country)
RETURN count(vehicle)
----
+
.Good
[source, cypher]
----
MATCH (:Person)-->(vehicle:Car)-->(:Company)<--
      (:Country)
RETURN count(vehicle)
----

* Use anonymous nodes and relationships when the variable would not be used.
+
.Bad
[source, cypher]
----
CREATE (a:End {prop: 42}),
       (b:End {prop: 3}),
       (c:Begin {prop: elementId(a)})
----
+
.Good
[source, cypher]
----
CREATE (a:End {prop: 42}),
       (:End {prop: 3}),
       (:Begin {prop: elementId(a)})
----

* Chain patterns together to avoid repeating variables.
+
.Bad
[source, cypher]
----
MATCH (:Person)-->(vehicle:Car), (vehicle:Car)-->(:Company)
RETURN count(vehicle)
----
+
.Good
[source, cypher]
----
MATCH (:Person)-->(vehicle:Car)-->(:Company)
RETURN count(vehicle)
----

* Put named nodes before anonymous nodes.
+
.Bad
[source, cypher]
----
MATCH ()-->(vehicle:Car)-->(manufacturer:Company)
WHERE manufacturer.foundedYear < 2000
RETURN vehicle.mileage
----
+
.Good
[source, cypher]
----
MATCH (manufacturer:Company)<--(vehicle:Car)<--()
WHERE manufacturer.foundedYear < 2000
RETURN vehicle.mileage
----

* Keep anchor nodes at the beginning of the `MATCH` clause.
+
.Bad
[source, cypher]
----
MATCH (:Person)-->(vehicle:Car)-->(manufacturer:Company)
WHERE manufacturer.foundedYear < 2000
RETURN vehicle.mileage
----
+
.Good
[source, cypher]
----
MATCH (manufacturer:Company)<--(vehicle:Car)<--(:Person)
WHERE manufacturer.foundedYear < 2000
RETURN vehicle.mileage
----

* Prefer outgoing (left to right) pattern relationships to incoming pattern relationships.
+
.Bad
[source, cypher]
----
MATCH (:Country)-->(:Company)<--(vehicle:Car)<--(:Person)
RETURN vehicle.mileage
----
+
.Good
[source, cypher]
----
MATCH (:Person)-->(vehicle:Car)-->(:Company)<--(:Country)
RETURN vehicle.mileage
----


[[cypher-styleguide-meta-characters]]
== Meta-characters

* Use single quotes, `'`, for literal string values.
+
.Bad
[source, cypher]
----
RETURN "Cypher"
----
+
.Good
[source, cypher]
----
RETURN 'Cypher'
----

** Disregard this rule for literal strings that contain a single quote character.
If the string has both, use the form that creates the fewest escapes.
In the case of a tie, prefer single quotes.
+
.Bad
[source, cypher]
----
RETURN 'Cypher\'s a nice language', "Mats' quote: \"statement\""
----
+
.Good
[source, cypher]
----
RETURN "Cypher's a nice language", 'Mats\' quote: "statement"'
----

* Avoid having to use back-ticks to escape characters and keywords.
+
.Bad
[source, cypher]
----
MATCH (`odd-ch@racter$`:`Spaced Label` {`&property`: 42})
RETURN labels(`odd-ch@racter$`)
----
+
.Good
[source, cypher]
----
MATCH (node:NonSpacedLabel {property: 42})
RETURN labels(node)
----

* Do not use a semicolon at the end of the statement.
+
.Bad
[source, cypher]
----
RETURN 1;
----
+
.Good
[source, cypher]
----
RETURN 1
----

