[[relationship-type-expressions]]
= Relationship type expressions

Relationship type expressions evaluate to `true` or `false` when applied to the type of a relationship.

Assuming no other filters are applied, then a relationship type expression evaluating to `true` means the relationship is matched.

[NOTE]
====
Relationships must have exactly one type.
For example, the pattern `(a)-[r:R&Q]-(b)` or `(a)-[r:!%]-(b)` will never return any results.
====

xref:patterns/reference.adoc#variable-length-relationships[Variable-length relationships] may only have relationship type expressions consisting of `|`.
This means that `()-[r:!R*]-()` is not allowed, whereas `()-[r:Q|R*]-()` is allowed.

The following table displays whether the relationship type expression matches the relationship:

[cols="^3,^2,^2,^2"]
|===
|
3+^|*Relationship*

|*Relationship type expression* |  `[:A]` | `[:B]` | `[:C]`

| `[]`
| {check-mark}
| {check-mark}
| {check-mark}

| `[:A]`
| {check-mark}
|
|

| `[:A&B]`
|
|
|

| `[:A\|B]`
| {check-mark}
| {check-mark}
|

| `[:!A]`
|
| {check-mark}
| {check-mark}

| `[:!!A]`
| {check-mark}
|
|

| `[:A&!A]`
|
|
|

| `[:A\|!A]`
| {check-mark}
| {check-mark}
| {check-mark}

| `[:%]`
| {check-mark}
| {check-mark}
| {check-mark}

| `[:!%]`
|
|
|

| `[:%\|!%]`
| {check-mark}
| {check-mark}
| {check-mark}

| `[:%&!%]`
|
|
|

| `[:A&%]`
|
|
|

| `[:A\|%]`
| {check-mark}
| {check-mark}
| {check-mark}

|===

Label expressions cannot be combined with label syntax.
For example, `:A:B&C` will throw an error.
Instead, use either `:A&B&C` or `:A:B:C`.

The following graph is used for the examples below:

image:graph_relationship_type_expressions.svg[]

////
[source, cypher, role=test-setup]
----
MATCH (_) DETACH DELETE _;
CREATE
  (:A:B)-[:R1 {name:'Teaches'}]->(:B),
  (:C)-[:R2 {name:'Studies'}]->(:D),
  (:E)-[:R3 {name:'Parents'}]->(:F)
----
////

[[relationship-type-expressions-pattern-without-relationship-type-expression]]
== Relationship pattern without relationship type expression

A relationship pattern without a relationship type expression returns all relationships in the graph.


.Relationship type expressions
======

[source, cypher]
----
MATCH ()-[r]->()
RETURN r.name as name
----

[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +name+
| +"Teaches"+
| +"Studies"+
| +"Parents"+
1+d|Rows: 3
|===

======


[[relationship-type-expressions-pattern-on-single-relationship-type]]
== Relationship pattern with a single relationship type

A relationship pattern with a single relationship type returns the relationships that contain the specified type.


.Relationship type expression
======

[source, cypher]
----
MATCH ()-[r:R1]->()
RETURN r.name AS name
----

[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +name+
| +"Teaches"+
1+d|Rows: 1
|===

======

[[relationship-type-expressions-pattern-with-or-expression]]
=== Relationship pattern with an `OR` expression for the relationship types

A relationship pattern with an `OR` expression for the relationship type returns all relationships that contain either of the specified types.


.Relationship type expression
======

[source, cypher]
----
MATCH ()-[r:R1|R2]->()
RETURN r.name AS name
----

[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +name+
| +"Teaches"+
| +"Studies"+
1+d|Rows: 2
|===

======


[[relationship-type-expressions-pattern-with-not-expression]]
== Relationship pattern with a `NOT` expression for the relationship types

A relationship pattern with a `NOT` expression for the relationship type returns all relationships that do not contain the specified type.


.Relationship type expression
======

[source, cypher]
----
MATCH ()-[r:!R1]->()
RETURN r.name AS name
----

[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +name+
| +"Studies"+
| +"Parents"+
1+d|Rows: 2
|===

======

[[relationship-type-expressions-pattern-nested-type-expression]]
== Relationship pattern with a nested relationship type expression

A relationship pattern with a nested relationship type expression returns all relationships for which the full expression is `true`.


.Relationship type expression
======

[source, cypher]
----
MATCH ()-[r:(!R1&!R2)|R3]->()
RETURN r.name as name
----

[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +name+
| +"Parents"+
1+d|Rows: 1
|===

======

[[relationship-type-expressions-pattern-predicate-type-expression]]
== `WHERE` clause with a relationship type expression in the predicate

A relationship type expression can also be used as a predicate in the `WHERE` clause.


.Relationship type expression
======

[source, cypher]
----
MATCH (n)-[r]->(m)
WHERE r:R1|R2
RETURN r.name AS name
----

[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +name+
| +"Teaches"+
| +"Studies"+
1+d|Rows: 2
|===

======

[[relationship-type-expressions-pattern-with-return-type-expression]]
== `WITH` and `RETURN` clauses with a relationship type expression

A relationship type expression can also be used in the `WITH` or `RETURN` clauses.


.Relationship type expression
======

[source, cypher]
----
MATCH (n)-[r]->(m)
RETURN r:R1|R2 AS result
----

[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +result+
| +true+
| +true+
| +false+
1+d|Rows: 3
|===

======

[[relationship-type-expressions-case-type-label-expression]]
== `CASE` expression with relationship type and label expressions

A relationship type expression and a label expression can also be used in `CASE` expressions.


.Relationship type expression
======

[source, cypher]
----
MATCH (n)-[r]->(m)
RETURN
CASE
  WHEN n:A&B THEN 1
  WHEN r:!R1&!R2 THEN 2
  ELSE -1
END AS result
----

[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +result+
| +1+
| +-1+
| +2+
1+d|Rows: 3
|===

======

