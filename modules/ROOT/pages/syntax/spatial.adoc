[[cypher-spatial]]
= Spatial values
:description: Cypher has built-in support for handling spatial values (points), and the underlying database supports storing these point values as properties on nodes and relationships. 

* xref:syntax/spatial.adoc#cypher-spatial-introduction[Introduction]
* xref:syntax/spatial.adoc#cypher-spatial-crs[Coordinate Reference Systems]
 ** xref:syntax/spatial.adoc#cypher-spatial-crs-geographic[Geographic coordinate reference systems]
 ** xref:syntax/spatial.adoc#cypher-spatial-crs-cartesian[Cartesian coordinate reference systems]
* xref:syntax/spatial.adoc#cypher-spatial-instants[Spatial instants]
 ** xref:syntax/spatial.adoc#cypher-spatial-specifying-spatial-instants[Creating points]
 ** xref:syntax/spatial.adoc#cypher-spatial-accessing-components-spatial-instants[Accessing components of points]
* xref:syntax/spatial.adoc#cypher-spatial-index[Spatial index]
* xref:syntax/spatial.adoc#cypher-comparability-orderability[Comparability and Orderability]


[NOTE]
====
Refer to xref:functions/spatial.adoc[Spatial functions] for information regarding spatial _functions_ allowing for the creation and manipulation of spatial values.

Refer to xref:syntax/operators.adoc#cypher-ordering[Ordering and comparison of values] for information regarding the comparison and ordering of spatial values.


====

[[cypher-spatial-introduction]]
== Introduction


Neo4j supports only one type of spatial geometry, the _Point_ with the following characteristics:

* Each point can have either 2 or 3 dimensions. This means it contains either 2 or 3 64-bit floating point values, which together are called the _Coordinate_.
* Each point will also be associated with a specific xref:syntax/spatial.adoc#cypher-spatial-crs[Coordinate Reference System] (CRS) that determines the meaning of the values in the _Coordinate_.
* Instances of _Point_ and lists of _Point_ can be assigned to node and relationship properties.
* Nodes with _Point_ or _List(Point)_ properties can be indexed using a spatial index. This is true for all CRS (and for both 2D and 3D).
  There is no special syntax for creating spatial indexes, as it is supported using the existing xref:indexes-for-search-performance.adoc#administration-indexes-create-a-single-property-index-for-nodes[indexes].
* The xref:functions/spatial.adoc#functions-distance[distance function] will work on points in all CRS and in both 2D and 3D but only if the two points have the same CRS (and therefore also same dimension).
        

[[cypher-spatial-crs]]
== Coordinate Reference Systems

Four Coordinate Reference Systems (CRS) are supported, each of which falls within one of two types: _geographic coordinates_ modeling points on the earth, or _cartesian coordinates_ modeling points in euclidean space:

* xref:syntax/spatial.adoc#cypher-spatial-crs-geographic[Geographic coordinate reference systems]
 ** WGS-84: longitude, latitude (x, y)
 ** WGS-84-3D: longitude, latitude, height (x, y, z)
* xref:syntax/spatial.adoc#cypher-spatial-crs-cartesian[Cartesian coordinate reference systems]
 ** Cartesian: x, y
 ** Cartesian 3D: x, y, z


Data within different coordinate systems are entirely incomparable, and cannot be implicitly converted from one to the other.
This is true even if they are both cartesian or both geographic. For example, if you search for 3D points using a 2D range, you will get no results.
However, they can be ordered, as discussed in more detail in xref:syntax/operators.adoc#cypher-ordering[Ordering and comparison of values].
        

[[cypher-spatial-crs-geographic]]
=== Geographic coordinate reference systems

Two Geographic Coordinate Reference Systems (CRS) are supported, modeling points on the earth:

* http://spatialreference.org/ref/epsg/4326/[WGS 84 2D]
 ** A 2D geographic point in the _WGS 84_ CRS is specified in one of two ways:
  *** `longitude` and `latitude` (if these are specified, and the `crs` is not, then the `crs` is assumed to be `WGS-84`)
  *** `x` and `y` (in this case the `crs` must be specified, or will be assumed to be `Cartesian`)
 ** Specifying this CRS can be done using either the name 'wgs-84' or the SRID 4326 as described in xref:functions/spatial.adoc#functions-point-wgs84-2d[Point(WGS-84)]
* http://spatialreference.org/ref/epsg/4979/[WGS 84 3D]
 ** A 3D geographic point in the _WGS 84_ CRS is specified one of in two ways:
  *** `longitude`, `latitude` and either `height` or `z` (if these are specified, and the `crs` is not, then the `crs` is assumed to be `WGS-84-3D`)
  *** `x`, `y` and `z` (in this case the `crs` must be specified, or will be assumed to be `Cartesian-3D`)
 ** Specifying this CRS can be done using either the name 'wgs-84-3d' or the SRID 4979 as described in xref:functions/spatial.adoc#functions-point-wgs84-3d[Point(WGS-84-3D)]


The units of the `latitude` and `longitude` fields are in decimal degrees, and need to be specified as floating point numbers using Cypher literals.
It is not possible to use any other format, like 'degrees, minutes, seconds'. The units of the `height` field are in meters. When geographic points
are passed to the `distance` function, the result will always be in meters. If the coordinates are in any other format or unit than supported, it
is necessary to explicitly convert them.
For example, if the incoming `$height` is a string field in kilometers, you would need to type `height: toFloat($height) * 1000`. Likewise if the
results of the `distance` function are expected to be returned in kilometers, an explicit conversion is required.
For example: `RETURN distance(a,b) / 1000 AS km`. An example demonstrating conversion on incoming and outgoing values is:
          


.Query
[source, cypher]
----
WITH
  point({latitude:toFloat('13.43'), longitude:toFloat('56.21')}) AS p1,
  point({latitude:toFloat('13.10'), longitude:toFloat('56.41')}) AS p2
RETURN toInteger(distance(p1, p2)/1000) AS km
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +km+
| +42+
1+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
WITH
  point({latitude:toFloat('13.43'), longitude:toFloat('56.21')}) AS p1,
  point({latitude:toFloat('13.10'), longitude:toFloat('56.41')}) AS p2
RETURN toInteger(distance(p1, p2)/1000) AS km
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

[[cypher-spatial-crs-cartesian]]
=== Cartesian coordinate reference systems

Two Cartesian Coordinate Reference Systems (CRS) are supported, modeling points in euclidean space:

* http://spatialreference.org/ref/sr-org/7203/[Cartesian 2D]
 ** A 2D point in the _Cartesian_ CRS is specified with a map containing `x` and `y` coordinate values
 ** Specifying this CRS can be done using either the name 'cartesian' or the SRID 7203 as described in xref:functions/spatial.adoc#functions-point-cartesian-2d[Point(Cartesian)]
* http://spatialreference.org/ref/sr-org/9157/[Cartesian 3D]
 ** A 3D point in the _Cartesian_ CRS is specified with a map containing `x`, `y` and `z` coordinate values
 ** Specifying this CRS can be done using either the name 'cartesian-3d' or the SRID 9157 as described in xref:functions/spatial.adoc#functions-point-cartesian-3d[Point(Cartesian-3D)]


The units of the `x`, `y` and `z` fields are unspecified and can mean anything the user intends them to mean. This also means that when two cartesian
points are passed to the `distance` function, the resulting value will be in the same units as the original coordinates. This is true for both 2D and 3D
points, as the _pythagoras_ equation used is generalized to any number of dimensions. However, just as you cannot compare geographic points to cartesian
points, you cannot calculate the distance between a 2D point and a 3D point. If you need to do that, explicitly transform the one type into the other.
For example:
          


.Query
[source, cypher]
----
WITH
  point({x: 3, y: 0}) AS p2d,
  point({x: 0, y: 4, z: 1}) AS p3d
RETURN
  distance(p2d, p3d) AS bad,
  distance(p2d, point({x: p3d.x, y: p3d.y})) AS good
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +bad+ | +good+
| +<null>+ | +5.0+
2+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
WITH
  point({x: 3, y: 0}) AS p2d,
  point({x: 0, y: 4, z: 1}) AS p3d
RETURN
  distance(p2d, p3d) AS bad,
  distance(p2d, point({x: p3d.x, y: p3d.y})) AS good
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

[[cypher-spatial-instants]]
== Spatial instants

[[cypher-spatial-specifying-spatial-instants]]
=== Creating points


All point types are created from two components:

* The _Coordinate_ containing either 2 or 3 floating point values (64-bit)
* The Coordinate Reference System (or CRS) defining the meaning (and possibly units) of the values in the _Coordinate_
          


For most use cases it is not necessary to specify the CRS explicitly as it will be deduced from the keys used to specify the coordinate. Two rules
are applied to deduce the CRS from the coordinate:

* Choice of keys:
  ** If the coordinate is specified using the keys `latitude` and `longitude` the CRS will be assumed to be _Geographic_ and therefor either `WGS-84` or `WGS-84-3D`.
  ** If instead `x` and `y` are used, then the default CRS would be `Cartesian` or `Cartesian-3D`
* Number of dimensions:
  ** If there are 2 dimensions in the coordinate, `x` & `y` or `longitude` & `latitude` the CRS will be a 2D CRS
  ** If there is a third dimensions in the coordinate, `z` or `height` the CRS will be a 3D CRS
          


All fields are provided to the `point` function in the form of a map of explicitly named arguments. We specifically do not support an ordered list
of coordinate fields because of the contradictory conventions between geographic and cartesian coordinates, where geographic coordinates normally
list `y` before `x` (`latitude` before `longitude`).
See for example the following query which returns points created in each of the four supported CRS. Take particular note of the order and keys
of the coordinates in the original `point` function calls, and how those values are displayed in the results:
          


.Query
[source, cypher]
----
RETURN
  point({x: 3, y: 0}) AS cartesian_2d,
  point({x: 0, y: 4, z: 1}) AS cartesian_3d,
  point({latitude: 12, longitude: 56}) AS geo_2d,
  point({latitude: 12, longitude: 56, height: 1000}) AS geo_3d
----

.Result
[role="queryresult",options="header,footer",cols="4*<m"]
|===
| +cartesian_2d+ | +cartesian_3d+ | +geo_2d+ | +geo_3d+
| +point({x: 3.0, y: 0.0, crs: 'cartesian'})+ | +point({x: 0.0, y: 4.0, z: 1.0, crs: 'cartesian-3d'})+ | +point({x: 56.0, y: 12.0, crs: 'wgs-84'})+ | +point({x: 56.0, y: 12.0, z: 1000.0, crs: 'wgs-84-3d'})+
4+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
RETURN
  point({x: 3, y: 0}) AS cartesian_2d,
  point({x: 0, y: 4, z: 1}) AS cartesian_3d,
  point({latitude: 12, longitude: 56}) AS geo_2d,
  point({latitude: 12, longitude: 56, height: 1000}) AS geo_3d
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]


For the geographic coordinates, it is important to note that the `latitude` value should always lie in the interval `[-90, 90]` and any other value
outside this range will throw an exception. The `longitude` value should always lie in the interval `[-180, 180]` and any other value
outside this range will be wrapped around to fit in this range. The `height` value and any cartesian coordinates are not explicitly restricted,
and any value within the allowed range of the signed 64-bit floating point type will be accepted.
          

[[cypher-spatial-accessing-components-spatial-instants]]
=== Accessing components of points


Just as we construct points using a map syntax, we can also access components as properties of the instance.
          


.Components of point instances and where they are supported
[options="header"]
|===
| Component      | Description  | Type | Range/Format   | WGS-84 | WGS-84-3D | Cartesian | Cartesian-3D
| `instant.x` | The first element of the _Coordinate_ | Float | Number literal, range depends on CRS | {check-mark} | {check-mark} | {check-mark} | {check-mark}
| `instant.y` | The second element of the _Coordinate_ | Float | Number literal, range depends on CRS | {check-mark} | {check-mark} | {check-mark} | {check-mark}
| `instant.z` | The third element of the _Coordinate_ | Float | Number literal, range depends on CRS |  | {check-mark} |  | {check-mark}
| `instant.latitude` | The _second_ element of the _Coordinate_ for geographic CRS, degrees North of the equator | Float | Number literal, `-90.0` to `90.0` | {check-mark} | {check-mark} |   |
| `instant.longitude` | The _first_ element of the _Coordinate_ for geographic CRS, degrees East of the prime meridian | Float | Number literal, `-180.0` to `180.0` | {check-mark} | {check-mark} |  |
| `instant.height` | The third element of the _Coordinate_ for geographic CRS, meters above the ellipsoid defined by the datum (WGS-84) | Float | Number literal, range limited only by the underlying 64-bit floating point type |  | {check-mark} |  |
| `instant.crs` | The name of the CRS | String | One of `wgs-84`, `wgs-84-3d`, `cartesian`, `cartesian-3d` | {check-mark} | {check-mark} | {check-mark} | {check-mark}
| `instant.srid` | The internal Neo4j ID for the CRS | Integer | One of `4326`, `4979`, `7203`, `9157` | {check-mark} | {check-mark} | {check-mark} | {check-mark}
|===


The following query shows how to extract the components of a _Cartesian 2D_ point value:


.Query
[source, cypher]
----
WITH point({x: 3, y: 4}) AS p
RETURN
  p.x AS x,
  p.y AS y,
  p.crs AS crs,
  p.srid AS srid
----

.Result
[role="queryresult",options="header,footer",cols="4*<m"]
|===
| +x+ | +y+ | +crs+ | +srid+
| +3.0+ | +4.0+ | +"cartesian"+ | +7203+
4+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
WITH point({x: 3, y: 4}) AS p
RETURN
  p.x AS x,
  p.y AS y,
  p.crs AS crs,
  p.srid AS srid
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

The following query shows how to extract the components of a _WGS-84 3D_ point value:


.Query
[source, cypher]
----
WITH point({latitude: 3, longitude: 4, height: 4321}) AS p
RETURN
  p.latitude AS latitude,
  p.longitude AS longitude,
  p.height AS height,
  p.x AS x,
  p.y AS y,
  p.z AS z,
  p.crs AS crs,
  p.srid AS srid
----

.Result
[role="queryresult",options="header,footer",cols="8*<m"]
|===
| +latitude+ | +longitude+ | +height+ | +x+ | +y+ | +z+ | +crs+ | +srid+
| +3.0+ | +4.0+ | +4321.0+ | +4.0+ | +3.0+ | +4321.0+ | +"wgs-84-3d"+ | +4979+
8+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
WITH point({latitude: 3, longitude: 4, height: 4321}) AS p
RETURN
  p.latitude AS latitude,
  p.longitude AS longitude,
  p.height AS height,
  p.x AS x,
  p.y AS y,
  p.z AS z,
  p.crs AS crs,
  p.srid AS srid
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

[[cypher-spatial-index]]
== Spatial index


If there is a xref:indexes-for-search-performance.adoc#administration-indexes-create-a-single-property-index-for-nodes[index] on a particular `:Label(property)` combination, and a spatial point
is assigned to that property on a node with that label, the node will be indexed in a spatial index. For spatial indexing, Neo4j uses
space filling curves in 2D or 3D over an underlying generalized B+Tree. Points will be stored in up to four different trees, one for each of the
xref:syntax/spatial.adoc#cypher-spatial-crs[four coordinate reference systems].
This allows for both xref:query-tuning/indexes.adoc#administration-indexes-equality-check-using-where-single-property-index[equality]
and xref:query-tuning/indexes.adoc#administration-indexes-range-comparisons-using-where-single-property-index[range] queries using exactly the same syntax and behaviour as for other property types.
If two range predicates are used, which define minimum and maximum points, this will effectively result in a
xref:query-tuning/indexes.adoc#administration-indexes-spatial-bounding-box-searches-single-property-index[bounding box query].
In addition, queries using the `distance` function can, under the right conditions, also use the index, as described in the section
xref:query-tuning/indexes.adoc#administration-indexes-spatial-distance-searches-single-property-index['Spatial distance searches'].
        

[[cypher-comparability-orderability]]
== Comparability and Orderability


Points with different CRS are not comparable.
This means that any function operating on two points of different types will return `null`.
This is true of the xref:functions/spatial.adoc#functions-distance[distance function] as well as inequality comparisons.
If these are used in a predicate, they will cause the associated `MATCH` to return no results.
        


.Query
[source, cypher]
----
WITH
  point({x: 3, y: 0}) AS p2d,
  point({x: 0, y: 4, z: 1}) AS p3d
RETURN
  distance(p2d, p3d),
  p2d < p3d,
  p2d = p3d,
  p2d <> p3d,
  distance(p2d, point({x: p3d.x, y: p3d.y}))
----

.Result
[role="queryresult",options="header,footer",cols="5*<m"]
|===
| +distance(p2d, p3d)+ | +p2d < p3d+ | +p2d = p3d+ | +p2d <> p3d+ | +distance(p2d, point({x: p3d.x, y: p3d.y}))+
| +<null>+ | +<null>+ | +false+ | +true+ | +5.0+
5+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
WITH
  point({x: 3, y: 0}) AS p2d,
  point({x: 0, y: 4, z: 1}) AS p3d
RETURN
  distance(p2d, p3d),
  p2d < p3d,
  p2d = p3d,
  p2d <> p3d,
  distance(p2d, point({x: p3d.x, y: p3d.y}))
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]


However, all types are orderable.
The Point types will be ordered after Numbers and before Temporal types.
Points with different CRS with be ordered by their SRID numbers.
For the current set of four xref:syntax/spatial.adoc#cypher-spatial-crs[CRS], this means the order is WGS84, WGS84-3D, Cartesian, Cartesian-3D.
        


.Query
[source, cypher]
----
UNWIND [
  point({x: 3, y: 0}),
  point({x: 0, y: 4, z: 1}),
  point({srid: 4326, x: 12, y: 56}),
  point({srid: 4979, x: 12, y: 56, z: 1000})
] AS point
RETURN point ORDER BY point
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +point+
| +point({x: 12.0, y: 56.0, crs: 'wgs-84'})+
| +point({x: 12.0, y: 56.0, z: 1000.0, crs: 'wgs-84-3d'})+
| +point({x: 3.0, y: 0.0, crs: 'cartesian'})+
| +point({x: 0.0, y: 4.0, z: 1.0, crs: 'cartesian-3d'})+
1+d|Rows: 4
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
none
]]></database><command><![CDATA[
UNWIND [
  point({x: 3, y: 0}),
  point({x: 0, y: 4, z: 1}),
  point({srid: 4326, x: 12, y: 56}),
  point({srid: 4979, x: 12, y: 56, z: 1000})
] AS point
RETURN point ORDER BY point
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

