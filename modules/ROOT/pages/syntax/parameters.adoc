[[cypher-parameters]]
= Parameters


* xref:syntax/parameters.adoc#cypher-parameters-introduction[Introduction]
* xref:syntax/parameters.adoc#cypher-parameters-string-literal[String literal]
* xref:syntax/parameters.adoc#cypher-parameters-regular-expression[Regular expression]
* xref:syntax/parameters.adoc#cypher-parameters-case-sensitive-pattern-matching[Case-sensitive string pattern matching]
* xref:syntax/parameters.adoc#cypher-parameters-create-node-with-properties[Create node with properties]
* xref:syntax/parameters.adoc#cypher-parameters-create-multiple-nodes-with-properties[Create multiple nodes with properties]
* xref:syntax/parameters.adoc#cypher-parameters-setting-all-properties-on-a-node[Setting all properties on a node]
* xref:syntax/parameters.adoc#cypher-parameters-skip-and-limit[`SKIP` and `LIMIT`]
* xref:syntax/parameters.adoc#cypher-parameters-node-id[Node id]
* xref:syntax/parameters.adoc#cypher-parameters-multiple-node-ids[Multiple node ids]
* xref:syntax/parameters.adoc#cypher-parameters-call-procedure[Calling procedures]
* xref:syntax/parameters.adoc#cypher-parameters-index-value-explicit[Index value (explicit indexes)]
* xref:syntax/parameters.adoc#cypher-parameters-index-query-explicit[Index query (explicit indexes)]


[[cypher-parameters-introduction]]
== Introduction
Cypher supports querying with parameters.
This means developers don't have to resort to string building to create a query.
Additionally, parameters make caching of execution plans much easier for Cypher, thus leading to faster query execution times.

Parameters can be used for:

* literals and expressions
* node and relationship ids
* for explicit indexes only: index values and queries

Parameters cannot be used for the following constructs, as these form part of the query structure that is compiled into a query plan:

* property keys; so, `MATCH (n) WHERE n.$param = 'something'` is invalid
* relationship types
* labels

Parameters may consist of letters and numbers, and any combination of these, but cannot start with a number or a currency symbol.

For details on using parameters via the Neo4j HTTP API, see the link:{neo4j-docs-base-uri}/http-api/{page-version}/index#http-api[HTTP API documentation].

We provide below a comprehensive list of examples of parameter usage.
In these examples, parameters are given in JSON; the exact manner in which they are to be submitted depends upon the driver being used.

[NOTE]
====
It is recommended that the new parameter syntax `$param` is used, as the old syntax `+{param}+` is deprecated and will be removed altogether in a later release.
====


[[cypher-parameters-string-literal]]
== String literal

// tag::neo4j-cypher-docs/docs/dev/syntax/includes/exampleWithStringLiteralAsParameter.asciidoc[]
// tag::include-neo4j-documentation[]

.Parameters
[source,javascript]
----
{
  "name" : "Johan"
}
----

.Query
[source,cypher]
----
MATCH (n:Person)
WHERE n.name = $name
RETURN n
----
// end::include-neo4j-documentation[]
// end::neo4j-cypher-docs/docs/dev/syntax/includes/exampleWithStringLiteralAsParameter.asciidoc[]

You can use parameters in this syntax as well:

// tag::neo4j-cypher-docs/docs/dev/syntax/includes/exampleWithShortSyntaxStringLiteralAsParameter.asciidoc[]
// tag::include-neo4j-documentation[]

.Parameters
[source,javascript]
----
{
  "name" : "Johan"
}
----

.Query
[source,cypher]
----
MATCH (n:Person { name: $name })
RETURN n
----
// end::include-neo4j-documentation[]
// end::neo4j-cypher-docs/docs/dev/syntax/includes/exampleWithShortSyntaxStringLiteralAsParameter.asciidoc[]


[[cypher-parameters-regular-expression]]
== Regular expression

// tag::neo4j-cypher-docs/docs/dev/syntax/includes/exampleWithParameterRegularExpression.asciidoc[]
// tag::include-neo4j-documentation[]

.Parameters
[source,javascript]
----
{
  "regex" : ".*h.*"
}
----

.Query
[source,cypher]
----
MATCH (n:Person)
WHERE n.name =~ $regex
RETURN n.name
----
// end::include-neo4j-documentation[]
// end::neo4j-cypher-docs/docs/dev/syntax/includes/exampleWithParameterRegularExpression.asciidoc[]


[[cypher-parameters-case-sensitive-pattern-matching]]
== Case-sensitive string pattern matching

// tag::neo4j-cypher-docs/docs/dev/syntax/includes/exampleWithParameterCSCIStringPatternMatching.asciidoc[]
// tag::include-neo4j-documentation[]

.Parameters
[source,javascript]
----
{
  "name" : "Michael"
}
----

.Query
[source,cypher]
----
MATCH (n:Person)
WHERE n.name STARTS WITH $name
RETURN n.name
----
// end::include-neo4j-documentation[]
// end::neo4j-cypher-docs/docs/dev/syntax/includes/exampleWithParameterCSCIStringPatternMatching.asciidoc[]


[[cypher-parameters-create-node-with-properties]]
== Create node with properties

// tag::neo4j-cypher-docs/docs/dev/syntax/includes/create_node_from_map.asciidoc[]
// tag::include-neo4j-documentation[]

.Parameters
[source,javascript]
----
{
  "props" : {
    "name" : "Andy",
    "position" : "Developer"
  }
}
----

.Query
[source,cypher]
----
CREATE ($props)
----
// end::include-neo4j-documentation[]
// end::neo4j-cypher-docs/docs/dev/syntax/includes/create_node_from_map.asciidoc[]


[[cypher-parameters-create-multiple-nodes-with-properties]]
== Create multiple nodes with properties

// tag::neo4j-cypher-docs/docs/dev/syntax/includes/create_multiple_nodes_from_map.asciidoc[]
// tag::include-neo4j-documentation[]

.Parameters
[source,javascript]
----
{
  "props" : [ {
    "awesome" : true,
    "name" : "Andy",
    "position" : "Developer"
  }, {
    "children" : 3,
    "name" : "Michael",
    "position" : "Developer"
  } ]
}
----

.Query
[source,cypher]
----
UNWIND $props AS properties
CREATE (n:Person)
SET n = properties
RETURN n
----
// end::include-neo4j-documentation[]
// end::neo4j-cypher-docs/docs/dev/syntax/includes/create_multiple_nodes_from_map.asciidoc[]


[[cypher-parameters-setting-all-properties-on-a-node]]
== Setting all properties on a node

Note that this will replace all the current properties.

// tag::neo4j-cypher-docs/docs/dev/syntax/includes/set_properties_on_a_node_from_a_map.asciidoc[]
// tag::include-neo4j-documentation[]

.Parameters
[source,javascript]
----
{
  "props" : {
    "name" : "Andy",
    "position" : "Developer"
  }
}
----

.Query
[source,cypher]
----
MATCH (n:Person)
WHERE n.name='Michaela'
SET n = $props
----
// end::include-neo4j-documentation[]
// end::neo4j-cypher-docs/docs/dev/syntax/includes/set_properties_on_a_node_from_a_map.asciidoc[]


[[cypher-parameters-skip-and-limit]]
== `SKIP` and `LIMIT`

// tag::neo4j-cypher-docs/docs/dev/syntax/includes/exampleWithParameterForSkipLimit.asciidoc[]
// tag::include-neo4j-documentation[]

.Parameters
[source,javascript]
----
{
  "s" : 1,
  "l" : 1
}
----

.Query
[source,cypher]
----
MATCH (n:Person)
RETURN n.name
SKIP $s
LIMIT $l
----
// end::include-neo4j-documentation[]
// end::neo4j-cypher-docs/docs/dev/syntax/includes/exampleWithParameterForSkipLimit.asciidoc[]


[[cypher-parameters-node-id]]
== Node id

// tag::neo4j-cypher-docs/docs/dev/syntax/includes/exampleWithParameterForNodeId.asciidoc[]
// tag::include-neo4j-documentation[]

.Parameters
[source,javascript]
----
{
  "id" : 0
}
----

.Query
[source,cypher]
----
MATCH (n)
WHERE id(n)= $id
RETURN n.name
----
// end::include-neo4j-documentation[]
// end::neo4j-cypher-docs/docs/dev/syntax/includes/exampleWithParameterForNodeId.asciidoc[]


[[cypher-parameters-multiple-node-ids]]
== Multiple node ids

// tag::neo4j-cypher-docs/docs/dev/syntax/includes/exampleWithParameterForMultipleNodeIds.asciidoc[]
// tag::include-neo4j-documentation[]

.Parameters
[source,javascript]
----
{
  "ids" : [ 0, 1, 2 ]
}
----

.Query
[source,cypher]
----
MATCH (n)
WHERE id(n) IN $ids
RETURN n.name
----
// end::include-neo4j-documentation[]
// end::neo4j-cypher-docs/docs/dev/syntax/includes/exampleWithParameterForMultipleNodeIds.asciidoc[]


[[cypher-parameters-call-procedure]]
== Calling procedures

// tag::neo4j-cypher-docs/docs/dev/syntax/includes/exampleWithParameterProcedureCall.asciidoc[]
// tag::include-neo4j-documentation[]

.Parameters
[source,javascript]
----
{
  "indexname" : ":Person(name)"
}
----

.Query
[source,cypher]
----
CALL db.resampleIndex($indexname)
----
// end::include-neo4j-documentation[]
// end::neo4j-cypher-docs/docs/dev/syntax/includes/exampleWithParameterProcedureCall.asciidoc[]


[[cypher-parameters-index-value-explicit]]
== Index value (explicit indexes)

// tag::neo4j-cypher-docs/docs/dev/syntax/includes/exampleWithParameterForIndexValue.asciidoc[]
// tag::include-neo4j-documentation[]

.Parameters
[source,javascript]
----
{
  "value" : "Michaela"
}
----

.Query
[source,cypher]
----
START n=node:people(name = $value)
RETURN n
----
// end::include-neo4j-documentation[]
// end::neo4j-cypher-docs/docs/dev/syntax/includes/exampleWithParameterForIndexValue.asciidoc[]


[[cypher-parameters-index-query-explicit]]
== Index query (explicit indexes)

// tag::neo4j-cypher-docs/docs/dev/syntax/includes/exampleWithParametersForQuery.asciidoc[]
// tag::include-neo4j-documentation[]

.Parameters
[source,javascript]
----
{
  "query" : "name:Bob"
}
----

.Query
[source,cypher]
----
START n=node:people($query)
RETURN n
----
// end::include-neo4j-documentation[]
// end::neo4j-cypher-docs/docs/dev/syntax/includes/exampleWithParametersForQuery.asciidoc[]


