:description: Syntax for how to manage constraints used for ensuring data integrity.

[[constraints-syntax]]
= Syntax
:check-mark: icon:check[]

This page contains the syntax for creating, listing, and dropping the constraints available in Neo4j.

More details about the syntax can be found in the link:{neo4j-docs-base-uri}/operations-manual/{page-version}/database-administration/syntax/[Operations Manual -> Cypher syntax for administration commands].

[[constraints-syntax-create]]
== CREATE CONSTRAINT

[NOTE]
Creating a constraint requires the link:{neo4j-docs-base-uri}/operations-manual/{page-version}/authentication-authorization/database-administration/#access-control-database-administration-constraints[`CREATE CONSTRAINT` privilege].

The `CREATE CONSTRAINT` command is optionally idempotent. 
This means its default behavior is to throw an error if an attempt is made to create the same constraint twice.
With the `IF NOT EXISTS` flag, no error is thrown and nothing happens should a constraint with the same name or same schema and constraint type already exist.
It may still throw an error if conflicting data, indexes, or constraints exist.
Examples of this are nodes with missing properties, indexes with the same name, or constraints with same schema but a different conflicting constraint type.
As of Neo4j 5.17, an informational notification is returned in case nothing happens showing the existing constraint which blocks the creation.

For constraints that are backed by an index, the index provider for the backing index can be specified using the `OPTIONS` clause.
Only one valid value exists for the index provider, `range-1.0`, which is the default value.
There is no supported index configuration for range indexes.

[[create-uniqueness-constraint]]
=== Create a uniqueness constraint

.Syntax for creating a node uniqueness constraint on a single property
[source, syntax]
----
CREATE CONSTRAINT [constraint_name] [IF NOT EXISTS]
FOR (n:LabelName)
REQUIRE n.propertyName IS [NODE] UNIQUE
[OPTIONS "{" option: value[, ...] "}"]
----

.Syntax for creating a composite node uniqueness constraint on multiple properties
[source, syntax]
----
CREATE CONSTRAINT [constraint_name] [IF NOT EXISTS]
FOR (n:LabelName)
REQUIRE (n.propertyName_1, ..., n.propertyName_n) IS [NODE] UNIQUE
[OPTIONS "{" option: value[, ...] "}"]
----

.Syntax for creating a relationship uniqueness constraint on a single property label:new[Introduced in 5.7]
[source, syntax]
----
CREATE CONSTRAINT [constraint_name] [IF NOT EXISTS]
FOR ()-"["r:RELATIONSHIP_TYPE"]"-()
REQUIRE r.propertyName IS [REL[ATIONSHIP]] UNIQUE
[OPTIONS "{" option: value[, ...] "}"]
----

.Syntax for creating a composite relationship uniqueness constraint on multiple properties label:new[Introduced in 5.7]
[source, syntax]
----
CREATE CONSTRAINT [constraint_name] [IF NOT EXISTS]
FOR ()-"["r:RELATIONSHIP_TYPE"]"-()
REQUIRE (r.propertyName_1, ..., r.propertyName_n) IS [REL[ATIONSHIP]] UNIQUE
[OPTIONS "{" option: value[, ...] "}"]
----

For more examples on how to create a uniqueness constraint, see  xref:constraints/managing-constraints.adoc#create-uniqueness-constraint[Create, show, and drop constraints -> Create a uniqueness constraint].
Uniqueness constraints are xref:constraints/managing-constraints.adoc#constraints-and-indexes[index-backed].

[role=label--enterprise-edition]
[[create-existence-constraint]]
=== Create an existence constraint

.Syntax for creating a node property existence constraint
[source, syntax]
----
CREATE CONSTRAINT [constraint_name] [IF NOT EXISTS]
FOR (n:LabelName)
REQUIRE n.propertyName IS NOT NULL
[OPTIONS "{" "}"]
----

.Syntax for creating a relationship property existence constraint
[source, syntax]
----
CREATE CONSTRAINT [constraint_name] [IF NOT EXISTS]
FOR ()-"["r:RELATIONSHIP_TYPE"]"-()
REQUIRE r.propertyName IS NOT NULL
[OPTIONS "{" "}"]
----

There are no supported `OPTIONS` values for existence constraints, but an empty options map is allowed for consistency.

For more examples on how to create an existence constraints, see  xref:constraints/managing-constraints.adoc#create-existence-constraint[Create, show, and drop constraints -> Create an existence constraint].

[role=label--enterprise-edition]
[[create-key-constraint]]
=== Create a key constraint

.Syntax for creating a node key constraint on a single property
[source, syntax]
----
CREATE CONSTRAINT [constraint_name] [IF NOT EXISTS]
FOR (n:LabelName)
REQUIRE n.propertyName IS [NODE] KEY
[OPTIONS "{" option: value[, ...] "}"]
----

.Syntax for creating a composite node key constraint on multiple properties
[source, syntax]
----
CREATE CONSTRAINT [constraint_name] [IF NOT EXISTS]
FOR (n:LabelName)
REQUIRE (n.propertyName_1, ..., n.propertyName_n) IS [NODE] KEY
[OPTIONS "{" option: value[, ...] "}"]
----

.Syntax for creating a relationship key constraint on a single property
[source, syntax]
----
CREATE CONSTRAINT [constraint_name] [IF NOT EXISTS]
FOR ()-"["r:RELATIONSHIP_TYPE"]"-()
REQUIRE r.propertyName IS [REL[ATIONSHIP]] KEY
[OPTIONS "{" option: value[, ...] "}"]
----

.Syntax for creating a composite relationship key constraint on multiple properties
[source, syntax]
----
CREATE CONSTRAINT [constraint_name] [IF NOT EXISTS]
FOR ()-"["r:RELATIONSHIP_TYPE"]"-()
REQUIRE (r.propertyName_1, ..., r.propertyName_n) IS [REL[ATIONSHIP]] KEY
[OPTIONS "{" option: value[, ...] "}"]
----

An index provider can be specified using the `OPTIONS` clause.

For more examples on how to create a key constraint, see  xref:constraints/managing-constraints.adoc#create-key-constraint[Create, show, and drop constraints -> Create a key constraint].
Key constraints are xref:constraints/managing-constraints.adoc#constraints-and-indexes[index-backed].


[role=label--enterprise-edition label--new-5.9]
[[create-type-constraint]]
=== Create a type constraint

.Syntax for creating a node property type constraint
[source, syntax]
----
CREATE CONSTRAINT [constraint_name] [IF NOT EXISTS]
FOR (n:LabelName)
REQUIRE n.propertyName {[IS] :: | IS TYPED} <TYPE>
[OPTIONS "{" "}"]
----

.Syntax for creating a relationship property type constraint
[source, syntax]
----
CREATE CONSTRAINT [constraint_name] [IF NOT EXISTS]
FOR ()-"["r:RELATIONSHIP_TYPE"]"-()
REQUIRE r.propertyName {[IS] :: | IS TYPED} <TYPE>
[OPTIONS "{" "}"]
----

The three variations of the expression, `IS ::`, `::`, and `IS TYPED` are syntactic synonyms for the same expression.
The preferred syntax is the `IS ::` variant.

Where `<TYPE>` is one of the following property types:

* `BOOLEAN`
* `STRING`
* `INTEGER`
* `FLOAT`
* `DATE`
* `LOCAL TIME`
* `ZONED TIME`
* `LOCAL DATETIME`
* `ZONED DATETIME`
* `DURATION`
* `POINT`
* `LIST<BOOLEAN NOT NULL>` label:new[Introduced in 5.10]
* `LIST<STRING NOT NULL>` label:new[Introduced in 5.10]
* `LIST<INTEGER NOT NULL>` label:new[Introduced in 5.10]
* `LIST<FLOAT NOT NULL>` label:new[Introduced in 5.10]
* `LIST<DATE NOT NULL>` label:new[Introduced in 5.10]
* `LIST<LOCAL TIME NOT NULL>` label:new[Introduced in 5.10]
* `LIST<ZONED TIME NOT NULL>` label:new[Introduced in 5.10]
* `LIST<LOCAL DATETIME NOT NULL>` label:new[Introduced in 5.10]
* `LIST<ZONED DATETIME NOT NULL>` label:new[Introduced in 5.10]
* `LIST<DURATION NOT NULL>` label:new[Introduced in 5.10]
* `LIST<POINT NOT NULL>` label:new[Introduced in 5.10]
* Any closed dynamic union of the above types, e.g. `INTEGER | FLOAT | STRING`. label:new[Introduced in 5.11]

Allowed syntax variations of these types are listed xref::values-and-types/property-structural-constructed.adoc#types-synonyms[here].

There are no supported `OPTIONS` values for property type constraints, but an empty options map is allowed for consistency.

For more examples on how to create a type constraint, see  xref:constraints/managing-constraints.adoc#create-type-constraint[Create, show, and drop constraints -> Create a type constraint].


[[list-constraints]]
== SHOW CONSTRAINTS

[NOTE]
Listing constraints requires the link:{neo4j-docs-base-uri}/operations-manual/{page-version}/authentication-authorization/database-administration/#access-control-database-administration-constraints[`SHOW CONSTRAINTS` privilege].

The simple version of the command allows for a `WHERE` clause and will give back the default set of output columns:

.Syntax to list constraints with default return columns
[source, syntax]
----
SHOW [
      ALL
     |NODE UNIQUE[NESS]
     |REL[ATIONSHIP] UNIQUE[NESS]
     |UNIQUE[NESS]
     |NODE [PROPERTY] EXIST[ENCE]
     |REL[ATIONSHIP] [PROPERTY] EXIST[ENCE]
     |[PROPERTY] EXIST[ENCE]
     |NODE PROPERTY TYPE
     |REL[ATIONSHIP] PROPERTY TYPE
     |PROPERTY TYPE
     |NODE KEY
     |REL[ATIONSHIP] KEY
     |KEY
] CONSTRAINT[S]
  [WHERE expression]
----

.Syntax for listing constraints with full return columns
[source, syntax]
----
SHOW [
      ALL
     |NODE UNIQUE[NESS]
     |REL[ATIONSHIP] UNIQUE[NESS]
     |UNIQUE[NESS]
     |NODE [PROPERTY] EXIST[ENCE]
     |REL[ATIONSHIP] [PROPERTY] EXIST[ENCE]
     |[PROPERTY] EXIST[ENCE]
     |NODE PROPERTY TYPE
     |REL[ATIONSHIP] PROPERTY TYPE
     |PROPERTY TYPE
     |NODE KEY
     |REL[ATIONSHIP] KEY
     |KEY
] CONSTRAINT[S]
YIELD { * | field[, ...] } [ORDER BY field[, ...]] [SKIP n] [LIMIT n]
  [WHERE expression]
  [RETURN field[, ...] [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
----

The type filtering keywords filters the returned constraints on constraint type:

[[constraints-syntax-list-type-filter]]
.Type filters
[options="header", width="100%", cols="4m,6a"]
|===
| Filter | Description

|ALL
| Returns all constraints, no filtering on constraint type.
This is the default if none is given.

|NODE UNIQUE[NESS]
| Returns the node property uniqueness constraints.
label:new[Introduced in 5.7]

|REL[ATIONSHIP] UNIQUE[NESS]
| Returns the relationship property uniqueness constraints.
label:new[Introduced in 5.7]

|UNIQUE[NESS]
| Returns all property uniqueness constraints, for both nodes and relationships.
label:new[Allowing `UNIQUENESS` was introduced in 5.3]

|NODE [PROPERTY] EXIST[ENCE]
| Returns the node property existence constraints.

|REL[ATIONSHIP] [PROPERTY] EXIST[ENCE]
| Returns the relationship property existence constraints.

|[PROPERTY] EXIST[ENCE]
| Returns all property existence constraints, for both nodes and relationships.

|NODE PROPERTY TYPE
| Returns the node property type constraints.
label:new[Introduced in 5.9]

|REL[ATIONSHIP] PROPERTY TYPE
| Returns the relationship property type constraints.
label:new[Introduced in 5.9]

|PROPERTY TYPE
| Returns all property type constraints, for both nodes and relationships.
label:new[Introduced in 5.9]

|NODE KEY
| Returns the node key constraints.

|REL[ATIONSHIP] KEY
| Returns the relationship key constraints.
label:new[Introduced in 5.7]

|KEY
| Returns all node and relationship key constraints.
label:new[Introduced in 5.7]

|===

[[drop-constraint]]
== DROP CONSTRAINT

Dropping a constraint is done by specifying the name of the constraint.

[NOTE]
====
Dropping a constraint requires the link:{neo4j-docs-base-uri}/operations-manual/{page-version}/authentication-authorization/database-administration/#access-control-database-administration-constraints[`DROP CONSTRAINT` privilege].
====

.Syntax for dropping a constraint by name
[source, syntax]
----
DROP CONSTRAINT constraint_name [IF EXISTS]
----

This command is optionally idempotent.
This means its default behavior is to throw an error if an attempt is made to drop the same constraint twice.
With the `IF EXISTS` flag, no error is thrown and nothing happens should the constraint not exist.
As of Neo4j 5.17, an informational notification is instead returned detailing that the constraint does not exist.

