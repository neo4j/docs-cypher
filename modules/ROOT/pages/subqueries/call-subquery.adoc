= CALL subqueries
:description: This page describes how to use the CALL subquery with Cypher.

`CALL` subqueries execute operations within a defined scope, thereby optimizing data handling and query efficiency. 
Unlike other subqueries in Cypher, `CALL` subqueries can be used to perform changes to the database (e.g. xref:clauses/create.adoc[] new nodes).

[NOTE]
The `CALL` clause is also used for calling procedures.
For descriptions of the `CALL` clause in this context, refer to the xref::clauses/call.adoc[`CALL` procedure].

[[call-example-graph]]
== Example graph

A graph with the following schema is used for the examples below:

image::call_subquery_graph.svg[]

To recreate the graph, run the following query in an empty Neo4j database:

[source, cypher, role=test-setup]
----
CREATE (teamA:Team {name: 'Team A'}),
       (teamB:Team {name: 'Team B'}),
       (teamC:Team {name: 'Team C'}),
       (playerA:Player {name: 'Player A', age: 21}),
       (playerB:Player {name: 'Player B', age: 23}),
       (playerC:Player {name: 'Player C', age: 19}),
       (playerD:Player {name: 'Player D', age: 30}),
       (playerE:Player {name: 'Player E', age: 25}),
       (playerF:Player {name: 'Player F', age: 35}),
       (playerA)-[:PLAYS_FOR]->(teamA),
       (playerB)-[:PLAYS_FOR]->(teamA),
       (playerC)-[:PLAYS_FOR]->(teamA),
       (playerD)-[:PLAYS_FOR]->(teamB),
       (playerE)-[:PLAYS_FOR]->(teamC),
       (playerF)-[:PLAYS_FOR]->(teamC),
       (playerA)-[:FRIEND_OF]->(playerB),
       (playerA)-[:FRIENDS_OF]->(playerC),
       (playerB)-[:FRIENDS_OF]->(playerF),
       (playerC)-[:FRIENDS_OF]->(playerD),
       (teamA)-[:OWES {dollars: 1500}]->(teamB),
       (teamA)-[:OWES {dollars: 3000}]->(teamB),
       (teamB)-[:OWES {dollars: 1700}]->(teamC),
       (teamC)-(:OWES {dollars: 5000})->(teamB);
----

== Semantics and performance

A `CALL` subquery is executed once for each incoming row.

.Basic example
====
In this example, the `CALL` subquery executes three times, one for each row that the xref:clauses/unwind.adoc[`UNWIND`] clause outputs.

.Query
[source, cypher]
----
UNWIND [0, 1, 2] AS x
CALL () {
  RETURN 'hello' AS innerReturn
}
RETURN innerReturn
----

.Result
[role="queryresult",options="header,footer",cols="m"]
|===
| innerReturn

| 'hello'
| 'hello'
| 'hello'

d|Rows: 3
|===
====

.Incremental updates 
====

Each execution of a `CALL` subquery can observe changes from previous executions.
In this example, each iteration of the `CALL` subquery adds 1 to the age of `Player A` and the returned `newAge` reflects the `age` after each increment.

.Query
[source, cypher]
----
UNWIND [1, 2, 3] AS x
CALL (x) {
    MATCH (p:Player {name: 'Player A'})
    SET p.age = p.age + 1
    RETURN p.age AS newAge
}
WITH x, newAge
MATCH (p:Player {name: 'Player A'})
RETURN x AS iteration, newAge, p.age AS totalAge
----

.Result
[role="queryresult",options="header,footer",cols=""3*<m"]
|===

| iteration | newAge | totalAge

| 1 | 22 | 24

| 2 | 23 | 24

| 3 | 24 | 24

3+d|Rows: 3
|===
====

[NOTE]
All variables that are returned from a `CALL` subquery are afterwards available in the enclosing query.

.Performance
====

`CALL` subqueries are useful for scoping work.
In this example, a `CALL` subquery is used to xref:functions/aggregating.adoc#functions-collect[`collect`] a `LIST` containing all players who play for a particular team.

.Collect a list of all players playing for a particular team
[source, cypher]
----
MATCH (t:Team)
CALL (t) {
  MATCH (p:Player)-[:PLAYS_FOR]->(t)
  RETURN collect(p) as players
}
RETURN t AS teams, players
----

.Result
[source, role="queryresult",options="header,footer",cols="m,2m"]
|===
| teams
| players.name

| (:Team {name: "Team A"})
| [(:Player {name: "Player C", age: 19}), (:Player {name: "Player B", age: 23}), (:Player {name: "Player A", age: 24})]

| (:Team {name: "Team B"})
| [(:Player {name: "Player D", age: 30})]

| (:Team {name: "Team C"})
| [(:Player {name: "Player F", age: 35}), (:Player {name: "Player E", age: 25})]

2+d|Rows: 3
|===

The `CALL` subquery ensures that each `Team` node is processed into the correct `LIST` separately (one row per `Team` node), rather than having to hold every `Team` and `Player` in heap memory simultaneously before collecting them into lists.
Using a `CALL` subquery can, therefore, reduce the amount of heap memory required for an operation.

====

[[import-variables]]
== Import variables

[role=label--new-5.22]
[[variable-scope-clause]]
=== The variable scope clause

Variables can be imported into a `CALL` subquery using a scope clause: `CALL (<variable>)`.
As the subquery is evaluated for each incoming input row, the imported variables are assigned the corresponding values from that row.
Using the scope clause disables the deprecated xref:subqueries/call-subquery.adoc#importing-with[importing `WITH`] clause.

A scope clause can be used to import all, specific, or none of the variables from the outer scope.

.Import specific variables from the outer scope
====

This example only imports the `p` variable from the outer scope, and uses it to create a new, randomly generated, `rating` property for each `Player` node.
It then returns the `Player` node with the highest `rating`.

.Import one variable from the outer scope
[source, cypher, test=result-skip]
----
MATCH (p:Player), (t:Team)
CALL (p) {
  WITH p, rand() AS random
  SET p.rating = random
  RETURN p.name AS playerName, p.rating AS rating
}
RETURN playerName, rating, t AS team
ORDER BY rating
LIMIT 1
----

.Result
[source, role="queryresult",options="header,footer",cols="3*m"]
|===
| playerName
| rating
| team

| "Player C"
| 0.9307432039870395
| "Team A"

3+d|Rows: 1

|===

To import additional variables, include them within the parentheses after `CALL` (separated by commas).
For example, to import both variables from the `MATCH` clause in the above query, modify the scope clause accordingly: `CALL (p,t)`

====

.Import all variables 
====

To import all variables from the outer scope, use `CALL (*)`.
This example imports both the `p` and `t` variables and sets a new `lastUpdated` property on both.

.Import all variables from the outer scope
[source, cypher, role=test-result-skip]
----
MATCH (p:Player), (t:Team)
CALL (*) {
  SET p.lastUpdated = timestamp()
  SET t.lastUpdated = timestamp()
  RETURN p.name AS playerName, p.lastUpdated AS playerUpdated, t.name AS teamName, t.lastUpdated AS teamUpdated
}
RETURN playerName, playerUpdated, teamName, teamUpdated
LIMIT 1
----

.Result
[source, role="queryresult",options="header,footer",cols="4*m"]
|===

| playerName
| playerUpdated
| teamName
| teamUpdated

| "Player A"
| 1719304206653
| "Team A"
| 1719304206653

4+d|Rows: 1
|===

====

.Import no variables
====

To import no variables from the outer scope, use `CALL()`.

.Import no variables from the outer scope
[source, cypher]
----
MATCH (t:Team)
CALL () {
  MATCH (p:Player)
  RETURN count(p) AS totalPlayers
}
RETURN count(t) AS totalTeams, totalPlayers
----

.Result
[source, role="queryresult",options="header,footer",cols="2*m"]
|===

| totalTeams
| totalPlayers

| 3
| 6

2+d|Rows: 1
|===

[NOTE]
=====
As of Neo4j 5.22, naked subqueries without a scope clause is deprecated.

.Deprecated
[source, cypher]
----
MATCH (t:Team)
CALL {
  MATCH (p:Player)
  RETURN count(p) AS totalPlayers
}
RETURN count(t) AS totalTeams, totalPlayers
----
=====

====

=== Rules 

* The scope clause may not contain variable declarations.

.Not allowed
[cypher, source, role=test=fail]
----
CALL (1 AS a) {
 ...
}
RETURN a
----

* The scope clause’s variables can be globally referenced in the subquery.
A subsequent `WITH` within the subquery cannot delist an imported variable.
The deprecated xref:subqueries/call-subquery.adoc#importing-with[importing `WITH` clause] behaves differently because imported variables can only be referenced from the first line and can be delisted by subsequent clauses.

* The scope clause’s variables cannot be re-declared in the subquery.

.Not allowed
[cypher, source, role=test=fail]
----
WITH 1 AS a
CALL (a) {
  WITH 1 AS a
  RETURN a AS ignored 
}
RETURN a
----

* The subquery cannot return a variable name which already exists in the outer scope, regardless of whether it is in the scope clause or not.

.Not allowed
[cypher, source, role=test=fail]
----
WITH 1 AS a
CALL (a) {
  RETURN a
}
RETURN a
----

[role=label--deprecated]
[[importing-with]]
=== Importing `WITH` clause

Variables can also be imported into a `CALL` subquery using an importing `WITH` clause.
Note that this syntax is not xref:appendix/gql-conformance/index.adoc[GQL conformant].

.Query
[source, cypher]
----
UNWIND [0, 1, 2] AS x
CALL {
  WITH x
  RETURN x * 10 AS y
}
RETURN x, y
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| x | y
| 0 | 0
| 1 | 10
| 2 | 20
2+d|Rows: 3
|===

.Click to read more about importing variables using the `WITH` clause
[%collapsible]
====
An importing `WITH` clause must:

* Consist only of simple references to outside variables - e.g. `WITH x, y, z`. Aliasing or expressions are not supported in importing `WITH` clauses - e.g. `WITH a AS b` or `WITH a+1 AS b`.
* Be the first clause of a subquery (or the second clause, if directly following a `USE` clause).

Additionally, there are some restrictions that apply when using an importing `WITH` clause in a `CALL` subquery:

* Only variables imported with the importing `WITH` clause can be used.
* It is not possible to follow an importing `WITH` clause with any of the following clauses: `DISTINCT`, `ORDER BY`, `WHERE`, `SKIP`, and `LIMIT`.

Attempting any of the above, will throw an error.
For example, the following query using a `WHERE` clause after an importing `WITH` clause will throw an error:

.Query
[source, cypher, role=test-fail]
----
UNWIND [[1,2],[1,2,3,4],[1,2,3,4,5]] AS l
CALL {
    WITH l
    WHERE size(l) > 2
    RETURN l AS largeLists
}
RETURN largeLists
----

.Error message
[source, error]
----
Importing WITH should consist only of simple references to outside variables.
WHERE is not allowed.
----

A solution to this restriction, necessary for any filtering or ordering of an importing `WITH` clause, is to declare a second `WITH` clause after the importing `WITH` clause.
This second `WITH` clause will act as a regular `WITH` clause.
For example, the following query will not throw an error:

.Query
[source, cypher]
----
UNWIND [[1,2],[1,2,3,4],[1,2,3,4,5]] AS l
CALL {
 WITH l
 WITH size(l) AS size, l AS l
 WHERE size > 2
 RETURN l AS largeLists
}
RETURN largeLists
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| largeLists

| [1, 2, 3, 4]
| [1, 2, 3, 4, 5]
1+d|Rows: 2

|===

====


[[call-execution-order]]
== Execution order of CALL subqueries

The order in which subqueries are executed is not defined.
If a query result depends on the order of execution of subqueries, an `ORDER BY` clause should precede the `CALL` clause.

.Ordering results before `CALL` subquery
====
This example creates a linked list of all `Player` nodes in order of ascending `age`.

The `CALL` clause is relying on the incoming row ordering to ensure that a correctly ordered linked list is created, thus the incoming rows must be ordered with a preceding `ORDER BY` clause.

.Order results before a `CALL` subquery
[source, cypher]
----
MATCH (player:Player)
WITH player 
ORDER BY player.age ASC LIMIT 1
  SET player:ListHead
WITH *
MATCH (nextPlayer: Player&!ListHead)
WITH nextPlayer
ORDER BY nextPlayer.age
CALL (nextPlayer) {
  MATCH (current:ListHead)
    REMOVE current:ListHead
    SET nextPlayer:ListHead
    CREATE(current)-[:IS_YOUNGER_THAN]->(nextPlayer)
  RETURN current AS from, nextPlayer AS to
}
RETURN
  from.name AS name,
  from.age AS age,
  to.name AS closestOlderName,
  to.age AS closestOlderAge
----

.Result
[role="queryresult",options="header,footer",cols="4*m"]
|===
| name
| age
| closestOlderName
| closestOlderAge

| "Player C"
| 19
| "Player A"
| 21

| "Player A"
| 21
| "Player B"
| 23

| "Player B"
| 23
| "Player E"
| 25

| "Player E"
| 25
| "Player D"
| 30

| "Player D"
| 30
| "Player F"
| 35

4+d|Rows: 5
|===

====

[[call-post-union]]
== Post-union processing

Call subqueries can be used to further process the results of a xref:clauses/union.adoc[] query.

.Using `UNION` within a `CALL` subquery
====
This example query finds the youngest and the oldest `Player` in the graph.

.Find the oldest and youngest player
[source, cypher]
----
CALL () {
  MATCH (p:Player)
  RETURN p
  ORDER BY p.age ASC
  LIMIT 1
UNION
  MATCH (p:Player)
  RETURN p
  ORDER BY p.age DESC
  LIMIT 1
}
RETURN p.name AS name, p.age AS age
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| name | age
| "Player C" | 19
| "Player F" | 35
2+d|Rows: 2
|===

If different parts of a result should be matched differently, with some aggregation over the whole result, subqueries need to be used.
The example below query uses a `CALL` subquery in combination with `UNION ALL` to determine how much each `Team` in the graph owes or is owed.

.Query
[source, cypher]
----
MATCH (t:Team)
CALL (t) {
  OPTIONAL MATCH (t)-[o:OWES]->(other:Team)
  RETURN o.dollars * -1 AS moneyOwed
  UNION ALL
  OPTIONAL MATCH (other)-[o:OWES]->(t)
  RETURN o.dollars AS moneyOwed
}
RETURN t.name AS team, sum(moneyOwed) AS amountOwed
ORDER BY amountOwed DESC
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| team | amountOwed
| "Team B" | 7800
| "Team C" | -3300
| "Team A" | -4500

2+d|Rows: 3
|===

====

[[call-aggregation]]
== Aggregations

Returning subqueries change the number of results of the query.
The result of the `CALL` subquery is the combined result of evaluating the subquery for each input row.

.`CALL` subquery changing returned rows of outer query
====
The following example finds the name of each `Player` and the names of their friends:

.Query
[source, cypher]
----
MATCH (p:Player)
CALL (p) {
  MATCH (p)-[:FRIENDS_WITH]->(p2:Player)
  RETURN p2.name AS friend
}
RETURN p.name AS player, friend
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| p.name
| friend
| "Player A" 
| "Player C"

| "Player A"
| "Player B"

| "Player B" 
| "Player F"

| "Player C" 
| "Player D"

2+d|Rows: 4
|===

No rows are returned for the `Player` nodes without any `FRIEND_OF` relationships, the number of results of the subquery thus changed the number of results of the enclosing query.
====

.`CALL` subqueries and isolated aggregations
====

Subqueries can also perform isolated aggregations.
The below example uses the xref:functions/aggregating.adoc#functions-sum[sum()] function to count how much money is owed between the `Team` nodes in the graph.
Note that the `owedAmount` for `Team A` is the aggregated results of two `OWES` relationships to `Team B`.

.Query
[source, cypher]
----
MATCH (t:Team)
CALL (t) {
  MATCH (t)-[o:OWES]->(t2:Team)
  RETURN sum(o.dollars) AS owedAmount, t2.name AS owedTeam
}
RETURN t.name AS owingTeam, owedAmount, owedTeam
----

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===
| owingTeam
| owedAmount
| owedTeam

| "Team A"
| 4500
| "Team B"

| "Team B"
| 1700
| "Team C"

| "Team C"
| 5000
| "Team B"

3+d|Rows: 4
|===
====

[[call-returning-unit]]
== Note on returning subqueries and unit subqueries

The examples above have all used subqueries which end with a `RETURN` clause.
These subqueries are called _returning subqueries_.

A subquery is evaluated for each incoming input row.
Every output row of a returning subquery is combined with the input row to build the result of the subquery.
That means that a returning subquery will influence the number of rows.
If the subquery does not return any rows, there will be no rows available after the subquery.

Subqueries without a `RETURN` statement are called _unit subqueries_.
Unit subqueries are used for their ability to alter the graph with clauses such as `CREATE`, `MERGE`, `SET`, and `DELETE`.
They do not explicitly return anything, and this means that the number of rows present after the subquery is the same as was going into the subquery.

[[call-unit-subqueries]]
== Unit subqueries

Unit subqueries are used for their ability to alter the graph with updating clauses.
They do not impact the amount of rows returned by the enclosing query.

This example query creates 3 clones of each existing `Player` node in the graph.
As the subquery is a unit subquery, it does not change the number of rows of the enclosing query.

.Query
[source, cypher]
----
MATCH (p:Player)
CALL (p) {
  UNWIND range (1, 3) AS i
  CREATE (:Person {name: p.name})
}
RETURN count(*)
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| count(*)
| 6
1+d|Rows: 1 +
Nodes created: 18 +
Properties set: 18 +
Labels added: 18
|===


[[call-rules]]
== Summary

The following is true for `CALL` subqueries:

* A `CALL` subquery can only refer to variables from the enclosing query if they are explicitly imported by either a variable scope clause or an importing `WITH` clause.

* All variables that are returned from a `CALL` subquery are afterwards available in the enclosing query.

* A `CALL` subquery can be used to perform changes to the database.
