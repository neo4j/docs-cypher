[role=enterprise-edition]
[[access-control-privileges-writes]]
= Write privileges

[abstract]
--
This section explains how to use Cypher to manage write privileges on graphs.
--

Write privileges are defined for different parts of the graph:

* <<access-control-privileges-writes-create,`CREATE`>> - allows creating nodes and relationships.
* <<access-control-privileges-writes-delete,`DELETE`>> - allows deleting nodes and relationships.
* <<access-control-privileges-writes-set-label,`SET LABEL`>> - allows setting the specified node labels using the `SET` clause.
* <<access-control-privileges-writes-remove-label,`REMOVE LABEL`>> - allows removing the specified node labels using the `REMOVE` clause.
* <<access-control-privileges-writes-set-property,`SET PROPERTY`>> - allows setting properties on nodes and relationships.

There are also compound privileges which combine the above specific privileges:

* <<access-control-privileges-writes-merge,`MERGE`>> - allows `MATCH`, `CREATE` and `SET PROPERTY` to apply the `MERGE` command.
* <<access-control-privileges-writes-write,`WRITE`>> - allows all `WRITE` operations on an entire graph.
* <<access-control-privileges-writes-all,`ALL GRAPH PRIVILEGES`>> - allows all `READ` and `WRITE` operations on an entire graph.


[[access-control-privileges-writes-create]]
== The `CREATE` privilege

The `CREATE` privilege allows a user to create new node and relationship elements on a graph.
See the Cypher <<query-create, CREATE>> clause.

[source, cypher, role=noplay]
----
GRANT CREATE ON {HOME GRAPH | GRAPH[S] { * | name[, ...] }}
    [
        ELEMENT[S] { * | label-or-rel-type[, ...] }
        | NODE[S] { * | label[, ...] }
        | RELATIONSHIP[S] { * | rel-type[, ...] }
    ]
    TO role[, ...]
----

For example, to grant the role `regularUsers` the ability to `CREATE` elements on the graph `neo4j`, use:

[source, cypher, role=noplay]
----
GRANT CREATE ON GRAPH neo4j ELEMENTS * TO regularUsers
----

The `CREATE` privilege can also be denied:

[source, cypher, role=noplay]
----
DENY CREATE ON {HOME GRAPH | GRAPH[S] { * | name[, ...] }}
    [
        ELEMENT[S] { * | label-or-rel-type[, ...] }
        | NODE[S] { * | label[, ...] }
        | RELATIONSHIP[S] { * | rel-type[, ...] }
    ]
    TO role[, ...]
----

For example, to deny the role `regularUsers` the ability to `CREATE` nodes with the label `foo` on all graphs, use:

[source, cypher, role=noplay]
----
DENY CREATE ON GRAPH * NODES foo TO regularUsers
----

[NOTE]
====
If the user attempts to create nodes with a label that does not already exist on the database, then the user must also possess the <<access-control-database-administration-tokens, CREATE NEW LABEL>> privilege.
The same applies to new relationships: the <<access-control-database-administration-tokens, CREATE NEW RELATIONSHIP TYPE>> privilege is required.
====


[[access-control-privileges-writes-delete]]
== The `DELETE` privilege

The `DELETE` privilege allows a user to delete node and relationship elements on a graph.
See the Cypher <<query-delete, DELETE>> clause.

[source, cypher, role=noplay]
----
GRANT DELETE ON {HOME GRAPH | GRAPH[S] { * | name[, ...] }}
    [
        ELEMENT[S] { * | label-or-rel-type[, ...] }
        | NODE[S] { * | label[, ...] }
        | RELATIONSHIP[S] { * | rel-type[, ...] }
    ]
    TO role[, ...]
----

For example, to grant the role `regularUsers` the ability to `DELETE` elements on the graph `neo4j`, use:

[source, cypher, role=noplay]
----
GRANT DELETE ON GRAPH neo4j ELEMENTS * TO regularUsers
----

The `DELETE` privilege can also be denied:

[source, cypher, role=noplay]
----
DENY DELETE ON {HOME GRAPH | GRAPH[S] { * | name[, ...] }}
    [
        ELEMENT[S] { * | label-or-rel-type[, ...] }
        | NODE[S] { * | label[, ...] }
        | RELATIONSHIP[S] { * | rel-type[, ...] }
    ]
    TO role[, ...]
----

For example, to deny the role `regularUsers` the ability to `DELETE` relationships with the relationship type `bar` on all graphs, use:

[source, cypher, role=noplay]
----
DENY DELETE ON GRAPH * RELATIONSHIPS bar TO regularUsers
----

[NOTE]
====
Users with `DELETE` privilege, but restricted `TRAVERSE` privileges, will not be able to do `DETACH DELETE` in all cases.
See <<operations-manual#detach-delete-restricted-user, Operations Manual -> Fine-grained access control>> for more info.
====


[[access-control-privileges-writes-set-label]]
== The `SET LABEL` privilege

The `SET LABEL` privilege allows you to set labels on a node by using the <<set-set-a-label-on-a-node, SET clause>>:

[source, cypher, role=noplay]
----
GRANT SET LABEL { * | label[, ...] }
    ON {HOME GRAPH | GRAPH[S] { * | name[, ...] }}
    TO role[, ...]
----

For example, to grant the role `regularUsers` the ability to `SET` any label on nodes of the graph `neo4j`, use:

[source, cypher, role=noplay]
----
GRANT SET LABEL * ON GRAPH neo4j TO regularUsers
----

[NOTE]
====
Unlike many of the other `READ` and `WRITE` privileges, it is not possible to restrict the `SET LABEL` privilege to specific ELEMENTS, NODES or RELATIONSHIPS.
====

The `SET LABEL` privilege can also be denied:

[source, cypher, role=noplay]
----
DENY SET LABEL { * | label[, ...] }
    ON {HOME GRAPH | GRAPH[S] { * | name[, ...] }}
    TO role[, ...]
----

For example, to deny the role `regularUsers` the ability to `SET` the label `foo` on nodes of all graphs, use:

[source, cypher, role=noplay]
----
DENY SET LABEL foo ON GRAPH * TO regularUsers
----

[NOTE]
====
If no instances of this label exist on the database, then the <<access-control-database-administration-tokens, CREATE NEW LABEL>> privilege is also required.
====


[[access-control-privileges-writes-remove-label]]
== The `REMOVE LABEL` privilege

The `REMOVE LABEL` privilege allows you to remove labels from a node by using the <<remove-remove-a-label-from-a-node, REMOVE clause>>:

[source, cypher, role=noplay]
----
GRANT REMOVE LABEL { * | label[, ...] }
    ON {HOME GRAPH | GRAPH[S] { * | name[, ...] }}
    TO role[, ...]
----

For example, to grant the role `regularUsers` the ability to `REMOVE` any label from nodes of the graph `neo4j`, use:

[source, cypher, role=noplay]
----
GRANT REMOVE LABEL * ON GRAPH neo4j TO regularUsers
----

[NOTE]
====
Unlike many of the other `READ` and `WRITE` privileges, it is not possible to restrict the `REMOVE LABEL` privilege to specific ELEMENTS, NODES or RELATIONSHIPS.
====

The `REMOVE LABEL` privilege can also be denied:

[source, cypher, role=noplay]
----
DENY REMOVE LABEL { * | label[, ...] }
    ON {HOME GRAPH | GRAPH[S] { * | name[, ...] }}
    TO role[, ...]
----

For example, denying the role `regularUsers` the ability to remove the label `foo` from nodes of all graphs, use:

[source, cypher, role=noplay]
----
DENY REMOVE LABEL foo ON GRAPH * TO regularUsers
----


[[access-control-privileges-writes-set-property]]
== The `SET PROPERTY` privilege

The `SET PROPERTY` privilege allows a user to set a property on a node or relationship element in a graph by using the <<set-set-a-property, SET clause>>:

[source, cypher, role=noplay]
----
GRANT SET PROPERTY "{" { * | property[, ...] } "}"
    ON {HOME GRAPH | GRAPH[S] { * | name[, ...] }}
        [
            ELEMENT[S] { * | label-or-rel-type[, ...] }
            | NODE[S] { * | label[, ...] }
            | RELATIONSHIP[S] { * | rel-type[, ...] }
        ]
    TO role[, ...]
----

For example, to grant the role `regularUsers` the ability to `SET` any property on all elements of the graph `neo4j`, use:

[source, cypher, role=noplay]
----
GRANT SET PROPERTY {*} ON HOME GRAPH ELEMENTS * TO regularUsers
----

The `SET PROPERTY` privilege can also be denied:

[source, cypher, role=noplay]
----
DENY SET PROPERTY "{" { * | property[, ...] } "}"
    ON {HOME GRAPH | GRAPH[S] { * | name[, ...] }}
        [
            ELEMENT[S] { * | label-or-rel-type[, ...] }
            | NODE[S] { * | label[, ...] }
            | RELATIONSHIP[S] { * | rel-type[, ...] }
        ]
    TO role[, ...]
----

For example, to deny the role `regularUsers` the ability to `SET` the property `foo` on nodes with the label `bar` on all graphs, use:

[source, cypher, role=noplay]
----
DENY SET PROPERTY { foo } ON GRAPH * NODES bar TO regularUsers
----

[NOTE]
====
If the user attempts to set a property with a property name that does not already exist on the database, the user must also possess the <<access-control-database-administration-tokens, CREATE NEW PROPERTY NAME>> privilege.
====


[[access-control-privileges-writes-merge]]
== The `MERGE` privilege

The `MERGE` privilege is a compound privilege that combines `TRAVERSE` and `READ` (i.e. `MATCH`) with `CREATE` and `SET PROPERTY`.
This is intended to enable the use of <<query-merge, the MERGE command>>, but it is also applicable to all reads and writes that require these privileges.

[source, cypher, role=noplay]
----
GRANT MERGE "{" { * | property[, ...] } "}"
    ON {HOME GRAPH | GRAPH[S] { * | name[, ...] }}
        [
            ELEMENT[S] { * | label-or-rel-type[, ...] }
            | NODE[S] { * | label[, ...] }
            | RELATIONSHIP[S] { * | rel-type[, ...] }
        ]
    TO role[, ...]
----

For example, to grant the role `regularUsers` the ability to `MERGE` on all elements of the graph `neo4j`, use:

[source, cypher, role=noplay]
----
GRANT MERGE {*} ON GRAPH neo4j ELEMENTS * TO regularUsers
----

It is not possible to deny the `MERGE` privilege.
If you wish to prevent a user from creating elements and setting properties: use <<access-control-privileges-writes-create, DENY CREATE>> or <<access-control-privileges-writes-set-property,DENY SET PROPERTY>>.

[NOTE]
====
If the user attempts to create nodes with a label that does not already exist on the database, the user must also possess the
<<access-control-database-administration-tokens, CREATE NEW LABEL>> privilege.
The same applies to new relationships and properties - the
<<access-control-database-administration-tokens, CREATE NEW RELATIONSHIP TYPE>> or
<<access-control-database-administration-tokens, CREATE NEW PROPERTY NAME>> privileges are required.
====


[[access-control-privileges-writes-write]]
== The `WRITE` privilege

The `WRITE` privilege allows the user to execute any `WRITE` command on a graph.

[source, cypher, role=noplay]
----
GRANT WRITE
    ON {HOME GRAPH | GRAPH[S] { * | name[, ...] }}
    TO role[, ...]
----

For example, to grant the role `regularUsers` the ability to `WRITE` on the graph `neo4j`, use:

[source, cypher, role=noplay]
----
GRANT WRITE ON GRAPH neo4j TO regularUsers
----

[NOTE]
====
Unlike the more specific `WRITE` commands, it is not possible to restrict `WRITE` privileges to specific ELEMENTS, NODES or RELATIONSHIPS.
If you wish to prevent a user from writing to a subset of database objects, a `GRANT WRITE` can be combined with more specific `DENY` commands to target these elements.
====

The `WRITE` privilege can also be denied:

[source, cypher, role=noplay]
----
DENY WRITE
    ON {HOME GRAPH | GRAPH[S] { * | name[, ...] }}
    TO role[, ...]
----

For example, to deny the role `regularUsers` the ability to `WRITE` on the graph `neo4j`, use:

[source, cypher, role=noplay]
----
DENY WRITE ON GRAPH neo4j TO regularUsers
----

[NOTE]
====
Users with `WRITE` privilege but restricted `TRAVERSE` privileges will not be able to do `DETACH DELETE` in all cases.
See <<operations-manual#detach-delete-restricted-user, Operations Manual -> Fine-grained access control>> for more info.
====


[[access-control-privileges-writes-all]]
== The `ALL GRAPH PRIVILEGES` privilege

The `ALL GRAPH PRIVILEGES` privilege allows the user to execute any command on a graph:

[source, cypher, role=noplay]
----
GRANT ALL [ [ GRAPH ] PRIVILEGES ]
    ON {HOME GRAPH | GRAPH[S] { * | name[, ...] }}
    TO role[, ...]
----

For example, to grant the role `regularUsers` `ALL GRAPH PRIVILEGES` on the graph `neo4j`, use:

[source, cypher, role=noplay]
----
GRANT ALL GRAPH PRIVILEGES ON GRAPH neo4j TO regularUsers
----

[NOTE]
====
Unlike the more specific `READ` and `WRITE` commands, it is not possible to restrict `ALL GRAPH PRIVILEGES` to specific ELEMENTS,
NODES or RELATIONSHIPS.
If you wish to prevent a user from reading or writing to a subset of database objects, a `GRANT ALL GRAPH PRIVILEGES` can be combined with more specific `DENY` commands to target these elements.
====

The `ALL GRAPH PRIVILEGES` privilege can also be denied:

[source, cypher, role=noplay]
----
DENY ALL [ [ GRAPH ] PRIVILEGES ]
    ON {HOME GRAPH | GRAPH[S] { * | name[, ...] }}
    TO role[, ...]
----

For example, to deny the role `regularUsers` all graph privileges on the graph `neo4j`, use:

[source, cypher, role=noplay]
----
DENY ALL GRAPH PRIVILEGES ON GRAPH neo4j TO regularUsers
----
