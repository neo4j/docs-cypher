:description: This section explains how to use Cypher to manage privileges for Neo4j role-based access control and fine-grained security.
[[access-control-manage-privileges]]

= Managing privileges

[abstract]
--
This section explains how to use Cypher to manage privileges for Neo4j role-based access control and fine-grained security.
--

Privileges control the access rights to graph elements using a combined allowlist/denylist mechanism.
It is possible to grant access, or deny access, or a combination of the two.
The user will be able to access the resource if they have a grant (whitelist) and do not have a deny (blacklist) relevant to that resource.
All other combinations of `GRANT` and `DENY` will result in the matching path being inaccessible.
What this means in practice depends on whether we are talking about a xref::access-control/privileges-reads.adoc[read privilege] or a xref::access-control/privileges-writes.adoc[write privilege].

* If a entity is not accessible due to xref::access-control/privileges-reads.adoc[read privileges], the data will become invisible to attempts to read it.
It will appear to the user as if they have a smaller database (smaller graph).
* If an entity is not accessible due to xref::access-control/privileges-writes.adoc[write privileges], an error will occur on any attempt to write that data.

[NOTE]
====
In this document we will often use the terms _'allows'_ and _'enables'_ in seemingly identical ways. However, there is a subtle difference.
We will use _'enables'_ to refer to the consequences of xref::access-control/privileges-reads.adoc[read privileges] where a restriction will not cause an error, only a reduction in the apparent graph size.
We will use _'allows'_ to refer to the consequence of xref::access-control/privileges-writes.adoc[write privileges] where a restriction can result in an error.
====

[NOTE]
====
If a user was not also provided with the database `ACCESS` privilege then access to the entire database will be denied.
Information about the database access privilege can be found in xref::access-control/database-administration.adoc#access-control-database-administration-access[The ACCESS privilege].
====


[role=enterprise-edition]
[[access-control-graph-privileges]]
== Graph privilege commands (`GRANT`, `DENY`, and `REVOKE`)

Administrators can use Cypher commands to manage Neo4j graph administrative rights.
The components of the graph privilege commands are:

* the command:
** `GRANT` – gives privileges to roles.
** `DENY` – denies privileges to roles.
** `REVOKE` – removes granted or denied privilege from roles.

* _graph-privilege_
** Can be either a xref::access-control/privileges-reads.adoc[read privilege] or xref::access-control/privileges-writes.adoc[write privilege].

* _name_
** The graph or graphs to associate the privilege with.
Because in Neo4j {neo4j-version} you can have only one graph per database, this command uses the database name or alias to refer to that graph.
When using an alias, the command will be executed on the resolved graph.
+
[NOTE]
====
If you delete a database and create a new one with the same name, the new one will _NOT_ have the privileges assigned to the deleted graph.
====
** It can be `+*+` which means all graphs.
Graphs created after this command execution will also be associated with these privileges.

** `HOME GRAPH` refers to the graph associated with the home database for that user.
The default database will be used as home database if a user does not have a home database configured.
If the user's home database changes for any reason after privileges have been created then these privileges will be associated with the graph attached to the new database.
This can be quite powerful as it allows permissions to be switched from one graph to another simply by changing a user's home database.

* _entity_
** The graph elements this privilege applies to:
*** `NODES` label (nodes with the specified label(s)).
*** `RELATIONSHIPS` type (relationships of the specific type(s)).
*** `ELEMENTS` label (both nodes and relationships).
** The label or type can be `+*+` which means all labels or types.
** Multiple labels or types can be specified, comma-separated.
** Defaults to `ELEMENTS` `+*+` if omitted.
** Some of the commands for write privileges do not allow an _entity_ part, see  xref::access-control/privileges-writes.adoc[Write privileges] for details.
* _role[, ...]_
** The role or roles to associate the privilege with, comma-separated.


.General grant +ON GRAPH+ privilege syntax
[cols="<15s,<85"]
|===

| Command
m| +GRANT ... ON ... TO ...+

| Syntax
a|
[source, syntax, role="noheader", indent=0]
----
GRANT graph-privilege ON { HOME GRAPH \| GRAPH[S] { * \| name[, ...] } } [entity] TO role[, ...]
----

| Description
a| Grant a privilege to one or multiple roles.

|===


.General deny +ON GRAPH+ privilege syntax
[cols="<15s,<85"]
|===

| Command
m| +DENY ... ON ... TO ...+

| Syntax
a|
[source, syntax, role="noheader", indent=0]
----
DENY graph-privilege ON { HOME GRAPH \| GRAPH[S] { * \| name[, ...] } } [entity] TO role[, ...]
----

| Description
a| Deny a privilege to one or multiple roles.

|===


.General revoke +ON GRAPH+ privilege syntax
[cols="<15s,<85"]
|===

| Command
m| +REVOKE GRANT ... ON ... FROM ...+

| Syntax
a|
[source, syntax, role="noheader", indent=0]
----
REVOKE GRANT graph-privilege ON { HOME GRAPH \| GRAPH[S] { * \| name[, ...] } } [entity] FROM role[, ...]
----

| Description
a| Revoke a granted privilege from one or multiple roles.

|===


.General revoke +ON GRAPH+ privilege syntax
[cols="<15s,<85"]
|===

| Command
m| +REVOKE DENY ... ON ... FROM ...+

| Syntax
a|
[source, syntax, role="noheader", indent=0]
----
REVOKE DENY graph-privilege ON { HOME GRAPH \| GRAPH[S] {* \| name[, ...] } } [entity] FROM role[, ...]
----

| Description
a| Revoke a denied privilege from one or multiple roles.

|===


.General revoke +ON GRAPH+ privilege syntax
[cols="<15s,<85"]
|===

| Command
m| +REVOKE ... ON ... FROM ...+

| Syntax
a|
[source, syntax, role="noheader", indent=0]
----
REVOKE graph-privilege ON { HOME GRAPH \| GRAPH[S] { * \| name[, ...] } } [entity] FROM role[, ...]
----

| Description
| Revoke a granted or denied privilege from one or multiple roles.

|===

[NOTE]
====
`DENY` does NOT erase a granted privilege; they both exist.
Use `REVOKE` if you want to remove a privilege.
====

The general grant and deny syntax is illustrated in the following image:

image::privileges_grant_and_deny_syntax.png[title="GRANT and DENY Syntax"]

A more detailed syntax illustration would be the following image for graph privileges:

image::privileges_on_graph_syntax.png[title="Syntax of GRANT and DENY Graph Privileges. The `{` and `}` are part of the syntax and not used for grouping."]

The following image shows the hierarchy between the different graph privileges:

image::privileges_hierarchy.png[title="Graph privileges hierarchy"]


[role=enterprise-edition]
[[access-control-list-privileges]]
== Listing privileges

Available privileges can be displayed using the different `SHOW PRIVILEGES` commands.

.Show privileges syntax
[cols="<15s,<85"]
|===

| Command
m| +SHOW PRIVILEGE+

| Syntax
a|
[source, syntax, role="noheader", indent=0]
----
SHOW [ALL] PRIVILEGE[S] [AS [REVOKE] COMMAND[S]]
  [YIELD { * \| field[, ...] } [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
  [WHERE expression]
  [RETURN field[, ...] [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
----

| Description
| List all privileges.

|===


.Show role privileges syntax
[cols="<15s,<85"]
|===

| Command
m| +SHOW ROLE ... PRIVILEGE+

| Syntax
a|
[source, syntax, role="noheader", indent=0]
----
SHOW ROLE[S] name[, ...] PRIVILEGE[S] [AS [REVOKE] COMMAND[S]]
  [YIELD { * \| field[, ...] } [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
  [WHERE expression]
  [RETURN field[, ...] [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
----

| Description
| List privileges for a specific role.

|===


.Show user privileges syntax
[cols="<15s,<85"]
|===

| Command
m| +SHOW USER ... PRIVILEGE+

| Syntax
a|
[source, syntax, role="noheader", indent=0]
----
SHOW USER[S] [name[, ...]] PRIVILEGE[S] [AS [REVOKE] COMMAND[S]]
  [YIELD { * \| field[, ...] } [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
  [WHERE expression]
  [RETURN field[, ...] [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
----

| Description
| List privileges for a specific user, or the current user.

| Note
|
It is only possible for a user to show their own privileges.
Therefore, if a non-native auth provider like LDAP is in use, `SHOW USER PRIVILEGES` will only work in a limited capacity.
Other users' privileges cannot be listed when using a non-native auth provider.

|===


When using the `RETURN` clause, the `YIELD` clause is mandatory and may not be omitted.

For an easy overview of the existing privileges, it is recommended to use the `AS COMMANDS` version of the show command.
This returns the privileges as the commands that are granted or denied.

Omitting the `AS COMMANDS` clause instead gives the result as multiple columns describing the privilege:

* `access`: whether the privilege is granted or denied.
* `action`: which type of privilege this is, for example traverse, read, index management, or role management.
* `resource`: what type of scope this privilege applies to: the entire dbms, a database, a graph or sub-graph access.
* `graph`: the specific database or graph this privilege applies to.
* `segment`: when applicable, the scope this privilege applies to: labels, relationship types, procedures, functions, or transactions.
* `role`: the role the privilege is granted to.


[role=enterprise-edition]
[[access-control-list-all-privileges]]
=== Examples for listing all privileges

Available privileges can be displayed using the different `SHOW PRIVILEGES` commands.

.Command syntax
[source, cypher, role=noplay, indent=0]
----
SHOW [ALL] PRIVILEGE[S] [AS [REVOKE] COMMAND[S]]
  [WHERE expression]

SHOW [ALL] PRIVILEGE[S] [AS [REVOKE] COMMAND[S]]
  YIELD { * | field[, ...] } [ORDER BY field[, ...]] [SKIP n] [LIMIT n]
  [WHERE expression]
  [RETURN field[, ...] [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
----

[source, cypher, role=noplay, indent=0]
----
SHOW PRIVILEGES
----

Lists all privileges for all roles:

.Result
[options="header,footer", width="100%", cols="m,m,m,m,m,m"]
|===
|access
|action
|resource
|graph
|segment
|role

|"GRANTED"
|"execute"
|"database"
|"*"
|"FUNCTION(*)"
|"PUBLIC"

|"GRANTED"
|"execute"
|"database"
|"*"
|"PROCEDURE(*)"
|"PUBLIC"

|"GRANTED"
|"access"
|"database"
|"DEFAULT"
|"database"
|"PUBLIC"

|"GRANTED"
|"match"
|"all_properties"
|"*"
|"NODE(*)"
|"admin"

|"GRANTED"
|"write"
|"graph"
|"*"
|"NODE(*)"
|"admin"

|"GRANTED"
|"match"
|"all_properties"
|"*"
|"RELATIONSHIP(*)"
|"admin"

|"GRANTED"
|"write"
|"graph"
|"*"
|"RELATIONSHIP(*)"
|"admin"

|"GRANTED"
|"transaction_management"
|"database"
|"*"
|"USER(*)"
|"admin"

|"GRANTED"
|"access"
|"database"
|"*"
|"database"
|"admin"

|"GRANTED"
|"constraint"
|"database"
|"*"
|"database"
|"admin"

|"GRANTED"
|"dbms_actions"
|"database"
|"*"
|"database"
|"admin"

|"GRANTED"
|"index"
|"database"
|"*"
|"database"
|"admin"

|"GRANTED"
|"start_database"
|"database"
|"*"
|"database"
|"admin"

|"GRANTED"
|"stop_database"
|"database"
|"*"
|"database"
|"admin"

|"GRANTED"
|"token"
|"database"
|"*"
|"database"
|"admin"

|"GRANTED"
|"match"
|"all_properties"
|"*"
|"NODE(*)"
|"architect"

|"GRANTED"
|"write"
|"graph"
|"*"
|"NODE(*)"
|"architect"

|"GRANTED"
|"match"
|"all_properties"
|"*"
|"RELATIONSHIP(*)"
|"architect"

|"GRANTED"
|"write"
|"graph"
|"*"
|"RELATIONSHIP(*)"
|"architect"

|"GRANTED"
|"access"
|"database"
|"*"
|"database"
|"architect"

|"GRANTED"
|"constraint"
|"database"
|"*"
|"database"
|"architect"

|"GRANTED"
|"index"
|"database"
|"*"
|"database"
|"architect"

|"GRANTED"
|"token"
|"database"
|"*"
|"database"
|"architect"

|"GRANTED"
|"match"
|"all_properties"
|"*"
|"NODE(*)"
|"editor"

|"GRANTED"
|"write"
|"graph"
|"*"
|"NODE(*)"
|"editor"

|"GRANTED"
|"match"
|"all_properties"
|"*"
|"RELATIONSHIP(*)"
|"editor"

|"GRANTED"
|"write"
|"graph"
|"*"
|"RELATIONSHIP(*)"
|"editor"

|"GRANTED"
|"access"
|"database"
|"*"
|"database"
|"editor"

|"DENIED"
|"access"
|"database"
|"neo4j"
|"database"
|"noAccessUsers"

|"GRANTED"
|"match"
|"all_properties"
|"*"
|"NODE(*)"
|"publisher"

|"GRANTED"
|"write"
|"graph"
|"*"
|"NODE(*)"
|"publisher"

|"GRANTED"
|"match"
|"all_properties"
|"*"
|"RELATIONSHIP(*)"
|"publisher"

|"GRANTED"
|"write"
|"graph"
|"*"
|"RELATIONSHIP(*)"
|"publisher"

|"GRANTED"
|"access"
|"database"
|"*"
|"database"
|"publisher"

|"GRANTED"
|"token"
|"database"
|"*"
|"database"
|"publisher"

|"GRANTED"
|"match"
|"all_properties"
|"*"
|"NODE(*)"
|"reader"

|"GRANTED"
|"match"
|"all_properties"
|"*"
|"RELATIONSHIP(*)"
|"reader"

|"GRANTED"
|"access"
|"database"
|"*"
|"database"
|"reader"

|"GRANTED"
|"access"
|"database"
|"neo4j"
|"database"
|"regularUsers"

6+a|Rows: 39
|===

It is also possible to filter and sort the results by using `YIELD`, `ORDER BY` and `WHERE`:

[source, cypher, role=noplay, indent=0]
----
SHOW PRIVILEGES YIELD role, access, action, segment
ORDER BY action
WHERE role = 'admin'
----

In this example:

* The number of columns returned has been reduced with the `YIELD` clause.
* The order of the returned columns has been changed.
* The results have been filtered to only return the `admin` role using a `WHERE` clause.
* The results are ordered by the `action` column using `ORDER BY`.

`SKIP` and `LIMIT` can also be used to paginate the results.

.Result
[options="header,footer", width="100%", cols="m,m,m,m"]
|===
|role
|access
|action
|segment

|"admin"
|"GRANTED"
|"access"
|"database"

|"admin"
|"GRANTED"
|"constraint"
|"database"

|"admin"
|"GRANTED"
|"dbms_actions"
|"database"

|"admin"
|"GRANTED"
|"index"
|"database"

|"admin"
|"GRANTED"
|"match"
|"NODE(*)"

|"admin"
|"GRANTED"
|"match"
|"RELATIONSHIP(*)"

|"admin"
|"GRANTED"
|"start_database"
|"database"

|"admin"
|"GRANTED"
|"stop_database"
|"database"

|"admin"
|"GRANTED"
|"token"
|"database"

|"admin"
|"GRANTED"
|"transaction_management"
|"USER(*)"

|"admin"
|"GRANTED"
|"write"
|"NODE(*)"

|"admin"
|"GRANTED"
|"write"
|"RELATIONSHIP(*)"

4+a|Rows: 12
|===

`WHERE` can be used without `YIELD`:

[source, cypher, role=noplay, indent=0]
----
SHOW PRIVILEGES
WHERE graph <> '*'
----

In this example, the `WHERE` clause is used to filter privileges down to those that target specific graphs only.

.Result
[options="header,footer", width="100%", cols="m,m,m,m,m,m"]
|===
|access
|action
|graph
|resource
|role
|segment

|"GRANTED"
|"access"
|"DEFAULT"
|"database"
|"PUBLIC"
|"database"

|"DENIED"
|"access"
|"neo4j"
|"database"
|"noAccessUsers"
|"database"

|"GRANTED"
|"access"
|"neo4j"
|"database"
|"regularUsers"
|"database"

6+a|Rows: 3
|===

Aggregations in the `RETURN` clause can be used to group privileges.
In this case, by user and granted / denied:

[source, cypher, role=noplay, indent=0]
----
SHOW PRIVILEGES YIELD * RETURN role, access, collect([graph, resource, segment, action]) AS privileges
----

.Result
[options="header,footer", width="100%", cols="1m,1m,3m"]
|===
|role
|access
|privileges

|"PUBLIC"
|"GRANTED"
|[["\*","database","FUNCTION(*)","execute"],["\*","database","PROCEDURE(*)","execute"],["DEFAULT","database","database","access"]]

|"admin"
|"GRANTED"
|[["\*","all_properties","NODE(*)","match"],["\*","graph","NODE(*)","write"],["\*","all_properties","RELATIONSHIP(*)","match"],["\*","graph","RELATIONSHIP(*)","write"],["\*","database","USER(*)","transaction_management"],["\*","database","database","access"],["*","database","database","constraint"],["\*","database","database","dbms_actions"],["*","database","database","index"],["\*","database","database","start_database"],["*","database","database","stop_database"],["*","database","database","token"]]

|"architect"
|"GRANTED"
|[["\*","all_properties","NODE(*)","match"],["\*","graph","NODE(*)","write"],["\*","all_properties","RELATIONSHIP(*)","match"],["\*","graph","RELATIONSHIP(*)","write"],["\*","database","database","access"],["*","database","database","constraint"],["\*","database","database","index"],["*","database","database","token"]]

|"editor"
|"GRANTED"
|[["\*","all_properties","NODE(*)","match"],["\*","graph","NODE(*)","write"],["\*","all_properties","RELATIONSHIP(*)","match"],["\*","graph","RELATIONSHIP(*)","write"],["*","database","database","access"]]

|"noAccessUsers"
|"DENIED"
|[["neo4j","database","database","access"]]

|"publisher"
|"GRANTED"
|[["\*","all_properties","NODE(*)","match"],["\*","graph","NODE(*)","write"],["\*","all_properties","RELATIONSHIP(*)","match"],["\*","graph","RELATIONSHIP(*)","write"],["\*","database","database","access"],["*","database","database","token"]]

|"reader"
|"GRANTED"
|[["\*","all_properties","NODE(*)","match"],["\*","all_properties","RELATIONSHIP(*)","match"],["*","database","database","access"]]

|"regularUsers"
|"GRANTED"
|[["neo4j","database","database","access"]]

3+a|Rows: 8
|===

The `RETURN` clause can also be used to order and paginate the results, which is useful when combined with `YIELD` and `WHERE`.
In this example the query returns privileges for display five-per-page, and skips the first five to display the second page.

[source, cypher, role=noplay, indent=0]
----
SHOW PRIVILEGES YIELD * RETURN * ORDER BY role SKIP 5 LIMIT 5
----

.Result
[options="header,footer", width="100%", cols="2m,2m,1m,2m,1m,2m"]
|===
|access
|action
|graph
|resource
|role
|segment

|"GRANTED"
|"match"
|"*"
|"all_properties"
|"admin"
|"RELATIONSHIP(*)"

|"GRANTED"
|"write"
|"*"
|"graph"
|"admin"
|"RELATIONSHIP(*)"

|"GRANTED"
|"transaction_management"
|"*"
|"database"
|"admin"
|"USER(*)"

|"GRANTED"
|"access"
|"*"
|"database"
|"admin"
|"database"

|"GRANTED"
|"constraint"
|"*"
|"database"
|"admin"
|"database"

6+a|Rows: 5
|===

Available privileges can also be output as Cypher commands, by appending `AS COMMAND[S]` to the show command:

[source, cypher, role=noplay, indent=0]
----
SHOW PRIVILEGES AS COMMANDS
----

.Result
[options="header,footer", width="100%", cols="m"]
|===
|command
|"DENY ACCESS ON DATABASE `neo4j` TO `noAccessUsers`"
|"GRANT ACCESS ON DATABASE * TO `admin`"
|"GRANT ACCESS ON DATABASE * TO `architect`"
|"GRANT ACCESS ON DATABASE * TO `editor`"
|"GRANT ACCESS ON DATABASE * TO `publisher`"
|"GRANT ACCESS ON DATABASE * TO `reader`"
|"GRANT ACCESS ON DATABASE `neo4j` TO `regularUsers`"
|"GRANT ACCESS ON HOME DATABASE TO `PUBLIC`"
|"GRANT ALL DBMS PRIVILEGES ON DBMS TO `admin`"
|"GRANT CONSTRAINT MANAGEMENT ON DATABASE * TO `admin`"
|"GRANT CONSTRAINT MANAGEMENT ON DATABASE * TO `architect`"
|"GRANT EXECUTE FUNCTION * ON DBMS TO `PUBLIC`"
|"GRANT EXECUTE PROCEDURE * ON DBMS TO `PUBLIC`"
|"GRANT INDEX MANAGEMENT ON DATABASE * TO `admin`"
|"GRANT INDEX MANAGEMENT ON DATABASE * TO `architect`"
|"GRANT MATCH {*} ON GRAPH * NODE * TO `admin`"
|"GRANT MATCH {*} ON GRAPH * NODE * TO `architect`"
|"GRANT MATCH {*} ON GRAPH * NODE * TO `editor`"
|"GRANT MATCH {*} ON GRAPH * NODE * TO `publisher`"
|"GRANT MATCH {*} ON GRAPH * NODE * TO `reader`"
|"GRANT MATCH {*} ON GRAPH * RELATIONSHIP * TO `admin`"
|"GRANT MATCH {*} ON GRAPH * RELATIONSHIP * TO `architect`"
|"GRANT MATCH {*} ON GRAPH * RELATIONSHIP * TO `editor`"
|"GRANT MATCH {*} ON GRAPH * RELATIONSHIP * TO `publisher`"
|"GRANT MATCH {*} ON GRAPH * RELATIONSHIP * TO `reader`"
|"GRANT NAME MANAGEMENT ON DATABASE * TO `admin`"
|"GRANT NAME MANAGEMENT ON DATABASE * TO `architect`"
|"GRANT NAME MANAGEMENT ON DATABASE * TO `publisher`"
|"GRANT START ON DATABASE * TO `admin`"
|"GRANT STOP ON DATABASE * TO `admin`"
|"GRANT TRANSACTION MANAGEMENT (*) ON DATABASE * TO `admin`"
|"GRANT WRITE ON GRAPH * TO `admin`"
|"GRANT WRITE ON GRAPH * TO `architect`"
|"GRANT WRITE ON GRAPH * TO `editor`"
|"GRANT WRITE ON GRAPH * TO `publisher`"
a|Rows: 35
|===

Like other `SHOW` commands, the output can also be processed using `YIELD` / `WHERE` / `RETURN`:

[source, cypher, role=noplay, indent=0]
----
SHOW PRIVILEGES AS COMMANDS
WHERE command CONTAINS 'MANAGEMENT'
----

.Result
[options="header,footer", width="100%", cols="m"]
|===
|command
|"GRANT CONSTRAINT MANAGEMENT ON DATABASE * TO `admin`"
|"GRANT CONSTRAINT MANAGEMENT ON DATABASE * TO `architect`"
|"GRANT INDEX MANAGEMENT ON DATABASE * TO `admin`"
|"GRANT INDEX MANAGEMENT ON DATABASE * TO `architect`"
|"GRANT NAME MANAGEMENT ON DATABASE * TO `admin`"
|"GRANT NAME MANAGEMENT ON DATABASE * TO `architect`"
|"GRANT NAME MANAGEMENT ON DATABASE * TO `publisher`"
|"GRANT TRANSACTION MANAGEMENT (*) ON DATABASE * TO `admin`"
a|Rows: 8
|===

It is also possible to get the privilege commands formatted for revoking instead of granting or denying the privileges:

[source, cypher, role=noplay, indent=0]
----
SHOW PRIVILEGES AS REVOKE COMMANDS
----

.Result
[options="header,footer", width="100%", cols="m"]
|===
|command
|"REVOKE DENY ACCESS ON DATABASE `neo4j` FROM `noAccessUsers`"
|"REVOKE GRANT ACCESS ON DATABASE * FROM `admin`"
|"REVOKE GRANT ACCESS ON DATABASE * FROM `architect`"
|"REVOKE GRANT ACCESS ON DATABASE * FROM `editor`"
|"REVOKE GRANT ACCESS ON DATABASE * FROM `publisher`"
|"REVOKE GRANT ACCESS ON DATABASE * FROM `reader`"
|"REVOKE GRANT ACCESS ON DATABASE `neo4j` FROM `regularUsers`"
|"REVOKE GRANT ACCESS ON HOME DATABASE FROM `PUBLIC`"
|"REVOKE GRANT ALL DBMS PRIVILEGES ON DBMS FROM `admin`"
|"REVOKE GRANT CONSTRAINT MANAGEMENT ON DATABASE * FROM `admin`"
|"REVOKE GRANT CONSTRAINT MANAGEMENT ON DATABASE * FROM `architect`"
|"REVOKE GRANT EXECUTE FUNCTION * ON DBMS FROM `PUBLIC`"
|"REVOKE GRANT EXECUTE PROCEDURE * ON DBMS FROM `PUBLIC`"
|"REVOKE GRANT INDEX MANAGEMENT ON DATABASE * FROM `admin`"
|"REVOKE GRANT INDEX MANAGEMENT ON DATABASE * FROM `architect`"
|"REVOKE GRANT MATCH {*} ON GRAPH * NODE * FROM `admin`"
|"REVOKE GRANT MATCH {*} ON GRAPH * NODE * FROM `architect`"
|"REVOKE GRANT MATCH {*} ON GRAPH * NODE * FROM `editor`"
|"REVOKE GRANT MATCH {*} ON GRAPH * NODE * FROM `publisher`"
|"REVOKE GRANT MATCH {*} ON GRAPH * NODE * FROM `reader`"
|"REVOKE GRANT MATCH {*} ON GRAPH * RELATIONSHIP * FROM `admin`"
|"REVOKE GRANT MATCH {*} ON GRAPH * RELATIONSHIP * FROM `architect`"
|"REVOKE GRANT MATCH {*} ON GRAPH * RELATIONSHIP * FROM `editor`"
|"REVOKE GRANT MATCH {*} ON GRAPH * RELATIONSHIP * FROM `publisher`"
|"REVOKE GRANT MATCH {*} ON GRAPH * RELATIONSHIP * FROM `reader`"
|"REVOKE GRANT NAME MANAGEMENT ON DATABASE * FROM `admin`"
|"REVOKE GRANT NAME MANAGEMENT ON DATABASE * FROM `architect`"
|"REVOKE GRANT NAME MANAGEMENT ON DATABASE * FROM `publisher`"
|"REVOKE GRANT START ON DATABASE * FROM `admin`"
|"REVOKE GRANT STOP ON DATABASE * FROM `admin`"
|"REVOKE GRANT TRANSACTION MANAGEMENT (*) ON DATABASE * FROM `admin`"
|"REVOKE GRANT WRITE ON GRAPH * FROM `admin`"
|"REVOKE GRANT WRITE ON GRAPH * FROM `architect`"
|"REVOKE GRANT WRITE ON GRAPH * FROM `editor`"
|"REVOKE GRANT WRITE ON GRAPH * FROM `publisher`"
a|Rows: 35
|===

For more info about revoking privileges, please see xref::access-control/manage-privileges.adoc#access-control-revoke-privileges[The REVOKE command].


[role=enterprise-edition]
[[access-control-list-privileges-role]]
=== Examples for listing privileges for specific roles

Available privileges for specific roles can be displayed using `SHOW ROLE name PRIVILEGES`.

[source, cypher, role=noplay, indent=0]
----
SHOW ROLE[S] name[, ...] PRIVILEGE[S] [AS [REVOKE] COMMAND[S]]
  [WHERE expression]

SHOW ROLE[S] name[, ...] PRIVILEGE[S] [AS [REVOKE] COMMAND[S]]
  YIELD { * | field[, ...] } [ORDER BY field[, ...]] [SKIP n] [LIMIT n]
  [WHERE expression]
  [RETURN field[, ...] [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
----

[source, cypher, role=noplay, indent=0]
----
SHOW ROLE regularUsers PRIVILEGES
----

Lists all privileges for role `regularUsers`.

.Result
[options="header,footer", width="100%", cols="m,m,m,m,m,m"]
|===
|access
|action
|graph
|resource
|role
|segment

|"GRANTED"
|"access"
|"database"
|"neo4j"
|"database"
|"regularUsers"

6+a|Rows: 1
|===

[source, cypher, role=noplay, indent=0]
----
SHOW ROLES regularUsers, noAccessUsers PRIVILEGES
----

Lists all privileges for roles `regularUsers` and `noAccessUsers`.

.Result
[options="header,footer", width="100%", cols="m,m,m,m,m,m"]
|===
|access
|action
|graph
|resource
|role
|segment

|"DENIED"
|"access"
|"database"
|"neo4j"
|"database"
|"noAccessUsers"

|"GRANTED"
|"access"
|"database"
|"neo4j"
|"database"
|"regularUsers"

6+a|Rows: 2
|===

Similar to the other show privilege commands, the available privileges for roles can also be output as Cypher commands with the optional `AS COMMAND[S]`.

.Result
[options="header,footer", width="100%", cols="m"]
|===
|command
|"GRANT ACCESS ON DATABASE * TO `admin`"
|"GRANT ALL DBMS PRIVILEGES ON DBMS TO `admin`"
|"GRANT CONSTRAINT MANAGEMENT ON DATABASE * TO `admin`"
|"GRANT INDEX MANAGEMENT ON DATABASE * TO `admin`"
|"GRANT MATCH {*} ON GRAPH * NODE * TO `admin`"
|"GRANT MATCH {*} ON GRAPH * RELATIONSHIP * TO `admin`"
|"GRANT NAME MANAGEMENT ON DATABASE * TO `admin`"
|"GRANT START ON DATABASE * TO `admin`"
|"GRANT STOP ON DATABASE * TO `admin`"
|"GRANT TRANSACTION MANAGEMENT (*) ON DATABASE * TO `admin`"
|"GRANT WRITE ON GRAPH * TO `admin`"
a|Rows: 11
|===

The output can be processed using `YIELD` / `WHERE` / `RETURN` here as well.

[source, cypher, role=noplay, indent=0]
----
SHOW ROLE architect PRIVILEGES AS COMMANDS
WHERE command CONTAINS 'MATCH'
----

.Result
[options="header,footer", width="100%", cols="m"]
|===
|command
|"GRANT MATCH {*} ON GRAPH * NODE * TO `architect`"
|"GRANT MATCH {*} ON GRAPH * RELATIONSHIP * TO `architect`"
|Rows: 2
|===

Again, is it possible to get the privilege commands formatted for revoking instead of granting or denying the privileges.
For more info about revoking privileges, please see xref::access-control/manage-privileges.adoc#access-control-revoke-privileges[The REVOKE command].

[source, cypher, role=noplay, indent=0]
----
SHOW ROLE reader PRIVILEGES AS REVOKE COMMANDS
----

.Result
[options="header,footer", width="100%", cols="m"]
|===
|command
|"REVOKE GRANT ACCESS ON DATABASE * FROM `reader`"
|"REVOKE GRANT MATCH {*} ON GRAPH * NODE * FROM `reader`"
|"REVOKE GRANT MATCH {*} ON GRAPH * RELATIONSHIP * FROM `reader`"
a|Rows: 3
|===


[role=enterprise-edition]
[[access-control-list-privileges-user]]
=== Examples for listing privileges for specific users

Available privileges for specific users can be displayed using `SHOW USER name PRIVILEGES`.

[NOTE]
====
Please note that if a non-native auth provider like LDAP is in use, `SHOW USER PRIVILEGES` will only work in a limited capacity;
It is only possible for a user to show their own privileges.
Other users' privileges cannot be listed when using a non-native auth provider.
====

[source, cypher, role=noplay, indent=0]
----
SHOW USER[S] [name[, ...]] PRIVILEGE[S] [AS [REVOKE] COMMAND[S]]
  [WHERE expression]

SHOW USER[S] [name[, ...]] PRIVILEGE[S] [AS [REVOKE] COMMAND[S]]
  YIELD { * | field[, ...] } [ORDER BY field[, ...]] [SKIP n] [LIMIT n]
  [WHERE expression]
  [RETURN field[, ...] [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
----

[source, cypher, role=noplay, indent=0]
----
SHOW USER jake PRIVILEGES
----

Lists all privileges for user `jake`.

.Result
[options="header,footer", width="100%", cols="m,m,m,m,m,m,m"]
|===
|access
|action
|resource
|graph
|resource
|role
|segment

|"GRANTED"
|"execute"
|"database"
|"*"
|"FUNCTION(*)"
|"PUBLIC"
|"jake"

|"GRANTED"
|"execute"
|"database"
|"*"
|"PROCEDURE(*)"
|"PUBLIC"
|"jake"

|"GRANTED"
|"access"
|"database"
|"DEFAULT"
|"database"
|"PUBLIC"
|"jake"

|"GRANTED"
|"access"
|"database"
|"neo4j"
|"database"
|"regularUsers"
|"jake"

7+a|Rows: 4
|===

[source, cypher, role=noplay, indent=0]
----
SHOW USERS jake, joe PRIVILEGES
----

Lists all privileges for users `jake` and `joe`.

.Result
[options="header,footer", width="100%", cols="m,m,m,m,m,m,m"]
|===
|access
|action
|resource
|graph
|resource
|role
|segment

|"GRANTED"
|"execute"
|"database"
|"*"
|"FUNCTION(*)"
|"PUBLIC"
|"jake"

|"GRANTED"
|"execute"
|"database"
|"*"
|"PROCEDURE(*)"
|"PUBLIC"
|"jake"

|"GRANTED"
|"access"
|"database"
|"DEFAULT"
|"database"
|"PUBLIC"
|"jake"

|"GRANTED"
|"access"
|"database"
|"neo4j"
|"database"
|"regularUsers"
|"jake"

|"GRANTED"
|"execute"
|"database"
|"*"
|"FUNCTION(*)"
|"PUBLIC"
|"joe"

|"GRANTED"
|"execute"
|"database"
|"*"
|"PROCEDURE(*)"
|"PUBLIC"
|"joe"

|"GRANTED"
|"access"
|"database"
|"DEFAULT"
|"database"
|"PUBLIC"
|"joe"

|"DENIED"
|"access"
|"database"
|"neo4j"
|"database"
|"noAccessUsers"
|"joe"

7+a|Rows: 8
|===

The same command can be used at all times to review available privileges for the current user.
For this purpose, a shorter form of the the command also exists: `SHOW USER PRIVILEGES`

[source, cypher, role=noplay, indent=0]
----
SHOW USER PRIVILEGES
----

As for the other privilege commands, available privileges for users can also be output as Cypher commands with the optional `AS COMMAND[S]`.

[NOTE]
====
When showing _user_ privileges as commands, the roles in the Cypher commands are replaced with a parameter.
This can be used to quickly create new roles based on the privileges of specific users.
====

[source, cypher, role=noplay, indent=0]
----
SHOW USER jake PRIVILEGES AS COMMANDS
----

.Result
[options="header,footer", width="100%", cols="m"]
|===
|command
|"GRANT ACCESS ON DATABASE `neo4j` TO $role"
|"GRANT ACCESS ON HOME DATABASE TO $role"
|"GRANT EXECUTE FUNCTION * ON DBMS TO $role"
|"GRANT EXECUTE PROCEDURE * ON DBMS TO $role"
a|Rows: 4
|===

Like other `SHOW` commands, the output can also be processed using `YIELD` / `WHERE` / `RETURN`.
Additionally, similar to the other show privilege commands, it is also possible to show the commands for revoking the privileges.

[source, cypher, role=noplay, indent=0]
----
SHOW USER jake PRIVILEGES AS REVOKE COMMANDS
WHERE command CONTAINS 'EXECUTE'
----

.Result
[options="header,footer", width="100%", cols="m"]
|===
|command
|"REVOKE GRANT EXECUTE FUNCTION * ON DBMS FROM $role"
|"REVOKE GRANT EXECUTE PROCEDURE * ON DBMS FROM $role"
a|Rows: 2
|===


[role=enterprise-edition]
[[access-control-revoke-privileges]]
== Revoking privileges

Privileges that were granted or denied earlier can be revoked using the `REVOKE` command:

[source, cypher, role=noplay, indent=0]
----
REVOKE
  [ GRANT | DENY ] graph-privilege
  FROM role[, ...]
----

An example usage of the `REVOKE` command is given here:

[source, cypher, role=noplay, indent=0]
----
REVOKE GRANT TRAVERSE ON HOME GRAPH NODES Post FROM regularUsers
----

While it can be explicitly specified that revoke should remove a `GRANT` or `DENY`, it is also possible to revoke either one by not specifying at all as the next example demonstrates.
Because of this, if there happen to be a `GRANT` and a `DENY` on the same privilege, it would remove both.

[source, cypher, role=noplay, indent=0]
----
REVOKE TRAVERSE ON HOME GRAPH NODES Payments FROM regularUsers
----

