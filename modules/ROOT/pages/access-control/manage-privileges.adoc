:description: This section explains how to use Cypher to manage privileges for Neo4j role-based access control and fine-grained security.
[role=enterprise-edition aura-db-enterprise]
[[access-control-manage-privileges]]

= Managing privileges

[abstract]
--
This section explains how to use Cypher to manage privileges for Neo4j role-based access control and fine-grained security.
--

Privileges control the access rights to graph elements using a combined allowlist/denylist mechanism.
It is possible to grant or deny access, or use a combination of the two.
The user will be able to access the resource if they have a `GRANT` (allowlist) and do not have a `DENY` (denylist) relevant to that resource.
All other combinations of `GRANT` and `DENY` will result in the matching path being inaccessible.
What this means in practice depends on whether we are talking about a xref::access-control/privileges-reads.adoc[read privilege] or a xref::access-control/privileges-writes.adoc[write privilege]:

* If an entity is not accessible due to xref::access-control/privileges-reads.adoc[read privileges], the data will become invisible.
It will appear to the user as if they had a smaller database (smaller graph).
* If an entity is not accessible due to xref::access-control/privileges-writes.adoc[write privileges], an error will occur on any attempt to write that data.

[NOTE]
====
In this document we will often use the terms _'allows'_ and _'enables'_ in seemingly identical ways. However, there is a subtle difference.
We will use _'enables'_ to refer to the consequences of xref::access-control/privileges-reads.adoc[read privileges] where a restriction will not cause an error, only a reduction in the apparent graph size.
We will use _'allows'_ to refer to the consequence of xref::access-control/privileges-writes.adoc[write privileges] where a restriction can result in an error.
====

[NOTE]
====
If a user was not also provided with the database `ACCESS` privilege, then access to the entire database will be denied.
Information about the database access privilege can be found in xref::access-control/database-administration.adoc#access-control-database-administration-access[The ACCESS privilege].
====

[NOTE]
====
The syntax descriptions use xref:access-control/index.adoc#access-control-syntax[the style] from access control.
====

[[access-control-graph-privileges]]
== Graph privilege commands (`GRANT`, `DENY` and `REVOKE`)

Administrators can use Cypher commands to manage Neo4j graph administrative rights.
The components of the graph privilege commands are:

* _the command_:
** `GRANT` – gives privileges to roles.
** `DENY` – denies privileges to roles.
** `REVOKE` – removes granted or denied privileges from roles.

* _mutability_:
** `IMMUTABLE` can optionally be specified when performing a `GRANT` or `DENY` to indicate that the privilege cannot be subsequently removed unless auth is disabled. Auth must also be disabled in order to `GRANT` or `DENY` an immutable privilege. Contrastingly, when `IMMUTABLE` is specified in conjunction with a `REVOKE` command, it will act as a filter and only remove matching _immutable_ privileges. See also xref:access-control/index.adoc#access-control-privileges-immutable[immutable privileges].

* _graph-privilege_:
** Can be either a xref::access-control/privileges-reads.adoc[read privilege] or xref::access-control/privileges-writes.adoc[write privilege].

* _name_:
** The graph or graphs to associate the privilege with.
Because in Neo4j {neo4j-version} you can have only one graph per database, this command uses the database name or alias to refer to that graph.
When using an alias, the command will be executed on the resolved graph.
+
[NOTE]
====
If you delete a database and create a new one with the same name, the new one will _NOT_ have the privileges previously assigned to the deleted graph.
====
** It can be `+*+`, which means all graphs.
Graphs created after this command execution will also be associated with these privileges.

** `HOME GRAPH` refers to the graph associated with the home database for that user.
The default database will be used as home database if a user does not have one configured.
If the user's home database changes for any reason after privileges have been created, then these privileges will be associated with the graph attached to the new database.
This can be quite powerful as it allows permissions to be switched from one graph to another simply by changing a user's home database.

* _entity_
** The graph elements this privilege applies to:
*** `NODES` label (nodes with the specified label(s)).
*** `RELATIONSHIPS` type (relationships of the specific type(s)).
*** `ELEMENTS` label (both nodes and relationships).
** The label or type can be referred with `+*+`, which means all labels or types.
** Multiple labels or types can be specified, comma-separated.
** Defaults to `ELEMENTS` `+*+` if omitted.
** Some of the commands for write privileges do not allow an _entity_ part.
See xref::access-control/privileges-writes.adoc[Write privileges] for details.
* _role[, ...]_
** The role or roles to associate the privilege with, comma-separated.

.General grant +ON GRAPH+ privilege syntax
[cols="<15s,<85"]
|===

| Command
m| +GRANT ... ON ... TO ...+

| Syntax
a|
[source, syntax, role="noheader", indent=0]
----
GRANT [IMMUTABLE] graph-privilege ON { HOME GRAPH \| GRAPH[S] { * \| name[, ...] } } [entity] TO role[, ...]
----

| Description
a| Grants a privilege to one or multiple roles.

|===

.General deny +ON GRAPH+ privilege syntax
[cols="<15s,<85"]
|===

| Command
m| +DENY ... ON ... TO ...+

| Syntax
a|
[source, syntax, role="noheader", indent=0]
----
DENY [IMMUTABLE] graph-privilege ON { HOME GRAPH \| GRAPH[S] { * \| name[, ...] } } [entity] TO role[, ...]
----

| Description
a| Denies a privilege to one or multiple roles.

|===

.General revoke +ON GRAPH+ privilege syntax
[cols="<15s,<85"]
|===

| Command
m| +REVOKE GRANT ... ON ... FROM ...+

| Syntax
a|
[source, syntax, role="noheader", indent=0]
----
REVOKE [IMMUTABLE] GRANT graph-privilege ON { HOME GRAPH \| GRAPH[S] { * \| name[, ...] } } [entity] FROM role[, ...]
----
| Description
a| Revokes a granted privilege from one or multiple roles.

|===

.General revoke +ON GRAPH+ privilege syntax
[cols="<15s,<85"]
|===

| Command
m| +REVOKE DENY ... ON ... FROM ...+

| Syntax
a|
[source, syntax, role="noheader", indent=0]
----
REVOKE [IMMUTABLE] DENY graph-privilege ON { HOME GRAPH \| GRAPH[S] {* \| name[, ...] } } [entity] FROM role[, ...]
----

| Description
a| Revokes a denied privilege from one or multiple roles.

|===

.General revoke +ON GRAPH+ privilege syntax
[cols="<15s,<85"]
|===

| Command
m| +REVOKE ... ON ... FROM ...+

| Syntax
a|
[source, syntax, role="noheader", indent=0]
----
REVOKE [IMMUTABLE] graph-privilege ON { HOME GRAPH \| GRAPH[S] { * \| name[, ...] } } [entity] FROM role[, ...]
----

| Description
| Revokes a granted or denied privilege from one or multiple roles.
|===

[NOTE]
====
`DENY` does NOT erase a granted privilege; they both exist.
Use `REVOKE` if you want to remove a privilege.
====

The general `GRANT` and `DENY` syntaxes are illustrated in the following image:

image::privileges_grant_and_deny_syntax.svg[title="GRANT and DENY Syntax"]

A more detailed syntax illustration for graph privileges would be the following:

image::privileges_on_graph_syntax.svg[title="Syntax of GRANT and DENY Graph Privileges. The `{` and `}` are part of the syntax and not used for grouping."]

The following image shows the hierarchy between different graph privileges:

image::privileges_hierarchy.svg[title="Graph privileges hierarchy"]

[[access-control-list-privileges]]
== Listing privileges

Available privileges can be displayed using the different `SHOW PRIVILEGE[S]` commands.

.Show privileges command syntax
[cols="<15s,<85"]
|===

| Command
m| +SHOW PRIVILEGE+

| Syntax
a|
[source, syntax, role="noheader", indent=0]
----
SHOW [ALL] PRIVILEGE[S] [AS [REVOKE] COMMAND[S]]
  [YIELD { * \| field[, ...] } [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
  [WHERE expression]
  [RETURN field[, ...] [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
----
| Description
| List all privileges.

|===

.Show role privileges syntax
[cols="<15s,<85"]
|===

| Command
m| +SHOW ROLE ... PRIVILEGE+

| Syntax
a|
[source, syntax, role="noheader", indent=0]
----
SHOW ROLE[S] name[, ...] PRIVILEGE[S] [AS [REVOKE] COMMAND[S]]
  [YIELD { * \| field[, ...] } [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
  [WHERE expression]
  [RETURN field[, ...] [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
----

| Description
| Lists privileges for a specific role.

|===

.Show user privileges syntax
[cols="<15s,<85"]
|===

| Command
m| +SHOW USER ... PRIVILEGE+

| Syntax
a|
[source, syntax, role="noheader", indent=0]
----
SHOW USER[S] [name[, ...]] PRIVILEGE[S] [AS [REVOKE] COMMAND[S]]
  [YIELD { * \| field[, ...] } [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
  [WHERE expression]
  [RETURN field[, ...] [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
----

| Description
| Lists privileges for a specific user, or the current user.

[NOTE]
====
Please note that it is only possible for a user to show their own privileges.
Therefore, if a non-native auth provider like LDAP is in use, `SHOW USER PRIVILEGES` will only work in a limited capacity.

Other users' privileges cannot be listed when using a non-native auth provider.
====
|===

When using the `RETURN` clause, the `YIELD` clause is mandatory and must not be omitted.

For an easy overview of the existing privileges, it is recommended to use the `AS COMMANDS` version of the `SHOW` command.
This returns a column `command` of type `STRING` with the privileges as the commands that are granted or denied.

When omitting the `AS COMMANDS` clause, results will include multiple columns describing privileges:

[options="header", width="100%", cols="4m,6a,2m"]
|===
| Column | Description | Type

| access
| Whether the privilege is granted or denied.
| STRING

| action
| Which type of privilege this is, for example traverse, read, index management or role management.
| STRING

| resource
| What type of scope this privilege applies to, i.e. the entire DBMS, a specific database, a graph or sub-graph access.
| STRING

| graph
| The specific database or graph this privilege applies to.
| STRING

| segment
| When applicable, which labels, relationship types, procedures, functions, transactions or settings this privilege applies to.
| STRING

| role
| The role this privilege is granted to.
| STRING

| immutable
| Whether or not this privilege is immutable.

This column is also available for the `AS COMMAND` variant using `YIELD`.
| BOOLEAN

| user
| The user this privilege belongs to.

[NOTE]
====
This is only returned for `SHOW USER [username] PRIVILEGES`.
====
| STRING

|===

[[access-control-list-all-privileges]]
=== Examples for listing all privileges

Available privileges can be displayed using the different `SHOW PRIVILEGE[S]` commands.

[source, syntax]
----
SHOW [ALL] PRIVILEGE[S] [AS [REVOKE] COMMAND[S]]
  [WHERE expression]

SHOW [ALL] PRIVILEGE[S] [AS [REVOKE] COMMAND[S]]
  YIELD { * | field[, ...] } [ORDER BY field[, ...]] [SKIP n] [LIMIT n]
  [WHERE expression]
  [RETURN field[, ...] [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
----

[source, cypher, role=noplay]
----
SHOW PRIVILEGES
----

Lists all privileges for all roles:

.Result
[options="header,footer", width="100%", cols="m,m,m,m,m,m,m"]
|===
|access
|action
|resource
|graph
|segment
|role
|immutable

|"GRANTED"
|"execute"
|"database"
|"*"
|"FUNCTION(*)"
|"PUBLIC"
|false

|"GRANTED"
|"execute"
|"database"
|"*"
|"PROCEDURE(*)"
|"PUBLIC"
|false

|"GRANTED"
|"access"
|"database"
|"DEFAULT"
|"database"
|"PUBLIC"
|false

|"GRANTED"
|"match"
|"all_properties"
|"*"
|"NODE(*)"
|"admin"
|false

|"GRANTED"
|"write"
|"graph"
|"*"
|"NODE(*)"
|"admin"
|false

|"GRANTED"
|"match"
|"all_properties"
|"*"
|"RELATIONSHIP(*)"
|"admin"
|false

|"GRANTED"
|"write"
|"graph"
|"*"
|"RELATIONSHIP(*)"
|"admin"
|false

|"GRANTED"
|"transaction_management"
|"database"
|"*"
|"USER(*)"
|"admin"
|false

|"GRANTED"
|"access"
|"database"
|"*"
|"database"
|"admin"
|false

|"GRANTED"
|"constraint"
|"database"
|"*"
|"database"
|"admin"
|false

|"GRANTED"
|"dbms_actions"
|"database"
|"*"
|"database"
|"admin"
|false

|"GRANTED"
|"index"
|"database"
|"*"
|"database"
|"admin"
|false

|"GRANTED"
|"start_database"
|"database"
|"*"
|"database"
|"admin"
|false

|"GRANTED"
|"stop_database"
|"database"
|"*"
|"database"
|"admin"
|false

|"GRANTED"
|"token"
|"database"
|"*"
|"database"
|"admin"
|false

|"GRANTED"
|"match"
|"all_properties"
|"*"
|"NODE(*)"
|"architect"
|false

|"GRANTED"
|"write"
|"graph"
|"*"
|"NODE(*)"
|"architect"
|false

|"GRANTED"
|"match"
|"all_properties"
|"*"
|"RELATIONSHIP(*)"
|"architect"
|false

|"GRANTED"
|"write"
|"graph"
|"*"
|"RELATIONSHIP(*)"
|"architect"
|false

|"GRANTED"
|"access"
|"database"
|"*"
|"database"
|"architect"
|false

|"GRANTED"
|"constraint"
|"database"
|"*"
|"database"
|"architect"
|false

|"GRANTED"
|"index"
|"database"
|"*"
|"database"
|"architect"
|false

|"GRANTED"
|"token"
|"database"
|"*"
|"database"
|"architect"
|false

|"GRANTED"
|"match"
|"all_properties"
|"*"
|"NODE(*)"
|"editor"
|false

|"GRANTED"
|"write"
|"graph"
|"*"
|"NODE(*)"
|"editor"
|false

|"GRANTED"
|"match"
|"all_properties"
|"*"
|"RELATIONSHIP(*)"
|"editor"
|false

|"GRANTED"
|"write"
|"graph"
|"*"
|"RELATIONSHIP(*)"
|"editor"
|false

|"GRANTED"
|"access"
|"database"
|"*"
|"database"
|"editor"
|false

|"DENIED"
|"access"
|"database"
|"neo4j"
|"database"
|"noAccessUsers"
|false

|"GRANTED"
|"match"
|"all_properties"
|"*"
|"NODE(*)"
|"publisher"
|false

|"GRANTED"
|"write"
|"graph"
|"*"
|"NODE(*)"
|"publisher"
|false

|"GRANTED"
|"match"
|"all_properties"
|"*"
|"RELATIONSHIP(*)"
|"publisher"
|false

|"GRANTED"
|"write"
|"graph"
|"*"
|"RELATIONSHIP(*)"
|"publisher"
|false

|"GRANTED"
|"access"
|"database"
|"*"
|"database"
|"publisher"
|false

|"GRANTED"
|"token"
|"database"
|"*"
|"database"
|"publisher"
|false

|"GRANTED"
|"match"
|"all_properties"
|"*"
|"NODE(*)"
|"reader"
|false

|"GRANTED"
|"match"
|"all_properties"
|"*"
|"RELATIONSHIP(*)"
|"reader"
|false

|"GRANTED"
|"access"
|"database"
|"*"
|"database"
|"reader"
|false

|"GRANTED"
|"access"
|"database"
|"neo4j"
|"database"
|"regularUsers"
|false

6+a|Rows: 39
|===

It is also possible to filter and sort the results by using `YIELD`, `ORDER BY` and `WHERE`:

[source, cypher, role=noplay]
----
SHOW PRIVILEGES YIELD role, access, action, segment
ORDER BY action
WHERE role = 'admin'
----

In this example:

* The number of columns returned has been reduced with the `YIELD` clause.
* The order of the returned columns has been changed.
* The results have been filtered to only return the `admin` role using a `WHERE` clause.
* The results are ordered by the `action` column using `ORDER BY`.

`SKIP` and `LIMIT` can also be used to paginate the results.

.Result
[options="header,footer", width="100%", cols="m,m,m,m"]
|===
|role
|access
|action
|segment

|"admin"
|"GRANTED"
|"access"
|"database"

|"admin"
|"GRANTED"
|"constraint"
|"database"

|"admin"
|"GRANTED"
|"dbms_actions"
|"database"

|"admin"
|"GRANTED"
|"index"
|"database"

|"admin"
|"GRANTED"
|"match"
|"NODE(*)"

|"admin"
|"GRANTED"
|"match"
|"RELATIONSHIP(*)"

|"admin"
|"GRANTED"
|"start_database"
|"database"

|"admin"
|"GRANTED"
|"stop_database"
|"database"

|"admin"
|"GRANTED"
|"token"
|"database"

|"admin"
|"GRANTED"
|"transaction_management"
|"USER(*)"

|"admin"
|"GRANTED"
|"write"
|"NODE(*)"

|"admin"
|"GRANTED"
|"write"
|"RELATIONSHIP(*)"

4+a|Rows: 12
|===

`WHERE` can also be used without `YIELD`:

[source, cypher, role=noplay]
----
SHOW PRIVILEGES
WHERE graph <> '*'
----

In this example, the `WHERE` clause is used to filter privileges down to those that target specific graphs only.

.Result
[options="header,footer", width="100%", cols="m,m,m,m,m,m"]
|===
|access
|action
|graph
|resource
|role
|segment

|"GRANTED"
|"access"
|"DEFAULT"
|"database"
|"PUBLIC"
|"database"

|"DENIED"
|"access"
|"neo4j"
|"database"
|"noAccessUsers"
|"database"

|"GRANTED"
|"access"
|"neo4j"
|"database"
|"regularUsers"
|"database"

6+a|Rows: 3
|===

Aggregations in the `RETURN` clause can be used to group privileges.
In this case, by user and `GRANTED` or `DENIED`:

[source, cypher, role=noplay]
----
SHOW PRIVILEGES YIELD * RETURN role, access, collect([graph, resource, segment, action]) AS privileges
----

.Result
[options="header,footer", width="100%", cols="1m,1m,3m"]
|===
|role
|access
|privileges

|"PUBLIC"
|"GRANTED"
|[["\*","database","FUNCTION(*)","execute"],["\*","database","PROCEDURE(*)","execute"],["DEFAULT","database","database","access"]]

|"admin"
|"GRANTED"
|[["\*","all_properties","NODE(*)","match"],["\*","graph","NODE(*)","write"],["\*","all_properties","RELATIONSHIP(*)","match"],["\*","graph","RELATIONSHIP(*)","write"],["\*","database","USER(*)","transaction_management"],["\*","database","database","access"],["*","database","database","constraint"],["\*","database","database","dbms_actions"],["*","database","database","index"],["\*","database","database","start_database"],["*","database","database","stop_database"],["*","database","database","token"]]

|"architect"
|"GRANTED"
|[["\*","all_properties","NODE(*)","match"],["\*","graph","NODE(*)","write"],["\*","all_properties","RELATIONSHIP(*)","match"],["\*","graph","RELATIONSHIP(*)","write"],["\*","database","database","access"],["*","database","database","constraint"],["\*","database","database","index"],["*","database","database","token"]]

|"editor"
|"GRANTED"
|[["\*","all_properties","NODE(*)","match"],["\*","graph","NODE(*)","write"],["\*","all_properties","RELATIONSHIP(*)","match"],["\*","graph","RELATIONSHIP(*)","write"],["*","database","database","access"]]

|"noAccessUsers"
|"DENIED"
|[["neo4j","database","database","access"]]

|"publisher"
|"GRANTED"
|[["\*","all_properties","NODE(*)","match"],["\*","graph","NODE(*)","write"],["\*","all_properties","RELATIONSHIP(*)","match"],["\*","graph","RELATIONSHIP(*)","write"],["\*","database","database","access"],["*","database","database","token"]]

|"reader"
|"GRANTED"
|[["\*","all_properties","NODE(*)","match"],["\*","all_properties","RELATIONSHIP(*)","match"],["*","database","database","access"]]

|"regularUsers"
|"GRANTED"
|[["neo4j","database","database","access"]]

3+a|Rows: 8
|===

The `RETURN` clause can also be used to order and paginate the results, which is useful when combined with `YIELD` and `WHERE`.
In this example the query returns privileges for display five-per-page, and skips the first five to display the second page.

[source, cypher, role=noplay]
----
SHOW PRIVILEGES YIELD * RETURN * ORDER BY role SKIP 5 LIMIT 5
----

.Result
[options="header,footer", width="100%", cols="2m,2m,1m,2m,1m,2m,1m"]
|===
|access
|action
|graph
|resource
|role
|segment
|immutable

|"GRANTED"
|"match"
|"*"
|"all_properties"
|"admin"
|"RELATIONSHIP(*)"
|false

|"GRANTED"
|"write"
|"*"
|"graph"
|"admin"
|"RELATIONSHIP(*)"
|false

|"GRANTED"
|"transaction_management"
|"*"
|"database"
|"admin"
|"USER(*)"
|false

|"GRANTED"
|"access"
|"*"
|"database"
|"admin"
|"database"
|false

|"GRANTED"
|"constraint"
|"*"
|"database"
|"admin"
|"database"
|false

6+a|Rows: 5
|===

Available privileges can also be displayed as Cypher commands by adding `AS COMMAND[S]`:

[source, cypher, role=noplay]
----
SHOW PRIVILEGES AS COMMANDS
----

.Result
[options="header,footer", width="100%", cols="m"]
|===
|command
|"DENY ACCESS ON DATABASE `neo4j` TO `noAccessUsers`"
|"GRANT ACCESS ON DATABASE * TO `admin`"
|"GRANT ACCESS ON DATABASE * TO `architect`"
|"GRANT ACCESS ON DATABASE * TO `editor`"
|"GRANT ACCESS ON DATABASE * TO `publisher`"
|"GRANT ACCESS ON DATABASE * TO `reader`"
|"GRANT ACCESS ON DATABASE `neo4j` TO `regularUsers`"
|"GRANT ACCESS ON HOME DATABASE TO `PUBLIC`"
|"GRANT ALL DBMS PRIVILEGES ON DBMS TO `admin`"
|"GRANT CONSTRAINT MANAGEMENT ON DATABASE * TO `admin`"
|"GRANT CONSTRAINT MANAGEMENT ON DATABASE * TO `architect`"
|"GRANT EXECUTE FUNCTION * ON DBMS TO `PUBLIC`"
|"GRANT EXECUTE PROCEDURE * ON DBMS TO `PUBLIC`"
|"GRANT INDEX MANAGEMENT ON DATABASE * TO `admin`"
|"GRANT INDEX MANAGEMENT ON DATABASE * TO `architect`"
|"GRANT MATCH {*} ON GRAPH * NODE * TO `admin`"
|"GRANT MATCH {*} ON GRAPH * NODE * TO `architect`"
|"GRANT MATCH {*} ON GRAPH * NODE * TO `editor`"
|"GRANT MATCH {*} ON GRAPH * NODE * TO `publisher`"
|"GRANT MATCH {*} ON GRAPH * NODE * TO `reader`"
|"GRANT MATCH {*} ON GRAPH * RELATIONSHIP * TO `admin`"
|"GRANT MATCH {*} ON GRAPH * RELATIONSHIP * TO `architect`"
|"GRANT MATCH {*} ON GRAPH * RELATIONSHIP * TO `editor`"
|"GRANT MATCH {*} ON GRAPH * RELATIONSHIP * TO `publisher`"
|"GRANT MATCH {*} ON GRAPH * RELATIONSHIP * TO `reader`"
|"GRANT NAME MANAGEMENT ON DATABASE * TO `admin`"
|"GRANT NAME MANAGEMENT ON DATABASE * TO `architect`"
|"GRANT NAME MANAGEMENT ON DATABASE * TO `publisher`"
|"GRANT START ON DATABASE * TO `admin`"
|"GRANT STOP ON DATABASE * TO `admin`"
|"GRANT TRANSACTION MANAGEMENT (*) ON DATABASE * TO `admin`"
|"GRANT WRITE ON GRAPH * TO `admin`"
|"GRANT WRITE ON GRAPH * TO `architect`"
|"GRANT WRITE ON GRAPH * TO `editor`"
|"GRANT WRITE ON GRAPH * TO `publisher`"
a|Rows: 35
|===

Like other `SHOW` commands, the output can also be processed using `YIELD` / `WHERE` / `RETURN`:

[source, cypher, role=noplay]
----
SHOW PRIVILEGES AS COMMANDS
WHERE command CONTAINS 'MANAGEMENT'
----

.Result
[options="header,footer", width="100%", cols="m"]
|===
|command
|"GRANT CONSTRAINT MANAGEMENT ON DATABASE * TO `admin`"
|"GRANT CONSTRAINT MANAGEMENT ON DATABASE * TO `architect`"
|"GRANT INDEX MANAGEMENT ON DATABASE * TO `admin`"
|"GRANT INDEX MANAGEMENT ON DATABASE * TO `architect`"
|"GRANT NAME MANAGEMENT ON DATABASE * TO `admin`"
|"GRANT NAME MANAGEMENT ON DATABASE * TO `architect`"
|"GRANT NAME MANAGEMENT ON DATABASE * TO `publisher`"
|"GRANT TRANSACTION MANAGEMENT (*) ON DATABASE * TO `admin`"
a|Rows: 8
|===

It is also possible to get the privileges listed as revoking commands instead of granting or denying:

[source, cypher, role=noplay]
----
SHOW PRIVILEGES AS REVOKE COMMANDS
----

.Result
[options="header,footer", width="100%", cols="m"]
|===
|command
|"REVOKE DENY ACCESS ON DATABASE `neo4j` FROM `noAccessUsers`"
|"REVOKE GRANT ACCESS ON DATABASE * FROM `admin`"
|"REVOKE GRANT ACCESS ON DATABASE * FROM `architect`"
|"REVOKE GRANT ACCESS ON DATABASE * FROM `editor`"
|"REVOKE GRANT ACCESS ON DATABASE * FROM `publisher`"
|"REVOKE GRANT ACCESS ON DATABASE * FROM `reader`"
|"REVOKE GRANT ACCESS ON DATABASE `neo4j` FROM `regularUsers`"
|"REVOKE GRANT ACCESS ON HOME DATABASE FROM `PUBLIC`"
|"REVOKE GRANT ALL DBMS PRIVILEGES ON DBMS FROM `admin`"
|"REVOKE GRANT CONSTRAINT MANAGEMENT ON DATABASE * FROM `admin`"
|"REVOKE GRANT CONSTRAINT MANAGEMENT ON DATABASE * FROM `architect`"
|"REVOKE GRANT EXECUTE FUNCTION * ON DBMS FROM `PUBLIC`"
|"REVOKE GRANT EXECUTE PROCEDURE * ON DBMS FROM `PUBLIC`"
|"REVOKE GRANT INDEX MANAGEMENT ON DATABASE * FROM `admin`"
|"REVOKE GRANT INDEX MANAGEMENT ON DATABASE * FROM `architect`"
|"REVOKE GRANT MATCH {*} ON GRAPH * NODE * FROM `admin`"
|"REVOKE GRANT MATCH {*} ON GRAPH * NODE * FROM `architect`"
|"REVOKE GRANT MATCH {*} ON GRAPH * NODE * FROM `editor`"
|"REVOKE GRANT MATCH {*} ON GRAPH * NODE * FROM `publisher`"
|"REVOKE GRANT MATCH {*} ON GRAPH * NODE * FROM `reader`"
|"REVOKE GRANT MATCH {*} ON GRAPH * RELATIONSHIP * FROM `admin`"
|"REVOKE GRANT MATCH {*} ON GRAPH * RELATIONSHIP * FROM `architect`"
|"REVOKE GRANT MATCH {*} ON GRAPH * RELATIONSHIP * FROM `editor`"
|"REVOKE GRANT MATCH {*} ON GRAPH * RELATIONSHIP * FROM `publisher`"
|"REVOKE GRANT MATCH {*} ON GRAPH * RELATIONSHIP * FROM `reader`"
|"REVOKE GRANT NAME MANAGEMENT ON DATABASE * FROM `admin`"
|"REVOKE GRANT NAME MANAGEMENT ON DATABASE * FROM `architect`"
|"REVOKE GRANT NAME MANAGEMENT ON DATABASE * FROM `publisher`"
|"REVOKE GRANT START ON DATABASE * FROM `admin`"
|"REVOKE GRANT STOP ON DATABASE * FROM `admin`"
|"REVOKE GRANT TRANSACTION MANAGEMENT (*) ON DATABASE * FROM `admin`"
|"REVOKE GRANT WRITE ON GRAPH * FROM `admin`"
|"REVOKE GRANT WRITE ON GRAPH * FROM `architect`"
|"REVOKE GRANT WRITE ON GRAPH * FROM `editor`"
|"REVOKE GRANT WRITE ON GRAPH * FROM `publisher`"
a|Rows: 35
|===

For more info about revoking privileges, please see xref::access-control/manage-privileges.adoc#access-control-revoke-privileges[The REVOKE command].

[[access-control-list-privileges-role]]
=== Examples for listing privileges for specific roles

Available privileges for specific roles can be displayed using `SHOW ROLE name PRIVILEGE[S]`:

[source, syntax]
----
SHOW ROLE[S] name[, ...] PRIVILEGE[S] [AS [REVOKE] COMMAND[S]]
  [WHERE expression]

SHOW ROLE[S] name[, ...] PRIVILEGE[S] [AS [REVOKE] COMMAND[S]]
  YIELD { * | field[, ...] } [ORDER BY field[, ...]] [SKIP n] [LIMIT n]
  [WHERE expression]
  [RETURN field[, ...] [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
----

[source, cypher, role=noplay]
----
SHOW ROLE regularUsers PRIVILEGES
----

Lists all privileges for role `regularUsers`.

.Result
[options="header,footer", width="100%", cols="m,m,m,m,m,m,m"]
|===
|access
|action
|graph
|resource
|role
|segment
|immutable

|"GRANTED"
|"access"
|"database"
|"neo4j"
|"database"
|"regularUsers"
|false

6+a|Rows: 1
|===

[source, cypher, role=noplay]
----
SHOW ROLES regularUsers, noAccessUsers PRIVILEGES
----

Lists all privileges for roles `regularUsers` and `noAccessUsers`.

.Result
[options="header,footer", width="100%", cols="m,m,m,m,m,m,m"]
|===
|access
|action
|graph
|resource
|role
|segment
|immutable

|"DENIED"
|"access"
|"database"
|"neo4j"
|"database"
|"noAccessUsers"
|false

|"GRANTED"
|"access"
|"database"
|"neo4j"
|"database"
|"regularUsers"
|false

6+a|Rows: 2
|===

Similar to the other `SHOW PRIVILEGES` commands, the available privileges for roles can also be listed as Cypher commands with the optional `AS COMMAND[S]`.

[source, cypher, role=noplay]
----
SHOW ROLES regularUsers, noAccessUsers PRIVILEGES AS COMMANDS
----

.Result
[options="header,footer", width="100%", cols="m"]
|===
|command
|"GRANT ACCESS ON DATABASE * TO `admin`"
|"GRANT ALL DBMS PRIVILEGES ON DBMS TO `admin`"
|"GRANT CONSTRAINT MANAGEMENT ON DATABASE * TO `admin`"
|"GRANT INDEX MANAGEMENT ON DATABASE * TO `admin`"
|"GRANT MATCH {*} ON GRAPH * NODE * TO `admin`"
|"GRANT MATCH {*} ON GRAPH * RELATIONSHIP * TO `admin`"
|"GRANT NAME MANAGEMENT ON DATABASE * TO `admin`"
|"GRANT START ON DATABASE * TO `admin`"
|"GRANT STOP ON DATABASE * TO `admin`"
|"GRANT TRANSACTION MANAGEMENT (*) ON DATABASE * TO `admin`"
|"GRANT WRITE ON GRAPH * TO `admin`"
a|Rows: 11
|===

The output can be processed using `YIELD` / `WHERE` / `RETURN` here as well:

[source, cypher, role=noplay]
----
SHOW ROLE architect PRIVILEGES AS COMMANDS WHERE command CONTAINS 'MATCH'
----

.Result
[options="header,footer", width="100%", cols="m"]
|===
|command
|"GRANT MATCH {*} ON GRAPH * NODE * TO `architect`"
|"GRANT MATCH {*} ON GRAPH * RELATIONSHIP * TO `architect`"
|Rows: 2
|===

Again, it is possible to get the privileges listed as revoking commands instead of granting or denying.
For more info about revoking privileges, please see xref::access-control/manage-privileges.adoc#access-control-revoke-privileges[The REVOKE command].

[source, cypher, role=noplay]
----
SHOW ROLE reader PRIVILEGES AS REVOKE COMMANDS
----

.Result
[options="header,footer", width="100%", cols="m"]
|===
|command
|"REVOKE GRANT ACCESS ON DATABASE * FROM `reader`"
|"REVOKE GRANT MATCH {*} ON GRAPH * NODE * FROM `reader`"
|"REVOKE GRANT MATCH {*} ON GRAPH * RELATIONSHIP * FROM `reader`"
a|Rows: 3
|===

[[access-control-list-privileges-user]]
=== Examples for listing privileges for specific users

Available privileges for specific users can be displayed using `SHOW USER name PRIVILEGES`.

[NOTE]
====
Note that if a non-native auth provider like LDAP is in use, `SHOW USER PRIVILEGES` will only work with a limited capacity as it is only possible for a user to show their own privileges.
Other users' privileges cannot be listed when using a non-native auth provider.
====

[source, syntax]
----
SHOW USER[S] [name[, ...]] PRIVILEGE[S] [AS [REVOKE] COMMAND[S]]
  [WHERE expression]

SHOW USER[S] [name[, ...]] PRIVILEGE[S] [AS [REVOKE] COMMAND[S]]
  YIELD { * | field[, ...] } [ORDER BY field[, ...]] [SKIP n] [LIMIT n]
  [WHERE expression]
  [RETURN field[, ...] [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
----

[source, cypher, role=noplay]
----
SHOW USER jake PRIVILEGES
----

Lists all privileges for user `jake`.

.Result
[options="header,footer", width="100%", cols="m,m,m,m,m,m,m,m"]
|===
|access
|action
|resource
|graph
|resource
|role
|segment
|immutable

|"GRANTED"
|"execute"
|"database"
|"*"
|"FUNCTION(*)"
|"PUBLIC"
|"jake"
|false

|"GRANTED"
|"execute"
|"database"
|"*"
|"PROCEDURE(*)"
|"PUBLIC"
|"jake"
|false

|"GRANTED"
|"access"
|"database"
|"DEFAULT"
|"database"
|"PUBLIC"
|"jake"
|false

|"GRANTED"
|"access"
|"database"
|"neo4j"
|"database"
|"regularUsers"
|"jake"
|false

7+a|Rows: 4
|===

[source, cypher, role=noplay]
----
SHOW USERS jake, joe PRIVILEGES
----

Lists all privileges for users `jake` and `joe`.

.Result
[options="header,footer", width="100%", cols="m,m,m,m,m,m,m,m"]
|===
|access
|action
|resource
|graph
|resource
|role
|segment
|immutable

|"GRANTED"
|"execute"
|"database"
|"*"
|"FUNCTION(*)"
|"PUBLIC"
|"jake"
|false

|"GRANTED"
|"execute"
|"database"
|"*"
|"PROCEDURE(*)"
|"PUBLIC"
|"jake"
|false

|"GRANTED"
|"access"
|"database"
|"DEFAULT"
|"database"
|"PUBLIC"
|"jake"
|false

|"GRANTED"
|"access"
|"database"
|"neo4j"
|"database"
|"regularUsers"
|"jake"
|false

|"GRANTED"
|"execute"
|"database"
|"*"
|"FUNCTION(*)"
|"PUBLIC"
|"joe"
|false

|"GRANTED"
|"execute"
|"database"
|"*"
|"PROCEDURE(*)"
|"PUBLIC"
|"joe"
|false

|"GRANTED"
|"access"
|"database"
|"DEFAULT"
|"database"
|"PUBLIC"
|"joe"
|false

|"DENIED"
|"access"
|"database"
|"neo4j"
|"database"
|"noAccessUsers"
|"joe"
|false

7+a|Rows: 8
|===

The same command can be used at all times to review available privileges for the current user.
For this purpose, there is a shorter form of the command: `SHOW USER PRIVILEGES`:

[source, cypher, role=noplay]
----
SHOW USER PRIVILEGES
----

As for the other privilege commands, available privileges for users can also be listed as Cypher commands with the optional `AS COMMAND[S]`.

[NOTE]
====
When showing user privileges as commands, the roles in the Cypher commands are replaced with a parameter.
This can be used to quickly create new roles based on the privileges of specific users.
====

[source, cypher, role=noplay]
----
SHOW USER jake PRIVILEGES AS COMMANDS
----

.Result
[options="header,footer", width="100%", cols="m"]
|===
|command
|"GRANT ACCESS ON DATABASE `neo4j` TO $role"
|"GRANT ACCESS ON HOME DATABASE TO $role"
|"GRANT EXECUTE FUNCTION * ON DBMS TO $role"
|"GRANT EXECUTE PROCEDURE * ON DBMS TO $role"
a|Rows: 4
|===

Like other `SHOW` commands, the output can also be processed using `YIELD` / `WHERE` / `RETURN`.
Additionally, similar to the other show privilege commands, it is also possible to show the commands for revoking the privileges.

[source, cypher, role=noplay]
----
SHOW USER jake PRIVILEGES AS REVOKE COMMANDS
WHERE command CONTAINS 'EXECUTE'
----

.Result
[options="header,footer", width="100%", cols="m"]
|===
|command
|"REVOKE GRANT EXECUTE FUNCTION * ON DBMS FROM $role"
|"REVOKE GRANT EXECUTE PROCEDURE * ON DBMS FROM $role"
a|Rows: 2
|===

[[access-control-revoke-privileges]]
== Revoking privileges

Privileges that were granted or denied earlier can be revoked using the `REVOKE` command:

[source, syntax]
----
REVOKE
  [ IMMUTABLE ]
  [ GRANT | DENY ] graph-privilege
  FROM role[, ...]
----

An example usage of the `REVOKE` command is given here:

[source, cypher, role=noplay]
----
REVOKE GRANT TRAVERSE ON HOME GRAPH NODES Post FROM regularUsers
----

While it can be explicitly specified that `REVOKE` should remove a `GRANT` or `DENY`, it is also possible to `REVOKE` both by not specifying them at all, as the next example demonstrates.
Because of this, if there happens to be a `GRANT` and a `DENY` for the same privilege, it would remove both.

[source, cypher, role=noplay]
----
REVOKE TRAVERSE ON HOME GRAPH NODES Payments FROM regularUsers
----

Adding `IMMUTABLE` explicitly specifies that only immutable privileges should be removed. Omitting it specifies that both immutable and regular privileges should be removed.
