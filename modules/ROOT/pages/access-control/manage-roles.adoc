:description: This section explains how to use Cypher to manage roles in Neo4j.

[role=enterprise-edition aura-db-enterprise]
[[access-control-manage-roles]]
= Managing roles

////
[source, cypher, role=test-setup]
----
CREATE USER bob SET PASSWORD 'abcd1234' CHANGE NOT REQUIRED;
CREATE USER user1 SET PASSWORD 'abcd1234' CHANGE NOT REQUIRED;
CREATE USER user2 SET PASSWORD 'abcd1234' CHANGE NOT REQUIRED;
CREATE USER user3 SET PASSWORD 'abcd1234' CHANGE NOT REQUIRED;
CREATE ROLE myrole IF NOT EXISTS;
CREATE ROLE role1 IF NOT EXISTS;
CREATE ROLE role2 IF NOT EXISTS;
----
////

[abstract]
--
This section explains how to use Cypher to manage roles in Neo4j.
--

Roles can be created and managed using a set of Cypher administration commands executed against the `system` database.

When connected to the DBMS over `bolt`, administration commands are automatically routed to the `system` database.


[[access-control-role-syntax]]
== Role management command syntax

[NOTE]
====
The syntax descriptions use xref:access-control/index.adoc#access-control-syntax[the style] from access control.
====

[cols="<15s,<85"]
|===

| Command
m| SHOW ROLES

| Syntax
a|
[source, syntax, role="noheader"]
----
SHOW [ALL\|POPULATED] ROLE[S]
  [YIELD { * \| field[, ...] } [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
  [WHERE expression]
  [RETURN field[, ...] [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
----

| Description
a|
Lists roles.

When using the `RETURN` clause, the `YIELD` clause is mandatory and must not be omitted.

For more information, see xref::access-control/manage-roles.adoc#access-control-list-roles[Listing roles].

| Required privilege
a|
[source, privilege, role="noheader"]
----
GRANT SHOW ROLE
----


(see xref::access-control/dbms-administration.adoc#access-control-dbms-administration-role-management[DBMS ROLE MANAGEMENT privileges]).
|===


[cols="<15s,<85"]
|===

| Command
m| SHOW ROLES WITH USERS

| Syntax
a|
[source, syntax, role="noheader"]
----
SHOW [ALL\|POPULATED] ROLE[S] WITH USER[S]
  [YIELD { * \| field[, ...] } [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
  [WHERE expression]
  [RETURN field[, ...] [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
----

| Description
a|
Lists roles and users assigned to them.

When using the `RETURN` clause, the `YIELD` clause is mandatory and must not be omitted.

For more information, see xref::access-control/manage-roles.adoc#access-control-list-roles[Listing roles].

| Required privilege
a|
[source, privilege, role="noheader"]
----
GRANT SHOW ROLE
----

(see xref::access-control/dbms-administration.adoc#access-control-dbms-administration-role-management[DBMS ROLE MANAGEMENT privileges])


[source, privilege, role="noheader"]
----
GRANT SHOW USER
----


(see xref::access-control/dbms-administration.adoc#access-control-dbms-administration-user-management[DBMS USER MANAGEMENT privileges])

|===


[cols="<15s,<85"]
|===

| Command
m| SHOW ROLE PRIVILEGES

| Syntax
a|
[source, syntax, role="noheader"]
----
SHOW ROLE[S] name[, ...] PRIVILEGE[S] [AS [REVOKE] COMMAND[S]]
  [YIELD { * \| field[, ...] } [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
  [WHERE expression]
  [RETURN field[, ...] [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
----

| Description
a|
Lists the privileges granted to the specified roles.

When using the `RETURN` clause, the `YIELD` clause is mandatory and must not be omitted.

For more information, see xref::access-control/manage-privileges.adoc#access-control-list-privileges[Listing privileges].

| Required privilege
a|
[source, privilege, role="noheader"]
----
GRANT SHOW PRIVILEGE
----

(see xref::access-control/dbms-administration.adoc#access-control-dbms-administration-privilege-management[DBMS PRIVILEGE MANAGEMENT privileges])

|===


[cols="<15s,<85"]
|===


| Command
m| CREATE ROLE

| Syntax
a|
[source, syntax, role="noheader"]
----
CREATE ROLE name [IF NOT EXISTS] [AS COPY OF otherName]
----

| Description
a|
Creates a new role.

For more information, see xref::access-control/manage-roles.adoc#access-control-create-roles[Creating roles].

| Required privilege
a|
[source, privilege, role="noheader"]
----
GRANT CREATE ROLE
----

(see xref::access-control/dbms-administration.adoc#access-control-dbms-administration-role-management[DBMS ROLE MANAGEMENT privileges])

|===

[cols="<15s,<85"]
|===
| Command
m| CREATE OR REPLACE ROLE

| Syntax
a|
[source, syntax, role="noheader"]
----
CREATE OR REPLACE ROLE name [AS COPY OF otherName]
----

| Description
a|
Creates a new role, or if a role with the same name exists, replace it.

For more information, see xref::access-control/manage-roles.adoc#access-control-create-roles[Creating roles].

| Required privilege
a|
[source, privilege, role="noheader"]
----
GRANT CREATE ROLE
----

[source, privilege, role="noheader"]
----
GRANT DROP ROLE
----

(see xref::access-control/dbms-administration.adoc#access-control-dbms-administration-role-management[DBMS ROLE MANAGEMENT privileges])

|===


[cols="<15s,<85"]
|===

| Command
m| RENAME ROLE

| Syntax
a|
[source, syntax, role="noheader"]
----
RENAME ROLE name [IF EXISTS] TO otherName
----

| Description
a|
Changes the name of a role.

For more information, see xref::access-control/manage-roles.adoc#access-control-rename-roles[Renaming roles].

| Required privilege
a|
[source, privilege, role="noheader"]
----
GRANT RENAME ROLE
----

(see xref::access-control/dbms-administration.adoc#access-control-dbms-administration-role-management[DBMS ROLE MANAGEMENT privileges])

|===


[cols="<15s,<85"]
|===

| Command
m| DROP ROLE

| Syntax
a|
[source, syntax, role="noheader"]
----
DROP ROLE name [IF EXISTS]
----

| Description
a|
Removes a role.

For more information, see xref::access-control/manage-roles.adoc#access-control-drop-roles[Deleting roles].

| Required privilege
[source, privilege, role="noheader"]
----
GRANT DROP ROLE
----

(see xref::access-control/dbms-administration.adoc#access-control-dbms-administration-role-management[DBMS ROLE MANAGEMENT privileges])

|===


[cols="<15s,<85"]
|===

| Command
m| GRANT ROLE TO

| Syntax
a|
[source, syntax, role="noheader"]
----
GRANT ROLE[S] name[, ...] TO user[, ...]
----

| Description
a|
Assigns roles to users.

For more information, see xref::access-control/manage-roles.adoc#access-control-assign-roles[Assigning roles to users].

| Required privilege
a|
[source, privilege, role="noheader"]
----
GRANT ASSIGN ROLE
----

(see xref::access-control/dbms-administration.adoc#access-control-dbms-administration-role-management[DBMS ROLE MANAGEMENT privileges])

|===


[cols="<15s,<85"]
|===

| Command
m| REVOKE ROLE

| Syntax
a|
[source, syntax, role="noheader"]
----
REVOKE ROLE[S] name[, ...] FROM user[, ...]
----

| Description
a|
Removes roles from users.

For more information, see xref::access-control/manage-roles.adoc#access-control-revoke-roles[Revoking roles from users].

| Required privilege
a|
[source, privilege, role="noheader"]
----
GRANT REMOVE ROLE
----

(see xref::access-control/dbms-administration.adoc#access-control-dbms-administration-role-management[DBMS ROLE MANAGEMENT privileges])

|===


[[access-control-list-roles]]
== Listing roles


Available roles can be seen using `SHOW ROLES`.
This returns a single column `role` of type `STRING`, containing the role name.

[source, cypher, role=noplay]
----
SHOW ROLES
----

This is the same command as `SHOW ALL ROLES`.

.Result
[options="header,footer", width="100%", cols="m"]
|===
|role

|"PUBLIC"
|"admin"
|"architect"
|"editor"
|"publisher"
|"reader"

1+a|Rows: 6
|===

When first starting a Neo4j DBMS, there are a number of built-in roles:

* `PUBLIC` - a role that all users have granted.
By default it gives access to the home database and to execute privileges for procedures and functions.
* `reader` - can perform traverse and read operations in all databases except `system`.
* `editor` - can perform traverse, read, and write operations in all databases except `system`, but cannot create new labels or relationship types.
* `publisher` - can do the same as `editor`, but also create new labels and relationship types.
* `architect` - can do the same as `publisher` as well as create and manage indexes and constraints.
* `admin` - can do the same as all the above, as well as manage databases, aliases, users, roles, and privileges.

More information about the built-in roles can be found in link:{neo4j-docs-base-uri}/operations-manual/{page-version}/authentication-authorization/built-in-roles[Operations Manual -> Built-in roles]

There are multiple versions of this command, the default being `SHOW ALL ROLES`.
To only show roles that are assigned to users, the command is `SHOW POPULATED ROLES`.
To see which users are assigned to which roles, `WITH USERS` can be added to the command.
This will return an additional `STRING` column, `member`, containing the username.
Since this gives a result with one row for each user, if a role is assigned to two users it will show up twice.

[source, cypher, role=noplay]
----
SHOW POPULATED ROLES WITH USERS
----

The table of results will show information about the role and what database it belongs to:

.Result
[options="header,footer", width="100%", cols="m,m"]
|===
|role
|member

|"PUBLIC"
|"neo4j"

|"PUBLIC"
|"bob"

|"PUBLIC"
|"user1"

|"PUBLIC"
|"user2"

|"PUBLIC"
|"user3"

|"admin"
|"neo4j"

2+a|Rows: 6
|===

It is also possible to filter and sort the results by using `YIELD`, `ORDER BY` and `WHERE`:

[source, cypher, role=noplay]
----
SHOW ROLES YIELD role
ORDER BY role
WHERE role ENDS WITH 'r'
----

In this example:

* The results have been filtered to only return the roles ending in 'r'.
* The results are ordered by the `action` column using `ORDER BY`.

It is also possible to use `SKIP` and `LIMIT` to paginate the results.

.Result
[options="header,footer", width="100%", cols="m"]
|===
|role

|"editor"
|"publisher"
|"reader"

1+a|Rows: 3
|===

[NOTE]
====
The `SHOW ROLE name PRIVILEGES` command is found in xref::access-control/manage-privileges.adoc#access-control-list-privileges[Listing privileges].
====


[[access-control-create-roles]]
== Creating roles

Roles can be created using `CREATE ROLE`:

[source, syntax]
----
CREATE ROLE name [IF NOT EXISTS] [AS COPY OF otherName]
----

Roles can be created or replaced by using `CREATE OR REPLACE ROLE`:

[source, syntax]
----
CREATE OR REPLACE ROLE name [AS COPY OF otherName]
----

[NOTE]
====
The following naming rules apply:

* The first character must be an ASCII alphabetic character.
* Subsequent characters can be ASCII alphabetic, numeric characters, and underscore.
* Role names are case sensitive.
====

A role can be copied, keeping its privileges, using `CREATE ROLE name AS COPY OF otherName`.

.Copy a role
======
[source, cypher, role=noplay]
----
CREATE ROLE mysecondrole AS COPY OF myrole
----
======

Created roles will appear on the list provided by `SHOW ROLES`.

.List roles
======
[source, cypher, role=noplay]
----
SHOW ROLES
----

.Result
[options="header,footer", width="100%", cols="m"]
|===
|role

|"PUBLIC"
|"admin"
|"architect"
|"editor"
|"myrole"
|"mysecondrole"
|"publisher"
|"reader"

1+a|Rows: 8
|===
======

The `CREATE ROLE` command is optionally idempotent, with the default behavior to throw an exception if the role already exists.
Adding `IF NOT EXISTS` to the `CREATE ROLE` command will ensure that no exception is thrown and nothing happens should the role already exist.

.Create role if not exists
======

[source, cypher, role=noplay]
----
CREATE ROLE myrole IF NOT EXISTS
----

======


The `CREATE OR REPLACE ROLE` command will result in any existing role being deleted and a new one created.


.Create or replace role
======

[source, cypher, role=noplay]
----
CREATE OR REPLACE ROLE myrole
----

This is equivalent to running `DROP ROLE myrole IF EXISTS` followed by `CREATE ROLE myrole`.

======


[NOTE]
====
* The `CREATE OR REPLACE ROLE` command does not allow you to use the `IF NOT EXISTS`.
====


[[access-control-rename-roles]]
== Renaming roles

Roles can be renamed using `RENAME ROLE` command:

[source, cypher, role=noplay]
----
RENAME ROLE mysecondrole TO mythirdrole
----

[source, cypher, role=noplay]
----
SHOW ROLES
----

.Result
[options="header,footer", width="100%", cols="m"]
|===
|role

|"PUBLIC"
|"admin"
|"architect"
|"editor"
|"myrole"
|"mythirdrole"
|"publisher"
|"reader"

1+a|Rows: 8
|===

[NOTE]
====
The `RENAME ROLE` command is only available when using native authentication and authorization.
====


[[access-control-assign-roles]]
== Assigning roles to users

Users can be given access rights by assigning them roles using `GRANT ROLE`:

[source, cypher, role=noplay]
----
GRANT ROLE myrole TO bob
----

The roles assigned to each user can be seen on the list provided by `SHOW USERS`:

[source, cypher, role=noplay]
----
SHOW USERS
----

.Result
[options="header,footer", width="100%", cols="2m,3m,3m,2m,2m"]
|===
|user
|roles
|passwordChangeRequired
|suspended
|home

|"bob"
|["myrole","PUBLIC"]
|false
|false
|<null>

|"neo4j"
|["admin","PUBLIC"]
|true
|false
|<null>

|"user1"
|["PUBLIC"]
|true
|false
|<null>

|"user2"
|["PUBLIC"]
|true
|false
|<null>

|"user3"
|["PUBLIC"]
|true
|false
|<null>

5+a|Rows: 5
|===

It is possible to assign multiple roles to multiple users in one command:

[source, cypher, role=noplay]
----
GRANT ROLES role1, role2 TO user1, user2, user3
----

[source, cypher, role=noplay]
----
SHOW USERS
----

.Result
[options="header,footer", width="100%", cols="2m,3m,3m,2m,2m"]
|===
|user
|roles
|passwordChangeRequired
|suspended
|home

|"bob"
|["myrole","PUBLIC"]
|false
|false
|<null>

|"neo4j"
|["admin","PUBLIC"]
|true
|false
|<null>

|"user1"
|["role1","role2","PUBLIC"]
|true
|false
|<null>

|"user2"
|["role1","role2","PUBLIC"]
|true
|false
|<null>

|"user3"
|["role1","role2","PUBLIC"]
|true
|false
|<null>

5+a|Rows: 5
|===


[[access-control-revoke-roles]]
== Revoking roles from users

Users can lose access rights by revoking their role using `REVOKE ROLE`:

[source, cypher, role=noplay]
----
REVOKE ROLE myrole FROM bob
----

The roles revoked from users can no longer be seen on the list provided by `SHOW USERS`:

[source, cypher, role=noplay]
----
SHOW USERS
----

.Result
[options="header,footer", width="100%", cols="2m,3m,3m,2m,2m"]
|===
|user
|roles
|passwordChangeRequired
|suspended
|home

|"bob"
|["PUBLIC"]
|false
|false
|<null>

|"neo4j"
|["admin","PUBLIC"]
|true
|false
|<null>

|"user1"
|["role1","role2","PUBLIC"]
|true
|false
|<null>

|"user2"
|["role1","role2","PUBLIC"]
|true
|false
|<null>

|"user3"
|["role1","role2","PUBLIC"]
|true
|false
|<null>

5+a|Rows: 5
|===

It is possible to revoke multiple roles from multiple users in one command:

[source, cypher, role=noplay]
----
REVOKE ROLES role1, role2 FROM user1, user2, user3
----


[[access-control-drop-roles]]
== Deleting roles

Roles can be deleted using `DROP ROLE` command:

[source, cypher, role=noplay]
----
DROP ROLE mythirdrole
----

When a role has been deleted, it will no longer appear on the list provided by `SHOW ROLES`:

[source, cypher, role=noplay]
----
SHOW ROLES
----

.Result
[options="header,footer", width="100%", cols="m"]
|===
|role

|"PUBLIC"
|"admin"
|"architect"
|"editor"
|"myrole"
|"publisher"
|"reader"

1+a|Rows: 8
|===

This command is optionally idempotent, with the default behavior to throw an exception if the role does not exist.
Adding `IF EXISTS` to the command will ensure that no exception is thrown and nothing happens should the role not exist:

[source, cypher, role=noplay]
----
DROP ROLE mythirdrole IF EXISTS
----
