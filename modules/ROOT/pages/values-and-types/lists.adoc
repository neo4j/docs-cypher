:description: Cypher has comprehensive support for lists.

[[cypher-lists]]
= Lists

[abstract]
--
Cypher has comprehensive support for lists.
--

[NOTE]
====
Information regarding operators, such as list concatenation (`+`), element existence checking (`IN`), and access (`[]`) can be found xref::syntax/operators.adoc#query-operators-list[here].
The behavior of the `IN` and `[]` operators with respect to `null` is detailed xref::values-and-types/working-with-null.adoc[here].
====


[[cypher-lists-general]]
== Lists in general

A literal list is created by using brackets and separating the elements in the list with commas.

.Query
[source, cypher]
----
RETURN [0, 1, 2, 3, 4, 5, 6, 7, 8, 9] AS list
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +list+
| +[0,1,2,3,4,5,6,7,8,9]+
1+d|Rows: 1
|===

In the examples, you use the xref::functions/list.adoc#functions-range[`range`] function.
It gives you a list containing all numbers between given start and end numbers.
Range is inclusive in both ends.

To access individual elements in the list, you can use the square brackets again.
This extracts from the start index and up to, but not including, the end index.

.Query
[source, cypher]
----
RETURN range(0, 10)[3]
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +range(0, 10)[3]+
| +3+
1+d|Rows: 1
|===

You can also use negative numbers, to start from the end of the list instead.

.Query
[source, cypher]
----
RETURN range(0, 10)[-3]
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +range(0, 10)[-3]+
| +8+
1+d|Rows: 1
|===

Finally, you can use ranges inside the brackets to return ranges of the list.

.Query
[source, cypher]
----
RETURN range(0, 10)[0..3]
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +range(0, 10)[0..3]+
| +[0,1,2]+
1+d|Rows: 1
|===

.Query
[source, cypher]
----
RETURN range(0, 10)[0..-5]
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +range(0, 10)[0..-5]+
| +[0,1,2,3,4,5]+
1+d|Rows: 1
|===

.Query
[source, cypher]
----
RETURN range(0, 10)[-5..]
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +range(0, 10)[-5..]+
| +[6,7,8,9,10]+
1+d|Rows: 1
|===

.Query
[source, cypher]
----
RETURN range(0, 10)[..4]
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +range(0, 10)[..4]+
| +[0,1,2,3]+
1+d|Rows: 1
|===

[NOTE]
====
Out-of-bound slices are simply truncated, but out-of-bound single elements return `null`.
====

.Query
[source, cypher]
----
RETURN range(0, 10)[15]
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +range(0, 10)[15]+
| +<null>+
1+d|Rows: 1
|===

.Query
[source, cypher]
----
RETURN range(0, 10)[5..15]
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +range(0, 10)[5..15]+
| +[5,6,7,8,9,10]+
1+d|Rows: 1
|===

You can get the xref::functions/scalar.adoc#functions-size[`size`] of a list as follows:

.Query
[source, cypher]
----
RETURN size(range(0, 10)[0..3])
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +size(range(0, 10)[0..3])+
| +3+
1+d|Rows: 1
|===


[[cypher-list-comprehension]]
== List comprehension

List comprehension is a syntactic construct available in Cypher for creating a list based on existing lists.
It follows the form of the mathematical set-builder notation (set comprehension) instead of the use of map and filter functions.

.Query
[source, cypher]
----
RETURN [x IN range(0,10) WHERE x % 2 = 0 | x^3 ] AS result
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +result+
| +[0.0,8.0,64.0,216.0,512.0,1000.0]+
1+d|Rows: 1
|===

Either the `WHERE` part, or the expression, can be omitted, if you only want to filter or map respectively.

.Query
[source, cypher]
----
RETURN [x IN range(0,10) WHERE x % 2 = 0 ] AS result
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +result+
| +[0,2,4,6,8,10]+
1+d|Rows: 1
|===

.Query
[source, cypher]
----
RETURN [x IN range(0,10) | x^3 ] AS result
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +result+
| +[0.0,1.0,8.0,27.0,64.0,125.0,216.0,343.0,512.0,729.0,1000.0]+
1+d|Rows: 1
|===


[[cypher-pattern-comprehension]]
== Pattern comprehension

Pattern comprehension is a syntactic construct available in Cypher for creating a list based on matchings of a pattern.
A pattern comprehension matches the specified pattern like a normal `MATCH` clause, with predicates like a normal `WHERE` clause, but yields a custom projection as specified.

The following graph is used for the pattern comprehension examples:

image:graph5.svg[]

This example returns a list that contains the year when the movies was released.
The pattern matching in the pattern comprehension looks for `Matrix` in the movie title and that the node `a` (`Person` node with the name `Keanu Reeves`) has a relationship with the movie.

////
[source, cypher, role=test-setup]
----
CREATE
  (keanu:Person {name: 'Keanu Reeves'}),
  (johnnymnemonic:Movie {title: 'Johnny Mnemonic', released: 1995}),
  (somethingsgottagive:Movie {title: 'Somethings Gotta Give', released: 2003}),
  (thematrixrevolutions:Movie {title: 'The Matrix Revolutions', released: 2003}),
  (thematrixreloaded:Movie {title: 'The Matrix Reloaded', released: 2003}),
  (thereplacements:Movie {title: 'The Replacements', released: 2000}),
  (thematrix:Movie {title: 'The Matrix', released: 1999}),
  (thedevilsadvocate:Movie {title: 'The Devils Advocate', released: 1997}),
  (matrix4:Movie {title: 'The Matrix Resurrections', released: 2021}),
  (keanu)-[:ACTED_IN]->(johnnymnemonic),
  (keanu)-[:ACTED_IN]->(somethingsgottagive),
  (keanu)-[:ACTED_IN]->(thematrixrevolutions),
  (keanu)-[:ACTED_IN]->(thematrixreloaded),
  (keanu)-[:ACTED_IN]->(thereplacements),
  (keanu)-[:ACTED_IN]->(thematrix),
  (keanu)-[:ACTED_IN]->(thedevilsadvocate),
  (keanu)-[:ACTED_IN]->(matrix4)
----
////

.Query
[source, cypher]
----
MATCH (a:Person {name: 'Keanu Reeves'})
RETURN [(a)-->(b:Movie) WHERE b.title CONTAINS 'Matrix' | b.released] AS years
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +years+
| +[2021,2003,2003,1999]+
1+d|Rows: 1
|===

The whole predicate, including the `WHERE` keyword, is optional and may be omitted.

This example returns a sorted list that contains years.
The pattern matching in the pattern comprehension looks for movie nodes that has a relationship with the node `a` (`Person` node with the name `Keanu Reeves`).

.Query
[source, cypher]
----
MATCH (a:Person {name: 'Keanu Reeves'})
WITH [(a)-->(b:Movie) | b.released] AS years
UNWIND years AS year
WITH year ORDER BY year
RETURN COLLECT(year) AS sorted_years
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +sorted_years+
| +[1995,1997,1999,2000,2003,2003,2003,2021]+
1+d|Rows: 1
|===

