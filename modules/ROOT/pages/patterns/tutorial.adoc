:description: tutorial to show the fundamental features of graph pattern matching with Cypher. 
= Tutorial

This section contains a tutorial covering some fundamental features of graph pattern matching with Cypher queries.

[[example-graph]]
== Example graph

The example graph we will be using is a model of train `Stations` in London, UK, and different train services with `Stops` that call at the `Stations`:

image::patterns_tutorial.svg[width="700",role="middle"]

To recreate the graph, run the following query against an empty Neo4j database:

[source, cypher, role=test-setup]
----
CREATE (n0:Station {name: "Peckham Rye"}),
(n1:Station {name: "Denmark Hill"}),
(n2:Station {name: "Clapham High Street"}),
(n3:Station {name: "Wandsworth Road"}),
(n4:Station {name: "Clapham Junction"}),
(p0:Stop {arrives: time("17:00"), departs: time("17:01")}),
(p1:Stop {arrives: time("17:05"), departs: time("17:07")}),
(p2:Stop {arrives: time("17:09"), departs: time("17:11")}),
(p3:Stop {arrives: time("17:12"), departs: time("17:13")}),
(p4:Stop {arrives: time("17:19"), departs: time("17:20")}),
(p5:Stop {arrives: time("17:08"), departs: time("17:10")}),
(p6:Stop {arrives: time("17:17"), departs: time("17:18")}),
(p0)-[:CALLS_AT]->(n0), (p1)-[:CALLS_AT]->(n1),
(p2)-[:CALLS_AT]->(n2), (p3)-[:CALLS_AT]->(n3),
(p4)-[:CALLS_AT]->(n4), (p5)-[:CALLS_AT]->(n1),
(p6)-[:CALLS_AT]->(n4), (p0)-[:NEXT]->(p1)-[:NEXT]->(p2)-[:NEXT]->(p3)-[:NEXT]->(p4),
(p5)-[:NEXT]->(p6)
----

[[fixed-length-paths]]
== Matching fixed length paths

Get a count of all the nodes in the graph:

[source, cypher]
----
MATCH ()
RETURN count(*) AS numNodes
----

.Result
[role="queryresult",options="footer",cols="1*<m"]
|===
| +numNodes+

| +12+
1+d|Rows: 1

|===

Get a count of all the nodes with the label `Stop`:

[source, cypher]
----
MATCH (:Stop)
RETURN count(*) AS numStops
----

.Result
[role="queryresult",options="footer",cols="1*<m"]
|===
| +numStops+

| +7+
1+d|Rows: 1

|===

Get the arrival time of all trains calling at the `Station` `Denmark Hill`:

[source, cypher]
----
MATCH (s:Stop)-[:CALLS_AT]->(:Station { name: "Denmark Hill" })
RETURN s.arrives AS arrivalTime
----

.Result
[role="queryresult",options="footer",cols="1*<m"]
|===
| +arrivalTime+

| +"17:08:00Z"+
| +"17:05:00Z"+
1+d|Rows: 2

|===


Get the next calling point of the service that departs from the `Station` `Wandsworth Road` at `17:13`:

[source, cypher]
----
MATCH (n:Station { name: "Wandsworth Road" })<-[:CALLS_AT]-
  (s:Stop WHERE s.departs = time("17:13"))-[:NEXT]->()
  -[:CALLS_AT]->(d:Station)
RETURN d.name AS nextCallingPoint
----

.Result
[role="queryresult",options="footer",cols="1*<m"]
|===
| +nextCallingPoint+

| +Clapham Junction+
1+d|Rows: 1

|===

[[variable-length-paths]]
== Matching variable length paths

List all services going from Denmark Hill to Clapham Junction:

.Query
[source, cypher]
----
MATCH (a:Station)<-[:CALLS_AT]-(s0:Stop)-[:NEXT]->+(s1:Stop)
  -[:CALLS_AT]->(b:Station)
WHERE a.name = "Denmark Hill" AND b.name =  "Clapham Junction"
RETURN s0.departs AS departure, s1.arrives AS arrival
----

.Result
[role="queryresult",options="footer",cols="2*<m"]
|===
|  departure | arrival

| "17:07:00Z" | "17:19:00Z"
| "17:10:00Z" | "17:17:00Z"

2+d|Rows: 2
|===







